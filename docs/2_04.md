

在过去三章里面，我们介绍了 Object Pascal 在 OOP 当中的基础，解释了当中的原理、并介绍了目前在大多数面向对象编程语言当中的功能是如何被实作出来的。从早期的 Delphi 开始，Object Pascal 就已经是一个完整的面向对象编程语言，但还包含了一些特别的作法。事实上，当时 Object Pascal也被质疑只是一个以组件为主的视觉开发工具。

还有一些不相关的功能：这个开发模型的支持是基于一些核心的语言功能而来，例如属性与事件，这两个特性是 Object Pascal 最早提出的，之后才有部分功能被一些 OOP 的语言复制过去使用。像是属性，也可以在 Java跟 C#里面看到，后来也普及在各种语言中，但这都是直接从 Object Pascal承袭过去的，即使如此，我个人还是偏好最原始的实作版本，我稍后会简短的说明一下。

Object Pascal 能够支持快速应用程序开发工具(RAD)以及可视化程序设计的原因，就是属性、published 存取描述关键词、组件的原理，以及在这一章里面会介绍的一些其他的原理。这个开发模式时常被简写成 PME (Property-Method-Event)，这是达成快速开发目标的一种实现作法。


## 定义属性

什么是属性?属性可以被想成是让我们可以用来存取跟变更一个对象的状态，具有潜在的可能性可以透过其副作用来影响对象的规则。在 Object Pascal 里面，属性把对字段、方法的数据存取进行了抽象化与隐藏，使得他们达成了数据封装的主要实作。对属性的一个描述，可以说是『最极致的数据封装』。

从技术面来说，属性是可以说是一个具有型别的识别符号，而这个识别符号是透过 read 跟 write 叙述字来跟实际的数据或方法进行链接的。跟 Java, C#不同的是，在 Object Pascal 里面 read 跟 write 叙述字可以链接到一个取得数据/设定数据的方法，也可以直接连结到数据字段。

举个实例，以下是一个使用一般功能日期对象的属性(从数据字段读取，透过方法来写入):

```pascal
private
 FMonth: Integer;
 procedure SetMonth(Value: Integer);
public
 property Month: Integer read FMonth write SetMonth;
```

要存取 Month 这个属性时，这段源码必须读取私有字段 FMonth，而要变更Month 的内容时，则会去呼叫 SetMonth 这个方法。更改这个数据(只会排除负值而已)的方法，源码会长的像这样:

```pascal
procedure TDate.SetMonth (Value: Integer);
begin
 if (Value <= 0) or (Value > 12) then
 FMonth := 1
 else
 FMonth := Value;
end;
```

!!! note "笔记"
    当出现错误的输入时，例如月份的号码输入了负值，一般最好是直接显示错误(透过触发例外事件)，再从背景修正这个输入的数值，但为了维持范例的单纯，所以我把源码保留原样。

请注意，数据字段跟属性的数据型别必须完全一致(如果当中有不同点，我们可以使用简单的方法来进行转换)。设定数据的方法当中的唯一参数、取得数据的回传值，以及属性这三者的型别，必须完全一致才行。

属性的定义当中也可能出现不同的组合(例如我们也可以用一个方法来读取资料，或者直接把一个数据字段指派在 write 关键词之后直接让其他源码来修改)，但使用方法来修改属性的数值是最常见的。以下是一些定义属性的不同组合，仍以上面这个属性做例子:

 ```pascal
property Month: Integer read GetMonth write SetMonth;
property Month: Integer read FMonth write FMonth;
```

!!! note "笔记"
    当我们写了源码要存取属性时，了解一个方法是怎么被呼叫的就很重要。问题是在于有些方法要花上一些时间来执行，这些方法可能会有一些副作用，通常会包含(延迟)更新屏幕上的视觉组件。即使这些副作用并不常被既载下来，我们仍然应该要记得会有这些副作用，尤其当我们想要优化源码的时候。

一个属性的 write 关键词如果没有被写出来的话，就代表该属性是只读的：

`property Month: Integer read GetMonth;`

技术上来说，我们也可以不写 read 关键词，直接定义一个只能被更新的属性，但通常这样做没什么实质作用，所以也不常被这样用。



### 和其他编程语言的属性相比较

现在，我们如果把这一点跟 Java 或 C#做个比较，会发现这两个语言在属性都是直接对照到方法，但 Java 是隐晦的对照(属性基本上是进行转换)，而C#比较像 Object Pascal 是要直接写明了如何对照，即使只是跟方法对照：

```java
// properties in Java language
private int mMonth;
public int getMonth() { return mMonth; }
public void setMonth(int value) {
 if (value <= 0)
 mMonth = 1;
else
 mMonth = value;
}
int s = date.getMonth ();
date.setMonth (s+1);
// properties in C# language
private int mMonth;
public int Month {
 get { return mMonth; }
 set {
 if (value <= 0)
 mMonth = 1;
 else
 mMonth = value;
 }
}
date.Month++;
```

并不是我想要深入讨论不同编程语言中对于属性的相关优点，不过就像我们在本章的介绍当中所述，在属性定义的时候务必要明确定义，另外透过把属性对应到数据字段提升抽象化的等级，而不造成方法的额外负担也是一个好的作法。

这也是为什么我在这么多种编程语言里面，特别偏爱 Object Pascal 对属性的实作方法了。


### 属性实现了封装概念

属性是非常 OOP 的机制，一个非常棒的作法，让应用程序能够达成数据封装的概念。尤其必要的是，我们必须提供个名称来隐藏一个类别实际上是如何存取信息的(不管是直接存取或是呼叫一个方法)。

事实上，当我们在一个完成的接口中使用了属性，就不可能再去改变它了。同时，如果我们只想让使用者存取到这个类别中的部分数据字段，我们可以把这些字段透过属性加以公布，而不用把数据字段直接搬移到公开区。使用属性，我们不用特别多写什么源码，而且我们仍旧可以更改类别的实作源码。就算我们把直接存取数据字段的作法改成了以方法来存取，我们也不用修改这些属性的原始码。我们只需重新编译器就行了。

!!! note "笔记"
    您可能会觉得奇怪，如果我们在属性里面定义让该属性直接存取一个私有区的数据字段，难道不会因此失去数据封装的优点之一吗。用户不就可以直接动到私有区的变量内容，不用透过 getter 或 setter 方法，这样不会失去对数据的保护吗?然而假设用户会直接透过属性存取数据，设计这个类别的开发人员也可以随时更改数据型别，并加入新的 getter 或 setter 方法来因应，这样就算有其他源码使用到该类别，那些源码也不用因此需要修改。这就是我所谓『封装到极致』的意思。另一方面，这也展示了 Object Pascal 程序化的一面，开发人员可以选择任何一种较简单的方法(或者程序执行效率较高的方法)来处理当前的状况，并在有需要的时候，和缓的把程序转换成适当的 OOP 方法。

然而，在使用 Object Pascal 的属性时，也有一个限制。我们可以把一个数值指派给属性，也可以从属性读取数值，我们也可以自由的在判断式里面使用属性，但我们不能把属性当成传址型的参数来传递给函式或程序。这是因为属性并不是一个实际的内存地址，而是一个抽象的概念，所以不能够用 var加以传递。举例来说，我们不能把属性传给 Inc 函式，但 C#可以。

!!! note "笔记"
    相关的功能，把属性以传址的方式传递，在本章稍后会加以介绍。然而这样的用法非常罕见，而且需要透过对编译器特别的设定，这并不是主流的用法。


### 在宣告属性时使用代码自动完成的功能

在类别里面加入属性是件很枯燥的工作，所以 IDE 的编辑器让我们可以简单的透过『代码自动完成』这个功能来处理。当我们在宣告属性的时候(当然是在类别宣告的时候)，只需要写好前面开头的部份，如以下的例子:

```pascal
type
 TMyClass = class
 public
 property Month: Integer;
 end;
```

先把编辑的文字光标停在这个新增的属性上面，然后按下 Ctrl+Shift+C，编辑器就会自动帮我们把这个属性相应的数据字段、用来修改该属性的方法一起产生出来。不仅是宣告的源码，编辑器会连着实作的源码，例如用来修改该属性的方法以及当中基本的源码，需要宣告什么参数，里头该怎么写，都一起产生出来。换句话说，透过这个热键，上面的属性宣告立刻会变成:

```pascal
type
 TMyClass = class
 private
 FMonth: Integer;
 procedure SetMonth(const Value: Integer);
 public
 property Month: Integer read FMonth write SetMonth;
 end;
{ TMyClass }
procedure TMyClass.SetMonth(const Value: Integer);
begin
 FMonth := Value;
end;
```

如果我们想要修改 getter 方法，只要把 read 部分的定义修改成 GetMonth，像是:

`property Month: Integer read GetMonth write SetMonth;`

然后再按一次 Ctrl+Shift+C，GetMonth 这个方法也又会自动被建立出来了，不过 GetMonth 这个方法的内容我们就得自己写了：

```pascal
function TMyClass.GetMonth: Integer;
begin
end;
```

### 为窗体加入属性

我们来看一个使用属性进行封装的特例。这次我们不建立自定的类别，改成修改 IDE 建立的窗体类别，当然，也会享受到类别自动完成带来的便利。

当应用程序中有多个窗体时，我们通常为了方便，会让窗体之间可以互相存取彼此的信息。我们可以用暴力的作法，就是直接把数据字段宣告在公开区，但这样做在 OOP 的程序概念中其实不好。每当我们需要让窗体之间交换数据的时候，我们应该使用属性。

我们在窗体类别里面宣告简单的属性名称跟型别:

`property Clicks: Integer;`

然后按下 Ctrl+Shift+C，启动代码自动完成功能，我们就会看到以下的效果:

```pascal
type
 TFormProp = class(TForm)
 private
 FClicks: Integer;
 procedure SetClicks(const Value: Integer);
 public
 property Clicks: Integer read FClicks write SetClicks;
 end;
implementation
procedure TForm1.SetClicks(const Value: Integer);
begin
 FClicks := Value;
end;
```

不用说，这功能真的帮我们省下很多打字的时间(更别说有时候会打错字)。

现在当用户点击窗体的时候，我们就可以透过下面这行程序把计数器的数字递增，我们可以从 FormProperties 范例项目里面的 OnMouseDown 事件处理程序看到:

`Clicks := Clicks + 1;`

您可能会觉得奇怪，怎么不直接对 FClicks 做递增呢? 当然，在这个例子里面，直接做递增也是可行的，但我们可能会在 SetClicks 这个方法里面同时更新一些用户接口，然后把目前的数值做更新：如果我们不管属性，直接存取数据字段，setter 方法的副作用可能就不会被执行，例如数值的更新就不会同时被在接口上同步更新了。

这个封装的另一个优点，是我们可以从另一个窗体来读取这个窗体被点击的数字，当然，是透过适当的抽象方式。事实上，窗体类别的属性可以用来读取自定的数据，也可以封装窗体的组件存取。举个例子，如果我们有一个窗体，上面有个卷标用来显示一些信息，而我们想要透过另一个窗体来修改上头的文字，我们可能会用这种暴力写法:

`Form1.StatusLabel.Text := 'new text';`

这是通常的实用写法，但并不是个好作法，因为它并没有对窗体的结构或组件提供任何封装。如果我们在整个程序项目中有很多地方都这样写，而稍后我们决定要修改窗体上面的用户接口(或许可以让每次 Label 更新时都做些事情)，我们就得修改很多地方了。

另一个替代方案则是使用方法，或者使用属性来隐藏特定的控件，每个如下所示的宣告:

`property StatusText: string read GetStatusText write SetStatusText;`

如果我们输入了上面这行宣告，然后按下 Ctrl+Shift+C，让编辑器帮我们把这两个方法的宣告都先产生好:

```pascal
function TFormProp.GetStatusText: string;
begin
 Result := LabelStatus.Text
end;
procedure TFormProp.SetStatusText(const Value: string);
begin
 LabelStatus.Text := Value;
 end;
```

请注意，在这个案例中，属性并没有跟任何类别中的数据字段对应，而是跟一个子控件，也就是卷标对应(万一我们使用了代码自动完成功能，请记得先把编辑器帮我们产生的 FStatusText 数据字段给删掉喔)。

在程序中的另一个窗体，我们就可以直接参照这个窗体的 StatusText 属性，就算用户接口做了一些修改，到时候也只要改 Get 跟 Set 这两个方法。即使是在窗体当中的其他方法，我们只需要直接使用该属性，不用再去存取原本的控件了：

```pascal
procedure TFormProp.SetClicks(const Value: Integer);
begin
 FClicks := Value;
 StatusText := FClicks.ToString + ' clicks';
end;
```

### 为 TDate 类别加入属性

我们在第七章里面建立了一个名为 TDate 的类别，现在我们来用属性帮这个类别做些延伸。这个新的范例项目名为 DateProperties，基本上是第七章的ViewDate 范例项目的延伸。以下是这个类别的重新宣告，里面包含了一些新的方法(作为取得与设定属性的内容)以及四个属性:

```pascal
type
 TDate = class
 private
 FDate: TDateTime;
 function GetYear: Integer;
 function GetDay: Integer;
 function GetMonth: Integer;
 procedure SetDay (const Value: Integer);
 procedure SetMonth (const Value: Integer);
 procedure SetYear (const Value: Integer);
 public
 constructor Create; overload;
 constructor Create (y, m, d: Integer); overload;
 procedure SetValue (y, m, d: Integer); overload;
 procedure SetValue (NewDate: TDateTime); overload;
 function LeapYear: Boolean;
  procedure Increase (NumberOfDays: Integer = 1);
 procedure Decrease (NumberOfDays: Integer = 1);
 function GetText: string; virtual;
 property Day: Integer read GetDay write SetDay;
 property Month: Integer read GetMonth write SetMonth;
 property Year: Integer read GetYear write SetYear;
 property Text: string read GetText;
 end;
```

Year, Day,跟 Month 属性的读写都是透过对应的方法，以下是其中两个跟Month 属性相关的方法：

```pascal
function TDate.GetMonth: Integer;
var
 Y, M, D: Word;
begin
 DecodeDate (FDate,Y, M, D);
 Result := M;
end;
procedure TDate.SetMonth(const Value: Integer);
begin
 if (Value < 1) or (Value > 12) then
 raise EDateOutOfRange.Create (‘Invalid month’);
 SetValue (Year, Value, Day);
end;
```

呼叫 SetValue 会对日期进行实际的编码，万一有错的时候也会触发例外。我定义了一个自定的例外类别，会当数值超过范围的时候被触发：

```pascal
type
EDateOutOfRange = class (Exception);
```

第四个属性，Text 只对应到一个读取的方法。这个方法被宣告为虚拟方法，因为它会被 TNewDate 子类别取代。而属性的 Get 或 Set 方法没有理由，不应该使用延迟绑定(这个技术在第八章里面已经用很长的篇幅介绍过)。

!!! note "笔记"
    在这个范例里面值得一提的，是属性不要直接对应到数据字段。属性可以简单的从储存在不同型别的信息中被计算出来，并用不同的数据结构加以储存。

在类别里面加入了新的属性之后，我们现在可以把范例程序透过属性做一些适当的修改了。例如我们可以直接使用 Text 属性，我们也可以使用一些文字框让用户读写这三个主要属性的数值。我们可以让用户按下 BtnRead 按钮来读取数据:

 ```pascal
procedure TDateForm.BtnReadClick(Sender: TObject);
begin
 EditYear.Text := TheDay.Year.ToString;
 EditMonth.Text := TheDay.Month.ToString;
 EditDay.Text := TheDay.Day.ToString;
end;
```

BtnWrite 按钮则是提供写入的动作，我们可以用以下两种方式来达成这个功能:

```pascal
// Direct use of properties
TheDay.Year := EditYear.Text.ToInteger;
TheDay.Month := EditMonth.Text.ToInteger;
TheDay.Day := EditDay.Text.ToInteger;
// update all values at once TheDay.SetValue (EditMonth.Text.ToInteger,
 EditDay.Text.ToInteger,
 EditYear.Text.ToInteger);
```

这两个方法之间的差异，是在使用者没有输入正确的日期时要怎么处理。当我们让每个数值分开设定的时候，程序可能只修改了年，然后触发例外，并跳过其他的源码，所以到时候日期就只有一部分被修改。当我们把所有的数值一起设定时，要不就格式正确，全部一起设定好，要不就格式错误，原来的日期数值都不变。其实，这就是为何这三个属性要设定为只读的主要原因：要维持数据的完整性。


### 使用数组属性

通常属性是让我们存取一个数据，甚至是一个复杂的数据型别。但 Object Pascal 也可以定义数组属性，在 C#里面这样的属性也被称为 indexer。数组型别透过进一步的参数，是可以包含任意数据型别的，只要该型别可以被当成索引或者实际数值的撷取者。

以下是定义一个数组属性的范例，在这个范例里面使用了整数作为索引值，储存的内容也是整数：

```pascal
private
 function GetValue(I: Integer): Integer;
 procedure SetValue(I: Integer; const Value: Integer);
public
 property Value [I: Integer]: Integer read GetValue write SetValue;
```

数组属性必须有对应的读取与写入方法，这两个方法都得有个额外的参数用来作为索引值，我们可以透过代码自动完成的功能来定义一般的属性。

在 RTL 里面有一些类别很常使用数组属性，它们的索引值跟数据有很多种组合。例如，在 TStrings 类别里面就定义了以下五个数组属性：

```pascal
property Names[Index: Integer]: string read GetName;
property Objects[Index: Integer]: TObject
 read GetObject write PutObject;
property Values[const Name: string]: string
 read GetValue write SetValue;
property ValueFromIndex[Index: Integer]: string
 read GetValueFromIndex write SetValueFromIndex;
property Strings[Index: Integer]: string
 read Get write Put; default;
```

这些数组属性当中，大多数都是用整数索引值作为列表的参数，少部分则是直接用字符串作为搜寻值(像是上面所列出的 Values 属性)。上面这五个定义当中的最后一个，使用了另一个重要的功能:default 关键词。这也是一个很强大的语法助手: 这个数组属性的名称就可以直接被省却，所以我们可以直接在对象后面加上方括号来存取该属性的内容。

换句话说，当我们有个 TStrings 型别对象 sList 时，以下两种写法都是相同的意义：

```pascal
sList.Strings[1]
sList[1]
```

也就是说，默认的数组属性，可以让任何对象透过自定的方括号[ ]直接存取里面的数据。


### 以参考设定属性

!!! warning "提示"
    这是一个相对进阶的主题(且这个功能也很少被用到)，所以如果您对 Object Pascal 还没有很精通的话，可能会想直接跳过这一节。

在 Object Pascal 编译器被延伸去支持 Windows COM 程序的时候，就被赋予了处理”以参考设定”(put by ref)属性的能力(用 COM 的术语来说)，或者说可以接受指针参考值的属性，这属性中储存的不是实际数据。

!!! note "笔记"
    ”以参考设定”(put by ref)，是由 Chris Bensen 为这个功能命名的，它是在该功能被介绍之后，在 Chris 的部落格上介绍的: http://chrisbensen.blogspot.com/2008/04/delphi-put-by-ref-properties.html (Chris 当时是这个产品的研发工程师)

在它的设定方法(setter method)当中，是以 var 参数来描述的。假设这会导致相对尴尬的情况，这个功能就该被认为是例外，而不是规则了，这也是为何它为何预设是不使用的。

换句话说，如果我们要使用这个功能，得要自己写上这么一行指令来启用编译设定：

`{$VARPROPSETTER ON}`

没有写上这行指令的话，以下的源码就无法被编译，而且编译器会显示这个错误讯息: ”E2282	Property	setters	cannot	take	var	parameters”:

```
type
TMyIntegerClass = class
 private
 FNumber: Integer;
 function GetNumber: Integer;
 procedure SetNumber(var Value: Integer);
 public
 property Number: Integer read GetNumber write SetNumber;
 end;
```

这个类别是 VarProp 范例项目的一部分。现在我们会在这个属性的 setter 方法里面看到很奇怪的副作用了:

```pascal
procedure TMyIntegerClass.SetNumber(var Value: Integer);
begin
 Inc (Value); // Side effect
 fNumber := Value;
end;
```

另一个非常不寻常的效应，是我们无法把常数数据指派给属性，不是变量喔(变量应该是可以的，就像把变量当成任何一个传址函式的参数一样传递)：

```pascal
var
 mic: TMyIntegerClass;
 n: Integer;
begin
 ...
 mic.Number := 10; // Error: E2036 Variable required
 mic.Number := n;
```

这跟我们常用的功能不同，这是相对比较进阶的功能，让我们思考一下是要为一个属性初始化，或是为即将指派给它的数值资料提出警告。这可能导致很奇怪的源码：

```pascal
 n := 10;
 mic.Number := n;
 mic.Number := n;
 Show(mic.Number.ToString);
``` 

这两种写法效果都一样，但看上去就很奇怪，但它们也都产生了副作用，就是把实际的数字变成了 12。这也许是取得运算结果的方法中，最让人无法理解、觉得荒谬的一种。


## 发布(Published)存取关键词

除了 public, protected, 以及 private 这几个存取关键词之外(当然还有比较不常用的 strict private 跟 strict protected)，在 Object Pascal 里面还有另一个奇特的存取关键词，叫做 published(发布)。一个发布区的属性(或数据字段、方法)，不只可以在运行时间具备公开属性的特性，同时也会建立出延伸的运行时间型别信息(Run-time Type Information, RTTI)以供查询。

事实上在一个编译后执行的语言里面，编译过的符号会被编译器处理过，在测试程序的时候，可以被侦错程序所使用，但在运行时间并不会被追踪。换句话说(至少在早期的 Object Pascal 是如此)，如果一个类别拥有一个名为Name 的属性，我们就可以在源码里面跟该类别进行互动，但这个互动会透过被转译成数字化的内存地址进行，在运行时间当中，这些关键词将不复存在，所以我们无从得知一个类别是否有相符的关键词，例如 Name。

!!! note "笔记"
    Java 跟 C#也都是编译后执行的语言，两者也都从复杂的虚拟执行环境而获得好处，也因为虚拟执行环境，在这两者当中都可以取得延伸的执行时期信息，通常称为 reflection。Object Pascal 从早期开始就具备有基本的reflection 功能(透过 published 关键词)，比当时的其他编译后执行的编程语言更早，且之后也提供了更多的 reflection 或延伸 RTTI 功能。基本的 RTTI信息是跟着 published 关键词，在本章里面被提到的，在第十六章里面会对reflection 有更全面的介绍。

为什么类别会需要延伸的信息呢?它是 Object Pascal 函式库所依赖的组件模型与可视化程序模型的基础之一。这些信息当中，有一部分会在设计时间的开发环境中被使用到，例如当我们点击了视觉组件时，该组件的相关属性、事件就会被显示在对象查看器中。这可不是写死的程序行表，它们是由编译完成的源码在运行时间检测延伸 RTTI 信息而实时产生出来的。

另一个例子，或许现在来介绍有点太复杂了，则是在建立 FMX 跟 DFM 档案时背后使用到的串流机制。串流机制我们会在第十八章里面介绍，因为它比较属于运行时间函式库的一部分，跟编程语言的核心比较没那么相关。

为这个概念做个整理，published 关键词一般的重要使用时机，是当我们在写组件给自己的程序或者给其他人使用的时候。通常组件的 published 部分只会包含属性，而窗体类别的则还会把 published 用来宣告数据字段跟方法，我们稍后会介绍。

### 设计时间属性

我们在本章的前面已经介绍过，属性在类别的数据封装技术中扮演了重要的角色。它们也在视觉开发模型的功能中扮演了基础角色。事实上，我们可以写一个组件类别，让它能够在设计时间的窗体中，透过 IDE 的组件列表直接用拖拉的方式被加到一个视觉窗体或者视觉组件上面去，并且可以透过对象查看器(这个工具是 Delphi 提供来存取属性之用。)来跟它的属性做互动。并非所有的属性都可以透过这样的情形来使用，只有在组件类别中被标注为published 的属性才可以。这也是 Object Pascal 的开发人员要把属性在**设计时间**跟**运行时间**之间做出区隔的原因。

设计时间的属性必须被宣告在类别的 published 区里面，这些属性可以在设计时间的 IDE 跟源码里面使用。而其他宣告在 public 区里面的属性，则不能在 IDE 里面使用，只能在源码里面使用，所以这些属性通常也被称为运行时间专用(Runtime only)。

换句话说，我们可以在 Object Pascal 设计时间的 IDE 中，透过对象查看器里面看到该属性的内容，也可以修改它。在运行时间，我们可以从源码里面用同样的方法来存取所有类别宣告在公开区或发布区的属性。

并不是所有的类别都有属性。属性只会在组件，以及 TPersistent 类别的衍生类别才会出现，因为属性可以被串流化，并且存到档案里面。像是窗体档案，就只是窗体上面所有组件的 published 属性的集合而已，也没有记录什么其他的了。

更精确一点，我们不需要从 TPersistent 衍生类别，也可以把属性宣告在published 区，但我们需要在编译类别的时候设定$M 这个编译器开关。每个透过这个编译器开关进行编译的类别，或者从这样的类别所衍生的子类别，都可以支持 published 区。假设 TPersistent 类别就是包含着这个编译器开关做编译的，所以其下的所有衍生类别也都获得了支持。

!!! note "笔记"
    接下来的两小节在默认程序可见区域跟自动化的 RTTI 中都加了额外的信息以支持$M 编译器开关跟 published 关键词。



### 发布(published)与窗体

当IDE建立了一个窗体，就会把它的组件跟方法放在初始的定义中，在public跟 private 关键词之前。这些放在初始部分的数据字段跟方法都会被视为该类别的 published 区块。

要记得，虽然这些都是全局可视的变量，真的在全局的程序中随意使用它们并不是好主意，真的要这么做，一定要打起十二分精神，在稍早介绍在窗体中加入属性的篇幅已经讨论过了。也要记得，当我们宣告一个 component类别的元素，在前面明确显清楚存取的关键词时，默认的存取层级就是published。

!!! note "笔记"
    更精准一点来说，published 只有以$M 作为编译器开关，或者从这一类的类别衍生出的子类别中，才会是默认的关键词。就因为在 TPersistent 类别中使用了这个开关，所以函式库里面大多数的类别，以及大多数的组件类别都默认是 publish 区。然而非组件的类别(像是 TStream 跟 TList)则是以$M-这个设定编译的，所以预设只有到 public 区段。

以下是一个例子:

```pascal
type
 TForm1 = class(TForm)
 Memo1: TMemo;
 btnTest: TButton;
```

要被指派给任何事件的方法，都必须是被发布的(published)方法，且在窗体中对应到组件的数据字段也该是被发布的，这样才能自动跟窗体档案中的对象链接起来，并且自动被建立在窗体里面。也只有被宣告在窗体中最初那区的组件跟方法才能在对象查看器里面显示出来(在窗体的组件列表或者当我们在对象查看器里面以下拉选单选取可以被对应到的事件处理程序时)。

为什么类别的组件应该要被宣告为被发布的呢?如果放在私有区，不是更依循 OOP 的封装规则吗?原因是因为这些组件会在读取串流化表示的数据时就被建立出来，但一旦建立出来，它们就需要被指派给对应的表单域。

这必须透过 RTTI 为被发布的字段来产生。

!!! note "笔记"
    技术上来说，组件并没有义务要使用被发布的字段。我们可以把这些字段放在私有区，这样更符合 OOP 的精神。然而，这也需要更多额外的运行时间的源码。我会在本章的最后『RAD 跟 OOP』那个小节来做些说明。


### 自动 RTTI 

另一个在 Object Pascal 编译器当中的特别规则，则是如果我们在类别当中加入 published 关键词，而这个类别并不是从 TPersistent 类别衍生而来，编译器就会自动启动 RTTI，加入{$M+}的规则。

假设我们建立了以下这个类别：

```pascal
type
 TMyTestClass = class
 private
 FValue: Integer;
 procedure SetValue(const Value: Integer);
  published
 property Value: Integer read FValue write SetValue;
end;
```

编译器就会显示以下的警告讯息:

`[dcc32 Warning] AutoRTTIForm.pas(27): W1055 PUBLISHED caused RTTI ($M+) to be added to type 
'TMyTestClass'`

编译器就自动的把{$M+}加入了源码。我们可以从范例项目 AutoRTTI 里面找到这段源码。在这个程序中，我们可以写成以下的源码，使用动态方式来存取一个属性(使用传统的 TypInfo 单元):

```pascal
uses
 TypInfo;
procedure TFormAutoRtti.btnTetClick(Sender: TObject);
var
 test1: TMyTestClass;
begin
 test1 := TMyTestClass.Create;
 try
 test1.Value := 22;
 Memo1.Lines.Add (GetPropValue (test1, 'Value'));
 finally
 test1.Free;
 end;
end;
```

!!! note "笔记"
    虽然我不常使用 TypInfo 单元跟其中的函式，像是 GetPropValue，RTTI 的真正力量是来自于更现代的 RTTI 单元文件，以及在其中对 reflection 的外延支持。由于这是比较复杂的主题，我觉得应该要用单独的一章来介绍它，并同时介绍 Object Pascal 支持的 RTTI 功能的两个效益。


## 事件驱动程序作法

在以组件为基础的函式库(当然还有其他很多情境也是)中，我们写的源码并不是把一连串的动作摊开来，而是许多动作在不同的时间点触发的动作累积而成的。以这个概念来看，我想我们在写程序时，都只是在定义程序遇到这些触发的动作时应该如何反应。而这个『动作』可能是使用者的操作，例如点击按钮，可能是操作系统的动作，例如某个传感器的状态改变，或者是远程联机的数据已经准备好了，可能是我们想得到或想不到的任何情形。

这些外部或内部的触发动作，都被通称为事件。事件最初是与讯息导向的操作系统对应的，像 Window，但已经跟原始的理论有很大的不同了。事实上在比较新的函式库里面，大多数的事件都是当用户在设定属性、呼叫方法，或者跟特定的组件互动时由内部驱动的(或者间接由其他类别驱动)。

事件跟事件驱动是怎么跟 OOP 产生关连的?这两个要求在建立新的衍生类别的方式以及时间点都是不同的。

在纯粹 OOP 的格式当中，当一个对象的行为(或方法)跟另一个对象不同时，他们就该被归类为不同的类别。我们已经看过了一些关于这些效应的范例。

让我们思考一下这个情形。一个有四颗按钮的窗体，让每个按钮在被点击时都需要有不同的反应。所以在纯粹的 OOP 名词里，我们应该为这个窗体上的四个按钮都单独做出一个不同的衍生类别，再单独赋予不同的 click 方法。这样的要求理论上是对的，但实际上会产生很多程序的工作要做，而且没有什么弹性。

事件驱动的程序写法，则考虑到相似的情境，并建议开发人员为按钮对象加上一些不同的规则，这些按钮对象仍然可以属于同一个类别。规则上变成了为可以为每个对象的状态进行扩充，而不用衍生不同的类别。这个模式称为委任(delegation)，因为一个对象的行为是委任给一个类别的方法，而不是由该对象所属的类别来处理的。

事件在不同的编程语言中可以用不同的方式加以实现，例如：

+ 透过方法参考(在 Object Pascal 里面则称之为方法指标)，或者在内部方法中使用事件对象(像 C#的作法)。
+ 把处理事件的源码委任给一个特定的类别，实作一个接口(像在 Java 里面最常见的作法)
+ 使用程序区块，就像 JavaScript 里面最常使用的(在 Object Pascal 里面也支持了匿名方法，我们在第 15 章里面介绍)。虽然在 JavaScript 里面所有的方法也都是程序区块，但在该语言里面，这两个概念就有点混淆了。

如果事件跟事件驱动的程序写作概念，在过去十几年里面已经变得很平常，且在大多数不同的编程语言里面都已经支持，那么 Object Pascal 实作事件的方法就是相当独特的，我们在接下来这一小节里面来谈谈。


### 方法指标(Method	Pointers)

我们在第四章的最后一部分已经介绍过 Object Pascal 的函式指标概念。这是以一个变量来储存函式的内存地址，我们可以依此间接的使用这个函式。一个函式指标是以一个特别的方式来宣告的(以一系列的参数型别跟回传值加以宣告，如果该函式有参数跟回传值的话)。

跟函式指标很像，Object Pascal 里面还有方法指标。方法指针是记录属于一个类别的方法的内存地址。就像函式指标型别，方法指标型别也有一个特殊的宣告方法。然而，方法指针带有更多的信息，像是该方法指标是属于哪个对象的(或者这个方法里面如果需要使用到对象的内容时，应该透过 self 来作为参考)。

换句话说，方法(亦即该方法在内存中的进入地址，由该类别所有的个体共享之)指针是一个对象所属方法的参考(特定个体在内存空间中专属的空间，用以储存该个体的数据)。当我们把一个值指派给方法指标时，我们必须以特定的对象来作为该方法的拥有者，例如特定对象实体的方法。

!!! note "笔记"
    如果能够看一下方法指针的定义与数据结构是如何建构出来的，一定会对了解方法指标的实作过程有更深入的了解，这个型别就称为 TMethod。这个记录型别有两个数据字段 Code 跟 Data，分别表示方法的地址跟它所属的对象。在其他的语言里面，程序参考是由委任类别(C#)或者由接口的方法(Java)来掌控的。

方法指针型别的宣告跟程序方法也很像，除了方法指标需要在结尾加上一个object 关键词：

```pascal
type
 IntProceduralType = procedure (Num: Integer);
 TStringEventType = procedure (const s: string) of object;
```

当我们宣告了一个方法指标，就像上面这样的宣告，我们可以宣告一个以这个方法指针作为型别的变量，并把任何对象中兼容的方法指派给这个变量。什么是兼容的方法?第一是需要跟方法指标拥有相同的参数，例如上述的例子中的方法指针是要求一个字符串为参数。任何一个对象所属方法的参考，只要兼容于方法指标型别，就可以被指派到以该方法指标为型别所宣告的变量里面。

现在，我们有了一个方法指标型别，就可以用这个类型声明一个变量，然后把一个兼容的方法指派给这个变量了：

```pascal
type
 TEventTest = class
 public
 procedure ShowValue (const s: string);
 procedure UseMethod;
 end;
procedure TEventTest.ShowValue (const s: string);
begin
 Show (s);
end;
procedure TEventTest.UseMethod;
var
 StringEvent: TStringEventType;
begin
 StringEvent := ShowValue;
 StringEvent ('Hello');
end;
```

目前这简单的源码还无法真的解释事件的好用，因为上面这源码还聚焦在方法指标型别的低阶理论上。事件就是以此为基础建立起来的，并以储存一个对象的方法指针，指派给不同的对象的作法，超越了这个简单的概念(例如，我们把窗体的 OnClick 事件处理程序指派给按钮)。在大多数的情形下，事件也是透过属性加以实现的。

!!! note "笔记"
    这作法不太常见，在 Object Pascal 我们则也可以使用匿名方法来定义一个事件处理程序。不太常见的原因或许是当这个功能在 Object Pascal 里面最近才支持，而许多函式库已经存在很久了。而且这功能的复杂度又更高一些。我们可以在第 15 章里面找到一些范例是关于这个公囊的。另一个可能的延伸则是对单一一个事件定义了多种事件处理程序，像是 C#就支持这作法，虽然不是标准的功能，但我们可以自己实作出这样的功能。


### 委任的原理

猛一看，这个技术的目的不太清楚，但它的确是 Object Pascal 组件科技的奠基石之一。秘密就在于委任这个名词。如果我们建立了一个对象，在对象里面有一些方法指针，我们就可以很自由的把对象的规则改来改去，只需要把这些指标指派给新的方法就行了。这听起来很习惯吗?你应该习惯的。

当我们为按钮加入一个 OnClick 事件，开发环境就是帮我们做了这件事。该按钮有一个方法指针，名为 OnClick，我们可以直接或间接的把窗体的方法指派给它。当用户点击了这个按钮，这个方法就会被执行，即使我们把这方法定义在另一个类别里面(通常是在窗体中)。

以下我们列出在 Object Pascal 函式库里面的一些实战源码，用来把一个按钮的事件处理程序跟窗体的方法关连在一起:

```pascal
type
 TNotifyEvent = procedure (Sender: TObject) of object;
 TMyButton = class
 OnClick: TNotifyEvent;
 end;
 TForm1 = class (TForm)
 procedure Button1Click (Sender: TObject);
 Button1: TMyButton;
 end;
var
 Form1: TForm1;
```

接下来是一个内部的方法，我们可以这样写：

`MyButton.OnClick := Form1.Button1Click;`

上面的源码跟实际上在函式库里面的只有一点点不同，不同的是在函式库里面是以 OnClick 作为属性的名称，而实际的数据则将之命名为 FOnClick。组件的事件会被列在对象查看器的事件分页里面。事实上，事件这种属性也就是方法指针。

这表示我们可以在设计时间动态的把事件处理程序改为与特定对象连接，或者在运行时间建立一个新的组件，然后把事件处理程序指派给新的对象当中的方法。DynamicEvents 范例项目中有这两种作法的案例。窗体中有一个按钮，使用标准的 OnClick 事件处理程序。然而我也在窗体中加入了第二个公开方法，这个方法的参数跟该事件处理常数一致(具备方法兼容性):

```pascal
public
 procedure btnTest2Click(Sender: TObject);
```

当按钮被点击的时候，除了显示讯息，还会把事件处理程序指派到第二个风法去，把额外的动作加入到点击的处理程序中：

```pascal
procedure TForm1.btnTestClick(Sender: TObject);
begin
 ShowMessage ('Test message');
 btnTest.OnClick := btnTest2Click;
end;
procedure TForm1.btnTest2Click(Sender: TObject);
begin
 ShowMessage ('Test message, again');
end;
```

这样一来，当用户第一次点击按钮的时候，原先的事件处理程序会被执行，点第二次的时候，我们就会看到第二个事件处理程序被执行了。

_____________________________________________________________________
!!! note "笔记"
    当我们要输入源码，把一个方法指派给一个事件的时候，代码自动完成的功能会建议我们可用的事件名称，并把它转成一个实际的程序呼叫写法，连小括号都会附上。这是不对的。我们是要把它指派给一个事件处理程序，不是要呼叫它。否则编译器就会试着把该方法执行的结果指派到事件处理程序的变量去(但如果是一个程序，没有回传值，就又会出错)，最后就会发生错误了。

项目源码的第二个部分，介绍了完整的动态事件关连。当我们点击了窗体的画面，就会随机建立出一个新的按钮出来，并把所属的按钮的文字显示出来(Sender 对象会把事件处理程序自动绑定到动态建立的按钮上，该按钮被点击的时候，就会呼叫对应的事件处理程序)：

```pascal
procedure TForm1.btnNewClick(Sender: TObject);
begin
 ShowMessage ('You selected ' + (Sender as TButton).Text);
end;
procedure TForm1.FormMouseDown(Sender: TObject; Button: TMouseButton;
 Shift: TShiftState; X, Y: Single);
var
 AButton: TButton;
begin
 AButton := TButton.Create(Self);
 AButton.Parent := Self;
 AButton.SetBounds(X, Y, 100, 40);
 Inc (FCounter);
 AButton.Text := 'Button' + IntToStr (FCounter);
 AButton.OnClick := btnNewClick;
end;
```

透过这段源码，每一个随机建立出来的按钮都会在自己被点击的时候，把自己的按钮文字显示出来，即使使用的是共享的一个事件处理程序，感谢事件里面有使用到 sender 这个参数。这个程序的执行画面，如图 10.1 所示：

图 10.1: DynamicEvents项目的执行画面，动态产生按钮与讯息

**事件也是属性**

在 Object Pascal 里面的事件，几乎都是使用方法指针型别的属性来实作的，这一点也很重要。这表示要处理一个组件的事件，我们只需要把方法指派给对应的事件属性即可。用程序的术语来说，这代表着我们可以把一个对象的方法指派作为一个事件处理程序，我们用前几节里面的源码作为范例来看一下:

`Button1.OnClick := ButtonClickHandler;`

再提一次，规则是该事件的方法指标型别必须跟我们想要指派的方法兼容，不然的话编译器会指出这个错误。

系统中原就为事件定义了几种方法指标型别，这些型别很常用到，我们从简单的开始看：

```pascal
type
TNotifyEvent = procedure (Sender: TObject) of object;
```

这就是最常见的 OnClick 事件处理程序的型别，我们用以上这个型别做个简单的方法宣告:

`procedure ButtonClickHandler (Sender: TObject);`

这听起来如果有点疑惑，请回想一下在开发环境中的操作。点选任何一个按钮，例如 Button1，我们在按钮上面双击，OnClick 事件就会在开发环境的组件查看器当中被列出来了，并且会在该组件的类别需告终加入一个空白的方法实作源码：

```pascal
procedure TForm1.Button1Click (Sender: TObject) begin
end;
```

我们要自己在上述的这个空的方法里面撰写源码，把任何我们想要的功能加进去。这是因为事件处理程序的方法已经在背景被指派给该事件了。我们在设计时间中，为任何其他的属性进行数值、方法的设定，也都会以完全相同的方法被指派给该组件。

从上面的描述里，我们可以了解到，在事件跟被指派给该事件的方法之间并没有一步一步的对应。相当奇怪。我们可以让很多个事件共享一个事件处理程序，这也说明了为何要很常用到 Sender 这个参数的原因，这个参数让我们知道是哪个对象驱动了这个事件。举例来说，如果我们把 OnClick 这个事件处理程序指派给两个按钮，Sender 这个值在一个按钮对象被点击的时候，就会包含被该按钮的指针。


!!! note "笔记"
    我们可以透过源码把同一个方法指派给不同的事件，如上所述，但在设计时间也行。当我们从组件查看器的事件当中点选特定的一个事件，我们可以点选该事件右方的下拉选单钮，它会把目前类别中兼容于该事件的方法都列上来。我们就可以从当中选取一个要用来处理该事件的方法。透过这个方法，我们可以让不同的组件所属的事件用同一个方法来处理。

**为 TDate 类别加⼊⼀个事件**

我们刚刚已经为 TDate 类别加入了一些属性，现在我们再来加入一些方法吧。这个事件将会很简单，它的名字是 onChange，可以用来告诉用户组件的内容已经改变了。要定义一个事件，我们只需定义一个与之对应的属性，然后加入一些数据字段来储存对应的方法指标即可。以下就是 DateEvent 范例项目中，新增的一些定义：

```pascal
type
 TDate = class
 private
 FOnChange: TNotifyEvent;
 ...
 protected
 procedure DoChange; dynamic;
 ...
 public
 property OnChange: TNotifyEvent read FonChange write FOnChange;
 ...
 end;
```

这些属性的定义实在相当简单。使用这个类别的开发人员可以指派一个新的值给它，然后这个值会被存放在私有区的数据字段 FOnChange 里面。在程序启动的时候，这个数据字段通常是未被指派的: 事件处理程序是给组件的用户用的，不是给组件的制作者用的。组件的制作者如果需要一些规则的话，则需要在组件方法中加入。

换句话说，TDate 类别单纯的接收到一个事件处理程序，当日期数据被改变成，就呼叫储存在 FOnChange 字段的方法。当然，这个方法只会在事件的属性有被指派的时候才会被呼叫。

DoChange 方法(以动态方法来宣告，是传统对事件触发方法的处理准则)会先进行检测，并呼叫该方法：

```pascal
procedure TDate.DoChange;
begin
 if Assigned (FOnChange) then
 FOnChange (Self);
end;
```
!!! note "笔记"
    您可能还有印象，在第八章里面，动态方法跟虚拟方法是类似的，但在实作上有些不同，动态方法节省了内存的用量，并节省了相当多的时间。

DoChange 方法会在每次数值改变的时候被呼叫，就像以下的程序所示：

```pascal
procedure TDate.SetValue (y, m, d: Integer);
begin
 fDate := EncodeDate (y, m, d);
 // fire the event
 DoChange;
```

现在，如果我们深入看一下使用这个类别的城市，我们可以简化一下源码。首先我们为窗体类别加入一个自定的方法:

```pascal
type
 TDateForm = class(TForm)
 ...
 procedure DateChange(Sender: TObject);
```

这个方法里面的源码会简单的把目前的卷标文字改为 TDate 对象的 Text 属性的文字内容。

```pascal
procedure TDateForm.DateChange;
begin
 LabelDate.Text := TheDay.Text;
end;
```

而事件处理程序则会在 FormCreate 方法里面被指派:

```pascal
procedure TDateForm.FormCreate(Sender: TObject);
begin
 TheDay := TDate.Init (7, 4, 1995);
 LabelDate.Text := TheDay.Text;
 // assign the event handler for future changes
 TheDay.OnChange := DateChange;
end;
```

这看起来动作很多，难道我们刚刚说事件处理程序可以省下很多源码是骗人的吗?并没有，我们在加入一些源码之后，现在我们可以完全不用再去管日期内容被修改的时候要怎么更新文字卷标了。以下是另一个简单的按钮的OnClick 事件：

```pascal
procedure TDateForm.BtnIncreaseClick(Sender: TObject);
begin
 TheDay.Increase;
end;
```

相同的简单源码也在很多其他的事件处理程序中出现。一旦我们指派了事件处理程序，就不用再一直去想着要更新文字卷标了。这把程序中一个很明显的潜在原始码错误给去除了。我们还得记得要在程序开始的地方写一些源码，因为这是个简单的类别，不是组件。如果我们处理的是组件，就只要在组件查看器的事件列表中建立一个事件处理程序，然后在里面写一行程序，更新文字卷标的内容，就打完收工了。

这也带出了另一个问题，到底在 Delphi 里面写个组件是有多困难?其实真的很简单，我们在接下来这几个小节来介绍一下。

!!! note "笔记"
    本书不会讲解太多关于撰写自定组件的细节，但会用很简短的以属性、事件的角色来稍微提一下写组件的方法，对这些功能的基本理解，是对每个Delphi 开发人员来说都很重要的。


## 建立 TDate 组件

现在，我们已经理解属性跟事件了，下一步我们要来看一下，什么是组件。我们透过把 TDate 类别转换为组件来简单的探索一下这个主题。首先，我们必须从 TComponent 类别来衍生出我们的新类别，这里不再从 TObject 来衍生了，以下是范例源码：

```pascal
type
 TDate = class (TComponent)
 ...
 public
 constructor Create (AOwner: TComponent); overload; override;
  constructor Create (y, m, d: Integer); reintroduce; overload;
```

我们可以看到，第二步就是帮这个类别加入一个建构函式，我们把默认的组件建构函式覆写，以提供一个适当的数据初始化。因为有一个多载的版本，我们也需要为它用上 reintroduce 这个关键词，以避免编译器显示警告讯息。新的建构函式就简单的设定了今天的日期，在呼叫基础类别的建构函式之后:

```pascal
constructor TDate.Create (AOwner: TComponent);
var
 Y, D, M: Word;
begin
 inherited Create (AOwner);
 FDate := Date; // Today
```

完成到这里之后，我们需要在撰写这个新的组件类别的单元档案里面，加入一个名为 Register 的程序(DateComp 范例项目里面的 Dates 单元文件)。(这个程序的第一个字母 R 务必大写，以免它被误认)。加入这个程序则是 IDE要新增一个组件的规范。

单纯的宣告这个程序，它不需要任何参数，在这个单元文件的 interface 区段宣告，然后再于 implementation 区段里面撰写源码:

```pascal
procedure Register;
begin
 RegisterComponents ('Sample', [TDate]);
end;
```

这源码把新的组件加入到工具盘的 Sample 分页当中，如果当时没有这个分页，系统就会自动新增这个分页。

最后一个步骤就是把这个写好的组件安装好。我们得建立一个套件(Package)，这是一种特殊的项目，专门用来安装组件用的。我们只需要:

+ 点选 IDE 的 File->New->Other menu 选单项目，打开新增项目的对话窗口
+ 选择 Package。
+ 把这个套件取个名字存盘(可以在该组件的目录中直接把这个套件项目档存起来)。
+ 在新建的套件项目中，请看 Project Manager 这个画面，在内容节点上点鼠标右键，然后为这个项目加入一个新的单元文件，当然就是把刚刚制作好的 TDate 组件类别的档案加入啰。
+ 在 Project Manager 画面上用鼠标右键点击套件项目，先选取里面的Build 选单项目，建置整个套件，然后再选取鼠标右键选单中的 Install，就可以把我们刚刚写好的 TDate 当成一个组件安装到 IDE 的工具盘了。
+ 如果您已经下载了本书的范例项目，那您开启套件项目之后就只需要做上述最后的两个步骤：建置、安装即可。

我们现在建立一个新的项目，然后把目光移到工具盘上面，应该就可以在Sample 分页里面看到刚刚这个新的组件(TDate)了。此时我们就可以把该组件直接拖拉到窗体上面，也可以从组件查看器上面来处理它的属性了，就如图 10.2 所示。我们也可以更简单的处理 OnChange 这个事件了，很简单，对吧?

图 10.2: 在对象查看器当中显示我们刚做好的TDate 组件属性

除了我们自己写一个新的项目来使用这个组件(我强烈建议您应该要试试看)，您也可以打开 DateComponent 范例项目，这是我们在上一节里面一步一步制作出来的组件的更新版。这是 DateEvent 范例项目的简化版，因为现在事件处理程序已经可以直接从对象查看器来处理了。

!!! note "笔记"
    如果您在编译、安装好组件之前就打开 DateCompTest 范例项目的话，IDE会认不得当中的组件，所以会有错误讯息，记得先把组件安装好喔。请注意到，在这个案例里面，开发环境提供给我们的错误讯息有两个选项：Cancel会单纯的忽略未定义的组件，Ignore 则会把相关源码移除。

## 在类别中实作对于列举功能的支持

我们在第三章里面介绍了以 for-in 循环来作为传统的 for 循环的替代方案，在该节里面，我们介绍了如何对数组、字符串、集合以及其他的系统数据型别使用 for-in 循环。对任何类别都可以用这种循环来处理，只要该类别有支持列举功能。大多数的例子都是内部有一些列表的类别，从技术的观点来看，这个功能是相当具有扩充性的。

我们在 Object Pascal 中实作让类别支持元素列举的功能时，我们必须要加入一个名为 GetEnumerator 的方法，让它回传一个类别(真的会处理列举动作的类别); 为这个列举动作的类别定义 MoveNext 方法与 Current 属性。

MoveNext方法提供在元素之间来回移动的功能，Current则回传实际的元素。一旦加好了(稍后我也会用实例来介绍)，编译器就能够解析 for-in 循环，在这循环里面，要处理的目标是我们的类别，当中的每个元素都必须跟列举功能的 Current 属性型别相同。

即使这并不是硬性规定，看起来为类别实作列举功能，把类别当做嵌套类型(我们在第七章已经对这个功能做了介绍)会是个好主意，因为硬要在特定的型别上面用到列举这个功能，其实也没什么意义。

以下的类别是 NumbersEnumerator 范例项目的一部分，会储存一个范围的数字(抽象集合的一种)，并允许从这些数字之中进行列举。这也就使得该类别可以定义列举功能，以嵌套类型进行宣告，并以 GetEnumerator 函式进行回传:

```pascal
type
 TNumbersRange = class
 public
 type
 TNumbersRangeEnum = class
 private
 NPos: Integer;
 FRange: TNumbersRange;
 public
 constructor Create (aRange: TNumbersRange);
 function MoveNext: Boolean;
 function GetCurrent: Integer;
 property Current: Integer read GetCurrent;
 end;
 private
 FNStart: Integer;
  FNEnd: Integer;
 public
 function GetEnumerator: TNumbersRangeEnum;
 procedure set_NEnd(const Value: Integer);
 procedure set_NStart(const Value: Integer);
 property NStart: Integer read FNStart write set_NStart;
 property NEnd: Integer read FNEnd write set_NEnd;
 end;
```

GetEnumerator 方法建立一个嵌套类型的对象用以储存列举出来的状态信息。请留意列举类别的建构函式是如何在该对象被列举时保留该对象的参考的(对象会以 self 这个参数被传递)，并在最初设定好起始对象的位置:

```pascal
function TNumbersRange.GetEnumerator: TNumbersRangeEnum;
begin
 Result := TNumbersRangeEnum.Create (self);
end;
constructor TNumbersRange.TNumbersRangeEnum. Create(aRange: TNumbersRange);
begin
 inherited Create;
 fRange := aRange;
 nPos := fRange.nStart - 1;
end;
```

_____________________________________________________________________
!!! note "笔记"
    为何建构函式会把第一个数值设定成-1 呢，而不是如一般期望的设定成第一个数值呢?这披露了编译器为 for-in 循环建立的源码中，对应着建立出的列举功能，以及源码 while MoveNext 的确使用了 Current 属性。测试的动作会在取出第一个值之前就先进行，当整个列表里面还没有任何值存在时。这也披露了 MoveNext 会在第一个元素被使用前就先被呼叫过。这样的作法不需要太复杂的逻辑，上述的例子就简单的把初始值设定成-1，好让第一个值被使用前就会呼叫 MoveNext 方法的副作用消匿于无形。

最后，列举方法会提供一个能够存取到资料的途径，以及可以读取列表中下一个元素的方法(或者在范围中下一个元素):

```pascal
function TNumbersRange.TNumbersRangeEnum. GetCurrent: Integer;
begin
 Result := nPos;
end;
function TNumbersRange.TNumbersRangeEnum. MoveNext: Boolean;
begin
 Inc (nPos);
 Result := nPos <= fRange.nEnd;
end;
```

我们在上述的源码里面可以看到，Next 方法满足了两种不同的需求，一到列表中的下一个元素，以及检查列表是否已经到了尽头，如果已经到了尽头，这方法就会回传 False。

完成了所有源码以后，我们现在就可以用 for-in 循环来列举出整个范围的对象了：

```pascal
var
 aRange: TNumbersRange;
 I: Integer;
begin
 aRange := TNumbersRange.Create;
 aRange.nStart := 10;
 aRange.nEnd := 23;
 for I in aRange do
 Show (IntToStr (I));
```

执行的结果就很单纯的把列表中的数值列出来，从 10 到 23 之间:

```
10
11
12
13
14
15
16
17
18
19
20
21
22
23
```

!!! note "笔记"
    在 RTL 跟 VCL 函式库中，有很多案例定义了列举功能，例如每一个TComponent 都需要列举该组件所拥有的子组件(Component)。目前还缺的是子控件(Control)的列举功能。在第 12 章的『用类别帮手提供列举功能』中，我们会介绍建立该功能的方法。不在这里介绍的原因，是我们得先讨论类别帮手这个工具。

## 结合 RAD 开发环境跟 OOP 的 15 个小提示

在这一章里面，我们介绍了属性、事件、发布(published)关键词，这些功能把核心的语言功能跟快速开发环境，或者说视觉开发，也可以说是事件驱动程序写法(这三个名词在概念上几乎是相同的模式)关连在一起。这个模式非常强大，它是以非常坚固的 OOP 为基础的。RAD 的要求与功能常常让开发人员忘却了好的 OOP 实务作法。与此同时，回归纯粹的程序撰写，忘却 RAD的要求与功能则常常失去效率。在本章的最后一节，我要列出一些结合这两个功能的提示跟建议。我们也可以把这一节称为”超越在 RAD 之上的 OOP”。

!!! note "笔记"
    本章最后一小节的材料，是从 1999 年七月份发行的”The Delphi Magazine”的第 17 个议题而来的。当时该篇文章的标题是”用 Delphi 写 OOP 的 20 个规则”。现在我删去了部分规则，把其他的做了些整理，剩下来的这些，每个都很必要。


### 提示一:窗体是个类别

开发人员常常会把窗体当作对象，但事实上窗体是类别。之间的差异在于我们是否会拿相同的窗体类别来建立多个窗体对象。

让人搞不懂的原因，是 IDE 会预设建立出全局变量以及(要看我们怎么设定)会在程序启动时帮每个项目里面的窗体类别都先产生一个窗体对象出来。这对初学者来说很友善，很容易上手，但对于任何并不直觉的应用程序来说，并不是个好习惯。

当然，帮每个窗体(以及窗体型别)、单元文件取个有意义的名字是很重要的。不幸的，这两个名字必须不同，但我们可以自己定个不成文的规则让两者之间容易被辨识(例如 AboutForm 跟 About.pas)。

当我们在处理以下的步骤时，就会发现到”窗体是个类别”这个概念是很实用的了。




### 提示二:为组件命名

为组件取个容易一眼看出其功能的名字也是很重要的。最常用的命名方法，是用一些小写字母为类别型别，然后把该组件的角色放在前面，例如BtnAddCustomer 或者 EditName。这些都是使用这类规则，很类似的注记法。很难说哪种命名方法最好，这要看你自己的喜好了。



### 提示三:为事件命名

为属性或者事件处理方法命名也一样的重要。如果我们把组件命名得宜，例如 为 该 组 件 的 onClick 事 件 建 立 的处理程序，预设的 名 称 就 会 是BtnAddCustomerClick。即使我们可以从按钮的名字猜出那个方法的作用，但 能从方 法 的 名 称 一 眼 看 出 该 方 法 的 作 用还是 比 较 好的。 例 如 ，BtnAddCustomer 按钮的 OnClick 事件可能被命名为 AddCustomerToList，如果这是该方法要做的动作。

这会让源码更具可读性，特别是当我们需要从另一个类别的方法中呼叫事件处理程序时，也可以帮助开发人员把同一个方法指派给多个不同组件的事件，即使我得说使用 Actions 来把同一个事件指派给多个用户接口的元素是比较好的作法，尤其是对任何一个不直觉的程序来说。

!!! note "笔记"
    Actions 跟 ActionList 组件在 VCL 跟 FireMonkey 这两个接口函式库当中都是很好的结构性功能，它们提供了让程序人员可以从概念上把使用者操作(与状态)跟接口控件分离的功能。启动该控件就可以执行对应的动作。如果我们把 Action 给 disable 了，对应的 UI 元素也会呈现为 disable 的状态。这个主题超越了本书的范围，但如果您需要使用这些框架的话，值得花时间了解一下。




### 提示四:使用窗体的方法

如果窗体是类别，它的源码就会被集结成方法。除了事件处理程序，事件处理程序扮演了一个特别的角色，但还是可以像其他方法一样被呼叫，它对于在窗体类别中加入自定方法是很有用的。我们可以加入方法来执行动作或者存取该窗体各组件的状态。为窗体加入公开的方法，比让其他窗体直接存取这个窗体上头的组件来的好上许多。



### 提示五:建立窗体建构函式

在运行时间建立的第二个窗体，可以在默认的建构函式之外，在另外提供特定功能的建构函式(从 TComponent 类别衍生而来的窗体)。当我们需要特别的初始化程序时，我的建议是为 Create 方法进行多载，加入需要的初始化参数，就像以下的范例源码:

```pascal
public
constructor Create (Text: string); reintroduce; overload;
constructor TFormDialog.Create(Text: string);
begin
 inherited Create (Application);
 Edit1.Text := Text;
end;
```

### 提示六:避免使用全局变量

应该避免使用全局变量(在此是指宣告在单元文件的 interface 部分的变量)这样可以避免导致源码难以维护或者产生难以避免的错误。

如果您需要为窗体加入额外的储存数据，请在私有区加入一些数据字段。这样一来每个窗体实体就都可以有自己的一份数据了。

我们也可以使用单元文件变量(在单元文件的 implementation 区段宣告)，这种变量是期望在同一窗体类别的多个实体之间进行数据共享，但这个作法比使用类别的数据字段更好(我们会在第十二章说明)。


### 提示七:绝不要在类别实作的源码中使用特定实例变量名

我们绝不能在类别的方法中提及特定的对象。换句话说，在 TMyForm 类别的任何方法里面，绝对不要直接用 MyForm 这个变量。如果需要参考到目前的窗体对象，请用 self 关键词。请牢记在心，大多数的时间都不需要直接写出变量名称，当我们希望能够参考到目前这个对象的方法或数据时。如果您不遵循这个规则，在建立多个窗体实体的时候，就会遇上大麻烦了。


### 提示八:尽量少用 Form 变量名

即使是在其他窗体的源码里面，也尽量不要直接参考到全局对象，例如MyForm。最好还是宣告局部变量，或者私有区的数据字段来参考其他窗体，如 FMyForm。例如，一个程序的主窗体可能拥有一个参考到对话框的私有数据字段。显然的，如果我们计划要让第二个窗体类别建立多个实体时，这个规则就变得很必要了。我们可以在主窗体里面建立一个动态数组，或者直接用全局对象 Screen 的 Forms 变量来参考目前应用程序中的任何一个窗体。



### 提示九:去除全局的 Form1 变数

事实上，我的建议是，当我们把一个窗体加入项目的时候，拿掉 IDE 自动帮我们建立的全局 form 对象，像是 Form1。这个可能性只在我们取消自动建立该窗体的功能时。我建议的这个动作是让您可以摆脱 IDE 的某些副作用。而您可以删掉某些对应的源码，就是被放在项目档里面那些用来自动建立窗体实体的源码。

不用说，如果窗体没有自动被建立，我们就需要在应用程序中加入一些源码来建立它，可能也需要宣告一些变量来储存被建立的窗体。

我想，对于 Object Pascal 的初学者来说，去除全局的窗体对象是很有用的，因为初学者就不会再被类别跟全局对象搞混了。事实上，移除了全局对象之后，所有参考到这个对象的源码都会发生错误了。

### 提示十:加入窗体属性

犹如我在本章的”为窗体加入属性”小节所说的，当我们需要在窗体里储存数据的时候，就加一个私有的数据字段。如果我们需要从别的类别存取这个数据，并且为这个窗体加入属性。为了这个要求，我们可以改变窗体的源码跟它的资料(包含在它的用户接口中)而不改动任何其他窗体或类别的源码。我们应该也要使用属性跟方法来为第二个窗体或对话框做初始化，然后读取它的最终状态。初始化的动作也是使用建构函式，如我已经说过的那样。



### 提示十一:把组件属性披露

当我们需要存取另一个窗体的状态，我们无法直接参考它的对象。这样会把其他的窗体或类别的源码跟用户接口绑定在一起，这是应用程序的移植当中变化最大的，但也可规避这些组件管理逻辑中的封装逻辑。另外，宣告一个窗体的属性会对应到关联到组件的属性：这会以一个用来读取组件状态的Get 方法，以及用来写入它的 Set 方法。假设我们现在修改了用户接口，把组件替代另一个。而我们需要做的，就是把 Get 跟 Set 方法改成与属性相关联。我们不用检查或更改任何一个使用到这个组件档案的方法。


### 提示十二:适时的在需要使用数组属性

如果我们需要在窗体里面处理一系列的值，可以宣告一个数组属性。假如这个信息对窗体来说很重要，我们也可以把它设定为窗体的默认属性，那我们就可以直接存取这些属性了，语法上可以直接写成 Self[3]或者用另一种写法FDataForm[3]。这个作法已经在本章里面”使用数组属性”那一小节里面透过几个一般的案例介绍过了(不过该案例没有使用窗体类别就是了)。


### 提示十三:使用属性的副作用

记得使用属性，而不要直接存取全局数据的优点之一，就是在读写属性的时候，我们可以顺便呼叫某些方法或做一些我们想加入的处理。例如我们可以在设定很多属性的内容时，顺便把窗体的画面直接更新一下，或者呼叫其他方法，甚至可以一次把多个组件的状态更新，或者在需要的时候触发事件。

另一个相关的范例是使用属性的取得函式(getter)来实作延迟建立。在类别的建构函式里面不建立子对象，我们可以在该对象第一次被要求存取的时候才建立，可以写成像这样:

```pascal
private
 FBitmap: TBitmap;
public
 property Bitmap: TBitmap read GetBitmap;
function TBitmap.GetBitmap: TBitmap;
begin
 if not Assigned (FBitmap) then
 FBitmap := ... // create it and initialize it
 Result := FBitmap;
end;
```

### 提示十四:隐藏组件

我很常听到 OOP 的狂热份子这么抱怨: 在窗体里面的发布区里面放了一堆组件。岂不是就不符合封装的精神了吗?他们的确只出了一个重要的问题，但大多数人似乎并没有意识到不用重写函式库或变更编程语言的解决方法就在眼前。窗体上面各个组件的参考可以搬到私有区，这样就不能直接被其他窗体存取，接着我们可以把组件宣告成属性(请见前面的几个小节)，这样就刀切豆腐两面光，既符合了封装的精神，又可以让组件存取很有弹性了。

如果 IDE 把所有的组件都放在发布区。这是因为要能够把窗体透过串流化储存到档案里面(DFM 檔或者 FMX 檔)。当我们为组件命名之后，VCL 会自动把该组件的对象与参考都加到窗体里面去。这也只有当该组件的参考是位于发布区才办的到，因为串流系统使用了传统的 RTTI 跟一些 TObject 的方法来完成这个功能。

所以如果我们直接把组件的参考从发布区搬到私有区，我们就会遗失这个自动建立的规则。要解决这个问题的话，只要把这个动作改成人工处理，把以下的源码加入到窗体的 OnCreate 事件处理程序即可：

`Edit1 := FindComponent('Edit1') as TEdit;`

我们得做的第二个动作则是在系统中注册这个组件类别，这样 RTTI 信息才会能够在编译后的程序中，以及 IDE 系统里面使用。这动作对每个组件类别都只需要做一次，且只有当我们把属于该型别所有组件的参考都搬到私有区的时候才需要。我们可以把这个函式加入到源码里面，就算我们不确定程序里需要用到它也没关系，例如呼叫 RegisterClasses 这个方法对系统没有负面影响。这个方法通常是在储存该类别的单元文件的初始区段(initialization section)里面呼叫的：

`RegisterClasses([TEdit]);`


### 提示十五:使用 OOP 窗体精灵

一直重复前面讲到的，对每个窗体里面每个组件做两个处理程序真的很无聊，也很花时间。为了免除这些沉重的负担，我写了一个简单的精灵，在一个小窗口里面把这些源码加到程序里面去。您只要简单的把这些产生好的源码剪贴到每个窗体里面就行了，因为这个精灵不会自动把原是码放到单元文件的初始区段里去。

要怎么拿到这个精灵?您可以在以下网址里面找一下”Cantools Wizards”:http://www.marcocantu.com/tools


### 提示结论

以上只是一些提示的简单整理，以及让您在 RAD 跟 OOP 开发模式中取得更好平衡的一些建议。当然关于这个主题还有更多的东西要讲，已经超越本书要介绍的范畴了，本书主要是集中介绍语言本身，而不是如何找到最佳的应用程序架构。

!!! note "笔记"
有一些书介绍了关于 Delphi 的应用程序架构，但是没有一本比 Nick Hodges
写的一系列更好，这系列的书包含了”Coding in Delphi”, “More Coding in 
Delphi”, 以及”Dependency Injection in Delphi”。您可以在下面这个网址找到
跟这些书相关的信息: http://www.codingindelphi.com/
