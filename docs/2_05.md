相异于 C++跟一些其他的编程语言，Object Pascal 的继承模式并不支持多重继承。这表示每个类都只有一个相同的基础类。

多重继承的功效一直都是被 OOP 专家们热烈讨论的议题。Object Pascal 不使用多重继承这个功能，一直都是两面评价，说这是缺点的人，就说这样一来功能就没有 C++强，说这是优点的人，就说这可以简化语言本身，并避免掉不少多重继承导致的麻烦。Object Pascal 用来弥补没有多重继承功能缺憾的作法，是使用了接口的概念，透过接口，我们可以宣告一个类，同时实现多种抽象化定义出来的功能。

!!! note "笔记"
    今天大多数的面向对象编程语言并不支持多重继承，而改为使用接口(interface)，包含 Java 跟 C#。接口提供了弹性、让类能够宣告支持多个接口，并加以实现的能力，同时避免因多重继承而可能产生的问题。多重继承的支持目前大多受限于 C++语言。一些动态的面向对象编程语言则支持 mixins，这是另一个达到类似多重继承功能的方法。

为了避免陷入这个讨论的泥淖，我很简单的假设能够从多种面向来处理一个对象是有用的。但在我们用这个理论建立范例之前，我们得先介绍在Object Pascal 里面，接口所扮演的角色，以及它的工作原理。

从一个宏观的角度来看，接口比类支持了更多不同的面向对象程序设计的模式。实作接口的对象，可以视作它所实作的每个接口的多重型态，实际上以接口为基础的模式是很强的。跟类相较，接口比较偏重在封装，并提供给类之间一种比继承更宽松一点的连结(当实作没有被继承下去时)。

!!! note "笔记"
    在本章所介绍的技术，是在 Object Pascal 语言发展之初就已经被支持，并用来实作 Windows COM 架构的。后来这个功能又被延伸在该情境之外，成为可以广泛使用的技术，但 COM 里面的一些元素，例如接口代号、参考计数这些功能，仍然留在了目前 Object Pascal 对接口的实作当中，也使得Object Pascal 跟其他语言有明显的不同。

## 使用接⼝

除了宣告抽象类(拥有抽象方法的类)，在 Object Pascal 里面我们也可以撰写纯粹的抽象类;也就是只包含虚拟抽象方法的类。透过使用特别的关键词，interface 来定义一组作为接口(interfaces)的数据类型。

从技术面来看，接口不算是类，虽然接口可以重组类。因为类可以建立实体，但接口不行。接口可以被一个或多个类加以实作，所以这些实体就可以算是支持了或者实作了该接口。

在 Object Pascal 当中，接口有一些独特的功能:

+ 接口类型的变量会使用参考计数，跟类类型的变量不同，接口提供了一系列的自动内存管理机制
+ 类是从单一前代类继承而来，但可以实作多个接口
+ 就像所有类都是自 TObject 衍生而来，所有的接口都是从 IInterface衍生出来的，两者各归属于独立、正交的架构 
+ 接口的名称，会以大写字母 I 开头，跟类的 T 大写字母是不同的。

!!! note "笔记"
    起初在 Object Pascal 里面基础的接口类型被称为 IUnknown，这是 COM 所需求的。后来，IUnknown 接口被更名为 IInterface，为了强调这个事实，您仍然可以在没有使用 COM 的情形下使用接口，即使在没有 COM 存在的操作系统里面也一样。反正，IInterface 实际上的规则仍然跟 IUnknown 完全相同。

### 宣告接口

看过了核心理论之后，让我们来看看实际的例子吧，实例会帮助我们了解Interface 在 Object Pascal 是如何作业的。在实务层面，接口的定义就像类的定义写法。这个定义包含有一些方法，但这些方法不用实作，就像在一般类里面的抽象方法一样。

以下就是一个接口的定义:

```pascal
type
 ICanFly = interface
 function Fly: string;
 end;
```

假设每个接口都直接或间接的从基础接口类型继承而来，相对的写法就变成了:

```pascal
type
 ICanFly = interface (IInterface)
 function Fly: string;
 end;
```

稍后我们会说明从 IInterface 继承的含义为何，并作一个比较表。现在我们就先把 IInterface 也有一些基础类的方法吧(再提一下，跟 TObject 不一样)。还有跟接口宣告相关最后一个重点。对接口来说，部分的类型检查是动态进行的，系统要求每个接口都要具备一个唯一的标识符，或者称为 GUID，我们可以在 IDE 里面按下 Ctrl+Shift+G，编辑环境就会自动帮我们产生一组。以下是接口宣告的完整源码：

```pascal
type
 ICanFly = interface
 ['{D7233EF2-B2DA-444A-9B49-09657417ADB7}']
 function Fly: string;
 end;
```

这个接口跟相关的实作都可以在 Intf101 范例项目里面找到。

!!! note "笔记"
    虽然我们就算没指定 GUID 给接口，编译器也仍然可以编译成功，我们也会想要建立出 GUID 给它，因为在进行接口查询，或者透过 as 进行动态类型转换的时候都会用到 GUID。接口的重点，是因为它在运行时间具备了强大的扩充性，这些都是因为接口拥有 GUID。

### 实作接口

任何类都可以实作接口(个数不限)，只要把它们列在基础类后面，并且提供这些接口方法的执行源码即可：

```pascal
type
 TAirplane = class (..., ICanFly)
 function Fly: string;
 end;
function TAirplane.Fly: string;
begin
 // actual code
end;
```

当类实作接口的时候，它必须提供该接口所有方法(参数也必须要跟接口中该方法的宣告完全相同)的实作，所以在本例中的 TAirplane 类就必须实作 Fly 方法，该方法必须回传一个字符串。假设这个接口也是从一个基础接口(IInterface)衍生而来，这个类就必须实作出该接口，以及其基础接口的所有方法。

这就是为什么从已经实作了 IInterface 基础接口的基础类衍生新的类时，在新类中实作该接口也相当常见的原因。Object Pascal 运行时间函数库已经提供了一些基础类是已经实作好基本的方法的。最简单的一个就是TInterfacedObject 类，这个源码就会变成：

```pascal
type
 TAirplane = class (TInterfacedObject, ICanFly)
 function Fly: string;
 end;
```

_____________________________________________________________________
!!! note "笔记"
    当我们实作接口时，我们可以选择以静态或虚拟方法来实作。如果我们准备在衍生类里面覆写方法，使用虚拟方法就有意义。然而同一个功能也有替代作法，我们可以指定基础类也继承同一个接口，然后覆写该接口的方法。我建议需要的时候，把接口的方法宣告为虚拟方法，这样可以保留未来的扩充弹性。

现在我们已经定义了一个接口，以及用来实作它的类。我们可以为这个类建立物件，就把它当成一般类来对待，可以这么写:

```pascal
var
 Airplane1: TAirplane;
begin
 Airplane1 := TAirplane.Create;
 try
 Airplane1.Fly;
 finally
 Airplane1.Free;
 end;
end;
```

在这个例子里面我们忽略了类实现接口的事实。不同的是，现在我们也可以宣告以接口为类型的变量了。用接口作为类型来宣告变量，会自动启动参考内存模式，所以我们就可以省略掉 try-finally 区块：

```pascal
var
 Flyer1: ICanFly;
begin
 Flyer1 := TAirplane.Create;
 Flyer1.Fly;
end;
```

这个简单的程序片段是从 Intf101 范例项目节录出来的，其中的第一行一定会引发许多联想。首先，当我们把一个对象指派给接口变量之后，透过 as这个特殊指令，运行时间会自动检查该对象是否有实作这个接口。我们可以把整个指令这样写:


`Flyer1 := TAirplane.Create as ICanFly;`

其次，看我们是使用了直接指派的方式，或是透过 as 指令，运行时间会多做一件事: 它会调用对象的_AddRef 方法，增加它的参考计数。这是透过调用了从 TInterfacedObject 衍生而来的对象方法而完成的。

同时，当 Flyer1 变量离开了其使用范围(例如执行到 end 区块)，运行时间就会调用_Release 方法，接着该变量的参考计数就会自动减一，等到参考计数变成 0，如果需要，该对象就会自动被释放掉。因为有了这个功能，就没有必要在程序里面手动控制对象的释放了。

!!! note "笔记"
    前述的范例中没有 try-finally 区块，编译器会自动在编译的时候，在调用_Release(有时可能是 Free)时自动加入 try-finally 区块。这在 Object Pascal当中有很多情形都会发生: 基本上每当一个方法使用了一个或多个受管理的类型时(例如字符串、接口或者动态数组)，编译器就会在该方法的源码编译的时候自动在背景套用 try-finally 区块。

### 接口与参考计数

我们回头看一下上面的源码，Object Pascal 的对象被以接口变量进行参考的时候，会进行参考计数(除非接口类型的变量有被标注为 weak 或者 unsafe，在稍后的范例中会提到)。我们也看过它们会自动的在没有接口变量参考到时，会自动把对象释放掉。

值得注意的是，当一些编译器的背景动作涉入时(在背景调用的_AddRef 跟_Release)，实际上，参考计算器制还是取决于开发人员或者函数库在特定的源码当中如何实现。在最后一个例子当中，参考计数则真的作用了，因为在 TInterfacedObject 类的方法中，源码的内容触发了它: (这里仅列出简化过的版本):

```pascal
function TInterfacedObject._AddRef: Integer;
begin
 Result := AtomicIncrement(FRefCount);
end;
function TInterfacedObject._Release: Integer;
begin
 Result := AtomicDecrement(FRefCount);
 if Result = 0 then
 begin
 Destroy;
 end;
end;
```

现在，我们来思考一下，实作 IInterface 在 RTL 里面的另一个基础类：

```pascal
TNoRefCountObj，。这个类会把实际的参考计算器制关闭:
function TNoRefCountObject._AddRef: Integer;
begin
 Result := -1;
end;
function TNoRefCountObject._Release: Integer;
begin
 Result := -1;
end;
```

!!! note "笔记"
    在 Delphi 11 当中，提供了一个新的 TNoRefCountObject 类，当中定义了让对象可以忽略参考技术的机制，这个类取代了旧版 Delphi 中名为TSingletonImplementation 类(定义在 Generics.Defaults 单元里面)。旧的TSingletonImplementation 类仍旧以新的这个 TNoRefCountObject 类的别名继续存在着。这两个类的源码基本上是完全相同的。这个改变的主要理由，是因为原本的类名字看起来名不符实，它当中并没有提供实质的 Singleton 功能。我们会在下一章里面来看一些这个模式的范例。

TNoRefCountObject 并不常被使用，系统中还有另一个类，实作了可以忽略参考计算器制的接口，因为它有自己的内存管理模式，这就是 TComponent类。

如果我们想要建立一个自定组件，在里面实作接口，我们就不用担心参考计数跟内存管理了。我们会在本章的最后，”用接口来实作设计模式(Patterns)”那一小节里面来看一个自定组件的例子，用它来实作一个接口。


### 混和参考的错误

使用对象的时候，我们应该只透过对象变量或只透过接口变量来存取对象。把两种作法混着用会打乱 Object Pascal 提供的参考计算器制，就可能导致非常难以发现的内存错误。实务上，如果我们决定要用接口，我们就应该只使用接口类型的变量。

以下是一个可能发生比较不一样错误的案例。假设我们建立了一个接口，用一个类来实作它，并且用一个全局程序用这个接口作为参数：

```pascal
type
 IMyInterface = interface
 ['{F7BEADFD-ED10-4048-BB0C-5B232CF3F272}']
 procedure Show;
 end;
 TMyIntfObject = class (TInterfacedObject, IMyInterface)
 public
 procedure Show;
 end;
procedure ShowThat (anIntf: IMyInterface);
begin
 anIntf.Show;
end;
```

这段程序看起来很直观，而且百分之百正确。可能发生的错误，只会来自于我们调用它的方法(这段程序是 IntfError 范例项目的一部分):

``pascal
procedure TForm1.BtnMixClick(Sender: TObject);
var
 AnObj: TMyIntfObject;
begin
 AnObj := TMyIntfObject.Create;
 try
 ShowThat (AnObj);
 finally
 AnObj.Free;
 end;
end;
```

上面这段程序则是把一个一般的对象传给原本要求接口当做参数的函数。假设这个对象没有支持该接口，编译器也不会对这个函数的使用提出可能发生错误的警告讯息。在这里的问题，是内存管理的方式。

一开始，对象的参考计数会被设成 0，表示没有参数使用到它。在进入ShowThat 程序的时候，参考计数会被加成 1。这没问题，程序的调用也会正常发生。等程序执行结束的时候，参考计数会被递减为 0，所以该对象就会被正常释放。换句话说，当程序结束，回到调用该程序的程序区段时，anObj对象就会被释放，这实在相当尴尬。当我们执行这段程序时，则会发生内存错误。

有几个方法可以解决。我们可以手动增加参考计数的数字，并使用一些低阶的技巧。但实际的解法，就是不要把接口跟对象的参考混着用，并只使用接口来指向对象(以下这个源码也是 IntfError 范例项目的一部分):

```pascal
procedure TForm1.BtnIntfOnlyClick(Sender: TObject);
var
 AnObj: IMyInterface;
begin
 AnObj := TMyIntfObject.Create;
 ShowThat (AnObj);
end;
```

在这个特例里，就使用了这个解决方案，但在许多其他的状况下，很难发现到正确的写法。再次强调，最终解决方案就是不要把不同类型的参考混在一起使用。


### 弱化(weak)与不安全(unsafe)的接口参考

从 Delphi 10.1 Berlin 开始，Object Pascal 语言提供了一种新的模型用以管理所有平台上的接口参考。Object Pascal 语言事实上提供了几种不同形式的参考:

+ 一般参考会在对象被指派与释放的时候，对该对象的参考进行增减，最终在释放对象的时候，参考计数回归为 0
+ 弱化参考 (weak references, 以[weak]关键词进行标注)不会在对象被参考或指派时增加参考计数。这些参考完全都是由系统管理的，所以在指向的对象被释放的时候，会自动被设定为 nil
+ 不安全参考(unsafe references, 以[unsafe] 关键词进行标注) 不会在对象被参考或指派时增加参考计数。系统也不会管理这些参考-跟一般的指标没有什么不同。

!!! note "笔记"
    弱化与不安全参考的运用，一开始是在移动设备平台中 ARC 内存管理的一部分。但随着 ARC 已经淡出，这个功能目前只剩在接口参考上还有用。

在一般情形中，参考计数会被启用，我们可以写出以下的源码，依靠参考计数的机制来释放临时配置的对象：

```pascal
procedure TForm3.Button2Click(Sender: TObject);
var
 oneIntf: ISimpleInterface;
begin
 oneIntf := TObjectOne.Create;
 oneIntf.DoSomething;
end;
```

要是这个对象有标准的参考计数实作，而我们想要建立一个接口参考，让这个接口参考完全脱离参考计数的总数，该怎么做呢?我们现在可以在接口变量的前面加上一个[unsafe]属性，就可以达到这个目的了，只要把前面的宣告改成这样:

```pascal
procedure TForm3.Button2Click(Sender: TObject); var
 [unsafe] one: ISimpleInterface;
begin
 one := TObjectOne.Create;
 one.DoSomething;
end;
```

这个作法并不好，上述的源码可能会造成内存泄漏。摆脱了参考计数，当变量离开了其有效范围，就不会有任何事情发生。有些情境是需要这样的『优点』的，我们可以使用接口，而不影响其他额外的参考。换句话说，不安全的参考可以被视为…..指标，而且不需要任何编译器的额外支持。

现在，在我们考虑使用 unsafe 属性来宣告一个不会增加参考计数的变量之前，要先想想在大多数情境中，我们还有另一个更好的选择: 使用弱化参考(weak reference)。弱化参考也可以避免增加参考计数的内容，但弱化参考还是在系统控制中的。这表示系统会持续监控弱化参考，当对象被释放的时候，指到该对象的所有参考都会自动被设为 nil。但如果使用 unsafe 参考，我们就无从得知该参考所指向的对象的情况了(这情形被称为 dangling 参考)。在这种情境下，弱化参考是不是很有用呢？很常见的情境，就是两个对象交叉参考。在这样的案例中，事实上，对象会被我们的源码拖累，增加了对彼此的参考计数，这么一来，这两个对象就会永远被留在内存里面，(因为他们的参考计数永远是 1)即使这两个对象已经不会再被使用到。


用以下的程序来做个范例，以下的接口会同时接受一个指到另一个接口的参考，并以一个类来实作该接口，当做内部参考：

```pascal
type
 ISimpleInterface = interface
 procedure DoSomething;
 procedure AddObjectRef (simple: ISimpleInterface);
 end;
 TObjectOne = class (TInterfacedObject, ISimpleInterface)
 private
 AnotherObj: ISimpleInterface;
 public
 procedure DoSomething;
 procedure AddObjectRef (Simple: ISimpleInterface);
 end;
```

如果我们建立两个对象，并让他们彼此参考，下场就是内存泄漏：

```pascal
var
 One, Two: ISimpleInterface;
begin
 One := TObjectOne.Create;
 Two := TObjectOne.Create;

One.AddObjectRef (Two);
 Owo.AddObjectRef (One);
```

现在，在 Delphi 里面，有效的解法就是在 private 中的字段把 AnotherObj标注成 weak:

```pascal
private
 [weak] AnotherObj: ISimpleInterface;
```

这样一改，参考计数就不会在我们把一个对象当成参数丢给 AddObjectRef的时候被增加了，这个数字会维持在 1，而当该变量脱离生命周期，参考计数就会自动回到 0，该变量使用的内存也就会被释放掉了。

现在有许多其他案例可以因为这个功能变得方便，但这个功能在实作上非常复杂，不容易解释。这是个非常强大的功能，但我们需要些时间才能运用自如。同时，它也会在执行时多花些时间，因为弱化参考是受管理的(而 unsafe参考是没有受管理的)。

如果您对接口使用弱化参考有兴趣，您可以参考第 13 章『对象与内存』当中的章节:『内存管理与接口』。


## 进阶接⼝科技

要深入了解接口的能力，在我们探讨在实务上使用的可能情境之前，了解更进阶的功能是很重要的，像是如何实作多个接口，或是如何用不同的名称来帮接口的特定方法提供实作(万一有类的方法名称重复时)。

另一个重要的功能，是接口也有属性。为了把这些进阶的接口功能做个示范，我们以 IntfDemo 范例项目来做实例。


### 接口的属性

本节的范例源码是以两个不同的接口为基础的: IWalker 跟 IJumper，两者都定义了一个新的方法跟一个属性。

接口的属性也只是对应了读取跟写入方法而已。跟类不同的是，我们不能`把接口的属性直接对应到数据字段，这就只是因为接口当中不能包含任何源码。

以下是实际的接口定义：

```pascal
IWalker = interface
 ['{0876F200-AAD3-11D2-8551-CCA30C584521}']
 function Walk: string;
 function Run: string;
 procedure SetPos (Value: Integer);
 function GetPos: Integer;
 property Position: Integer read GetPos write SetPos;
end;
IJumper = interface
 ['{0876F201-AAD3-11D2-8551-CCA30C584521}']
 function Jump: string;
 function Walk: string;
 procedure SetPos (Value: Integer);
 function GetPos: Integer;
 property Position: Integer read GetPos write SetPos;
end;
```

当我们实作一个具有属性的接口，我们只需要实作其中的访问方法，因为接口的属性对类来说是透明的，而且无法直接使用：

```pascal
TRunner = class (TInterfacedObject, IWalker)
private
 FPos: Integer;
public
 function Walk: string;
 function Run: string;
 procedure SetPos (Value: Integer);
 function GetPos: Integer;
end;
```

这段实作源码看起来很简单、直觉(它是 IntfDemo 范例项目的一部分)，透过方法来计算新的位置，并显示要显示的的数据：

```pascal
function TRunner.Run: string;
begin
 Inc (FPos, 2);
 Result := FPos.ToString + ': Run';
end;
```

范例源码中使用了 IWalker 接口，以及以下的 TRunner 实作源码：

```pascal
var
 Intf: IWalker;
begin
 Intf := TRunner.Create;
 Intf.Position := 0;
 Show (Intf.Walk);
 Show (Intf.Run);
 Show (Intf.Run);
end;
```

输出的信息应该不让人惊讶:

```
1: Walk
3: Run
5: Run
```

### 接口委任

用类似的方法，我们可以定义一个简单的类来实作 IJumper 接口:

```pascal
TJumperImpl = class (TAggregatedObject, IJumper)
private
 FPos: Integer;
public
 function Jump: string;
 function Walk: string;
 procedure SetPos (Value: Integer);
 function GetPos: Integer;
end;
```

这 次 的 实 作 跟前一版不同的地方，是使用了不同的基 础 类 别 : TAggregatedObject。它是一个用来定义内部支持接口的类，其语法我们稍后介绍。

!!! note "笔记"
    TAggregatedObject类是在System单元里面定义的另一个有实作IInterface的类。跟 TInterfacedObject 比较一下，TAggregatedObject 对参考计数的实作方法是不同的(基本上是把所有的参考计数委任给控制组件或容器组件)，在控制组件或容器组件中，则实作接口查询的功能。

我们会用不同的方式来使用它。在以下的类: TMyJumper，我不想用类似的方法来重复实作出 IJumper 接口。反之，我想把这个接口的实作委任给已经实作出该方法的类来处理。这个作法无法透过继承来达成(我们无法从两个类作为基础类来衍生一个新类)，幸好我们可以透过 Object Pascal语言的另一个功能：接口委任来达成这个需求。以下的类透过把方法参考指向一个实作了该接口方法的对象，来达成实作该接口方法的需求。这样就不用真的实作该接口了:

```pascal
TMyJumper = class (TInterfacedObject, IJumper)
private
 FJumpImpl: TJumperImpl;
public
 constructor Create;
 destructor Destroy; override;
 property Jumper: TJumperImpl read FJumpImpl implements IJumper;
end;
```

这个需告指出 IJumper 接口是由 TMyJumper 类以 FJumpImpl 数据字段加以实作的。当然，这个字段必须实际上实作出该接口的所有方法。为了让这个作法行得通，我们需要在 TMyJumper 对象被建立的时候(建构函数的参数是由身为基础类的 TAggregatedObject 类所要求的)，为这个数据字段建立一个适当的对象:

```pascal
constructor TMyJumper.Create;
begin
 FJumpImpl := TJumperImpl.Create (self);
end;
```

这个类也有一个解构函数用来释放内部对象，它会参考到一个一般数据字段，而不会参考到接口(因为参考计数在这个情境下不会发生作用)。


这个范例虽简单，但并不常见，当我们开始修改一些方法，或者加入新的方法时，仍然需要对内部的 FJumpImpl 对象进行处理。此处的通则则是我们可以在许多类中重复使用这个接口的实作。这个接口中所使用的源码是以间接的方式实作接口，跟标准的源码完全相同，可以写成:

```pascal
procedure TForm1.Button2Click(Sender: TObject);
var
 Intf: IJumper;
begin
 Intf := TMyJumper.Create;
 Intf.Position := 0;
 Show (Intf.Walk);
 Show (Intf.Jump);
 Show (Intf.Walk);
end;
```

### 多重接口以及方法别名

接口的另一个很重要的功能，是为类提供能够实作多于一个接口的能力。

我们透过以下的 TAthlete 来做介绍，它同时实作了 IWalker 跟 IJumper 接口:

```pascal
TAthlete = class (TInterfacedObject, IWalker, IJumper)
private
 FJumpImpl: TJumperImpl;
public
 constructor Create;
 destructor Destroy; override;
 function Run: string; virtual;
 function Walk1: string; virtual;
 function IWalker.Walk = Walk1;
 procedure SetPos (Value: Integer);
 function GetPos: Integer;
 property Jumper: TJumperImpl read FJumpImpl implements IJumper;
end;
```

其中一个接口是直接被实作出来的，而其他的接口则是透过内部对象FJumpImpl 进行委任，就像我们在前一个范例中所做的一样。

现在我们面临了一个问题。这两个我们想实作的接口，都有一个名为 Walk的方法，它们除了名称相同，参数也完全一样，所以我们要怎么在一个类里面同时实作它们呢?Object Pascal 语言里面是怎么处理多个接口中的方法名称重复的呢?解决方法就是把其中一个方法改名，可以透过以下这种写法:

`function IWalker.Walk = Walk1;`

这个宣告是指该类会用 Walk1 这个方法来实作 IWalker 接口的 Walk 方法(代替用具有相同名称的方法)。最后，在这个类当中对所有方法的实作里，我们需要参照到内部对象 FJimpImpl 的 Position 属性。透过为 Position 属性宣告一个新的实作方法，我们最后可以得到两个独立的 athlete 对象，这是一个奇怪的状况，以下就是这个范例的两个方法:

```pascal
function TAthlete.GetPos: Integer;
begin
 Result := FJumpImpl.Position;
end;
function TAthlete.Run:string;
begin
 fJumpImpl.Position := FJumpImpl.Position + 2;
 Result := IntToStr (FJumpImpl.Position) + ': Run';
end;
```

我们要怎么为 TAthlete 对象建立一个接口，同时把它指向 IWalker 跟 IJumper接口?好吧，我们的确不能那样作，因为并没有我们可以使用的基础接口。

然而，接口允许更动态的类型检查与类型转换，所以我们可以把一个接口转换成另一个，只要我们所使用的对象的确同时支持两种接口，编译器就能在运行时间中找到让这转换兼容的蛛丝马迹。以下就是这种情形的源码:

```pascal
procedure TForm1.Button3Click(Sender: TObject);
var
 Intf: IWalker;
begin
 Intf := TAthlete.Create;
 Intf.Position := 0;
 Show (Intf.Walk);
 Show (Intf.Run);
 Show ((Intf as IJumper).Jump);
end;
```

当然，我们可以挑选这两个接口的其中之一，然后把它转换成另外一个。透过 as 指令作转换是我们可以进行运行时间类型转换的方法之一，但我们还有更多方法可以用来处理接口，就像我们在下一节要介绍的那样:


### 接口的多型

在前一节，我们已经介绍过，我们可以定义多个接口，然后用一个类来实作其中的两个接口。当然，这也可以延伸成多个，数量不拘。我们也可以建立一个接口架构，因为接口也可以从一个既存的接口衍生而来：

```pascal
ITripleJumper = interface (IJumper)
 [‘{0876F202-AAD3-11D2-8551-CCA30C584521}’]
 function TripleJump: string;
end;
```

这个新的接口类型包含了其基础接口的所有方法，并新增了一个新的方法。当然，接口兼容性的规则也跟类一样。

我们在这一节里面想要强调的是一个相当不同的主题，也就是以类型为基础的多型(polymorphism)。假设有一个一般的基础类对象，我们可以建立一个虚拟方法，并确定正确的版本可以被调用。同样的作法也适用于接口。然而，透过接口，我们可以向前迈进一步并常可以透过接口的查询提供动态程序。假设对象跟接口的关系可以很复杂(一个对象可以实作多个接口，并间接的也实作这些接口的基础接口)，那么描绘出这种情境可以做到的愿景就显得更为重要。


首先，假设我们有一个通用的 IInterface 参考，我们怎么知道这个接口的参考有没有支持哪一个特定的接口呢? 这里面包含多种技术，明显的跟类的对应不太一样:

透过 is 指令进行测试(然后或许可以使用 as 进行接下来的类型转换)。这可以用来检查一个对象是否支持该接口，但如果一个对象所参考的接口也支持另一个接口就不行了(也就是说，我们不能用 is 来判断接口的从属关系)。请注意到，任何情况下，使用 as 来进行转换都是必要的:直接把接口类型进行转换，几乎不会有任何侥幸，都会发生错误。

调用全局函数 Support，使用它众多的多载版本之一，我们可以把要检查的对象(或者接口)以及要检查是否兼容的接口(透过 GUID 或者接口的类型名称)，当成参数传递给这个函数。如果这个检查过关了，我们就可以把这个接口变量传给实际上要用来储存该接口的变量了。

直接调用 IInterface 基础接口的 QueryInterface(这个作法则是比较低阶的作法)，永远要求一个接口变量作为额外的结果，并且会使用 HRESULT 数值作为回传值，不是布尔值喔。

以下是从 IntfDemo 范例项目节录的简单源码范例，用来介绍刚刚介绍的两种使用通用的 IInterface 变量的技术:

```pascal
procedure TForm1.Button4Click(Sender: TObject);
var
 Intf: IInterface;
 WalkIntf: IWalker;
begin
 Intf := TAthlete.Create;
 if Supports (Intf, IWalker, WalkIntf) then
 Show (WalkIntf.Walk);
 if Intf.QueryInterface (IWalker, WalkIntf) = S_OK
 then Show (WalkIntf.Walk);
end;
```

把 Support 跟 QueryInterface 作比较的话，我建议一定要使用 Support 函数来做检查。因为 Support 提供了比较简单、高阶的选项。

另一个情况下，我们可能会想透过接口来使用多型，这个情形是当我们拥有一个用来储存高阶的接口类型的数组时(但也可能是一个用来储存对象的数组，其中可能有部分对象支持一些接口)。



### 从接口参考解析出对象

这情形可能发生在 Object Pascal 的很多个版本，当我们已经把一个对象指派给了一个接口变量，原来的对象就再也没办法被存取了。有时候，开发人员会在接口中加入一个 GetObject 方法来处理这种情形，但这是个很奇怪的设计。

在现代编程语言中，我们可以把接口参考转型回原来的对象，我们有三种不同的方法可以做到这件事:

我们可以写一个 is 测试，来验证一个原本为特定类型的对象是否可以被从接口参考解析出来:

`IntfVar is TMyObject`

也可以写一个 as 指令来进行类型转换，万一出现错误时可以触发一个例外:

`IntfVar as TMyObject.`

最后，还可以直接强制转型，万一出现错误的时候，就回传一个 nil 指标:

`TMyObject(IntfVar)`

!!! note "笔记"
    在任何情形下，类型转换都只能用在该接口的确是从一个对象转换而来的时候，从 COM 服务器取得该接口的话就不行了。也要记得我们不只可以把它转换为原始对象的类型，也可以转换为原始对象的任一基础类型(这当然是完全遵循着类兼容性的基本规则喔)

我们把以下的简单接口跟实作类当做个范例(它是从 ObjFormIntf 范例项目节录而来的):

```pascal
type
 ITestIntf = interface (IInterface)
 ['{2A77A244-DC85-46BE-B98E-A9392EF2A7A7}']
 procedure DoSomething;
 end;
 TTestImpl = class (TInterfacedObject, ITestIntf)
 public
 procedure DoSomething;
 procedure DoSomethingElse; // not in the interface
 destructor Destroy; override;
 end;
```

有了这些定义，我们就可以定义一个接口变量，把这个对象指派到接口变量去，并且可以透过新的转型指令使用不存在该接口的方法：

```pascal
var
 intf: ITestIntf;
begin
 intf := TTestImpl.Create;
 intf.DoSomething;
 (intf as TTestImpl).DoSomethingElse;
```

我们也可以用以下的方式来写，使用 is 作检测，然后来个直接转型，我们当然可以把该对象转型成其原来的类，或该类的任一基础类:

```pascal
var
 intf: ITestIntf;
 original: TObject;
begin
 intf := TTestImpl.Create;
 intf.DoSomething;
 if intf is TObject then
 original := TObject (intf);
 (original as TTestImpl).DoSomethingElse;
```

考虑到直接转性在失败时会回传 nil，所以我们也要多加上以下的源码以防万一(就不用 is 测试了):

```pascal
original := TObject (intf);
if Assigned (original) then
 (original as TTestImpl).DoSomethingElse;
```

要注意，把对象从接口解析出来，然后储存到一个变量里，可能会导致参考计数的问题: 当接口变量被指派 nil 进去，或者离开执行范围时，该对象就会被删除，而该对象的参考指针就会变成非法的指标。我们可以从btnRefCountIssueClick 这个事件处理程序的源码里面找到这个问题。



## 透过接⼝来实作转换器模式(Adapter	Pattern)

我在这一章当中加入了一个小节来介绍转换器模式(Adapter Pattern)作为程序设计世界实战中使用接口的例子。您可以阅读附录 D 作为透过 Delphi 使用设计模式(Design Pattern)的基本介绍。

简单的说，转换器模式是用来把一个类的接口转换成用户的类中所预期的另一个。这方法让我们可以把一个既存的类当成架构中要求的一个被定义的接口来使用。我们可以透过建立一个新的类架构，让这个新的类有对应或者从现存的类衍生、实作需求的接口来完成这个模式的实作。我们可以用多重继承(在那些有支持这个功能的编程语言中就可以用这个方法)，或者透过接口来达到这个功能。在使用接口实作时(我们即将用这个方法作为范例)，一组新架构的类就会实作这个特定的接口，并且把它的方法对应到现存的规则去。

在特定的情境中，转换器提供了通用的接口让多种组件可以进行查询，这会发生在有些接口没有延续性的时候(这就常发生在 UI 函数库里面)。这个接口称为 ITextAndValue，因为它允许存取组件的属性，可以透过文字化的描述或者数字化的内容:

```pascal
type
 ITextAndValue = interface
 '[51018CF1-OD3C-488E-81B0-0470B09013EB]'
 procedure SetText(const Value: string);
  procedure SetValue(const Value: Integer);
 function GetText: string;
 function GetValue: Integer;
 property Text: string read GetText write SetText;
 property Value: Integer read GetValue write SetValue;
 end;
```

下一步是为每个我们想要使用这个接口的类都建立一个新的子类，例如我们可以这么写:

```pascal
type
 TApadterLabel = class(TLabel, ITextAndValue)
 protected
 procedure SetText(const Value: string);
 procedure SetValue(const Value: Integer);
 function GetText: string;
 function GetValue: Integer;
 end;
```

这些方法的实作都很简单，因为他们可以被对应到 Text 属性，并透过一个几单的类型转换，看要提供的是文字还是数字，都能很简单的达成。然而现在我们有了一个新的组件，我们得要先安装它(如我们在前一章的最后所介绍过的)。把我们建立的每个组件都安装上去，这会花上一些时间。

另一个比较简单的作法，会是使用穿插类的作法(interposer class idiom)，这个作法是定义一个跟基础类相同名字的类，但放在不同的单元文件里面。这会让编译器跟运行时间的串流系统可以适当的进行辨识，所以我们在运行时间就可以得到这个新的特定类所建立的对象了。唯一的不同，是在设计时间中，我们只能跟基础组件类的实体互动了。

_____________________________________________________________________
!!! note "笔记"
    穿插类(Interposer class)，这个名词第一次被提到，是在许多年前的 Delphi Magazine 上面。这个技术多少是有些要骇入类之中的意味，但有时也的确很有用。在我看来，穿插类是在不同单元文件之中去定义跟某些类的基础类同名的类，这也是众多 Delphi 常用名词之一。使用上要注意到，穿插类所在的单元文件，在 uses 引入的时候，必须要列在我们要取代掉的类所在的单元文件之后。换句话说，比较后来被引入的单元文件会取代掉顺序上先被引入的单元文件里面的同名标识符。当然我们可以透过把单元文件的名称明白的写出来作为识别，但大家一般都不会写的这么明白，于是透过这个方法，我们就可以在全局的名称解析规则里面投机取巧了。

要定义一个穿插类，我们通常要写一个新的单元文件，在新的单元文件里面会建立一个跟我们要取代的基础类同名的类。要参考基础类，我们必须把它所在的单元文件的名称完整写上来(不然编译器会以为这是一个递归参考，然后就报出错误讯息了):

```pascal
type
 TLabel = class(StdCtrls.TLabel, ITextAndValue)
 protected
 procedure SetText(const Value: string);
 procedure SetValue(const Value: Integer);
 function GetText: string;
 function GetValue: Integer;
 end;
```

在这个例子里，我们不用安装组件，也不用触及现存的程序，但要在 uses引入单元文件的最后加上这个单元文件的名字。在两种案例中(我在穿插类写的范例项目不行)，我们可以查询窗体中的组件是否支持这个转换器接口，然后当成练习，写个程序把所有值都设成 50，它会依次影响到不同组件的不同属性:

```pascal
var
 Intf: ITextAndValue;
 I: integer;
begin
 for I := 0 to ComponentCount - 1 do
 if Supports (Components [i], ITextAndValue, intf) then
 Intf.Value := 50;
end;
```

在这个特例中，这个源码会影响进度列组件或者数字显示盒，以及卷标或文字编辑框的 Text 属性。但也有不少的组件会直接忽略它，例如我们没有定义转换器接口的组件就是如此。

这是非常特别的例子，如果我们多看一些其他的设计模式，就会更容易发现到，透过 Object Pascal 的类以及充满弹性的接口，要实作这些设计模式相对简单多了(Java, C#以及其他透过接口来提供扩充性的热门的编程语言也跟 Object Pascal 一样)。
