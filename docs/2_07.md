这一章的重点非常特别，但是是相当重要的主题，也就是在 Object Pascal编程语言中的内存管理。这个编程语言与其执行时期环境提供了一个很独特的解决方法，它是介于 C++风格的人工内存管理，以及 Java 或 C#，由系统完全掌控之间的一个作法。

使用两者之间的作法，是因为它可以帮助我们避免掉内存管理的痛苦(但显然并非全是痛苦)，而避免掉因为垃圾清理所致的问题，从额外的内存配置到没有正确释放内存。

!!! note "笔记"
    我并没有特别想要深入到垃圾清理策略的问题，或在不同平台上面，是怎么实作这个功能的。这已经是需要进入研究的议题了。在这里相关的是在局限性比较大的装置(例如行动装置)，垃圾清理目前还不够理想，但在每个平台上还是有些共通的问题。忽略 Windows 应用程序内存使用量的趋势，已经让很多小工具都占用很海量存储器的情况了。

然而，在 Object Pascal 里面有些额外的复杂情形，是需要因为不同的数据类型而使用不同的方式管理内存，有些类型要使用参考计数，而有些要使用传统的管理模式。VCL 组件为主的拥有权模式，以及一些其他的选项让内存管理成为一个复杂的议题。本章就要厘清这些问题，我们会从现代编程语言对内存管理的一些基础开始，接着会介绍在对象参考模型之后的概念。

!!! note "笔记"
    这些年来，Delphi 行动装置编译器提供了一个不同的内存模式，称为自动参考计数(ARC，Automatic Reference Counting)。这个管理模式是由 Apple开始从 Objective-C 语言推广的，ARC 让编译器支持对象参考在内存中被使用的次数，并在这个次数被设为 0 的时候(也就是该对象没有在任何地方被使用到的时候)，就把该对象自动释放掉。这个作法跟 Delphi 在所有平台上对接口参考所处理的作法很像。从 Delphi 10.4 开始，对 ARC 的支持已经从所有平台全数取消了。


## 全局数据、堆栈以及 Heap

在任何一个 Object Pascal 的应用程序里面所使用的内存可以分成两个领域:源码与数据。在一个程序的执行文件里面包含几个部分，包含资源(例如图片、窗体描述档案)，以及由程序加载到内存使用的函数库。这些内存区块是只读的(在几个平台上，例如 Windows)，且可以让多个处理程序共享。

仔细研究资料部分也是很有趣的。Object Pascal 程序(像大多其他的编程语言的写法一样)的数据是分成三大块来储存的:全局内存、堆栈、Heap。

### 全局内存

在 Object Pascal 编译器建议执行档案的时候，就已经决定了用来储存生命周期从应用程序开始到结束为止的变量数据所需的内存空间。全局变量会在单元文件的 interface 区段或是 implementation 区段进行宣告，全局变量就会使用这一类的内存。要留意到，如果全局变量是一个类别类型(或是字符串、动态数组)，会使用到全局内存的空间就只有 4 byte 或 8 byte 的对象参考而已。

我们可以透过 Project|Information 这个选单项目来确定全局内存的大小，只需在编译完成之后，就可以看到这个数据所需的空间大小。在图 13.1 里面，显示了全局数据使用量约 50K(48,956 bytes)，这个用量是由我们的程序以及函数库所共同创造出来的。

图 13.1: 编译完成的程序相关信息

全局内存有时也被称为静态内存，因为一旦程序被加载了，这些变量所占用的空间就不会被从一开始的位置被搬走，也不会被释放，直到应用程序结束为止。


### 堆栈

堆栈是动态的内存区域，这个区域会依照后进先出的顺序来配置与释放。这意味着最后被配置的内存对象必须最先被删除掉。我们可以从图 13.2 看到内存堆栈的内容。

堆栈内存通常是由程序、函数以及方法使用到的，这些内存空间会用来储存传递的参数、回传的值，以及我们在函数或方法里面宣告的局部变量。一旦子程序执行完成，它在堆栈的内存空间就会被释放掉了。反正要记得，使用 Object Pascal 的默认缓存器调用转换功能，透过 CPU 缓存器来传递参数，是永远不可能的。

图 13.2: 内存堆栈区域的表示

也要注意到，堆栈内存一般是不会自动进行初始化也不会自动被清除的。这也是为什么如果我们在局部变量当中宣告了一个整数然后直接去读取这个变量的内容，我们不会知道里面会是什么数据。这也是为什么所有局部变量都必须先进行初始化，然后才能被使用。

堆栈的大小通常是固定的，并且在编译作业的过程就已经决定了。我们可以在项目选项的链接程序的设定选项中来设定这些参数。然而，默认值通常是 OK 的。如果我们看到了『stack overflow』这个错误讯息，这或许是因为我们的程序当中有不断调用自己的递归函数，而不是编译器指派的堆栈不够大。初始的堆栈大小是在 Project|Information 对话框里面的另一项信息。


### Heap	(堆)

Heap 是源码当中可以不依照顺序进行配置/释放作业的区域。这表示如果我们依序配置了三块内存，这三块内存可以在稍后用任何的顺序进行释放。Heap 管理程序会照看所有的细节，所以我们只要很简单的用低阶函数GetMem 来要求新的内存，也可以透过调用建构函数来建立一个对象，然后系统就会回传一个新的内存区块(也可能是从已经释放掉的内存当中回收使用的)。Object Pascal 使用 heap 来配置每个对象、字符串内容，动态数组，以及绝大多数数据结构的内存空间。

因为它是动态的，Heap 也是程序当中产生最大多数问题的内存区域:

+ 每次一个对象只要被建立了，它就一定得被释放。没有被释放的内存空间被称为”Memory leak” (翻成内存泄漏)，这情形一般来说不会有太大的伤害，除非这个情形反复发生，而导致整个 Heap 都被用光了。
+ 每次一个对象被释放了，我们必须确定被释放的对象已经没有在任何源码里面被使用了，当然，源码也不能对已经被释放的对象重复进行释放。
+ 对任何动态建立的数据结构也完全一样，但编程语言的函数库会在大多数的时间自动在字符串与动态数组之后进行处理，所以我们不用担心其余的处理动作。


## 对象参考模型

一如我们在第七章里面介绍过的，Object Pascal 里面的对象是以参考来实作的。一个类别类型的变量里，只储存了指向该对象在 Heap 里面所使用的内存空间而已。当然还有一些额外的信息，像是类别参考，我们可以透过它来存取该对象的虚拟方法列表，但这不是本章的主题。(我会在第 13 章的『指针是对象参考吗?』那一节做个简短的介绍)

我们也已经介绍过如何把一个对象指派给另一个对象，只复制该对象的参考，所以我们会有两个参考指向同一个内存内的对象。如果要拥有两个完整的对象，我们必须要建立一个新的对象，然后把原本对象的内容复制到新对象去(这个复制的动作不会自动发生，因为实作的细节会依据不同的数据结构而需要不同的作法)。

用源码的思维来看，如果我们写出以下的源码，程序不会建立出新的对象，只会把内存里面的同一个对象的参考复制一份而已:

```pascal
var
 Button2: TButton;
begin
 Button2 := Button1;
```

换句话说，内存里面只有一个对象，而有两个变量 Button1 跟 Button2 指向这个对象，请参考图 13.3 的示意。

图 13.3: 复制对象参考


### 把对象当成参数来传递

当我们把对象当成参数传递给一个函数或者方法的时候，其中的机制是很相似的。用一般的字眼来描述，我们只是把同一个对象的参考复制一份，然后在方法或函数中，我们就可以用这个对象来做任何处理了，当然也可以修改它，除非该参数是以 const 关键词来描述的。

举例来说，写一个像以下这段源码的程序，并调用它，我们可以变更 Button1的文字卷标，如果我们喜欢用 AButton，也可以：

```pascal
procedure ChangeCaption (AButton: TButton; Text: string);
begin
 AButton.Text := Text;
end;
// Call...
ChangeCaption (Button1, ‘Hello’)
```

假如我们需要一个新的对象呢，要怎么做?我们基本上得建立新对象，然后把每个属性的内容都复制过去。部分类别，只要是 TPesistent 类别的衍生类别，而不是从 TComponent 衍生出来的类别，就会定义有 Assign 这个方法可以用来复制一个对象的数据，举例来说，如果我们这么写:

`ListBox1.Items.Assign (Memo1.Lines);`

即使我们直接指派了这些属性，Object Pascal 会为我们执行类似的源码。事实上，SetItems 方法会把 items 属性跟 listbox 进行链接，靠的就是调用TStringList 类别的 Assign 方法，TStringList 就是 ListBox 的实际项目。所以，让我们试着回忆一下，当把对象当成参数传递时，不同的参数传递方法修饰词所做的:

+ 如果参数上没有修饰词，我们就可以对这个对象以及指向这个对象的变量作任何处理。我们可以对原始对象作修改，但如果我们把一个新的对象指派给参数，这个新的对象就无法作任何处理，当然指向它的变量也一样不行。
+ 如果参数上面有 const 修饰词，我们可以修改该对象里面的数据，也可以调用该对象的方法，但我们不能指派一个新的对象给这个参数。请注意，用 const 修饰词在效能上并没有比较好。
+ 如果参数上面有 var 修饰词，我们就可以修改该对象里面的任何数据，也可以用一个新的对象来取代原来传递进来的参数对象，就跟其他有var 参数的类型一样。守则就是我们必须传递一个变量(不能传一个表达式)作为参数，且这个参数的变量类型必须跟宣告的类型一致。
+ 最后，在把对象作为参数时有一个鲜为人知的选项，称为静态参考(const reference)，语法写为[ref] const. 当我们把一个对象用静态参考作为参数传递时，它的规则有点像是引用参数(var)，但在参数类型上有比较多的弹性，并没有要求完全一致的类型(参数可以是宣告的类别或者衍生类别都可以)。


## 内存管理小技巧

在 Object Pascal 里，内存管理是一个关于三个规则的议题:第一，我们必须建立每个对象，并配置所需的每一个内存区块。第二我们必须释放我们配置的每一个内存区块。第二，每个我们自己建立起来的对象跟内存区块都只能释放一次。Object Pascal 对动态元素支持三种形式的内存管理(也就是说，这些元素不在堆栈，也不在全局内存里面)，我们在本节其余的篇幅里面细细道来:

+ 当我们建立了一个对象，我们就有负责把它释放掉的义务。如果我们没有做到，这个对象所使用的内存空间就不会被释放，也没有机会回收给其他对象使用，直到程序结束为止。
+ 当我们建立一个组件，我们可以确立拥有这个组件的所有者，在建立组件的时候，把所有者当成该组件建构函数的参数。这个所有者对象(通常是窗体或是数据模块)就担负了对它所拥有的对象释放的责任，直到这个所有者对象被释放为止。换句话说，当我们释放该窗体(或是数据模块)，它所拥有的所有组件也会一起被释放掉。所以如果我们建立了一个新的组件，并且指派了该组件的所有者，我们就不用担心这个组件释放的问题了-但我们还是可以决定先释放该组件，这要感谢由TComponent 类别提供的释放提醒机制。
+ 当我们为字符串、动态数组跟被接口参考到的对象(请参考第 11 章的内容)配置了内存，Object Pascal 会在参考离开执行的范围时，自动把这些内存空间释放掉。我们不用释放字符串的空间，因为当字符串没有再被使用到的时候，内存空间就被自动释放掉了。如果我们需要提前释放这些组件以取得多一点可用的内存，我们可以直接把字符串或者动态数组变量指派为 nil，或者指派空字符串到字符串变量即可。


### 释放我们建立的对象

在大多数单纯的情境里，我们得释放由我们建立的对象。任何非临时的对象都应该要有所有者，它可以是一个集合的成员，可以被一些数据结构存取，这些所有者在执行到要结束的时机，要担负释放这些对象的义务。

用来建立、释放临时对象的源码通常都会用 try-finally 区块来封装，这样可以保证就算在使用该对象的过程中发生问题，也确保可以把该对象给释放掉：

```pascal
MyObj := TMyClass.Create;
try
 MyObj.DoSomething;
finally
 MyObj.Free;
end;
```

另一个常见的情境则是某个对象被另一个对象所使用，它就变成了这个对象的所有者：

```pascal
constructor TMyOwner.Create;
begin
 FSubObj := TSubObject.Create;
end;
destructor TMyOnwer.Destroy;
begin
 FSubObj.Free;
end;
```

还有一些常见的情境复杂的多，万一对象在需要被使用的时候还没有被建立出来(延迟初始化)或比所有者更早被释放掉了(因为后面的源码里面不会再用到它)。

要实作延迟初始化的话，我们就不能在该对象的所有者的建构函数里面建立它，而得要在需要该对象的时候才来建立，如下面的程序片段所示:

```pascal
function TMyOwner.GetSubObject: TSubObject
begin
 if not Assigned (FSubObj) then
FSubObj := TSubObject.Create;
 Result := FSubObj;
end;
destructor TMyOnwer.Destroy;
begin
 FSubObj.Free;
end;
```

请留意，上述的情境只在一个情境下成立，就是该对象的内存空间有在使用前先进行初始化，执行后 FSubObj 会变成 nil。此时我们就不需要在释放之前检查该对象是否已经被建立了，因为这动作在 Free 方法里面就会做了，我们会在下一节看到这一点。



### 只能把对象释放一次

另一个问题就是我们如果调用了一个对象的解构函数两次，我们就会导致错误。解构函数是用来释放一个对象所使用的内存空间的方法。我们可以为解构函数写一些源码，通常是覆写预设的 Destroy 解构函数，好让对象在被释放掉之前还能执行这些源码。

Destroy 是一个属于 TObject 的虚拟解构函数。大多数需要在对象被释放前进行自定清理动作的类别，都会覆写这个虚拟方法。

我们之所以不用定义一个新的解构函数，是因为大硕的对象通常都会调用Free 方法来释放对象，而 Free 这个方法会帮我们调用 Destroy 这个虚拟解构函数(也可能是覆写后的版本)。

刚刚我们有提到过，Free 是 TObject 类别的一个方法，几乎所有的类别都会继承它。Free 方法基本上会检查该对象(self)，确定它不是 nil，然后才会调用 Destroy 虚拟解构函数。

!!! note "笔记"
    您可能会觉得奇怪，为什么当对象参考是 nil 的时候，还可以安全的调用它的 Free 方法，但我们却不能直接调用 Destroy 方法?理由是 Free 方法是位于已知的内存空间，而虚拟函数 Destroy 是在运行时间依照该对象的类型进行搜寻以后决定的，搜寻的结果有可能会是对象不存在，那这个动作就非常危险了。

以下是 Free 方法的虚构源码:

```pascal
procedure TObject.Free;
begin
 if Self <> nil then
Destroy;
end;
```

接下来，我们可以把注意力移到 Assigned 函数了。当我们把一个指标传递给这个函数的时候，这个函数会单纯的检查指标内容是不是 nil。所以以下两个叙述句是相同的(至少在大多数情形下是如此):

```pascal
if Assigned (MyObj) then ...
if MyObj <> nil then ...
```

注意到这些叙述句只会在指标内容不是 nil 的时候进行检查，如果指标内容是不合法的，就不会进行检查的动作了。如果我们这么写:

```pascal
MyObj.Free;
if MyObj <> nil then
 MyObj.DoSomething;
```

检查的动作会通过喔，也就是说 MyObj 的内容不会是 nil，所以接下来的那行指令就会出错了，因为我们调用了一个已经被释放的对象的方法。要知道，调用了 Free 方法之后，该对象的指针并不会被设为 nil 的。

要自动把一个对象设成 nil 是不可能的。我们可能会有很多个参考指向同一个对象。同时，我们可以在方法当中对一个对象进行处理，但我们并不知道该对象的参考-该变量的内存地址(我们会用来调用这个方法)。

换句话说，在 Free 方法的内部，或一个类别当中任何其他的方法，我们可以知道该对象(self)的内存地址，但我们不会知道参考到这个对象的变量的内存地址，例如 ToDestroy。因此，Free 方法不会动到 MyObj 变量的内存内容，只会释放 MyObj 所指向的对象。

然而，当我们调用一个外部程序把对象作为引用参数传递时，这个函数可以异动该参数的原始内容。这恰恰就是 FreeAndNil 程序所做的事，以下是FreeAndNil 函数里面的源码：

```pascal
procedure FreeAndNil(const [ref] Obj: TObject); inline;
var
 Temp: TObject;
begin
 Temp := Obj;
 TObject(Pointer(@(Obj)^) := nil;
 Temp.Free;
end;
```

过去，参数只是一个指标，但缺点就是我们也可以把一个完全不是对象的指针也传递给 FreeAndNil 程序，如果这指针指向的是一个接口，或者其他不兼容的数据结构，就可能导致内存冲突或者很难抓出来的程序问题。从Delphi 10.4 开始，FreeAndNil 改成了刚刚范例程序的写法，使用 const 修饰词，这作法就可以限制传入的参数必须是 TObject 类型(或其衍生类别)。

!!! note "笔记"
    部分 Delphi 的专家可能会争论说 FreeAndNil 几乎从来没有使用过，因为参考到对象的变量几乎跟它的生命周期一样。如果对象中还有一个对象，而释放的时间点也确定是在解构函数当中，那其实可以不用特地把指标设定为 nil，因为后面的源码已经确定不会再用到它了。同样的，在 try-finally里面释放的局部变量，也无须再去把它设定为 nil，因为函数已经结束了，不会有机会再使用它了。

当做是个小笔记，除了 Free 方法之外，TObject 还有个 DisposeOf 方法可以用来释放对象，这是前几年为了支持 ARC 功能的时候所制作的。目前DisposeOf 方法就是直接调用 Free 了。

整理一下，以下是几个准则:

+ 一定要调用 Free 来释放对象，不要调用 Destroy 解构函数。
+ 使用 FreeAndNil，或在调用 Free 之后要把该变量的内容设成 nil，除非参考在该函数里面是最后一行，Free 完以后就没有任何源码用到该对象了。


## 内存管理与接⼝

在第 11 章里面，我们介绍了接口内存管理的关键要素，它跟对象不同，是受系统管理，且使用参考计算器制管理的。就像之前介绍过的，接口参考会增加被参考对象的计数数字，但我们可以用 weak 宣告接口参考，这个作法可以让被参考的对象计数数字不被异动(但是仍然会要求编译器管理这个参考)，或者也可以使用 unsafe 修饰词来让编译器完全不管理这个被宣告的参考变量。在这一节里，我们会稍微深入一点，用第 11 章里面提过的范例再做些延伸。

### 更深入 Weak 参考

Delphi 使用接口参考计数模式，当中有一个议题是: 如果两个对象互相参考，这情形称之为循环参考，此时它们的参考计数基本上永远不会变成 0。Weak参考提供了一个机制来打破这样的循环，让我们可以定义一个参考，而不增加这个参考的计数。

假设两个对象互相参考，把对方储存在自己的数据字段中，并用一个外部变量把第一个对象储存起来。此时第一个对象的参考计数将会是 2(外部变量，以及另一个对象的数据字段各占 1):而第二个对象的参考计数将会是1(第一个物件的数据字段)。图 13.4 描绘了这个情境。

图 13.4: 对象之间的参考可能发生循环，weak 参考可以处理这个情形

现在，当外部变量离开执行范围时，两个对象的参考计数都是 1，因为主要对象 Object1 拥有 Object2，并没有外部的所有者，于是这两个对象就会永远留在内存里面了。为了解决这一类的问题，我们需要打破其中的循环参考，要达到这一点并不简单，因为我们并不知道何时可以进行这项作业(这项作业应该要在最后一个外部连结参考脱离执行范围时处理，但两个循环参考的对象则无从得知这个条件是否发生了)。解决这个情况以及类似情况的方法，就是使用 weak 参考。一如我们所描述的，weak 参考是一种当对象参考被指派进来时，不会对该对象的参考计数进行递增的特殊参考。技术上来说，我们只需要在该参考上面加上[weak]标注即可。

!!! note "笔记"
    标注是进阶的 Object Pascal 语言功能之一，我们在第 16 章会介绍到这个功能，我只想说，这是对特定符号在运行时间当中加上一些信息的方法，这样一来，外部的源码就可以决定如何处理它。

接续前一个范例，如果第二个对象中指回第一个对象的参考是 weak 参考(请参阅图 13.5)，当外部变量离开了执行范围，两个对象就都可以被释放掉了。

图 13.5: 在图13.4 里面的循环参考，因为使用了 weak 参考而被打破了(虚线表示)


我们来看一下这个情形的实作源码。在 ArcExperiments 范例程序中宣告了两个接口，其中一个接口会参考到另一个:

```pascal
type
 IMySimpleInterface = interface
 [‘{B6AB548A-55A1-4D0F-A2C5-726733C33708}']
 procedure DoSomething(bRaise: Boolean = False);
function RefCount: Integer;
 end;
 IMyComplexInterface = interface
 ['{5E8F7B29-3270-44FC-B0FC-A69498DA4C20}']
 function GetSimple: IMySimpleInterface;
 function RefCount: Integer;
end;
```

源码则定义了两个不同的类别，各自实作了对应的接口。请留意当中互相参考的情况(FOwnedBy 跟 FSimple 都是接口类型的变量，其中一个我们标注为 weak):

```pascal
type
 TMySimpleClass = class (TInterfacedObject, IMySimpleInterface)
 private
 [Weak] FOwnedBy: TMyComplexInterface;
 public
 constructor Create(Owner: IMyComplexInterface);
 destructor Destroy (); override;
 procedure DoSomething(bRaise: Boolean = False);
 function RefCount: Integer;
 end;
 TMyComplexClass = class (TInterfacedObject, IMyMyComplexInterfce)
 private
 FSimple: IMySimpleInterface;
 public
 constructor Create();
 destructor Destroy (); override;
 class procedure CreateSimple: IMySimpleInterface;
 function RefCount: Integer;
 end;
```

以下是 TMyComplexClass 类别的建构函数，用来建立另一个类别的对象:

```pascal
constructor TMyComplexClass.Create;
begin
 inherited Create;
 FSimple := TMySimpleClass.Create(self);
end;
```

记得 FOwnedBy 字段是一个 weak 参考，所以它不会增加所指向的对象的参考计数，在这个例子中，就是指当前的对象 self。因为这样的类别结构，我们可以把源码这么写:

```pascal
class procedure TMyComplexClass.CreateOnly;
var
 MyComplex: IMyComplexInterface;
begin
 MyComplex := TMyComplexClass.Create;
 MyComplex.FSimple.DoSomething;
end;
```

这么做不会造成内存泄漏，因为 weak 参考已经被适当的使用了。例如:

```pascal
var
 MyComplex: IMyComplexInterface;
begin
 MyComplex := TMyComplexClass.Create;
Log(‘Complex = ‘ + MyComplex.RefCount.ToString);
 MyComplex.FSimple.DoSomething (False);
end;
```

这么一来每个建构函数跟解构函数都会在执行时记录下来，我们会看到如下的 Log:

```
Complex class created
Simple class created
Complex = 1
Simple class doing something
Complex class destroyed
Simple class destroyed 
```

如果我们把宣告那段程序中的 weak 属性拿掉，执行上面这段源码的时候就会发生内存泄漏，当中的参考计数值就会是 2，而不是像上面这个执行结果中的 1 了。



### Weak 参考是受到管理的


有个重要的概念要记得，就是 weak 参考是受到管理的。换句话说，系统会在内存里面维护一份 weak 参考的列表，当对象被释放的时候，系统会检查是否有 weak 参考指向它，并且适当的进行标示。这表示 weak 参考在运行时间是有额外的代价的，但话说回来，我们自己在源码里面做这样的交叉检测，代价一定更高。

具备这种受管理的 weak 参考，跟传统的作法相较之下，好处是我们可以检测一个接口参考是否还是可用的(意指所指向的对象是否已经被释放了)。但也这表示，当我们使用 weak 参考，我们应该在使用它之前永远要先检查它是否已经有被指派内容了。

在 ArcExperiments 范例里面，窗体有一个类型为 IMySimpleInterface 的私有字段，宣告为 weak 参考:

```pascal
private
 [weak] MySimple: IMySimpleInterface;
```

里面有一个按钮点击事件会指派该字段，另一个按钮则会使用它，但使用前会确认该对象是否仍然是可用的:

```pascal
procedure TForm3.BtnGetWeakClick(Sender: TObject);
var
 MyComplex: IMyComplexInterface;
begin
 MyComplex := TMyComplexClass.Create;
 MyComplex.GetSimple.DoSomething (False);
 MySimple := MyComplex.GetSimple;
end;
procedure TForm3.BtnUseWeakClick(Sender: TObject);
begin
 if Assigned (MySimple) then
 MySimple.DoSomething(False)
 else
 Log ('Nil weak reference');
end;
```

除非我们修改了上面的源码，否则当中的 if Assigned 检测结果一定是错误的，因为第一个按钮的事件处理程序建立了对象后就把对象释放掉了，所以 weak 参考的内容就变为 nil(在此案例中会是有错误的)。但如果它是受管理的状态，编译器就会帮我们追踪该对象的实际状态(跟对象的参考不一样)。


### 不安全的标注 (The	unsafe	Attribute)

有些情况是很特殊的(例如在一个实体建立的过程中)，在这些特别的情形中，可能是一个函数要把一个参考计数为 0 的对象刚建立出来，回传给另一段源码使用。在这个案例中，为了避免编译器马上把该对象删除掉(在源码还没来得及把建立出来的对象指派给变量之前，可能就得先把对象参考增加为 1 才行)，我们必须把这个对象标示为”unsafe”。

这里的含意是，该对象的参考计数必须先暂时被忽略，才能让源码安全(safe)。这个行为是透过使用新的特殊标注”[unsafe]”来达成的。这个功能我们应该指会在很特别的情形中才需要用到。以下是范例的语法:

```pascal
var
 [Unsafe] Intf1: IInterface;
 [Result: Unsafe] function GetIntf: IInterface;
```

这个标注的使用在实作建构模式的时候是很有用的。建构模式跟工厂模式一样，在一般用途的函数库当中都会用到。

!!! note "笔记"
    为了支持目前已经弃用的 ARC 内存模式，System 单元文件里面使用了unsafe 关键词，因为在定义宣告之前(在同一个单元文件比较后段的部份)它是无法被使用的。这并非假设要使用该单元文件之外的其他源码，而且ARC 模式已经完全不再被使用了(我们可以看到 System 单元里面它已经被用 $IFDEF 做了限制)



## 追踪与检测内存

在这一章，我们已经看过了 Object Pascal 里面内存管理的基础。在大多数的案例中，只要遵循在本章所提到的规则，就已经足够让整个程序维持稳定、避免过度使用内存，并基本上让我们可以不用去管内存管理。在本章后段还有一些进阶的实例，可以用来撰写强健的应用程序。

在这一节里面，我们要专注在可以用来追踪内存使用的技术，监控异常状况，并且找出内存泄漏的情形。这对开发人员来说是很重要的，即使这并非编程语言的一部分，而比较属于运行时间函数库的支持。同时，实作内存管理也是跟目标平台与操作系统息息相关的，我们是可以在 Object Pascal应用程序当中外挂一个自定的内存管理员的(这个作法目前已经很普遍了)。

留意到，我们目前所有的讨论都是关于追踪内存状态、内存管理、泄漏状况侦测则只跟 heap 内存有关。堆栈跟全局内存的管理是不同的，且我们没有办法干预，但这也是内存区域中不常发生问题的部分。

### 内存状态

要怎么追踪 heap 内存的状态?在 RTL 里面提供了一些有用的函数：GetMemoryManagerState 跟 GetMemoryMap。内存管理员的状态是指不同大小的配置区块数目，heap 地图则相当友善的在系统阶层描绘出应用程序的内存状态。

我们可以用以下的源码来检测每个内存区块的实际状态:

```pascal
for I := low(aMemoryMap) to high(aMemoryMap) do
begin
 case AMemoryMap[I] of
 csUnallocated: ...
 csAllocated: ...
 csReserved: ...
 csSysAllocated: ...
 csSysReserved: ...
 end;
end;
```

这段源码在 ShowMemory 范例项目中用来建立应用程序当时使用内存状态的图形表示。


### FastMM4

在 Windows 平台上，目前的 Object Pascal 内存管理名为 FastMM4，是主要由 Pierre La Riche 所领导的开放原始码项目开发出来的。在 Windows 之外的其他平台上，Delphi 则是使用各平台上原生的内存管理程序。

FastMM4 优化了内存的配置，加速、并节省了许多内存的使用量。FastMM4可以透过许多精简的内存清除程序，来达成许多进阶的内存检测，包含对已经删除的对象进行不正确的使用，包含透过接口存取数据、在内存上覆写，以及在缓冲区溢出等。它也对遗留在内存中的对象提供了一些响应，帮助我们追踪内存泄漏的状况。

FastMM4 的一些进阶功能，只能在完整版的函数库中执行(我们在『在完整版 FastMM4 上的缓冲区溢出』加以介绍)，标准版的 RTL 里面并没有提供这些功能。这也是为什么我们会需要完整版的原因了，我们可以从这里下载完整版的原始码:

`https://github.com/pleriche/FastMM4`

!!! note "笔记"
    这个函数库有个新版本叫做 FastMM5，这个版本中特别对多线程应用程序做了优化，在多核心的系统中执行起来表现也更好。这个新版的函数库是使用 GPL 授权(开源项目的授权)，也有付费的版本(真的值得花这个钱)，请参考该项目的介绍: https://github.com/pleriche/FastMM5


### 追踪泄漏以及其他全局设定

RTL 版本的 FastMM4 可以调整为使用 System 单元里面的全局设定。留意到，相关的全局设定是在 System 单元里，实际上的内存管理模块则是在getmem.inc 这个 RTL 原始码档案里面。


更容易使用的设定是 ReportMemoryLeaksOnShutdown 全局变量，这个变量让我们更容易追踪内存泄漏的情形。我们需要在程序执行前就启用它，在程序结束之后，它会告诉我们在执行过的源码(或者我们使用的任何函数库)里面有没有发生任何内存泄漏的情况。

!!! note "笔记"
    更进阶的内存管理模块设定，包含 NeverSleepOnMMThreadContention 全局变量，可以用来处理多 线 程 的 配 置 ; GetMinimumBlockAlignment 跟SetMinimumBlockAlignment 函数则可以加速一些 SSE 的处理，并降低使用更多内存的代价;全局程序 RegisterExpectedMemoryLeak 则提供了注册预期发生内存泄漏情形的能力。


为了展示标准的内存泄漏回报与注册功能，我写了简单的 LeakTest 范例项目。在这个项目中有一个按钮，它的事件处理程序源码如下:

```pascal
var
 p: Pointer;
begin
 GetMem (p, 100); //Memory leak!
end;
```

这段源码会配置 100 bytes，然后这 100 bytes 的空间没有被释放，所以算是泄漏了。如果我们透过 IDE 来执行 LeakTest 项目程序，假设我们在第一个按钮上点击一次，结束程序的时候，就会看到 IDE 回报了如图 13.6 的错误讯息。

另一个由程序产生的内存泄漏，是由于建立了一个 TButton 并且把它遗留在内存而导致的，但这个对象包含了很多子元素，所以泄漏回报讯息变得很复杂，就像图 13.6 底部的窗口画面。同样的，我们能取得的关于泄漏本身的信息相当有限。

图 13.6: 在Windows 平台上当程序结束时，由内存管理模块回报的内存泄漏警告

这个程序也为全局指针配置了一些内存空间，这些空间也不会被释放掉，但会把它注册成可预期的泄漏，所以不会得到系统回报警告讯息：

```pascal
procedure TFormLeakTest.FormCreate(Sender: TObject);
begin
 GetMem (GlobalPointer, 200);
 RegisterExpectedMemoryLeak(GlobalPointer);
end;
```

再次强调，这个基本的内存泄漏回报机制，预设只有在 Windows 平台可以使用，因为只有在 Windows 平台上，预设是使用 FastMM4 这个内存管理模块的。

### 在完整版 FastMM4 上的缓冲区溢出

!!! warning "提示"
    这个主题相对比较进阶，且特别限定只有 Windows 平台能用，所以我只建议非常有经验的开发人员阅读这一节。

如果我们想要对内存泄漏有更多的控制(像是启用以档案进行记录的功能)，调整配置策略以及使用 FastMM4 提供的内存检测功能，我们就需要下载完整版。包含了 FastMM4.pas 跟其配置文件 FastMM4Options.inc。

我们稍后会需要编辑后者(FastMM4Options.inc)来调整设定，只需简单的把一些设定批注掉，或者取消批注一些设定即可。依照惯例，我们只需要在$DEFINE 指令前面加上一个.就能够把该设定批注掉，以下是我们从 inc 档案里面拿前两行来当成范例:

```pascal
{.$DEFINE Align16Bytes} // comment
{$DEFINE UseCustomFixedSizeMoveRoutines} // Active setting
```

为了示范，我已经修改了下列相关的设定，在这里进行回报，让我们知道有哪些可用的定义：

```pascal
{$DEFINE FullDebugMode}
{$DEFINE LogErrorsToFile}
{$DEFINE EnableMemoryLeakReporting}
{$DEFINE HideExpectedLeaksRegisteredByPointer}
{$DEFINE RequireDebuggerPresenceForLeakReporting}
```

测试程序(在 FastMMCode 文件夹里面，里面也包含了我有用到的完整版的FastMM4，方便读者不用再去另外下载)启用了项目源码里面的客制化版本内存管理模块，只需把它设定成第一个单元文件即可：

```pascal
program FastMMCode;
uses
 FastMM4 in 'FastMM4.pas',
 Forms,
 FastMMForm in 'FastMMForm.pas'; {Form1}
```

我们也需要把 FastMM_FullDebugMode.dll 复制一份到测试程序的目录，这样才能正常运作。测试程序会在缓冲区里面填入超过缓冲区大小的数据来导致一个缓冲区溢出的情况，因为 Length(Caption)大于提供的五个字符：

```pascal
procedure TForm1.Button2Click(Sender: TObject);
var
 pch1: PChar;
begin
 GetMem (pch1, 5);
 GetWindowText(Handle, pch1, Length(Caption));
 ShowMessage (pch1);
 FreeMem (pch1);
end;
```

内存管理模块一开始会配置额外的空间，在每个内存区块的结尾会放特殊的值，在释放每个内存区块的时候也就会检查这个特殊值。这也是为何我们在调用 FreeMem 函数的时候会产生错误。当我们点击按钮(在侦错模式下)，我们会看到很长的一串错误讯息，这串讯息也会记录在以下这个档案里面:

`FastMMCode_MemoryManager_EventLog.txt`

以下是缓冲区溢出错误的讯息内容，包含了当时对堆栈的追踪，以及配置与释放的动作，再加上把当前堆栈内存直接倒出的内容(只列出一部分)：

```
FastMM has detected an error during a FreeMem operation. The block
footer has been corrupted.
The block size is: 5
Stack trace of when this block was allocated (return addresses):
40305E [System][System.@GetMem]
44091A [Controls][Controls.TControl.Click]
44431B [Controls][Controls.TWinControl.WndProc]
42D959 [StdCtrls][StdCtrls.TButtonControl.WndProc]
44446C [Controls][Controls.DoControlMsg]
44431B [Controls][Controls.TWinControl.WndProc]
45498A [Forms][Forms.TCustomForm.WndProc]
443A43 [Controls][Controls.TWinControl.MainWndProc]
41F31A [Classes][Classes.StdWndProc]
76281A10 [GetMessageW]
The block is currently used for an object of class: Unknown
The allocation number is: 381
Stack trace of when the block was previously freed (return addresses):
40307A [System][System.@FreeMem]
42DB8A [StdCtrls][StdCtrls.TButton.CreateWnd]
443863 [Controls][Controls.TWinControl.UpdateShowing]
44392B [Controls][Controls.TWinControl.UpdateControlState]
44431B [Controls][Controls.TWinControl.WndProc]
45498A [Forms][Forms.TCustomForm.WndProc]
44009F [Controls][Controls.TControl.Perform]
43ECDF [Controls][Controls.TControl.SetVisible]
45F770
76743833 [BaseThreadInitThunk]
The current stack trace leading to this error (return addresses):
40307A [System][System.@FreeMem]
44091A [Controls][Controls.TControl.Click]
44431B [Controls][Controls.TWinControl.WndProc]
42D959 [StdCtrls][StdCtrls.TButtonControl.WndProc]
44446C [Controls][Controls.DoControlMsg]
44431B [Controls][Controls.TWinControl.WndProc]
45498A [Forms][Forms.TCustomForm.WndProc]
443A43 [Controls][Controls.TWinControl.MainWndProc]
41F31A [Classes][Classes.StdWndProc]
76281A10 [GetMessageW]
Current memory dump of 256 bytes starting at pointer address 133DEF8:
46 61 73 74 4D 4D 43 6F 64 [... omitted...]
```

这看上去并不算很直觉，但它提供的信息应该已经足够让我们找出问题。请注意，如果在内存管理模块里面没有这些设定，我们基本上是不会看到任何错误的喔，程序也会继续执行下去。虽然我们可能会发现不定时出现问题，也只会当缓冲区溢出的情形影响到了我们用来储存数据的内存区块，错误才会发生。此时，我们会看到程序执行的结果很奇怪，而且很难找到问题发生的原因。

举例来说，我曾经看到过原本用来储存类别参考的内存，当中有一部分是被初始化数据给覆写掉了。由于这种内存数据损坏，类别会变成未被定义，且每一次调用它的虚拟方法时，可能都会导致程序崩溃…这很难去联想到是在跟执行中的源码完全不同的区域对内存写入的动作有关。


### 在 Windows 以外的平台的内存管理

想象一下在 Object Pascal 的编译器是怎么进行内存管理的作业，就值得思考一下我们可以用的一些作法，来确定在 Windows 以外的操作系统中，所有事情都在控制下。在我们继续之前，很重要的一点，是在 Windows 以外的操作系统中，Delphi 并没有使用 FastMM4 内存管理模块，所以设定ReportMemoryLeaksOnShutdown 这个全局选项，想要在程序关闭的时候侦测是否发生内存泄漏的情形是没用的，另一个理由是在行动装置上，关闭程序之后，程序并不是直接就被关闭了，因为 app 会在内存里面继续占用着，直到被用户或操作系统强制移除，内存空间才会被释放掉。

在 macOS, iOS 跟 Android 平台上，Object Pascal RTL 会直接调用原生 libc函数库的 malloc 跟 free 函数。所以要在这些平台上监测内存的使用，就必须靠额外的平台工具了。例如在 iOS 跟 macOS 上面，我们得要用 Apple 提供，附在 Xcode 里面的 Instruments 工具程序，它会全面性的监测我们在实体装置上执行的应用程序。


### 追踪每个类别的配置

最后，Object Pascal 有一个特殊的功能可以用来追踪特定的类别，而不用对整个内存全部追踪。事实上，对单一对象的内存配置会发生在 NewInstance这个虚拟类别方法被调用的时候，而会在 FreeInstance 虚拟方法被调用的时候进行清除。这是我们可以对特定类别进行覆写的虚拟方法来客制特定的内存配置策略。

好处是我们透过这个作法可以不用管建构函数(因为一个类别可能有多个建构函数)与解构函数，从标准的对象初始化跟结束化源码明确的把内存追踪的源码分离出来。

虽然这是一个比较极端的案例(可能只有在一些比较大型的内存结构中才值得这样做)，我们可以覆写这些方法，用以在建立后与释放后计算对象的数量或特定类别的数量，计算使用中的实体，并在最后确认对象的数量是否如预期的变成 0 了。


## 撰写强健的应用程序

在这一章里面，我们已经介绍了相当多的计数可以用来撰写强健的应用程序，以及适当的管理内存配置与释放。

在专心介绍内存管理这个章节的最后一节里，我决定要列出一些稍微进阶的主题，为之前讨论过的内容进行一些延伸。即使使用 try-finally 区块跟调用解构函数之前已经介绍过了，但在本章强调的情境里面显然更为复杂，并引入了更多编程语言的功能一起运作。

这并不全然是进阶的一节，但所有Object Pascal开发人员应该真正掌握它，才能写出强健的应用程序。只有上一节关于指针跟对象参考的部份算是真的很进阶的主题，因为它深入到一个对象跟类别参考的内部存储器结构。


### 建构函数,	 解构函数,	 以及例外

建构函数跟结构函数是正确内存管理的有利工具，但也可能是应用程序里面的问题来源。虚拟建构函数必须一定要先调用基础类别的建构函数(透过inherit 来调用)。而解构函数应该在最后才调用基础类别的解构函数(也是透过 inherit 来调用的)。

!!! note "笔记"
    要遵循好的程序实务，我们应该要在我们写的每一个 Object Pascal 源码的建构函数中记得要调用基础类别的建构函数，即使这并不是强迫性的，而且额外的调用可能不一定有用(像是调用 TObject.Create)

在这一节里面，我想要特别强调在类别的建构函数出错时的状况，像是:

```pascal
MyObj := TMyClass.Create;
try
 MyObj.DoSomething;
finally
 MyObj.Free;
end;
```

如果这个对象被建立了，并被指派给 MyObj 变量，finally 区块就会处理释放对象的动作，但如果 Create 动作触发了例外，程序就不会进入 try-finally区块了(这是对的!)。当一个建构函数触发例外时，此时对象只完成了部分初始化动作，对应的解构函数源码就会被自动执行。举例来说，如果在建构函数中建立了两个子对象，它们就需要透过调用相对的解构函数加以清除。然而这可能导致一个潜在的问题，如果在解构函数里面我们先假设了所有子对象都有被完整初始化的话……

从理论上来看，这并不容易理解，所以我们来看一个实际的源码当例子。SafeCode 这个范例项目包含了一个类别，在这个类别中有建构函数跟解构函数，通常这两个函数都会正常运作，除非建构函数本身出了错：

```pascal
type
 TUnsafeDesctructor = class
 private
 aList: TList;
 public
 constructor Create (positiveNumber: Integer);
 destructor Destroy; override;
end;
constructor TUnsafeDesctructor.Create(positiveNumber: Integer);
begin
 inherited Create;
 if positiveNumber <= 0 then
 raise Exception.Create ('Not a positive number');
 aList := TList.Create;
end;
destructor TUnsafeDesctructor.Destroy;
begin
 aList.Clear;
 aList.Free;
 inherited;
end;
```

在对象已经完全建立时，不会有什么问题，但当 FList 字段内容还是 nil 的时候，如果传递一个负数给建构函数就会发生问题了。在这个情形下，建构函数会发生错误，解构函数也会被触发，企图对 FList 调用 Clear 方法，FList 在建构函数被调用前，就已经被初始化成 nil 了。想要调用 nil 的 Clear方法，当然会发生『违规存取』(Access Violation)的例外。

这个解构函数比较安全的写法如下:

```pascal
destructor TUnsafeDesctructor.Destroy;
begin
 if assigned (aList) then
  aList.Clear;
 aList.Free;
 inherited;
end;
```

而这个例子的寓意，是我们永远不能在解构函数中假设对应的建构函数已经把所有子对象完整的建立完成了。我们可以在类别中的任何一个方法里面做这个前提假设，但就是不能在解构函数里面这样假设。

### 巢状 Finally 区块

Finally 区块是让我们的源码保持安全状态的技术中最重要，也最常见的。我也不觉得这是进阶的主题，但我们有把 finally 在该用的地方都用上吗?且我们们是否都在特定的案例中适当的使用了 finally 区块呢?例如巢状处理，或者我们是否有把多个完成源码组合在一个 finally 区块里面呢?这样可不是完美源码会做的事:

```pascal
procedure TForm1.btnTryFClick(Sender: TObject);
var
 A1, A2: TAClass;
begin
 A1 := TAClass.Create;
 A2 := TAClass.Create;
 try
 A1.whatever := 'one';
 A2.whatever := 'two';
 finally
 A2.Free;
 A1.Free;
 end;
end;
```

以下才是比较保险也比较正确的写法(也是节录自 SafeCode 范例项目):

```pascal
procedure TForm1.btnTryFClick(Sender: TObject);
var
 A1, A2: TAClass;
begin
 A1 := TAClass.Create;
 try
 A2 := TAClass.Create;
 try
 A1.whatever := 'One';
 A2.whatever := 'Two';
 finally
 A2.Free;
 end;
 finally
 A1.Free;
 end;
end;
```

### 动态类型检查

在类型之间进行动态的转换是常见的处理，且类别类型也可能是另一种错误的发生来源。特别是如果我们没有使用 is 或 as 运算符号，直接硬着进行类型转换的时候。事实上，每一个直接类型转换的动作就是潜在的原始码错误来源(除非进行转换之前有先做过 is 检查)。

从对象到指针的类型转换，转换成或转换自类别参考，从对象到接口，转换自字符串或者转换成字符串都是极度具有潜在危险性的，但在特别的情形下却难以避免。举例来说，我们可能希望把对象参考存放在一个组件的Tag 属性里，Tag 属性是一个整数，所以我们可以依照我们自己的设计直接做类型转换。另一个例子则是当我们把对象存放在一个指针列表里面，一个旧型的 TList(并不是类型安全的泛型列表，我们在下一章里面会介绍)。这是一个相当笨的范例：

```pascal
procedure TForm1.btnCastClick(Sender: TObject);
var
 List: TList;
begin
 List := TList.Create;
 try
 List.Add(Pointer(Sender));
 List.Add(Pointer (23422));
 // direct cast
 TButton(List[0]).Caption := 'Ouch';
 TButton(List[1]).Caption := 'Ouch';
  finally
 List.Free;
 end;
end;
```

执行上面的源码通常会触发违规存取。

!!! note "笔记"
    我写成通用的情形，因为当我们随机存取内存的时候，我们永远没有机会知道实际的效果。有时候程序会简单的重写内存，不会导致立即的错误……但我们接下来会很难搞清楚为什么其他数据会被破坏。

我们应该在任何时候都要避免类似的情形，但如果我们突然没有其他的替代方案，那要怎么修正这份源码?比较直觉的方法是使用 as 这种安全类型转换，或者透过 is 做类型检测，像以下的程序片段所做的:

 ```pascal
// “as” cast
(TObject(list[0]) as TButton).Caption := 'Ouch';
(TObject(list[1]) as TButton).Caption := 'Ouch';
// “is” cast
if TObject(list[0]) is TButton then
 TButton(list[0]).Caption := 'Ouch';
if TObject(list[1]) is TButton then
 TButton(list[1]).Caption := 'Ouch';
```

然而，这并不是解决方法，我们还是会继续发生违规存取。问题在于 is 跟as 最终还是会调用 TObject.InheritsFrom，这是另一个动作，会被执行好几次。

那解决方法呢?实际的解决方法是避免类似的情形一开始就发生(这种写法真的没什么意义)，使用 TObjectList 或者其他的安全机制(我们在下一章会介绍泛型的容器类别)。如果我们真的需要低阶对系统进行处理，像是跟指标深入处理，我们可以试着检查一个特定的『数值』是否是一个对象的参考。不过，这个动作并不直觉。它也有趣味的一面，这也是我拿来为范例说项的点，要为大家说明一个对象内部的结构，当然也适用在类别参考啦。


### 这个指针是对象参考吗?

!!! warning "提示"
    这一小节说明了对象跟类别参考的内部结构，并超越了本书绝大部分的章节所涉及的程度。它可以提供一些有趣的内部观点给更多专业的读者，所以我决定保留这些内容，这些是过去我曾为内存管理所写的一篇进阶的文章的内容。也要留意到，以下的实作有部分是只能在 Windows 上面使用的，尤其是内存检测的部份。

有些时候，我们会很常使用指标(指标只是一个数值，这个数值代表储存某些数据的物理内存地址)。这些指针可能实际上是某些对象的参考，我们通常会知道指标指向的是什么，以及要把它们拿来做什么。但每当我们要做低阶的类型转换，我们可能随时位于搞砸整个程序的边缘。有些技术可以让这样的指标管理稍微安全一点，但不是百分之百保证安全。

我们可能要使用指标之前，考虑这个作法的出发点会是『它是否是一个合法的指标』?Assigned 函数只会检测一个指标是否为 nil，而在这样的情境里面并没有实质帮助。然而，很少人知道在 Object Pascal 里面有个名为FindHInstance 的函数(在 System 单元文件里面，只有 Windows 平台可用)，会回传包含该函数的参数对象在 heap 当中的基础地址，如果该指针所指的是一个不合法的分页，就会回传 0(避免不常发生，但真的很难侦测的内存页面错误问题发生)。如果我们随机抓了一个数字，想要用这个数字当成内存地址来取得内存里面的数据，绝大多数的情形都不会抓到合法的内存的。

这是个很好的开始，但我们可以做的更好，因为如果这个值是一个字符串参考或任何有效的指针，而不是对象参考，就没有任何帮助。现在我们要怎么知道一个指针是否是一个对象的参考呢? 我已经提出以下的实证。每个对象为首的 4 个 bytes 是其类别的指标。如果我们想得知一个类别参考的内部数据结构，它是位于类别参考的 vmtSelfPtr 地址，当中是一个只到自己的指标。我们可以透过图 13.7 来表示：

图 13.7: 对象与类别参考的内部结构表示

换句话说，从类别参考指针的 vmtSelfPtr 位回溯到内存地址(这是在内存中向前寻找，是在比较前面的地址)，我们应该可以找到相同类别参考的指标才对。而且在类别参考的内部数据结构中，我们可以取得实体大小的信息(在 vmtInstacneSize 的位置)以及看到是否有合理的数字。以下是实际的源码:

```pascal
function IsPointerToObject (Address: Pointer): Boolean;
var
 ClassPointer, VmtPointer: PByte;
 InstSize: Integer;
begin
 Result := False;
 if (FindHInstance (Address) > 0) then
 begin
 Vmtpointer := PByte(Address^);
 Classpointer := Vmtpointer + VmtSelfPtr;
 if Assigned (Vmtpointer) and
 (FindHInstance (Vmtpointer) > 0) then
 begin
 instsize := (PInteger(
 Vmtpointer + VmtInstanceSize))^;
 // check self pointer and "reasonable" instance size
 if (Pointer(Pointer(classpointer)^) =
 Pointer(Vmtpointer)) and
 (InstSize > 0) and (InstSize < 10000) then
 Result := True;
 end;
 end;
end;
```

!!! note "笔记"
    这个函数有很高的机会回传正确的值，但并非百分之百笃定。不幸的是，在以内存以随机数据通过测试时发生。

透过这个函数，在前一个 SafeCode 范例项目，我们可以在进行安全类型转换前加上一个指针-到-对象的检查：

 ```pascal
if IsPointerToObject (List[0]) then
 (TObject(list[0]) as TButton).Caption := Ouch';
if IsPointerToObject (List[1]) then
 (TObject(list[1]) as TButton).Caption := 'Ouch';
```

相同的概念也可以直接应用在类别参考上，也可以用来实作类别参考之间的安全类型转换。而且，最好能够一开始就写好安全、明白的源码，试着避免类似的问题，但如果我们无法避免，IsPointerToObject 这个函数就有机会派上用场了。无论如何，这一节的用意应该是在对于这些系统数据结构的内容多做一些说明。