
在我们开始继续介绍 Object Pascal 类别的其他功能之前，我们需要先聚焦在处理错误条件的特定对象群组上，这个错误条件称之为例外。

例外处理的含意，是要透过加入一致性、简单的处理软件或硬件错误的能力，让程序更为自动化。一个程序可能在这样的错误发生后还继续执行，也可能安全的结束掉，让使用者能够在结束之前储存一些数据。例外事件允许我们把错误处理的源码从一般源码分离开来。这样我们就不用在源码之间交错一般程序功能跟错误处理的源码了。这样我们的源码可以更精简、不凌乱，把跟实际程序目标不相关的源码拢在一起。

另一个好处，是『例外』定义了一个统一、放诸四海阶准的错误回报机制，这个机制也同时用在组件函式库里面。在运行时间中，系统会在出错的时候发出例外事件。如果我们的源码已经适当处理了例外事件，我们的程序就会被通知，也会试着解决这个错误。

反之，这个例外会被传递到它所呼叫的源码，一层一层扩散。最终，如果我们的源码里面没有处理这个例外，系统通常会处理它，轮到系统处理时，画面上就会显示一个标准的错误讯息，并且试着继续执行程序。在大多数的情形下，我们的程序都是在例外处理区外面运作的，发出了例外讯息会使程序终止。

在整个 Object Pascal 的例外处理机制当中，是建构在五个关键词之上的:

+ try 注册一个源码保护区的开始
+ except 注册这个源码保护区的结束，并宣告开始进入例外处理的源码
+ on 标示每个特定的例外处理叙述句，与特定的例外进行连结，每个 on 的叙述句语法都是 on 例外型别 do 叙述句
+ finally 是用来标示不论如何都要被执行的源码，即使例外发生也一样。
+ Raise 是用来触发例外的叙述句，它的参数则是一个例外类别的对象(在其他语言中，使用的语法则是 throw)

以下是一个简单的比较表，用来比较 Object Pascal 跟其他以 C++语法为基础的编程语言(像是 C#跟 Java)处理例外的语法:

```
try try
except on catch
finally finally
raise throw
```

用 C++的说法，我们可以抛出一个例外对象，再依照例外所属型别来接住它。在 Object Pascal 里面也是一样的，我们可以透过 raise 指令传出一个例外对象，并在 except on 语法中以参数形式接收该对象。


## Try-Except 区块283

让我们从一个相对简单的 try-except 例子开始(这是 ExceptionsTest 范例项目的一部分)，以下则是一个通用的例外处理区块：

```pascal
function DividePlusOne (A, B: Integer): Integer;
begin
 try
// raises exception if B equals 0
 Result := A div B;
 Inc (Result);
 except
 Result := 0;
 end;
 //More code
end;
```

!!! note "笔记"
    当我们在 Delphi 除错模式下执行一个程序时，除错程序会默认在例外发生的时候停下程序，即使我们已经在程序中写好了异常处理程序也一样。这也是我们通常乐见的，当然，因为我们想要知道例外会在什么时候发生，然后才能一行行的看着源码执行。如果我们想让程序在例外发生时适当的处理它，然后看看一般使用者会看到的画面，我们可以用 IDE 里面的”以非侦错模式执行”的指令来执行程序。

并不是说，『掩盖掉』例外事件，然后把回传值设定为 0 这样是对的(因为把使用者的问题遮盖掉通常不是好事)，但这个源码是用来帮助我们用简单的情况理解核心机制的：

```pascal
var
 N: Integer;
begin
 N := DividePlusOne (10, Random(3));
 Show (N.ToString);
```

就像我们看到的，源码使用了一个随机产生的数值，因此当我们点击按钮的时候，我们可以处于合法的情形下(2 乘以 3 以外的值)或者处于错误的情形下。以下是可能发生的两种不同程序流程：

+ 如果 B 不是 0，程序会进行除法计算，然后把结果加一，最后忽略例外处理区块。
+ 如果 B 是 0，程序执行除法的时候会发生例外，在发生例外之后的其他源码会自动被跳过(在这个范例里面只有一行程序会被跳过)，直接跳到try-except 区块的第一行继续执行。在进入例外程序区之后，程序就不会再回到原来的源码了，但会接着再把例外程序区里面的所有源码都执行完毕。

我们可以用一个方法来描述这个例外模型，就是它遵循的工作目标是不会再回头。在发生错误的情形下，试着处理错误情况，并回到导致这个错误发生的源码，是很危险的作法。在这个情形下，程序可能已经进入了未预期的状况。例外很明确的改变了程序执行的流程，跳过了接下来的源码，回复到错误未发生前的状态，直到适当的错误处理源码出现。

上面的源码包含了一个很简单的例外程序区，但没有包含 on 这个叙述句。当我们需要处理多种不同的例外情形时(或者多种例外类别型别)，或者我们想要存取被触发、传递到例外程序区的例外对象，我们就需要有一个或多个的叙述句：

```pascal
function DividePlusOneBis (A, B: Integer): Integer;
begin
try
 Result := A div B; // error if B equals 0
 Result := Result + 1;
 except
 on E: EDivByZero do
 begin
 Result := 0;
 ShowMessage (E.Message);
 end;
 end;
end;
```

在例外处理的叙述句中，我们拦截到 EDivByZero 例外，这是由 RTL(运行时间函式库)所定义的。在 RTL 里面定义了许多这样的例外型别(像这里所介绍的除以 0 的例外，或者错误的动态型别切换都是其中之一)，也有一部分例外型别是由系统所定义(例如内存不足的例外)，或者组件错误(像是索引值有误)。所有这些例外的类别都是从例外的基础类别 Exception 衍生而来的，在这里面提供了最低限度的功能，例如 Message 属性，在上面的范例程序中我们就用到了这个属性。这些类别也依循一些逻辑结构组成了一个不小的类别架构。

!!! note "笔记"
    请注意，在 Object Pascal 里面一般只要是类别都会用 T 这个字母开头来命名，但例外类别是这个规则中的例外，它们是以 E 这个字母开头来命名的。

**例外类别架构**

以下的列表是 RTL 核心例外类别里面的一部分，这些例外类别都定义在SysUtils 这个单元文件里面(大多数其他的系统函式库还另外会在新增自己的例外型别):

```pascal
Exception
 EArgumentException
 EArgumentOutOfRangeException
 EArgumentNilException
 EPathTooLongException
 ENotSupportedException
 EDirectoryNotFoundException
 EFileNotFoundException
 EPathNotFoundException
 EListError
 EInvalidOpException
 ENoConstructException
 EAbort
 EHeapException
 EOutOfMemory
 EInvalidPointer
 EInOutError
 EExternal
 EExternalException
 EIntError
  EDivByZero
 ERangeError
 EIntOverflow
 EMathError
 EInvalidOp
 EZeroDivide
 EOverflow
 EUnderflow
 EAccessViolation
 EPrivilege
 EControlC
 EQuit
 EInvalidCast
 EConvertError
 ECodesetConversion
 EVariantError
 EPropReadOnly
 EPropWriteOnly
 EAssertionFailed
 EAbstractError
 EIntfCastError
 EInvalidContainer
 EInvalidInsert
 EPackageError
 ECFError
 EOSError
 ESafecallException
 EMonitor
 EMonitorLockException
 ENoMonitorSupportException
 EProgrammerNotFound
 ENotImplemented
 EObjectDisposed
 EJNIException
 ```

!!! note "笔记"
    我不知道大多数人会怎么做，但我自己仍然会先把实际的使用情境中可能遇到的奇怪例外做个设想，像是 EProgrammerNotFound 这样的例外。在Delphi 里面有一些隐藏彩蛋，这是其中一个。

现在我们已经介绍过了核心的例外类别架构，我们可以在先前的 except-on叙述句上面多加一点信息了。这些叙述句会一一被比对，直到系统找到跟目前发生的例外相符的例外类别。目前所使用的比对规则是检查其型别兼容性，这个主题我们在前一章已经介绍过了：例外对象会跟其所属的类别以及所有父代类别兼容(就像 TDog 对象会兼容于 TAnimal 类别)。

这表示我们可以提供多个例外处理型别是跟目前发生的例外相符的。如果我们想要使用更精确相符的规则来处理该例外的话(在类别架构中比较末端的例外类别)，我们就需要更精确的列出要处理的例外类别(列的越末端越能精确命中特定的例外)。当然我们也可以写一个异常处理程序，直接使用Exception 这个型别，这样所有的例外发生的时候都会由它来处理，所以这个区块也得放在整个 Exception 处理区的最后喔。以下就是在一个区块中包含两个处理程序的程序范例：

```pascal
function DividePlusOne (A, B: Integer): Integer;
begin
 try
 Result := A div B; // Error if B equals 0 Result := Result + 1;
 except
 on EDivByZero do
 begin
 Result := 0; MessageDlg (‘Divide by zero error’,
 mtError, [mbOK], 0);
 end;
 on E: Exception do
 begin
 Result := 0; MessageDlg (E.Message,
 mtError, [mbOK], 0);
 end;
 end; // End of except block
end;
```

在上面这段源码里面，同一个 try 区块当中，包含有两个不同的异常处理程序。我们可以在同一个 except 区块当中写入任意数量的异常处理程序，在例外发生的时候，它们会被系统一一比对，就像前文所述。

也请记得，为每一种可能发生的例外都写一个处理程序通常并不是好主意。最好还是把一些我们不知道的例外留给系统。预设的异常处理程序通常会把该例外的类别名称用讯息窗口加以显示，然后回复该程序的正常运作。

!!! tip "提示"
    我们也可以透过实作 Application.OnException 这个事件的处理程序来把一般异常处理程序替换掉。例如把例外的讯息存到档案里面，而不要显示给用户看。

### 触发例外

我们在 Object Pascal 里面会遇到的绝大多数的例外，都是由系统所建立的，但当我们发现在运行时间当中有不合法或者不连续的资料时，我们也可以在我们自己的源码里面触发例外。

在大多数的情形下，我们需要先定义我们自己的例外型别，好让我们需要触发时可以用到。我们可以用以下的源码简单的建立一个新的例外型别，只需要从预设的例外基础型别 Exception 衍生一个即可：

```pascal
type
EArrayFull = class (Exception);
```

在绝大多数的案例中，我们不用在新的例外类别中加入任何方法，只需纯粹宣告一个新的例外类别即可。

用到这个例外型别的情境，可能会是在一个方法试着在数组中加入元素时，当数组已经满了，而触发这个例外。实际上的写法只需建立例外对象，然后把它用 raise 关键词传递即可：

```pascal
if MyArray.Full then
raise EArrayFull.Create ('Array full');
```

这个 Create 方法(从 Exception 类别继承而来的)要求一个字符串作为参数，来描述该例外的情形让使用者知道。

!!! note "笔记"
    我们不用担心这个例外对象要怎么被释放，因为它会自动被例外处理机制给删除掉。

Raise 关键词还有一种情形下会使用到。就是在我们自己写的 except 区块当中，当我们所写的源码并没有真的抓到该例外，这时候该例外处理区块里面的源码应该都不会派上用场，此时我们就应该把这个例外再次触发，让系统去处理，这时我们只需要执行 raise，不用参数了。这个动作我们称为再次触发例外。


### 例外与堆栈

当程序触发了一个例外，而目前的源码无法处理它，那么在我们方法或函式堆栈里面会发生什么事呢？程序会在已经触发的所有函式或方法堆栈当中一层一层的搜寻可以处理这个例外的处理程序。这表示源码会从当时正在执行的源码里面跳脱，不会再继续执行剩下的源码。要了解其中的工作原理，我们可以用侦错程序或者在目前执行的源码里面加一些简单的输出值。在下一个范例项目，ExceptionFlow 里面，我选择以第二种方式来做示范。


举例来说，当我们按下 ExceptionFlow 项目里的窗体上面的 Raise1 按钮，就会触发一个没有被处理的例外，所以源码的最后一部分并不会被执行到：

```pascal
procedure TForm1.ButtonRaise1Click(Sender: TObject);
begin
 // unguarded call
AddToArray (24);
Show ('Program never gets here');
end;
```

请注意，这个方法呼叫了 AddToArray 程序，这个程序将会触发例外。当这个例外有被处理时，程序的流程会从该处理程序之后继续下去，而不是从例外发生的源码继续下去。以下面这个方法来看:

```pascal
procedure TForm1.ButtonRaise2Click(Sender: TObject);
begin
 try
 // This procedure raises an exception
 AddToArray (24);
 Show ('Program never gets here');
 except
 on EArrayFull do
 Show ('Handle the exception');
 end;
 Show ('ButtonRaise1Click call completed');
end;
```

最后一行的 Show 会在例外被处理完毕以后继续执行下去，第一个 Show 则永远不会有被执行到的机会。我建议在执行这个程序之前，先把源码做些修改，做些对应的实验，好帮助您能够理解例外被触发时，程序流程是怎么运作的。

!!! note "笔记"
    如果我们写来准备处理例外的源码跟该例外发生的位置不同，如果能够确切知道该例外是发生在哪个方法会是很有帮助的。虽然的确有方法可以在例外被触发的时候，取得函数调用堆栈的信息，但这是比较进阶的主题，我并不打算在此介绍。在绝大多数的案例中，Object Pascal 的开发者会依靠第三方组件跟函式库(像是 Jedi 组件函式库里面的 JclDebug、MadExcept，或者 EurekaLog)来取得这个信息。此外，我们也得先让编译器产生出 MAP档，并把它引用到我们的项目里面，这个档案会列出我们应用程序中每个方法与函式的内存地址。



## Finally 区块

例外处理还有第四个关键词，我们之前提到过，但还没有使用到，它就是finally。Finally 区块是用来定义一些一定要被执行到源码(通常是用来做清理的程序)。事实上，在 finally 区块里面所撰写的源码，不管是否有例外事件被触发，都一定会被执行到。而一般的源码则是写在 try 跟 finally 之间，只有在没发生例外的时候或者发生了例外且被妥善处理了，这些源码才会被执行。换句话说，不管有没有发生例外事件，finally 区块的源码都会被执行到。

以下面这个方法为例(它是 ExceptFinally 范例项目的一部分)，它会执行一些耗时间的指令，并在窗体的标题上显示它当时的状态：

```pascal
procedure TForm1.BtnWrongClick(Sender: TObject);
var
 I, J: Integer;
begin
 Caption := 'Calculating';
 J := 0; // Long (and wrong) computation...
 for I := 1000 downto 0 do
 J := J + J div I;
 Caption := 'Finished';
 Show ('Total: ' + J.ToString);
end;
```

因为在这个算法里面有一个错误(在我们可以改动的变量值当中，包含了 0，所以会发生除法除以 0 的错误)，程序会被中断，但并不会重设窗体的标题。这种情形就需要 try-finally 区块来解决了：

```pascal
procedure TForm1.BtnTryFinallyClick(Sender: TObject);
var
 I, J: Integer;
begin
 Caption := 'Calculating';
 J := 0;
 try
 // Long (and wrong) computation...
 for I := 1000 downto 0 do
 J := J + J div I;
 Show ('Total: ' + J.ToString);
 finally
 Caption := 'Finished';
 end;
end;
```

当这个函式被执行的时候，它永远都会记得把状态给重设，不管是否有发生任何一种例外。这个版本的函式唯一的缺点，应该就是它没有真的处理例外事件吧。



### Finally 跟例外

够奇怪的吧，在 Object Pascal 里面，try 区块后面只能跟着 except 或者 finally区块，但却不能两者一起出现。假设我们想同时处理两种区块，通常只能把两种 try 区块互相包含着使用，在里面那层 try 区块里面使用 finally，而在外层的 try 区块使用 except，或者反过来用，要视当时的情况而定。以下是ExceptFinally 范例中的第三个按钮的事件处理程序:

```pascal
procedure TForm1.BtnTryTryClick(Sender: TObject);
var
 I, J: Integer;
begin
 Caption := 'Calculating';
J := 0;
 try
 try
 // Long (and wrong) computation...
 for I := 1000 downto 0 do
 J := J + J div I;
 Show ('Total: ' + J.ToString);
 except
 on E: EDivByZero do
 begin
 // Re-raise the exception with a new message
 raise Exception.Create ('Error in Algorithm');
 end;
 end;
 finally
 Caption := 'Finished';
 end;
end;
```



### 透过 Finally 区块还原光标

Try-finally 区块很常用的一个情形，是用来处理资源的配置跟释放。另一种案例则是我们在特定作业完成后，需要把临时的设定重设，就算在这个案例中触发了例外事件，也要完成重设。

重置临时设定最容易理解的案例，就是把漏斗光标重设回箭头光标，通常我们会在可能执行很久的源码中，先把光标改成漏斗光标，等到该段程序执行完成再改回箭头光标。就算这源码再简单，也还是有发生例外事件的可能，所以我们一定都要使用 try-finally 区块把这源码包起来。

在范例 RestoreCursor 中(这是个 VCL 应用程序，要用 FireMonkey 管理光标有点复杂…)，在以下的源码里面，我把目前的光标状态先储存下来，暂时把光标改成漏斗光标，然后在执行结束时再改回执行前的光标:

```pascal
Var CurrCur := Screen.Cursor;
Screen.Cursor := crHourGlass;
try
 // 某些执行会花比较久的源码
 Sleep(5000);
finally
 Screen.Cursor := CurrCur;
end;
```



### 透过受管理的纪录还原光标

要保护已配置的资源，或者还原临时暂存的设定，如果不想明确的使用try-finally 区块，我们也可以使用受管理的纪录，因为编译器会自动在编译完成的执行码当中，为受管理的纪录加入 finally 区块。所以我们可以用这个效应，少写一些源码也能达到保护已配置的资源，或者还原暂存设定，但我们就得在定义这些记录的时候多写一些源码。


以下是一个受管理的纪录，它跟前一段的范例程序有一样的效果，会把目前的光标在 Initialize 方法中储存下来，然后在 Finalize 方法中还原:

```pascal
type
 THourCursor = record
 private
 FCurrCur: TCursor;
 public
 class operator Initialize (out ADest: THourCursor);
 class operator Finalize (var ADest: THourCursor);
 end;
class operator THourCursor.Initialize (out ADest: THourCursor);
begin
 ADest.FCurrCur := Scree.Cursor;
 Screen.Cursor := crHourGlass;
end;
class operator THourCursor.Finalize (var ADest: THourCursor);
begin
 Screen.Cursor := ADest.FCurrCur;
end;
```

我们定义了这个受管理的纪录后，就可以把前段程序写成这样:

```pascal
 var HC: THourglassCursor;
 // 某些执行会花比较久的源码
 Sleep(5000);
```

关于以受管理的纪录进行资源保护，您可以找到更多延伸的范例，是由 Erik van Bilsen 所发的部落格文章: https://blog.grijjy.com/2020/08/03/automate-restorable-operations-with-custom-managed-record/。这是一系列关于受管理的纪录有深入探讨的文章。


## 真实世界的例外


例外对于在大范围(也就是说并不是只针对单一程序，而是整个大架构的一部分)的程序设计中进行错误回报以及错误处理，是个很棒的机制。通常我们不应该完全依赖例外机制，而不仔细的进行区域错误条件检查(当然有些开发人员是喜欢这样用的)。举例来说，如果我们不确定特定档名是否可以使用，我们就应该在开启档案之前先检查一下档案是否存在，而不要依赖例外发生时才透过例外处理机制来处理档案不存在的情形。然而，如果在写档之前检查是否有足够的磁盘空间，就不是那么实用了，因为磁盘空间不足的情形并不常发生。

所以我们应该在程序撰写的时候，先自行处理常发生的问题，然后把不常发生的问题留给例外处理机制。当然常发生与不常发生之间的界线很模糊，端赖程序人员的判断，因此不同程序人员也会有不同的方法来加以界定。我们应该持续的在不同的类别跟模块之中使用例外处理，让例外事件可以在不同的类别跟模块之间互相传递。回传错误码的作法相当乏味，且使用例外更容易比对错误情形。在组件或函式库的类别中触发例外，比在事件处理程序里面触发更为常见。

在日常的源码中越来越常见，且格外重要的是使用 finally 区块来预先保护程序的资源免于因例外发生而失控。我们应该永远在使用外部资源的源码当中使用 finally 来避免万一例外发生而导致资源发生内存泄漏。我们每当需要对档案做启闭、对网络进行联机、断线，在单一区块中建立、释放某些资源时，我们就应该派 finally 区块上场了。最后，finally 区块让我们在即使万一发生例外事件时，也能够使程序保持稳定，这样用户就可以继续使用，或者还能依序(万一有很明确的问题发生时)的关闭应用程序。


## 全局例外处理

如果例外是由一个事件处理程序所触发，停止了标准的执行流程，此时如果没有找到对应的例外处理程序，整个程序也会停止运作吗? 在文本模式应用程序或者一些特别用途的程序结构里的确会这样。但大多数的窗口应用程序(包含使用 VCL 或 FireMonkey 架构的窗口应用程序)则有全局的讯息处理循环，会把整个源码包在一个 try-except 区块里面，所以如果有未被处理的例外被触发，就会在这里被捕捉到。

请记得，如果在程序启动的源码中，在讯息循环生效之前就已经有例外被触发了，这个例外事件就不会被程序或者任何函式接收、处理，程序就会直接因为发生错误而直接停止了。这个情况可以由我们自己在主程序里面加入 try-except 区块来解决一部分的情境。即使是使用这个等级的保护，在主程序执行前，还没有进入自定的 try-except 区块之前，函式库的初始化源码就会被触发，所以在还没进入例外控制之前，仍然有可能发生未被掌控的例外。

一般来说，执行中触发例外时要看函式库怎么处理它，在这里我们仍然可以写一个全局的异常处理程序来处理，或者显示例外事件的讯息。尽管细节处理规则可能稍有不同，但这个大原则在 VCL 跟 FireMonkey 则是都一样的。在前一个范例中，您会在例外事件发生时看到一个简单的错误讯息显示。如果您想要改变这个规则，可以为全局的 Application 对象建立 OnException事件的处理程序。虽然这个动作的归属更偏向视觉函式库以及应用程序的事件处理，它也还是跟例外处理相关，所以我们在这个章节也加以介绍。

我已经把前一个例子做成范例程序，名为 ErrorLog，现在我们再来它的主窗体上面加一个新的方法:

```pascal
public
 procedure LogException (Sender: TObject; E: Exception);
在 OnCreate 事件处理程序中，我加入了下面这个源码，把这个方法跟全局
的 OnException 事件做了绑定，在这里面我们就可以来实作全局的异常处理
程序了:
procedure TForm1.FormCreate(Sender: TObject);
begin
 Application.OnException := LogException;
end;
procedure TForm1.LogException(Sender: TObject; E: Exception);
begin
 Show('Exception ' + E.Message);
end;
```

!!! note "笔记"
    我们会在下一章里面介绍怎么把方法的指标指派给一个事件(就像我们刚在前一个程序片段刚介绍过的)。

透过新的方法来处理全局例外事件之后，这个程序会在未被处理的例外发生时，直接显示错误讯息，不会把程序停止下来了。


## 例外与建构函式

在例外这个主题上还有一个明显比较进阶的问题，就是当例外发生在一个对象的建构函式执行时。大多数的 Object Pascal 程序人员都不知道在这个情形下，该对象的解构函式就会被自动呼叫。

这个作法非常重要，我们必须记住，因为这种情形表示解构函式会在对象初始化进行到一半的时候就被呼叫。假设解构函式中本来就已经预备着把内部对象释放掉，也是因为我们一开始已经假设在建构函式中已经把这些内部对象建立好了，此时就可能在解构函式中试图释放还没有建立的这些内部对象，这种情形着实是很危险的(这表示可能会在第一个例外事件还没被处理好之前，就又诱发了第二个例外事件)。

这也同时指出了在建立对象的时候，最好使用 try-finally 区块来确保安全，因为 try-finally 可以自动被编译器加以保护。所以如果建构函式执行中发生了例外，就可以不用释放该对象了。这是为什么标准的 Object Pascal 程序模式会用以下这种写法来保护一个对象：

```pascal
AnObject := AClass.Create;
try
 // use the object...
finally
 AnObject.Free;
end;
```

!!! note "笔记"
    类似的情况也会发生在 TObject 类别的两个方法: AfterDestruction 跟BeforeConstruction 上面。这两个方法是为了与 C++兼容而提供的虚构建构函式与虚构解构函式(但在 Object Pascal 当中倒是很少被使用到)。请记得，在 AfterConstruction 方法中触发例外的话，BeforeDestruction 方法会被呼叫(然后正常的解构函式也会被呼叫)。

假设我们已经适当的在解构函式中适时的释放对象时显示了错误。我们可以更深入的用实例来说明可能的问题，包含如何处理，以及如何修复。假设我们有一个类别，内部包含一个字符串列表，而我们写这以下的程序来建立与释放这个类别(以下是 ConstructorExcept 范例项目的一部分):

```pascal
type
TObjectWithList = class
 private
 FStringList: TStringList;
 public
 constructor Create (Value: Integer);
 destructor Destroy; override;
 end;
constructor TObjectWithList.Create(Value: Integer);
begin
 if Value < 0 then
 raise Exception.Create('Negative value not allowed');
 FStringList := TStringList.Create;
 FStringList.Add('one');
end;
destructor TObjectWithList.Destroy;
begin
 FStringList.Clear;
 FStringList.Free;
 inherited;
end;
```

乍看之下，这两个源码没有什么不同。建构函式会配置子对象，而解构函式则会把它适当的释放掉。呼叫的源码则是写成如果在建构函式之后触发了例外事件，就会呼叫 Free 方法，但如果例外在建构函式中并没有发生:

```pascal
var
 Obj: TObjectWithList;
begin
 Obj := TObjectWithList.Create (-10);
 try
 // Do something
 finally
 Show ('Freeing object');
 Obj.Free;
 end;
 ```

这样行吗?当然不行，当建构函式在建立字符串列表之前就发生例外的话，系统就会立刻呼叫解构函式，这时就会试着释放还没建立的字符串列表对象，而引发第二个存取错误，或是类似的错误了。

为什么会这样?如果我们把建构函式的顺序颠倒(先建立字符串列表，然后触发例外事件)，则一切都不会有问题，因为解构函式需要把该字符串列表对象给是放掉。但这不算真的解决，它只是一个暂时的解法。我们真的该做的，是先考虑怎么保护源码，让解构函式在执行时不要假设建构函式已经完整执行过。以下可以作为一个简单的范例：

```pascal
destructor TObjectWithList.Destroy;
begin
 if Assigned (FStringList) then
 begin
 FStringList.Clear;
 FStringList.Free;
 end;
 inherited;
end;
``` 

## 例外的进阶功能

!!! warning "提示"
    这一节也是本书当中您可能在第一次读到的时候想要直接跳过的，因为这一节可能有点太过复杂。除非您已经对 Object Pascal 很有经验，不然我也会建议您直接跳到下一章，以后再回来看这一节。

在本章的最后一部分，我想介绍关于例外处理的一些进阶主题。我要来介绍巢 状 例 外 (RaiseOuterException) 跟 在 类 别 中 拦 截 例 外 的 概 念(RaisingException)。这些功能并不是早期版本 Delphi 所兼容的，而新版的Delphi 也因此多了很强大的功能。


### 巢状例外与内部例外机制

如果我们触发了例外事件，而没有例外事件处理程序来处理，会怎么样?传统的答案是新的例外事件会取代已经存在的例外事件，这也是为何一般的作法至少都要把错误讯息结合，然后用类似下面范例的写法(没有实际的处理，只显示例外相关的讯息):

```pascal
procedure TFormExceptions.ClassicReraise;
begin
 try
 // Do something...
 raise Exception.Create('Hello');
 except on E: Exception do
 // Try some fix...
 raise Exception.Create('Another: ' + E.Message);
 end;
end;
```

上面的源码是 AdvancedExcept 范例项目的一部分。当呼叫方法并处理例外时，我们会看到单一一个例外事件，其讯息是组合过的：

```pascal
procedure TFormExceptions.btnTraditionalClick( Sender: TObject);
begin
 try
 ClassicReraise;
 except
 on E: Exception do
 Show ('Message: ' + E.Message);
 end;
end;
```

相当直觉的输出就是:

`Message: Another: Hello`

现在在 Object Pascal 里面，已经全系统都支持巢状例外了。透过异常处理程序我们可以建立、触发一个新的例外，并保留现存的例外对象，并把新旧两个例外结合起来。要达到这个要求，Exception 类别中的 InnerException 属性，它会参照前一个例外，而 BaseException 属性则让我们可以存取前一个例外事件，因此例外事件得以透过递归方式存在。以下是 Exception 类别相关，用来管理巢状例外的元素：

```pascal
type
 Exception = class(TObject)
 private
 FInnerException: Exception;
 FAcquireInnerException: Boolean;
 protected
 procedure SetInnerException;
 public
 function GetBaseException: Exception; virtual;
 property BaseException: Exception read GetBaseException;
 property InnerException: Exception read FInnerException;
 class procedure RaiseOuterException(E: Exception); static;
 class procedure ThrowOuterException(E: Exception); static;
end;
```

!!! note "笔记"
    静态类别方法是类别方法中一个特别的形式。这些跟语言相关的功能我们会在第 12 章里面介绍。

从使用者的观点来透视一下，在保留已发生的例外时，触发一个新的例外，我们应该呼叫 RaiseOuterExcept 这个类别方法(或者使用 C++导向命名的星同方法 ThrowOuterException)。当我们正在处理一个类似的例外，我们可以使用这个新的属性来存取更多的信息。请注意我们在异常处理程序中呼叫RaiseOuterException，只能像原始码的里面注明的文件所述：

> 使用这个函式从一个例外事件处理程序来触发一个例外实体，您可以获得运作中的例外，并把新的例外事件与之串连、保留着原有例外事件的内容。这会使得 FInnerException 字段被设定为正在运作的例外实体。
> 您应该只从要处理这个新的例外事件的 except 区块来呼叫这个程序，其他任何情形都不该呼叫它。

我们可以从AdvancedExcept范例项目来看一下实际的范例。在这个范例中，我加入了一个方法，会用新的方法触发一个巢状例外(跟前面的范例中的

```pascal
ClassicReraise 方法做比较):
procedure TFormExceptions.MethodWithNestedException;
begin
 try
 raise Exception.Create ('Hello');
 except
 Exception.RaiseOuterException ( Exception.Create ('Another'));
 end;
end;
```

现在在异常处理程序中，我们就可以同时存取两个例外对象了(也可以看到呼叫新的 ToString 方法):

```pascal
 try
 MethodWithNestedException;
 except
 on E: Exception do
 begin
 Show ('Message: ' + E.Message);
 Show ('ToString: ' + E.ToString);
 if Assigned (E.BaseException) then
 Show ('BaseException Message: ' + E.BaseException.Message);
 if Assigned (E.InnerException) then Show ('InnerException Message: ' +
 E.InnerException.Message);
 end;
 end;
```

这个程序的输出结果如下:

```
Message: Another
ToString: Another
Hello
BaseException Message: Hello
InnerException Message: Hello
```

有两个相关的元素值得留意。第一个是在单一巢状例外的案例中，例外对象的 BaseException 属性与 InnerException 属性，这两个属性都是指向同一个例外对象，也就是原来的例外对象。第二个则是新例外对象的讯息只包含了实际的讯息。而呼叫了 ToString 之后，我们就得到了所有被包含在巢状例外事件的对象的所有讯息组合在一起的字符串，每个讯息之间会以 sLineBreak这个符号做区隔(您可以看一下 Exception.ToString 这个方法的源码)。使在这个例子里面使用换行符号作分隔，会让产生的结果看起来很奇怪，但我们是可以用我们想显示的方法加以重新组合的，只要把换行符号换成我们想要的符号，或者把回传的字符串直接指派给字符串列表的 Text 属性即可。

我们接着来看个更深入的范例，看看当两个巢状例外被触发时会怎样？以下是新的方法：

```pascal
procedure TFormExceptions.MethodWithTwoNestedExceptions;
begin
 try
 raise Exception.Create ('Hello');
 except
 begin
 try
 Exception.RaiseOuterException ( Exception.Create ('Another'));
 except
 Exception.RaiseOuterException ( Exception.Create ('A third'));
 end;
 end;
 end;
end;
```

这会呼叫一个方法，和前面我们看过的相同，并产生以下的输出:

```
 Message: A third
 ToString: A third
 Another
 Hello
 BaseException Message: Hello
 InnerException Message: Another
```

此时 BaseException 属性跟 InnerException 属性就会指向不同的对象，而ToString 的输出值就会变成三行了。

### 拦截例外

另一个随着新版本被加到例外处理系统的进阶功能是这个方法:

`procedure RaisingException(P: PExceptionRecord); virtual;`

根据原始码里面的说明文件所述:

> 这个虚拟函式会在该例外将要被触发之前被呼叫。在外部例外的案例中，这个函式则会在例外对象建立完成，且触发的条件已经在处理时，尽快被呼叫。

在 Exception 类别中这个函式的实作会管理内部例外(透过呼叫内部的SetInnerException)，这或许可以解释为何它会放在首位加以介绍，跟内部例外机制放在一起。

现在，我们已经可以在任何情形下透过使用这个功能来获益。透过覆写这个方法，事实上，我们在建立对象之后会有单一的函式被呼叫，不论是否透过建构函式来建立这个例外对象。换句话说，我们可以避免为我们的例外类别定义自定的建构函式，让使用者呼叫基础例外类别的任一个建构函式，然后再套用我们自定的规则。例如我们可以把特定类别的任何一个例外做记录(或者是例外的子类别)。

!!! note "笔记"
    除了建构函式之外，透过初始化源码的另一个用意，是覆写 TObject 的虚拟方法 AfterConstruction.


以下是一个自定的例外类别(在 AdvancedExcept 范例项目中)，覆写了

```pascal
RaisingException 方法：
type
ECustomException = class (Exception)
 protected
 procedure RaisingException( P: PExceptionRecord); override;
 end;
procedure ECustomException.RaisingException(P: PExceptionRecord);
begin
 // Log exception information
 FormExceptions.Show('Exception Addr: ' + IntToHex (
 Integer(P.ExceptionAddress), 8));
 FormExceptions.show('Exception Mess: ' + Message);
 // Modify the message Message := Message + ' (filtered)';
 // Standard processing
 inherited;
end;
```

这个方法实作的源码所做的，就是记录下关于例外的一些信息，把例外的讯息做修改，然后呼叫例外基础类别的标准处理流程(需要巢状例外机制才能运作喔)。这个方法会在例外对象被建立之后、触发之前被呼叫。这一点也值得留意，因为由 Show 函式所建立的讯息通常在例外被侦错程序所捕捉之前就已经建立好了。相似的，如果我们在 RaisingException 方法里面放一个断点，侦错程序也会在捕捉到例外事件之前就停在该断点上面。

巢状例外跟这个拦截机制在应用程序的源码里面并不常用到，因为这个功能对于函式库跟组件的开发人员比较有用。