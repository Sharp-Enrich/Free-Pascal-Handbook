如果 TObject 类别跟 System 单元可以被认为是 Object Pascal 的结构化部分，其他编译器本身需要用来建置任何应用程序的功能，其他在执行时期函数库的功能，都会被认为是核心系统的选项延伸。

RTL 当中有许多的系统函数，包含最常用的标准动作，以及有些部分可以回溯到 Turbo Pascal 的时代，在前 Object Pascal 编程语言的时期。在 RTL的许多单元文件都是函数跟子程序，包含核心的功能(SysUtils)，数学函数(Math)，字符串处理(StringUtils)，日期时间处理(DateUtils)等等。

在本书里，我并没有想要深入到 RTL 的传统部分，而是要专心介绍核心类别(这些是 Object Pascal(VCL 跟 FireMonkey)用于视觉组件的基础)，以及其他子系统。例如 TComponent 类别，定义了”以组件为基础”的概念架构。它也是内存管理跟其他基础功能的基石。TPersistent 类别则是组件串流化呈现的关键。

我们还会再看其他许多的类别，因为 RTL 涵盖的范围很大，包含了文件系统、核心线程的支持、平行程序函数库、字符串建立、许多不同型别的集合，以及容器类别、核心图形几何结构(像是点跟长方形)、核心数学结构(像是向量跟矩阵)，还有许许多多的功能。

因为本书的重点是 Object Pascal 编程语言，并不是函数库的手册，我们在此只会选择几个类别，选择的原因是因为它们的角色极为关键，或者是因为该类别是这几年才发表，而大多数的开发人员都没有留意到它们。


## Classes 单元

Object Pascal RTL 类别函数库的基础(也是可视化函数库的基础)可以说就是 System.Classes。这个单元包含了许多最常用到的类别的集合，不包含特殊用途的类别。当中重要的类别值得一看，我们接下来就深入分析当中最重要的一些类别吧。

### 在 Classes 单元中的类别

以下是一个简单的列表(我们大致上列出了该单元当中定义类别的一半):

+ TList 是一个核心的指标列表，它通常可以改编成未确立型别的列表，通常建议改用 TList<T>，详见第 14 章。
+ TInterfaceList 是一个线程安全(thread-safe)的接口列表，它实作出IInterfaceList，值得我们细细了解(在此暂不介绍)。
+ TBits 是一个非常简单的类别，用来操作在数据里面的独立位。它比较高阶，执行位操作时可以透过位移(shift)或是二元运算符号 or 跟 and。
+ TPersisitent 是一个很核心的类别(TComponent 的基础类别)，我们在下一节里面来介绍。
+ TCollectionItem 跟 TCollection 是用来定义集合的属性，这个属性会包含一个内容的数组。对开发组件的人员来说(当间接的使用组件时也是)，这些是很重要的类别，但对于开发应用程序的人员来说就没有这么重要了。
+ TStrings 是抽象的字符串列表，TStringList 才是实际实作出基础TStrings 的类别，TStringList 类别提供对实际字符串的储存功能。每个项目还可以再跟一个对象连接，这也是使用字符串列表的一个标准作法，透过名称/内容的字符串成对。在本章后面有一小节『使用字符串列表』会介绍更多的信息。
+ TStream 是一个抽象类，用来表现任何种类的连续字节，透过连续的存取，它可以包含许多不同的储存选项(内存、档案、字符串、网络 socket，二进制长数据(BLOB)等等)。在 Classes 单元定义了许多特定的串流类别，包含 THandleStream, TFileStream, TCustomMemoryStream, TMemoryStream, TBytesStream, TStringStream 以及 TResourceStream。其他特定串流则是在不同的 RTL 单元里宣告。我们可以在本章当中的『介绍串流』看到关于串流的介绍。
+ 低阶组件的串流类别，像是 TFiler, TReader, TWriter 以及 TParser，大多是开发组件的人员会使用……但不是只有他们会用到。
+ TThread 类别，定义了线程类别，可以支持跟操作系统无关的多线程应用程序。也有一个为异步操作设计的类别，名为 TBaseAsyncResult。
+ 实作了 observer(检查者)模式的类别(像 Visual live binding 就有使用到)，包含 TObservers, TLinkObservers 以及 TObserverMapping。
+ 为了自定标注而定义的类别，像是 DefaultAttribute, NoDefaultAttribute, StoredAttribute 以及 ObservableMemberAttribute。
+ 基础的 TComponent 类别，它是所有 VCL 跟 FireMonkey 当中可视与不可视组件的基础类别，我们在本章稍后来介绍。
+ 为了支持 action 跟 action 列表的类别(action 是抽象化的”命令”，会由接口元素或者内部程序发起这些命令)，包含 TBasicAction 跟TBasicActionLink。
+ 用来镶嵌非可视化组件的容器类别，TDataModule。
+ 对档案存取跟串流存取动作更高阶的接口，包含 TTextReader 跟TTextWriter ， TBinaryReader 跟 TBinaryWriter ， TStringReader 跟TStringWriter，TStreamReader 跟 TStreamWriter。这些类别也在本章稍后加以介绍。



### TPersistent 类别

TObject 类别有个非常重要的子类别，它是整个函数库的基础之一，名为TPersistent。如果我们仔细观察这个类别的方法，它的重要性将会让我们非常惊讶…因为这个类别所做的事非常少。TPersistent 的关键元素之一，是它定义了一个特别的编译器选项{M+}，它的角色是启用 published 关键词，我们在第十章里面介绍过了。

Published 关键词是属性串流化的一个基础角色，这一点也从类别的名字就能看得出来了。一开始，只有从 TPersistent 类别衍生出来的类别可以把数据字段当成发布区的属性。RTTI 的延伸在较新版的 Object Pascal 中可以允许不同的模型，VCL 跟 FMX 函数库的组件串流功能还是保留了基于published 关键词的角色以及{$M+}编译器选项。

!!! note "笔记"
    使用当代的编译器，如果我们在一个类别里加入了 published 关键词，而这个类别不是从 TPersistent 衍生而来，也没有设定{M+}这个编译器选项，系统会先加入适当的支持，然后也提出警告讯息。

TPersistent 在整个架构中的角色到底有什么特别的?首先，它是 TComponent类别的基础类别，我们会在下一节里介绍 TComponent。其次，它被当成属性数据型别的基础类别，所以这些属性跟它们的内部结构才得以被适当的串流化。实例就是字符串列表、位图、字型跟其他对象的表示。

跟 TPersistent 最相关的功能是它启用了 published 关键词来处理子类别的publishec 区段，不过这个类别里面还有一些有趣的方法值得我们花时间了解一下。首先，是 Assign 方法，这个方法可以用来把一个对象的所有内容复制到另一个对象实体去(是真的把数据完全复制，不是只把参考复制过去喔)。每个有使用到这个功能的persistent类别都需要自行实作对应的源码(编程语言或编译器没有聪明到自己会判定每个字段，并加以复制喔)。其次是相反的功能，AssignTo，这个方法是被保护的。这两个方法跟其他在此一类别中的方法大都都是组件开发人员会使用到，应用程序开发人员不太有机会用到。

### TComponent 类别

TComponent 类别是组件函数库的基石，它通常会被 Object Pascal 编译器直接拿来使用。组件的概念基本上就是在类别中具备一些额外的设计时间的规则、特定的串流化能力(所以在设计时间的设定可以被执行中的应用程序储存、重载)，我们已经在第十章里面介绍过 PME(Property-Method-Event，属性-方法-事件)的模式。

类别中定义了一些标准的规则跟功能，并以对象所有权的概念、跨组件的通知等概念为基础，把这些规则跟功能纳入了类别的内存模型当中。我们没有打算对所有的属性跟方法作完整的分析，当然 TComponent 类别当中的一些关键功能是值得我们重点关切，因为它在 RTL 是核心中的核心。

另一个 TComponent 类别的核心功能则是它提供了一个虚拟的 Create 建构函数，提供了从类别参考建立对象的能力，同时能够调用该类别的特定建构函数源码。我们在第 12 章里面对此进行过介绍，但这是 Object Pascal 比较奇特的功能，值得我们了解一下。

**组织所有权**

所有权的机制是 TComponent 类别的一个关键元素。如果一个组件在届例的时候就已经指派了拥有它的组件(会以参数传递到它的虚拟建构函数)，这个身为所有者的组件就要负责摧毁(或说释放)其拥有的所有组件。简单的说，每一个组件都有一个参考指向它的所有者(Owner 这个属性)，同时也有一个组件列表指向它所拥有的组件(Components 数组属性)以及所拥有的组件数目(ComponentCount 这个属性)。

在预设的情形下，当我们把一个组件放在设计接口中(窗体、Frame 或是 Data Module)，就等于是为这个组件指派了所有者。当我们用源码建立组件时，该组件的所有者可以随我们指派，指派 nil 也可以(这时我们就要自己负责把组件从内存当中释放掉了)。

我们可以透过 Components 跟 ComponetCount 属性来列出一个组件(在以下这个例子里，就是 aComp)所拥有的组件，程序片段如下:

```pascal
var
 I: Integer;
begin
 for I := 0 to AComp.ComponentCount – 1 do
 AComp.Components[I].DoSomething;
```

或使用原生的列举功能，改成这么写:

```pascal
var
 ChildComp: TComponent;
begin
 for ChildComp in AComp do
 ChildComp.DoSomething;
```

当组件被释放时，会把该组件从该组件所有者的对象列表中删除，并把自己所拥有的所有子组件释放掉。这个机制对于 Object Pascal 的内存管理非常关键: 因为没有回收清理机制，所有权机制可以解决掉大多数的内存管理议题，我们在第 13 章里面已经介绍了其中的部分。

一如我们介绍过的，通常在一个窗体或 Data Module 里所有的对象都是以窗体或 Data Module 作为其所有者。只要我们释放了该窗体或者该 Data Module，它们所拥有的组件也会一起被释放。这就是组件从串流中建立时所发生的。


**组织属性**

除了核心的所有权机制(这个机制包含了通知与我们在这里没有介绍的功能)，任何组件都有两个位于发布区的属性：

+ Name 是用来储存组件名称的字符串。这个属性是用来动态搜寻特定组件(调用组件所有者的 FindComponent 方法)并把有参照到这个组件的窗体数据字段与之链接。所有被同一个所有者所拥有的组件的名称不可以相同，但这些组件的名称可以是空白字符串。这儿有两个简单的规则:为组件设定适当的名称，让我们的源码更具可读性。另外，则是不要在执行时期变更组件的名称(除非我们完全了解到当中会有什么潜在的效果)。
+ Tag 是一个原生整数数值(过去是使用整数型别)，在函数库当中没有使用到，但我们可以透过它来把组件跟额外的信息进行链接。这个型别是在储存大小上跟指针、对象参考兼容的，所以我们可以把指针跟对象参考除存在一个组件的 Tag 属性之内。(译者: 在撰写这么久的面向对象程序的过程中，Tag 是 Object Pascal 发前人之所未见，这个属性后来也在 Objective-C 里面被广泛使用，我在 Objective-C 的程序中，最常用的动态接口程序搜寻跟处理，就是 viewWithTag，但 Objective-C 里面后来还陆续扩充了这个属性，提供了字符串型别的 Tag)

**组件串流**

串流机制同时在 FireMonkey 跟 VCL 平台上都被用来建立 FMX 或 DFM 档案，这两个档案就是围绕着 TComponent 类别为基础的。Delphi 串流机制会把组件跟子组件位于发布区内的属性跟事件都储存下来。储存的结果就是我们看到的 DFM 跟 FMX 档案，也是当我们把一个组件复制到文本编辑器所看到的结果。

在运行时间，有些方法可以取得相同的信息，包含 TStream 类别的WriteComponent, ReadComponent, ReadComponentRes, WriteComponentRes，以及用来处理组件串流功能的 TReader 与 TWriter 的 ReadRootComponent与 WriteRootComponent 方法。这些操作通常是用来处理窗体串流化时二进制数据的转换: 我们可以透过全局程序 ObjectResourceToText 把二进制窗体格式转换为文字格式，也可以透过另一个程序 ObjectTextToResource 来把文字窗体格式转换为二进制。

其中一个关键因素是，串流并不是组件在发布区当中所有的属性的完整集合。串流内容会包含：

+ 该组件在发布区当中内容并非默认值的所有属性(换句话说，内容是默认值的属性就不会被储存了，这样可以节省储存空间)
+ 该组件在发布区当中有被标示为 stored 的属性(不管是不是默认值，该属性都会被储存)。Stored 被设定为 False 的属性则不会被储存。
+ 还有另一个机制可以加入一个额外的『假』属性，用以将之进行串流以及把他们读回来。所以我们可以在串流当中储存跟发布区属性不同的内容。当一个属性被从串流档案重建回来的时候，会发生以下的流程：
+ 组件的虚拟建构函数 Create 会被调用(执行适当的初始化源码)
+ 会从串流中把属性跟事件加载(加载属性时，会重新把方法的名称对应到该方法在内存当中实际的名称)
+ Loaded 虚拟方法会在加载完成时被调用(且组件可以进行额外的自定程序，此时属性的内容已经完成加载了)。


## 现代档案存取

从前代的 Pascal 语言借来，Object Pascal 仍然有此一关键词，且在核心的语言机制当中也还是可以处理档案。当 Object Pascal 发表的时候，本来已经把档案存取的功能取消，而我也不打算在本书里面介绍这个功能的。反之，我本来要在下一节里面介绍一些现代处理档案的技术的，这些技术都在IOUtils 单元中，包含串流类别、reader 跟 writer 类别。



### 输入/输出工具单元

System.IOUtils 单元是最近几版才被加入到执行时期函数库的。当中定义了三个包含大多数类别方法的记录: TDirectory, TPath, 以及 TFile。

TDirectory 很显然是用来浏览文件夹、搜寻当中的档案以及子文件夹，TFile看起来跟 TPath 没有很明显的差异。首先，TPath 是用来处理文件名跟文件夹名称的，当中有可以用来分离出磁盘代号、不包含路径的文件名、扩展名，也用来操纵 UNC 路径。而 TFile 记录则让我们检查档案的时间戳跟文件属性，也可以用来操纵档案，包含写入档案跟复制档案。

我们来看些例子吧，IOFilesInFolder 范例项目会解析出特定目录里面的所有子目录，它也会抓出该目录里面所有特定扩展名的档案。

**解析子目录**

这个程序会把目录里面的文件夹名称找出来，填进 ListBox 的内容，使用的是 TDirectory 记录里面的 GetDirectories 方法，把 TSearchOption.soAllDirectories 当成参数传入。回传的结果会是一个字符串数组，我们可以用列举功能来处理它：

```pascal
procedure TFormIoFiles.BtnSubfoldersClick(Sender: TObject);
var
 PathList: TStringDynArray;
 StrPath: string;
begin
 if TDirectory.Exists (EdBaseFolder.Text) then
  begin
 ListBox1.Items.Clear;
 PathList := TDirectory.GetDirectories(EdBaseFolder.Text,
TSearchOption.soAllDirectories, nil);
 for StrPath in PathList do
 ListBox1.Items.Add (StrPath);
 end;
end;
```

**搜寻档案**

这个程序的第二个按钮，让我们可以取得所有子目录当中的所有档案，透过调用 GetFiles，以及特定的屏蔽，我们可以扫描每一个目录。我们可以把一个匿名方法，其型别是 TFilterPredicate，当成参数传给 GetFiles 的多载版本，这样就可以有更复杂的过滤条件了。

以下的范例使用的是比较简单的屏蔽型过滤方式，会建立内部的字符串列表。这个字符串列表里面的元素接着会先被去除掉路径，指留下文件名，然后复制到用户接口。

当我们调用 GetDirectories 方法的时候，回传的只有子目录，不会包含当前的目录。这也是为什么程序要先搜寻当前目录，然后再对子目录逐一搜寻的原因了:


```pascal
procedure TFormIoFiles.BtnPasFilesClick(Sender: TObject);
var
 PathList, FilesList: TStringDynArray;
 StrPath, StrFile: string;
begin
 if TDirectory.Exists (EdBaseFolder.Text) then
 begin
 // Clean up
 ListBox1.Items.Clear;
 // Search in the given folder
 FilesList := TDirectory.GetFiles (EdBaseFolder.Text, '*.pas');
 for StrFile in FilesList do
 SFilesList.Add(StrFile);
 // Search in all subfolders
 PathList := TDirectory.GetDirectories(EdBaseFolder.Text,
  TSearchOption.soAllDirectories, nil);
 for StrPath in PathList do
 begin
 FilesList := TDirectory.GetFiles (StrPath, '*.pas');
 for StrFile in FilesList do
SFilesList.Add(StrFile);
 end;
 // Now copy the file names only (no path) to a listbox
 for StrFile in SFilesList do
 ListBox1.Items.Add (TPath.GetFileName(StrFile));
 end;
end;
```


在最后几行里，TPath 的 GetFileName 函数会被用来从完整路径中解析出文件名。TPath 记录当中包含了一些有趣的方法，包含 GetTempFileName, GetRandomFileName，一个可以用来合并路径的方法，以及一些用来检查是否包含不合法字符的方法，还有更多其他的功能。


### 介绍串流

如果 IOUtils 单元是用来寻找档案跟操作档案，当我们想要读写档案的时候(或者任何类似依序存取的数据结构)，我们就可以用 TStream 类别跟它的几个衍生类别。TStream 抽象类中只有几个简单的属性(Size 跟 Position)，以及一些所有抽象类共享的基本接口，包含主要的 Read 跟 Write 方法。每当我们读写一些位数据时，目前的位置就会依照读写的数量移动。对大多数的串流来说，我们可以设定让目前位置向前移动，但也有一些串流类别是只能单向移动的。

**常见的串流类别**

一如稍早所提到的，Classes 单元定义了几个具体的串流类别，包含以下这一些:

+ THandleStream 定义了磁盘档案串流，可以透过 Handle 指向一个档案。
+ TFileStream 定义了磁盘档案串流，可以透过档名指向一个档案。
+ TBufferedFileStream 是优化过的磁盘档案串流，当中使用了内存做缓冲区，因此效能也得到改善。这个串流类别在 Delphi 10.1 Berlin 当中开始提供。
+ TMemoryStream 定义了一个在内存当中的数据串流，我们也可以使用指标存取。
+ TBytesStream 表示在内存当中的位串流，我们也可以把它当成像是位数组来存取。
+ TStringStream 跟在内存当中的字符串串流关连。
+ TResourceStream 定义了一个串流，可以读取跟应用程序执行文件链接的资源数据。
+ TPointerStream 透过 TStream 的接口提供了对内存中的数据区块进行存取的功能，只要指定指针的地址与内存区块大小。这个类别是在 Delphi 11 当中新增的。

**使用串流**

建立并使用串流就像建立一个特定型别的变量，以及调用组件的方法来从档案加载内容一样简单。例如，我们要把串流数据加载到 Memo 组件中，我们可以这么写:


```pascal
aStream := TFileStream.Create (FileName, fmOpenRead);
Memo1.Lines.LoadFromStream (aStream);
```

一如从这段源码中可以看出，档案串流的 Create 方法有两个参数:文件名，以及用来指定所需档案存取模式的设定旗标。我们提到过串流支持读与写的动作，但这些相对较为低阶，所以我建议使用下一节里面我们要介绍的reader 跟 writer 类别。

直接使用串流，提供的是全面的作业，像是在上面的源码中加载完整的串流，或者把一个串流复制到另一个:

```pascal
procedure CopyFile (SourceName, TargetName: String);
var
 Stream1, Stream2: TFileStream;
begin
 Stream1 := TFileStream.Create (SourceName, fmOpenRead);
 try
 Stream2 := TFileStream.Create (TargetName,
 fmOpenWrite or fmCreate);
 try
 Stream2.CopyFrom (Stream1, Stream1.Size);
 finally
 Stream2.Free;
 end
 finally
 Stream1.Free;
 end
end;
```

### 使用 Reader 跟 Writer

从串流进行读写的好处，是使用了身为 RTL 一部分的 reader 跟 writer 类别。在 Classes 单元当中一共定义了六个读写的类别:

+ TStringReader跟TStringWriter用来处理内存中的字符串(直接或间接的使用了 TStringBuilder 类别)
+ TStreamReader 跟 TStreamWriter 用来处理一般串流(例如档案串流、内存串流等等)
+ TBinaryReader 跟 TBinaryWriter 用来处理二进制数据。

这些文字 reader 当中的每一个都实作了一些基本的读取技术:

```pascal
function Read: Integer; overload;
function ReadLine: string;
function ReadToEnd: string;
```

这些文字 writer 当中的每一个都有两组多载的处理动作，不包含(Write)刚(WriteLine)，以及每行结尾的符号，以下是第一组:

```pascal
procedure Write(Value: Boolean); overload;
procedure Write(Value: Char); overload;
procedure Write(const Value: TCharArray); overload;
procedure Write(Value: Double); overload;
procedure Write(Value: Integer); overload;
procedure Write(Value: Int64); overload;
procedure Write(Value: TObject); overload;
procedure Write(Value: Single); overload;
procedure Write(const Value: string); overload;
procedure Write(Value: Cardinal); overload;
procedure Write(Value: UInt64); overload;
procedure Write(const Format: string;
 Args: array of const); overload;
procedure Write(Value: TCharArray;
 Index, Count: Integer); overload;
```

**文字Readers跟Writers**

为了写入串流，TStreamWriter 类别透过文件名使用了一个串流，或建立一个串流，一个建立或增加的属性，以及 Unicode 编码作为参数。所以我们可以像我在 ReaderWriter 范例项目里面这么写:

```pascal
var
 Sw: TStreamWriter;
begin
 Sw := TStreamWriter.Create('test.txt',
 False, TEncoding.UTF8);
 try
 Sw.WriteLine ('Hello, world');
 Sw.WriteLine ('Have a nice day');
 Sw.WriteLine (Left);
 finally
 Sw.Free;
end;
```

要从 TStreamReader 读取数据，我们可以再对串流或对档案作一次处理(在这个例子里，我用可以透过 UTF BOM 标注来侦测编码方式):

```pascal
var
 SR: TStreamReader;
begin
 SR := TStreamReader.Create('test.txt', True);
 try
 while not SR.EndOfStream do
 Memo1.Lines.Add (SR.ReadLine);
 finally
 SR.Free;
end;
```

留意到我们是怎么检查 EndOfStream 状态的。跟直接使用文字符串流或者字符串不同，这些类别是便于使用的，且效能很好。

**二进制Reader与Writer**

TBinaryReader 跟 TBinaryWriter 类别是用来管理二进制数据，而非文本文件。这些类别通常是封装起串流(可能是一个档案串流或内存内容串流，内容包含网络、数据库 BLOB 字段)，并覆写了 Read 跟 Write 方法。

我们提供了 BinaryFiles 范例项目作为例子。在这个程序当中的第一个部分会写一些二进制数据到档案中(一个字段的内容与目前的时间)然后把他们读出来，指派属性的内容：

```pascal
procedure TFormBinary.BtnWriteClick(Sender: TObject);
var
 BW: TBinaryWriter;
begin
 BW := TBinaryWriter.Create('test.data', False);
 try
 BW.Write(Left);
 BW.Write(Now);
 Log ('File size: ' + IntToStr (BW.BaseStream.Size));
 finally
 BW.Free;
 end;
end;
procedure TFormBinary.BtnReadClick(Sender: TObject);
var
 BR: TBinaryReader;
 ATime: TDateTime;
begin
 BR := TBinaryReader.Create('test.data');
 try
 Left := BR.ReadInt32;
 Log ('Left read: ' + IntToStr (Left));
 ATime := BR.ReadDouble;
 Log ('Time read: ' + TimeToStr (ATime));
 finally
 BR.Free;
 end;
end;
```

使用这些 reader 跟 writer 类别的关键规则是我们必须依照写入的顺序来读取数据，不然我们就会把数据搞乱了。事实上，只有独立字段的二进制数据被储存，该字段的信息则不会被储存。没有可以阻止我们在档案中插入资料或者 metadata，例如把下一个数据结构的大小写在实际的数据之前，或者把跟该字段相关的 token 写入。


## 建立字符串跟字符串列表

在介绍过档案跟串流之后，我想花一些时间来介绍处理字符串跟字符串列表方法。这是很常用的作法，而且在 RTL 当中有很多强大的功能是提供给字符串跟字符串列表使用的。我只会介绍其中一部分。


### TStringBuilder 类别

我在第六章里面曾经提到过，Object Pascal 跟其他语言不一样，完整了支持字符串直接链接的功能，这个功能的效能非常好。然而 Object Pascal 的 RTL也包含一个特别的类别，可以用来把字符串跟不同型别的数据进行重组，这个类别就是 TStringBuilder。

我们用以下的程序片段作为 TStringBuilder 类别的范例:

```pascal
var
 SBuilder: TStringBuilder;
 Str1: string;
begin
 SBuilder := TStringBuilder.Create;
 SBuilder.Append(12);
 SBuilder.Append('hello');
 Str1 := SBuilder.ToString;
 SBuilder.Free;
end;
```

留意到我们必须建立、释放这个 TStringBuilder 对象，另一个部分则是上面的源码当中，传给 Append 方法的参数中，包含了不同型别的资料。

TStringBuilder 里面有趣的方法还包含了 AppendFormat(在对象内部调用Format函数)，以及AppendLine(在对象内部加入了sLineBreak)。除了Append之外，还有一系列的多载方法，Insert，可以用来在字符串里面插入不同型别的数据，当然还有 Remove(删除部分字符串)跟 Replace(取代字符串)等常用的方法。

!!! note "笔记"
    TStringBuilder 类别有一个非常好的接口，提供了良好的可用性。然而从效能的观点来看，使用标准的字符串链接跟格式化函数可以提供比较好的效能，跟其他编程语言不一样的地方是，Object Pascal 的字符串是可以修改内容的，且在纯粹字符串链接时效能也都比其他语言的效能来的好上许多。

**在StringBuilder中的连续使用方法**

TStringBuilder 类别里面有个很特别的功能，是大多数的方法都会回传当时被参考到的对象。

这种程序的形式开启了连续使用方法的可能性，这意思是调用对象的方法，会回传前者，原本的写法是如此:

```pascal
SBuilder.Append(12);
SBuilder.AppendLine;
SBuilder.Append('hello');
```

我们可以改写成这样:

`SBuilder.Append(12).AppendLine.Append('hello');`

把源码格式化一下，可以变这样:

```pascal
SBuilder.
 Append(12).
 AppendLine.
 Append('hello');
```

我觉得这个语法会比原先的语法来的好，但我觉得这只是在语法上面稍微好一点，但大多数的人会比较偏好原始版本的写法，每一行都确实的把对象名字写出来。不管怎么写，要记得不同的 Append 调用并不会回传新的对象(所以不会有内存泄漏的危险)，但永远都会是用同一个对象来连续调用这些方法。



### 使用字符串列表

字符串列表是在许多视觉组件中很常用的抽象概念，也是被用来处理多行文字的一个方法。有两个类别是用来处理字符串列表的：

+ Strings 是一个抽象类，用来表现所有形式的字符串列表，不管它们是以什么形式进行储存的。这个类别定义了字符串列表的抽象概念。因此，TStrings 的对象只被用来作为组件的属性，这些属性可以用来为组件储存文字数据。

+ TStringList 是 TStrings 的子类别，定义了自行建立储存空间的字符串列表，我们可以用这个类别在程序中定义一个实际用来储存数据的字符串列表。

这两个字符串列表类别也有已经定义好的方法可以用来储存内容到档案，或者从档案读取内容了，分别是 SaveToFile 跟 LoadFromFile(这两个方法都已经完全支持 Unicode 了)。要把整个列表巡回一次，我们可以简单的用 for指令，以列表的索引值为基础进行列举，直接把该字符串列表当成一个数组，或者使用 for-in 列举都可以。



### 执⾏时期函数库是相当巨⼤的

我们在使用 Object Pascal 编译器的时候，其中一大部分都是由 RTL 所提供的功能，当中包含了许多在不同操作系统上面的核心开发功能。如果我们要介绍整个 RTL 的话，随便写就会跟现在这一本一样厚了。

如果我们只考虑到函数库的主要功能，算是 System 这个命名空间，会包含以下的单元(我移除了其中一些不常用到的单元):

+ System.Actions 包含了对 actions 架构的核心支持，它提供了跟用户者指令链接的方法，但是已经从用户接口层次抽象化了。
+ System.Character 是为了支持 Unicode 字符而设的固有型别助手(协助Char 型别)，我们已经在第三章里面介绍过。
+ System.Classes 提供了核心系统类别，也是我们在本章第一部分所介绍的单元。
+ System.Contnrs 提供了旧版，非泛型的容器类别，像是组件列表，Dictionary，队列(Queue)与堆栈(Stack)。我建议，可以的话尽量使用泛型版本的相同类别。
+ System.ConvUtils 提供了不同测量单位之间的转换工具。
+ System.DateUtils 提供了处理日期跟时间数值的函数。
+ System.Devices 提供了与系统装置的接口(像是 GPS, 重力传感器等系统内建装置)。
+ System.Diagnostics 定义了一个记录结构，用以精确测量在测试程序当中所花的时间，我在本书中几乎没有提到过。
+ System.Generics 有两个分开的单元，其中一个是泛型的集合类别，另一个是泛型型别。这些单元我们已经在第 14 章里面介绍过了。
+ System.Hash 提供了系统对定义哈希值的支持。
+ System.ImageList 包含了抽象定义，实作出跟函数库无关，用以管理图片列表，以及把单一图片当成集合的元素。
+ System.IniFiles 定义了一个接口来处理 INI 配置文件案，通常只会在Windows 平台里面找到。
+ System.IOUtils 定义了文件系统存取的记录(档案、文件夹、路径)，我们在本章前面的篇幅介绍过。
+ System.JSON 包含了一些用来处理跟 JavaScript 对象记录方法，或称JSON 的核心类别。
+ System.Math 定义了数学运算的函数包含三角函数跟财务函数。在同一个命名空间的其他单元中也定义了向量跟矩阵的函数。
+ System.Messaging 提供了在不同操作系统上进行讯息处理的共享源码。
+ System.NetEncoding 包含了处理一些常见的因特网编码法的能力，像是 base64, HTML 跟 URL。
+ System.RegularExpressions 定义了对常规表示法 (Regular Expression)的支持。
+ System.Rtti 有一整套的 RTTI 类别，我们在第 16 章里面介绍过。
+ System.StrUtils 提供了核心与传统字符串的处理函数。
+ System.SyncObjs 定义了一些类别用来支持同步与多线程应用程序。
+ System.SysUtils 有系统工具的基本集合，当中包含一些最传统的函数，以兼容于早期的编译器。
+ System.Threading 包含了最新的平行程序函数库所需的接口、记录以及类别。
+ System.Types包含了一些核心的延伸数据型别，像是TPoint, TRectangle以及 TSize 记录，TBitConverter 类别，以及许多在 RTL 里面有使用到的基本数据型别。
+ System.TypInfo 定义了旧版的 RTTI 接口，我们也在第 16 章里面介绍过，但目前已经以 System.Rtti 取代掉它了。
+ System.Variants 跟 System.VarUtils 有用来处理变异型别的函数(我们在第五章里面介绍过这个功能)。
+ System.Zip 提供了一个文件压缩跟解压缩的接口与函数库。

RTL 当中还有许多部分是 System 命名空间的成员，因为每一部分都包含许多单元(通常当中的单元都很多，像是 System.Win 命名空间)包含了 HTTP客户端(System.Net)，以及对物联网的支持(System.Beacon, System.Bluetooth,System.Sensors 以及 System.Tether)。当然当中也有从支持的操作系统当中转译进来的接口跟 API 宣告。

再强调一次，直接使用 RTL 函数、型别、记录、接口，以及记录，可以让我们的程序立刻获得许多福利，这是我们需要去发现的，这样可以让我们得以发挥 Object Pascal 的强大功能。花一些时间，浏览一下系统文件吧，我们可以从中获益更多。