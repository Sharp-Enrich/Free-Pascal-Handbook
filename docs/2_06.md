在前几章里面，我们介绍了 Object Pascal 语言在对象方面的基础:类别、对象、方法、建构函数、继承、延迟绑定、接口，以及更多其他技术。现在我们要更进一步了，深入观察一些更进阶的主题，以及 Object Pascal 里面跟管理类别相关的特定功能。从类别参考到类别助手，这一章会介绍更多在其他 OOP 语言里面没有的功能，或明显不同的实作方式。

焦点会是类别，以及在运行时间操作类别的方法，这个主题我们也会在第16 章介绍到 reflection 跟属性的时候更进一步的探讨。



## 类别⽅法跟类别数据

当我们在 Object Pascal 跟大多数其他 OOP 里面定义一个类别，我们是在为这个类别的对象(或叫实体) 定义它的数据结构，以及我们可以透过这个对象进行的动作。然而也有另一种可能，是要定义在这个类别里面所有对象之间分享的数据，以及任何一个从该类别中建立出的对象可以被调用的方法。

在 Object Pascal 里面需要一个类别方法，我们只要在该方法前面加上 class关键词即可，不管该方法是程序或是函数:

```pascal
type
 TMyClass = class
 class function ClassMeanValue: Integer;
```

假设我们建立了类别 TMyClass 的对象，名为MyObject，我们可以透过对象，也可以透过类别直接调用这个类别方法:

```pascal
var
 MyObject: TMyClass;
begin
 …
I := TMyClass.ClassMeanValue;
J := MyObject.ClassMeanValue
```

从上面的源码，我们可以看出，类别方法可以随时使用，即使该类别的对象还没有被建立出来，也可以直接透过该类别去调用。有些情境下，类别会由类别方法建立，透过不特别写出的方法，我们就不用建立出该类别的对象了。(有时我们会需要宣告建构函数 Create 来处理)

!!! note "笔记"
    通常类别方法的使用，以及只使用类别方法来建立对象是大多数不允许使用全局函数的 OOP 编程语言的常态。Object Pascal 还让我们宣告旧型的全局函数，但经过这几年，系统函数库和开发人员所写的源码已经迈向一致使用类别方法的途径。使用类别方法的优点是类别方法可以逻辑的链接一个类别，该类别为一整组相关的函数扮演了类似命名空间的角色。


### 类别数据

类别资料是可以在所有类型为同一类别中可以互相分享的数据，提供跨全局数据储存，但可以套用指定的类别存取限制。我们要怎么宣告类别数据呢?简单的定义一个区块然后用 class var 关键词来定义它即可:

```pascal
type
 TMyData = class
private
 class var CommonCount: Integer;
public
 class function GetCommon: Integer;
```

class var 关键词会建立一个区块，可以宣告一个或多个类别的数据字段。我们也可以用 var 关键词(这是 var 关键词的新用法)来宣告同一个存取区块中的另一个区段(以下的 private):

```pascal
type
 TMyData = class
private
 class var CommonCount: Integer;
 var MoreObjectData: string;
public
 class function GetCommon: Integer;
```

另外宣告类别数据，我们也可以定义类别属性，我们在后续的章节中会再介绍。

**虚拟类别⽅法跟隐藏的 Self 参数**

当类别方法的概念已经在许多编程语言里面变得普及，Object Pascal 的实作就变得有些独特的地方。首先，类别方法拥有一个隐晦(或者隐藏)的 self参数，就像一般的实体方法。然而，这个隐藏的 self 参数就是指向类别本身的一个参考，而不是类别的实体。

猛一看，类别方法拥有这个隐藏的指向类别自己的 self 参数似乎没有什么用。编译器最终会认得类别方法。然而有一个奇怪的语言功能可以解释这一点: 跟大多数编程语言不同，在 Object Pascal 里，类别方法可以是虚拟的，在衍生类别中，我们可以覆写基础类别的类别方法，就像覆写一般的方法一样。

!!! note "笔记"
    对虚拟类别方法的支持是和虚拟建构函数的支持(我们可以将之视为是一种特殊要求的类别方法)连结的。这两个功能在许多需要编译，支持强类型的 OOP 编程语言中都是不支持的。

### 类别静态方法

类别静态方法已经在 Object Pascal 对平台兼容性的章节中介绍过了，原始的类别方法跟类别静态方法之间的差异，在于类别静态方法并不需要参考到它的类别(也就是没有 self 参数指向类别本身)，而且不能是虚拟方法。

以下的范例源码里面包含了一些可能导致错误的源码，我们已经先把会导致错误的源码批注掉了，这些源码都是 ClassStatic 范例项目中所节录出来的：

```pascal
type
 TBase = class
 private
 FTmp: Integer;
 public
 class procedure One;
 class procedure Two; static;
 ...
 end;
class procedure TBase.One;
begin
 // Error: Instance member FTmp inaccessible here
 // Show (FTmp);
 Show ('One');
 Show (self.ClassName);
end;
 
class procedure TBase.Two;
begin
 Show ('Two');
 // error: Undeclared identifier: 'self'
 // Show (self.ClassName);
 Show (ClassName);
 Two;
end;
```

在以上两个情形下，我们都可以直接调用这些类别方法，或者透过对象来调用也行:

```pascal
TBase.One;
 TBase.Two;
 Base := TBase.Create;
 Base.One;
 Base.Two;
```

有两个有趣的功能，使得 Object Pascal 的类别静态方法变得很有用。第一个是它可以用来定义类别的属性，就像我们在下一节里面会提到的，第二个则是类别静态方法是完全跟 C 语言兼容的。

### 静态类别方法跟 Windows	API	callback

因为没有 self 这个隐藏参数，所以静态类别方法可以被传给操作系统(例如Windows)当做 callback 函数。在实务上，我们可以宣告一个静态类别方法，并加上 stdcall 这个调用转换关键词，就可以把它直接当成 Windows API 的callback，我们把 StaticCallBack 范例项目中的 TimerCallBack 方法做为范例:

```pascal
type
 TFormCallBack = class(TForm)
 ListBox1: TListBox;
 procedure FormCreate(Sender: TObject);
 private
  class var
 nTimerCount: Integer;
 public
 procedure AddToList(const AMessage: String);
 class procedure TimerCallBack (hwnd: THandle;
 uMsg, idEvent, dwTime: Cardinal); static; stdcall;
end;
```

类别的数据会被 callback 函数用来当做计数器。OnCreate 处理程序会调用SetTimer API，把这个类别静态程序的地址传递过去。

```pascal
procedure TFormCallBack.FormCreate(Sender: TObject);
var
 callback: TFNTimerProc;
begin
 nTimerCount := 0;
 callback := TFNTimerProc(@TFormCallBack.TimerCallBack);
 SetTimer(Handle, TIMERID, 1000, callback);
end;
```

!!! note "笔记"
    TFNTimeProc 的参数是一个方法指标，因此我们需要在类别静态方法的名称之前加上一个@符号或者用 Addr 函数传递。因为我们需要取得该方法的地址，而非执行它。

现在，实际的 callback 函数会增加计数器的数值，并且更新窗体，把它当成全局变量(它会被忽略掉，但也会让范例明显的变得复杂)作为对应，把它视为一个类别方法，而不是视为窗体本身:

```pascal
class procedure TFormCallBack.TimerCallBack(
 hwnd: THandle; uMsg, idEvent, dwTime: Cardinal);
begin
 try
 Inc (nTimerCount);
 FormCallBack.ListBox1.Items.Add (
 IntToStr (nTimerCount) + ' at ' + TimeToStr(Now));
 except on E: Exception do
 Application.HandleException(nil);
 end;
end;
```

这里使用的 try-except 区块，是用来避免由 Windows 所送回的任何例外事件。我们必须为 callback 或者 DLL 函数提供后续的程序控管。


### 类别属性

使用类别静态方法的原因之一，是要实作类别属性。什么是类别属性呢?就跟基本属性一样，它是包含有读写机制的符号。而跟基本属性不同的是，它是属于类别的而且必须透过类别数据或者类别静态方法来实作。这个TBase 类别(再提一次，是从 ClassStatic 范例项目节录的)有两个类别方法，我们用两种定义的写法来做示范:

```pascal
type
 TBase = class
 private
 class var
 FMyName: string;
 public
 class function GetMyName: string; static;
 class procedure SetMyName (Value: string); static;
 class property MyName: string read GetMyName write SetMyName;
 class property DirectName: string read fMyName write fMyName;
end;
```

### 具有实体个数计数器的类别

类别数据跟类别方法可以用来储存类别中整体的信息。这一类的信息中，最好的范例就是这个类别的实体目前一共被建立了几个，也就是目前还存在的实体的数目。CountObj 范例项目就建立了这个情境。这个程序并没有好用到吓人，假设我们只先看几个特定的问题跟它的解法。换句话说，我们先透过很简单的类别作范例，让这个类别里面先只储存一个数字:

```pascal
type
 TCountedObj = class (TObject)
 private
 FValue: Integer;
 private class var
 FTotal: Integer;
 FCurrent: Integer;
 public
  constructor Create;
 destructor Destroy; override;
 property Value: Integer read FValue write FValue;
 public
 class function GetTotal: Integer;
 class function GetCurrent: Integer;
end;
```

每当一个对象被建立，这个程序就会在调用基础类别的建构函数之后把上面源码的两个计数器。每次对象被释放，现有的对象数目也就会被减少：

```pascal
constructor TCountedObj.Create (AOwner: TComponent);
begin
 inherited Create;
 Inc (FTotal);
 Inc (FCurrent);
end;
destructor TCountedObj.Destroy;
begin
 Dec (FCurrent);
 inherited Destroy;
end;
```

类别信息可以无需透过任何一个特定对象，直接就能存取。事实上，也可能是因为在该时间内，内存里面并没有对象存在:

```pascal
class function TCountedObj.GetTotal: Integer;
begin
 Result := FTotal;
end;
```

我们可以用以下这段源码把目前的状态显示出来:

```pascal
Label1.Text := TCountedObj.GetCurrent.ToString + '/' +
 TCountedObj.GetTotal.ToString;
```

这段程序会由一个定时器来执行，执行后会更新一个文字卷标，所以不用透过特定对象实体，也不用透过任何手动程序来触发。而是会由范例项目里面的按钮建立并释放这些对象，或者把它们留在内存里面(因为事实上这个范例项目也可能造成一些内存泄漏的状况)。




## 类别建构函数(以及解构函数)

类别建构函数提供了让我们为跟类别相关的数据进行初始化的方法，同时它也扮演了类别初始者的角色，但它实际上并没有建构任何东西。类别的建构函数在建构实体的时候，其实不需要作什么事情:它只用来在类别要被使用之前，先为类别本身进行初始化而已。举例来说，一个类别建构函数可以为该类别的资料设定初始值、加载设定或者为该类别加载支持的档案等等。

在 Object Pascal 里面，类别的建构函数算是单元文件初始区(initialization)源码的替代方案。如果在一个单元文件里面，初始区跟类别的建构函数都存在，类别的建构函数会先执行，然后单元文件的初始区才会被执行到。相对的，我们也可以定义一个类别结构函数，它会在单元文件的终止区(finalization)源码之后被执行。

然而，有个很明显的不同是，当单元文件被编译进某个程序，该单元文件的初始区跟终止区源码一定会被执行到，但类别的建构跟解构函数则要看该类别有没有被使用到，才有被执行到的机会。这表示类别的建构函数是比初始区、终止区程序跟链接程序(linker)更为亲近的。

!!! note "笔记"
    换句话说，透过类别的建构跟解构程序，如果一个类型没有跟初始区源码连结，该类型就不会成为程序的一部分，也不会被执行。在传统的情形下，相反的逻辑也是成立的。初始区的程序也可能让链接程序带入部分类别的源码，即使这类别可能完全没有在任何地方被执行到。在实务上，这会跟手势架构一起介绍，该架构的源码很庞大，但在没有被用到的时候则不会被编译到执行档里面去。

用源码来做范例，我们可以这么写(节录自 ClassCtor 范例项目):

```pascal
type
 TTestClass = class
 public
 class var
 StartTime: TDateTime;
 EndTime: TDateTime;
 public
 class constructor Create;
 class destructor Destroy;
  end;
```

这个类别有两个类别数据字段，当我们在初始区跟终结区里面加入以下源码以后，这两个数据字段就会在类别建构函数中被初始化并在类别解构函数中被修改:

```pascal
class constructor TTestClass.Create;
begin
 StartTime := Now;
end;
class destructor TTestClass.Destroy;
begin
 EndTime := Now;
end;
initialization
 ShowMessage (TimeToStr (TTestClass.StartTime));
finalization
 ShowMessage (TimeToStr (TTestClass.EndTime));
```

单元文件被初始化时，程序就如我们预期的被执行，当我们显示出文字对话框时，类别的资料就已经准备好了。当结束程序时，另一个 ShowMessage的文字框则会在类别解构函数指派数据之前被显示出来。

注意到，我们可以定义多个一般的建构函数跟解构函数，像是预设的 Create跟 Destroy。但我们不能定义多个类别建构函数或类别解构函数。如果我们这么做，编译器会指出以下的错误讯息：

```pascal
[DCC Error] ClassCtorMainForm.pas(34): E2359 Multiple class constructors in class TTestClass: 
Create and Foo
```


### 在 RTL 里的类别建构函数

在 RTL 里面有些类别已经透过使用类别建构函数得到了一些优势，像是Exception 类别，就定义了类别建构函数(如下面这个范例程序片段所示)，以及类别解构函数：

```pascal
class constructor Exception.Create;
begin
 InitExceptions;
end;
```

InitExceptions 这个程序会在 SysUtils 单元文件的初始区程序里面先被调用。通常，我认为使用类别建构函数跟类别解构函数会比使用单元文件的初始区跟终结区来的好。在大多数情形下，这只是语法上方便，所以我们可能不会想要回头修改现有的源码。然而如果我们面对从未使用过的数据结构初始化所可能遭遇的风险(因为这个类型的类别从未被建立过)，把初始化的工作搬到类别建构函数去，可以提供很显著的好处。这在一般的函数库当中显然比较常被使用到，当然我们并不可能在程序中用到函数库面的所有功能。

!!! note "笔记"
    使用类别建构函数的一个特殊情况，会是在使用泛型类别(generic class)的时候。我们会在这一章里面提到泛型。


### 实作单一实体模式(Singleton	Pattern) 

有不少类别，是需要从头到尾只能建立一个实体的。单一实体模式(Singleton Pattern，另一个很常见的设计模式)，就有这样的要求，并建议提供一个可以对于该对象进行存取的全局指标。

!!! note "笔记"
    我们在前一章里面已经提过，我们在附录 D 里面提供了一些透过 Object Pascal 的特点介绍设计模式(Design Pattern)的文章。

单一实体模式可以用很多种方式来实现，但一个类别的要求则是调用用来存取唯一实体的函数，来取得该类别的唯一实体。在很多情形下，这样的实作也使用了延迟初始化的规则，所以这个全局的实体会直到整个程序有用到它的时候，才会被建立出来。

在这个实作方法中，我使用了类别数据、类别方法，但也在最后要进行结束的时候使用了类别解构方法。以下是相关的范例源码:

```pascal
type
 TSingleton = class(TObject)
 public
 class function Instance: TSingleton;
 private
 class var theInstance: TSingleton;
 class destructor Destroy;
 end;
class function TSingleton.Instance: TSingleton;
begin
 if theInstance = nil then
 theInstance := TSingleton.Create;
 Result := theInstance;
end;
class destructor TSingleton.Destroy;
begin
 FreeAndNil (theInstance);
end;
```

我们可以在源码里面以下面的写法取得该类别的实体(不管当时该类别的实体是否已经被建立出来):

```pascal
var
 aSingle: TSingleton;
begin
 aSingle := TSingleton.Instance;
```

更进一步的说，我们可以把一般的建构函数给隐藏起来，把一般的建构函数声明在私有区，让任何人都无法违反单一实体模式的规则，不然就用不到这个类别。


## 类别参考

我们介绍过了好几个跟方法相关的主题，现在我们来看一下跟类别参考以及把我们的范例透过动态建立组件的方法延伸的结果吧。首先要一直提醒自己，类别参考并不是类别，不是对象，它只是一个指向对象的参考而已，它只是简单的一个类别类型的参考而已。换句话说，类别参考类型的变量，其内容就是类别。

类别参考类型决定了类别参考变量的类型。听起来很绕口吧?我们用几行源码来说明这一点。假设我们定义了一个名为 TMyClass 的类别，我们就可以定义一个新的类别参考类型来跟这个类别产生关连:

```pascal
type
 TMyClassRef = class of TMyClass;
```

现在我们就可以定义这两种类型的变量了，第一个变量是指向一个对象，第二个则是指向一个类别:

```pascal
var
 AClassRef: TMyClassRef;
 AnObject: TMyClass;
begin
 AClassRef := TMyClass;
 AnObject := TMyClass.Create;
```

您可能会觉得奇怪，类别参考到底有什么用?通常，类别参考让我们可以在运行时间操作一个类别资料类型。我们可以在表达式里面用类别参考来判断一个数据类型是否合法。事实上，这样的表达式并不多，但部分案例是挺有趣的。最简单的例子，是建立对象的时候。我们可以把上面两行重写一下:

```pascal
AClassRef := TMyClass;
AnObject := AClassRef.Create;
```

此时，我已经用了类别参考的建构函数，而没有使用特定实际类别的建构函数。所以是使用了类别参考来建立该类别的对象。

!!! note "笔记"
    类别参考是跟 metaclass 的概念相关的，这个概念在许多其他的 OOP 语言里面也存在。然而在 Object Pascal 里面，类别参考并不是一个类别，只是用来定义类别数据的一个特殊的类别类型而已。因此，跟 metaclass(用来描述其他类别的类别)相比就很容易误导了想法。实际上 TMetaClass 也是在C++ Builder 里面用到的一个名词。

当我们建立了类别参考，我们可以用它来调用其对应类别的类别方法。所以如果 TMyClass有一个类别方法叫做Foo，我们就可以用以下的任一写法:

```pascal
TMyClass.Foo
AClassRef.Foo
```

如果类别参考不支持跟类别类型相同的类型兼容性规则的话，这看起来也不特别有用。当我们宣告了类别参考变量，例如上例中的 MyClassRef，我们可以把特定的类别或该类别的衍生类别指派给它。所以如果 MyNewClass是 TMyClass 的子类别，我们就可以这么写:

`AClassRef := MyNewClass;`

现在我们来了解一下，这为什么这么有趣，我们必须记得我们可以从类别参考调用的类别方法可以是虚拟方法，所以特定的子类别可以覆写这些方法。使用类别参考跟虚拟类别方法，我们就可以在类别方法的层次实作出多型了，这可是只有少数静态 OOP 编程语言才支持的。同时考虑到每个类别都是从 TObject 衍生而来的，所以我们可以让每个类别参考上都带有TObject 的方法，像是 InstanceSize, ClassName, ParentClass 跟 InheritsFrom。我们会在第 17 章里面介绍这些类别方法跟其他 TObject 类别的方法。

### 在 RTL 里面的类别参考 

System 单元文件跟其他核心的 RTL 单元文件里面宣告了许多的类别参考，包含:

```pascal
TClass = class of TObject;
ExceptClass = class of Exception;
TComponentClass = class of TComponent;
TControlClass = class of TControl;
TFormClass = class of TForm;
```

特别是 TClass 类别参考类型，可以用来储存任何我们在 Object Pascal 所写的类别的参考，因为任何类别都是从 TObject 所衍生出来的。而 TFormClass参考则是在 FireMonkey 或 VCL 为架构的 Object Pascal 项目里面都有用到的。在两个架构的函数库中，Application 对象的 CreateForm 方法都会要求窗体的类别当成参数，这样才能用来建立窗体:

`Application.CreateForm(TMyForm1, MyForm);`

第一个参数是一个类别参考，第二个参数则是一个用来储存 Application 对象建立出来的窗体实体的变量。

### 用类别参考来建立组件

在 Object Pascal 里面，实务上到底是怎么使用类别参考的?要能在运行时间操作一个数据结构，是开发环境的基本功能。当我们从组件盘上面选了一个组件，把它加到窗体上，我们就是选择了一个数据类型，并且建立了该数据类型的一个组件。(实际上，这是开发环境帮我们在背景偷偷做好的)为了帮助我们更了解类别参考的运作，我建立了一个名为 ClassRef 的范例项目。在这个范例中的窗体相当简单。上面只有三个 Radio 按钮，放在窗体上方的一个 Panel 里面。当我们点选了其中一个按钮，我们就可以建立Radio 按钮文字卷标所描述的组件:Radio 按钮，一般按钮，跟文字编辑框。

为了让这个程序能正常运作，需要适当为这三个组件命名，窗体必须也要有一个类别参考的字段:

```pascal
private
 FControlType: TControlClass;
 FControlNo: Integer;
```

第一个字段储存每次用户点击按钮时的新数据类型，并修改它的状态。以下是其中一个按钮的方法:

```pascal
procedure TForm1.RadioButtonRadioChange(Sender: TObject); begin
 FControlType := TRadioButton;
end;
```

另外两个 Radio 按钮都有跟这个方法类似的处理程序，会指派 TEdit 或者TButton 到 FControlType 这个数据字段。类似的指派也在窗体的 OnCreate事件处理程序中出现，当做初始化方法来使用。这个源码有趣的部份，是当使用者点击了画面上大多数的 TLayout 组件时都会执行。我选择了窗体的 OnMouseDown 这个事件来取得鼠标的位置:


```pascal
procedure TForm1.Layout1MouseDown(Sender: TObject;
 Button: TMouseButton; Shift: TShiftState; X, Y: Single);
var
 NewCtrl: TControl;
 NewName: String;
begin
 // Create the control
 NewCtrl := FControlType.Create (Self);
 // Hide it temporarily, to avoid flickering
 NewCtrl.Visible := False;
 // Set parent and position
 NewCtrl.Parent := Layout1;
 NewCtrl.Position.X := X;
 NewCtrl.Position.Y := Y;
 // Compute the unique name (and text)
 Inc (FControlNo);
 NewName := FControlType.ClassName + FControlNo.ToString;
  Delete (NewName, 1, 1);
 NewCtrl.Name := NewName;
 // Now show it
 NewCtrl.Visible := True;
end;
```

这段程序的第一行就是关键了。它会建立用储存在 FControlType 的类别参考来建立一个新的对象。我们简单的透过使用该类别参考的 Create 建构函数来完成。所以我们现在就可以设定 Parent 属性的值、设定它的位置、给他一个名字(名字当然是个字符串)，然后把它设定成可见。

特别介绍源码中用来建立名称这部份，我们模仿了 Object Pascal 的默认命名规则，我用了 FControlType.ClassName，也就是储存在类别参考中的类别名称来当开头，这也是 TObject 类别的类别方法之一。然后加入该组件目前的序号作为结尾，并把最开头的字符串移除。

所以用第一个 Radio 按钮来做说明，假设 FControlNo 是 1，FControlType是 TRadioButton，因此 FControlType.ClassName 回传 TRadioButton 这个字符串，FControlNo.AsString 会回传 1，我们复制第一个字符串(RadioButton)，然后在后面加上 1，这样就得到了 RadioButton1 这个字符串，很眼熟吧?

我们可以看一下图 12.1，这是程序执行的画面，要注意这个命名规则跟 IDE的规则并不是完全一致的。IDE 里面对每个类型的组件有独立的计数。而我们范例程序里面所有组件都用同一个计数。所以如果我们在画面上依序放了 Radio 按钮，一般按钮，以及文字编辑框，它们的名字会分别是：RadioButton1，Button2，以及 Edit3，就如图 12.1 所示(当然文字编辑框是不会显示名字的):

图 12.1: 在 Windows 环境下执行 ClassRef 的画面

记得这一点，跟窗体设计功能不一样，建立出来的 Edit 控件的名字是看不到的。

顺带一提，考虑到一旦我们建立了一般的组件，我们就可以用非常动态的方式来存取它的属性。例如Refection，这个主题我们会在第16章里面介绍。在那一章里面，我们也会介绍更多其他参考到类型的方法、以及透过类别方法引用类别信息等等。

## 类别与记录助⼿

我们在第八章里面介绍过，在类别当中继承的概念是用来帮类别提供新功能的一个方法，而且这个方法不会影响到原来的任何实作。这个实作方式也被称为开放的封闭原理:数据类型本身是完整定义了的(封闭)，但仍可以被修改(开放)。

虽然类型继承是一个很强大的机制，在某些情境底下它仍然还不完美。当我们的程序中使用了已经存在的复杂函数库，我们会想要能够对一个数据类型进行扩充，而不想继承类别。当对象是以某些自动化的方式建立时就可以这样做，而且替换掉建立的过程则会很复杂。

另一个对 Object Pascal 开发人员来说相对直觉的情境则是使用组件。如果我们想为一个组件类别加入新的方法，为它提供新的功能，我们就一定得要透过继承来做，但同时我们也就需要:建立新的衍生类别、建立新的套件并且安装它，把所有已经放在窗体上面的这个组件都替换掉、在项目里面所有将要使用到该组件的窗体画面上，也都要用新版本的组件(同时会影响到窗体跟原始文件的两个档案)。

的确很麻烦，另一个替代作法，则是使用类别或者记录助手。这些特殊用途的数据类型能够延伸现有的类型，为这些类型加上新的方法。即使使用类别助手这个作法有些限制，类别助手让我们可以处理像刚刚提到的，只是要简单的为现有组件加入新方法的这种需求，而且不用修改现有的组件类型喔。

!!! note "笔记"
    我们其实已经看过一个用来延伸函数库类别，而不用更换掉相关参考的方法了，透过使用继承和相同名称的类别，也就是前一章里面提到的穿插类别(interposer class idiom)。类别助手提供了一个更简洁的模式，然而被耶助手无法用来替换掉虚拟方法，也无法实作一些延伸的接口，这些我们在前一章都已经提到过了。

### 类别助手

类别助手是让我们为无法修改的类别(例如函数库类别)加入方法或属性的途径。使用类别助手来为我们自己写的类别做延伸并不常见，因为我们可以直接修改自己能掌握的类别的所有源码。

类别助手是 Delphi 的 Object Pascal 的特别创见，其他的编程语言则有像是扩充方法或者隐含类别来提供类似的功能。

类别助手无法加入实体数据，假设这个数据必须依赖实际的对象，而这些对象是以它们的原始类别定义的，或者碰触到原本定义在原始类别的实际架构中的虚拟方法，类别助手就帮不上忙了。

换句话说，助手类别只能加入或者换掉已存在的非虚拟方法。我们可以用这个方法在原始类别的对象上加入新的方法，即使该类别当中没有现存方法的线索也一样。

这么说或许不够清楚，我们来看个例子(这是 ClassHelperDemo 范例项目的一部分，这是一个我们不该做的范例-用类别助手来处理我们自己制作的类别):

```pascal
type
 TMyObject = class
 private
 Value: Integer;
 Text: string;
 public
 procedure Increase;
 end;
 TMyObjectHelper = class helper for TMyObject
 public
 procedure Show;
 end;
```

以上的源码宣告了一个类别跟这个类别的助手。这表示当我们生成了一个TMyObject 的对象时，我们也可以调用类别助手的方法:

```pascal
Obj := TMyObject.Create;
Obj.Text := 'foo';
Obj.Show;
```

这个助手的类别方法会变成该类别的一部分，我们可以用 self 来调用助手类别的方法，就像我们调用该对象的任何一个方法一样(类别助手只是扮演助手的角色，因为它并没有实际被建立成一个实体)，如下面的范例所示:

```pascal
procedure TMyObjectHelper.Show;
begin
 Show (Text + ' ' + IntToStr (Value) + ' -- ' +
 ClassName + ' -- ' + ToString);
end;
```

最后，请记得助手类别可以覆写原始的方法。在我们提供的范例源码里面，在类别跟着手类别当中都有名为 Show 的方法，但只有助手类别的方法会被调用到。

当然，宣告一个类别以及透过助手类别的语法来宣告同名类别的延伸也是有点用处的，这个作法适用于同一个单元文件，甚至同一个程序中。在范例程序中就是这么做的，但只是为了让大家更容易理解这个技术跟语法。类别助手在开发应用程序结构的时候，不应该用来当做一般语言的的建构函数，但可以用来为我们没有原始码的函数库里面的类别，或者我们不想动到原始码的类别避免未来修改时发生冲突。

使用类别助手的时候还是有些规则的。类别助手的方法：

+ 可以跟原始的类别方法位于不同的访问权限区。
+ 可以是类别方法或者实体方法，类别变量与属性
+ 可以是虚拟方法，未来可以在衍生类别中被覆写(即使我发现这个功能在实作时有些怪怪的)
+ 可以提供额外的建构函数
+ 可以在类型定义中加入巢状常数

类别助手中唯一缺少的功能就是常数数据字段。也要注意，类别助手在进入该类别的执行范围起就开始可以被使用了。我们必须在 uses 区段加入对定义了类别助手的单元文件的引入宣告，才能看见该类别助手的方法，而不是只要在编译的过程中引入就行了。

!!! note "笔记"
    某些情形下 Delphi 编译器当中的问题会让类别助手存取原始类别的私有区段，不论类别助手宣告在哪个单元文件都一样。这基本上算是打破了面向对象的封装规则。为了强化对象可视范围的语义，在最近几个版本的 Object Pascal 编译器(从 10.1 Berlin 开始)中，类别跟记录助手就无法存取他们延伸的原始类别私有区的属性跟方法了。这也的确让一些已经写好的项目无法被编译，当然，只有使用到类别助手，并使用到原始类别私有区属性与方法(这是错误，不能当做 Object Pascal 的新功能)的项目才会受影响。

### ListBox 的类别助手

类别助手的实际应用，是提供函数库中的类别一些额外的方法。原因可能是我们不希望直接动到这些类别的原始码(即使我们可能拥有这些原始码，但我们不一定会想要动到这些核心函数库的原始码)或者从这些类别制作出衍生类别(使用这个作法，我们就得把窗体里面的组件一一替换了)。

当成做个练习，思考一下这个简单的案例:我们想要有一个简单的方法，能够拿到一个 list box 目前被选取的项目的文字。但不想要用传统写法:

`ListBox1.Items [ListBox1.ItemIndex]`

我们可以定义一个像这样的类别助手(节录自 ControlHelper 范例):

```pascal
type
 TListboxHelper = class helper for TListBox
 function ItemIndexValue: string;
 end;
function TListboxHelper.ItemIndexValue: string;
begin
 Result := '';
 if ItemIndex >= 0 then
 Result := Items [ItemIndex];
end;
```

定义完成之后，我们就可以直接改写成这样了:

`Show (ListBox1.ItemIndexValue);`

这只是个很简单的例子，但也用很实用的文字把内涵给表达出来了。

### 类别助手跟继承

类别助手最明显的限制，就是我们每次对一个类别只能使用一个助手。如果编译器发现了有两个助手类别，第二个助手类别就会把第一个给取代掉。所以也没有任何方法可以对类别助手进行连锁使用，也就是说我们不能对类别助手再制作另一个类别助手。

有个方法可以解决一部分的问题，就是我们可以为一个类别制作一个类别助手，然后当这个类别出现了衍生类别时，再为衍生类别制作另一个类别助手。这样做并不是从助手类别衍生另一个助手类别喔。我并不鼓励让类别助手的结构过于复杂，因为那可能让我们的源码变得很难懂。

当我们要加到一个函数库或者系统已定义完成的数据结构时，助手就很有用。TGUID 记录就是一个例子，它是一个 Windows 的数据结构，我们可以在 Object Pascal 里，在不同的操作系统中使用它，有个类型助手就帮它加了一些常用的功能:

```pascal
type
 TGuidHelper = record helper for TGUID
 class function Create(const B: TBytes): TGUID; overload; static;
 class function Create(const S: string): TGUID; overload; static;
 // ... more Create overloads omitted
 class function NewGuid: TGUID; static;
 function ToByteArray: TBytes;
 function ToString: string;
 end;
```


### 使用类别助手加入对控件列举的功能 

任何在函数库中的 Delphi 组件都自动定义了列举功能，我们可以透过这个功能来列举该组件所拥有、保管的组件或子组件。例如，在窗体的方法中，我们可以用以下的简单源码来列举出该窗体所拥有的所有组件:

```pascal
for var AComp in self do
 … // 使用 AComp
```

另一种常见的作法，则是浏览子控件(control)，这只包含了以该窗体为直接parent(第二或第三层的不包含)的视觉组件，例如直接放在窗体上面的 Panel或者 Button (放在 Panel 里的已经算是第二层了)，非视觉组件，像是TMainMeu，也就不包含在内。我们可以用另一个方法快速让所有控件都能被列举出来，这方法就是为 TWinControl 类别制作一个类别助手：

```pascal
type
 TControlsEnumHelper = class helper for TWinControl
 type
 TControlsEnum = class
 private
 NPos: Integer;
 FControl: TWinControl;
 public
 constructor Create (aControl: TWinControl);
 function MoveNext: Boolean;
 function GetCutrrent: TControl;
 property Current: TControl read GetCurrent;
 end;
 public
 function GetEnumerator: TControlsEnum;
 end;
```
!!! note "笔记"
    这个类别助手之所以是为 TWinControl 而写，而非 TControl 是因为只有具备 Windows handle 的控件才能当其他控件的 parent (上代控件)。所以图形控件很自然就被排除在外了。

以下是上面这个范例中类别助手的完整源码，包含它自己的方法以及当中定义的类别 TControlsEnum:

```pascal
{ TControlsEnumHelper }
function TControlsEnumHelper.GetEnumerator: TControlsEnum;
begin
Result := TControlsEnum.Create (Self);
end;
{ TControlsEnumHelper.TControlsEnum }
constructor TcontrolsEnumHelper.TcontrolsEnum.Create (
 AControl: TWinControl);
begin
 FControl := AControl;
 NPos := -1;
end;
function TControlsEnumHelper.TControlsEnum.GetCurrent: TControl;
begin
 Result := FControl.Controls[nPos];
end;
function TControlsEnumHelper.TControlsEnum.MoveNext: Boolean;
begin
 Inc (NPos);
 Result := NPos < FControl.ControlCount;
end;
```

如果我们建立了一个像是图 12.2 的窗体，就可以测试一下不同的情境。我们是假设第一个情境来写出上面的源码的，也就是列举窗体上面的子控件:

图12.2: 在Windows环境下执行ClassRef的画面

```pascal
procedure TControlsEnumForm.BtnFormChildClick(Sender: TObject);
begin
 Memo1.Lines.Add (‘Form Child’);
 for var ACtrl in Self do
 Memo1.Lines.Add (ACtrl.Name);
 Memo1.Lines.Add (‘’);
end;
```

接下来则是执行后在 Memo 当中的输出结果，列出了窗体上所有的子控件，但放在 Panel 里面的组件或者控件则没有列出来了:

```pascal
Form Child
Memo1
BtnFormChild
Edit1
CheckBox1
RadioButton1
Panel1
BtnFormComps
BtnButtonChild
```

如果我们完整的进行列举所有组件，显示的内容应该更多。但是在使用本节开始的源码时，会遇到一个问题：因为我们用类别助手里面的新版GerNumerator 覆写了原本的版本，因此我们无法直接使用基础类别TComponent 的列举功能。这个类别助手是为 TWinControl 定义的，所以我们可以投机取巧一下。如果我们把源码中的对象转型为 TComponent，就可以调用原本的、标准版的列举功能了:

```pascal
procedure TControlsEnumForm.BtnFormChildClick(Sender: TObject);
begin
 Memo1.Lines.Add (‘Form Components);
 for var ACtrl in TComponent(self) do
 Memo1.Lines.Add (AComp.Name);
 Memo1.Lines.Add (‘’);
end;
```

这段源码执行以后，就会列出比前一版本更多的组件了:

```pascal
Form Child
Memo1
BtnFormChild
Edit1
CheckBox1
RadioButton1
Panel1
BtnPanelChild
ComboBox1
BtnFormComps
BtnButtonChild
MainMenu1
```

在范例 ControlsEnum 项目里面，我已经加入了可以列举出放在 Panel 里面所有子控件的源码了。

### 现有类型的记录助手

记录助手概念的延伸之一，就是为原生(或者说是编译器现有的类型)数据类型提供了加入方法的能力。即使我们用了”记录助手”这样的名词，但这写法并不是为了记录提供的，而是对一般的数据类型提供的。

!!! note "笔记"
    记录助手目前已经被用来延伸、为原生资料类型提供像方法一样的处理，但这在未来应该会有所改变。今天的执行时期函数库定义了的一些原生助手，未来可能就会消失，保持我们目前以这些助手写程序的方法，但在定义这些助手的源码中打破了兼容性。这也是我们为什么不应该过度使用这个功能的原因，即使它用起来很方便，且功能强大。

现有的类型助手是怎么运作的?我们来看看以下对整数类型所定义的助手:

```pascal
type
 TIntHelper = record helper for Integer
 function AsString: string;
 end;
```

现在假设有个整数变量 n，我们可以这么写:

`n.AsString;`

我们要怎么定义虚构的方法，而这个方法又是怎么让变量值能够参考到的呢?我们想象一下 self 这个关键词在该函数里被使用到：

```pascal
function TIntHelper.AsString: string; begin
 Result := IntToStr (Self);
end;
```

注意到，我们也可以在常数上面使用这个助手，可以这么写:

`Caption := 400000.AsString;`

然而我们不能对很小的数值作一样的动作，因为编译器会把太小的数值当成是其他的类型来处理。所以如果我们想要把 4 这个数字的常数用上面的方法来转换，就得用以下的第二个写法了:

```pascal
Caption := 4.AsString; // Nope!
Caption := Integer(4).AsString; // OK
```

或者我们也可以用第一个写法，但我们就得定义一些不同的类型助手了:

```pascal
type
 TByteHelper = record helper for Byte...
```

我们在第二章里面已经介绍过，我们用不着像上面这样写了很多的类型助手，像是整数跟 Byte，因为执行时期函数库已经定义了许多类别助手跟大多数核心类型的助手，以下这些都包含在 SysUtils 单元文件里面了:

```pascal
TGUIDHelper = record helper for TGUID
TStringHelper = record helper for string
TSingleHelper = record helper for Single
TDoubleHelper = record helper for Double
TExtendedHelper = record helper for Extended
TByteHelper = record helper for Byte
TShortIntHelper = record helper for ShortInt
TWordHelper = record helper for Word
TSmallIntHelper = record helper for SmallInt
TCardinalHelper = record helper for Cardinal
TIntegerHelper = record helper for Integer
TUInt64Helper = record helper for UInt64
TInt64Helper = record helper for Int64
TNativeUIntHelper = record helper for NativeUInt
TNativeIntHelper = record helper for NativeInt
TBooleanHelper = record helper for Boolean
TByteBoolHelper = record helper for ByteBool
TWordBoolHelper = record helper for WordBool
TLongBoolHelper = record helper for LongBool
TCurrencyHelper = record helper for Currency // added in Delphi 11
```

还有一些现存的类型，它们的助手则被定义在其他单元文件里面:

```pascal
// System.Character:
TCharHelper = record helper for Char
// System.Classes:
TUInt32Helper = record helper for UInt32
// System.DateUtils
TDateTimeHelper = record helper for TDateTime // added in Delphi 11
```

因为我们在本书前面的篇幅里面已经提供了许多范例，所以在这里就不赘述了。加入这一节，是为了让大家知道我们可以怎么为现存的类型定义一个类型助手而已。



### 类型别名的助手

如同刚刚介绍过的，我们不可能为一个类型定义两个助手，每个类型只会有一个助手。所以我们要怎么为一个原生类型，像是整数，加入一个直接的动作呢?这个需求并没有直接的解决方法，但有几个可以奏效的作法(简单的说，可以复制内部类别助手的原始码，然后用另一个类别助手方法来贴上这些源码)。

另一个我中意的解决方法，是定义一个类型别名。类型别名会被编译器当成一个全新的类型，所以它可以拥有它自己的类型助手，就不用改动原始类型的助手了。现在两个类型被当成不同类型了，我们仍然不能把两个类型助手透过同一个变量来使用，但我们可以透过类型转换来达成这个目的。我们用源码直接来说明吧。假设我们建立了这个类型别名:

```pascal
type
 MyInt = type Integer;
现在我们就可以定义这个类型的助手了:
type
 TMyIntHelper = record helper for MyInt
 function AsString: string;
 end;
function MyIntHelper.AsString: string;
begin
 Result := IntToStr (self);
end;
```

如果我们定义了一个这个新类型的变量，我们就可以调用特定助手的方法来做预设的存取了，但是仍然调用整数助手的方法，只要透过转型-以上节录自 TypeAliasHelper 范例项目，您可以用它来做些延伸练习：

```pascal
procedure TForm1.Button1Click(Sender: TObject);
var
 MI: MyInt;
begin
 MI := 10;
 Show (MI.AsString);
 // Show (MI.toString); // this doesn't work
 Show (Integer(MI).ToString)
end;
```
