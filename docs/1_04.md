在 Object Pascal 语言里面强调的另一个重要观念（和 C 语言的概念极为相似），就是子程序的概念。基本上子程序的概念就是把一系列的指令集合起来，并赋与一个独特的名字，子程序可以被调用许多次。子程序（或者称为函数）是透过它们的名字进行调用的。透过子程序的设计，我们可以重复使用相同逻辑的源码，不用一再重复撰写相同的源码。这样一来就在整个程序中需要使用该段程序逻辑的地方使用同样版本的源码。从这个观点来看，我们可以把子程序看成是封装机制的基础。



## 程序与函数

在 Object Pascal 里面，子程序以两种形式存在：程序(procedure)和函数(function)。理论上，程序是我们要求计算机执行的一个动作，而函数是计算后要把数据回传的动作。两者之间的差异，是函数会回传结果，这个回传的结果可能是数值、类型，而程序不会回传任何数据。在 C 语言里面，只提供了单一语法，就是函数，当不回传数据的时候，C 语言就要求程序人员把回传数据的类型写成 void，C 语言的这个作法就完全等同于 Object Pascal 的程序。


两种写法的子程序都可以传递多个相同或不同类型的参数，我们稍后会介绍，程序跟函数都是类(Class)当中用来实作方法(method)的基础，而在这个案例中，两种形式的区别仍旧存在。事实上，跟 C, C++, JAVA, C#或JavaScript 都不同，我们在宣告函数或者方法的时候，仍旧会用到 procedure或者 function 这两个关键词来宣告子程序或者方法。


实务上，除了宣告用的关键词不同，程序跟函数之间的差异微乎其微：我们可以宣告函数，然后最后不回传数据，或者完全不管回传值，这样就可以把函数当成程序来用（这可能会导致一些小错误发生）。也可以在程序中，透过传址（call by reference）的参数把最终结果回传给调用它的源码，这样也可以把程序当成函数来用（关于传址的参数，在本章稍后会介绍）。

以下是用 Pascal 语法定义一个程序的写法，会使用到 procedure 这个关键词，这段程序片段是范例程序 FunctionTest 项目的一部分：

```pascal
   procedure Hello;
   begin
     Show ('Hello world!');
 end;
```

比较一下，如果用 C 语言的语法来撰写上面这个子程序的功能，写起来应该会几乎完全相同。在这里面没有关键词，也不要求参数，且不用回传任何资料：

```pascal
void Hello ()
{
 Show ("Hello world!");
};
```

事实上 C 语言的语法在撰写程序跟函数时完全相同。而在 Pascal 语言的语法中，函数还需要一个特殊的关键词与回传值(或者回传的类型)。


!!! note "笔记"
    在 Object Pascal 的语法中，还有一个地方跟其他语言不同的，就是在函数声明的时候，结尾处必须用冒号来宣告函数要回传的数据类型。

在函数的撰写中，有两种写法可以用来定义最后的回传值，一个是直接把回传值指派给函数名称，另一种则是指派给 Result 这个关键词:

```pascal
// 传统 Pascal 写法
function DoubleOld (Value: Integer) : Integer;
begin
 DoubleOld := Value * 2;
end;
// 现代写法
function DoubleIt (Value: Integer) : Integer;
begin
 Result := Value * 2;
end;
```

!!! note "笔记"
    Object Pascal 的语法中，其实还有第三种写法可以用来指派回传值，我们会在本章的『附带回传值离开』这个小节进行讨论。

透过 Result 这个变量，而不使用函数名称来指派回传值，是比较常见的作法，同时也让源码比较容易阅读，直接把回传值指派给函数名称，是传统 Pascal的写法，近代已经比较少用了。

我们再以 C 语言的语法作为对照，把上面的范例源码改写成 C 语言，可以写成这样：

```pascal
int DoubleIt (int Value)
{
 return Value * 2;
};
```

!!! note "笔记"
    在 C 阵营的编程语言里，return 这个描述是定义函数的回传值，并且结束函数的执行，把程序执行的权限交回原本调用该函数的点。在 Object Pascal里面，指派数据给函数的回传值并不会终止该函数的运作。这也是为什么Result 变量很常在 Object Pascal 里面被使用，例如一开始就指派预设的回传值，或者是在算法里面修改回传结果。另外，如果我们想要停止函数的执行，得要在流程控制中调用 Exit。相关的议题会在之后的章节『附带回传值离开』里面再深入介绍。

相对于这些子程序的定义撰写法，调用这些子程序的语法就相对直觉的多，我们只需要直接输入子程序的名字，如果子程序需要参数的话，则以小括号把这些参数括起来即可。如果子程序不需要参数，则在调用的时候连空的小括号都可以省略了（这当然也是跟 C 语言作为对照的，C 语言就算子程序没有要求参数，也得输入个空的小括号才行）。以下的程序片段都是本章范例项目 FunctionsTest 的一部分：

```pascal
// 调用程序
Hello;
// 调用函数
X := DoubleIt (100);
Y := DoubleIt (X);
Show (Y.ToString);
```

我们示范一下源码封装的概念。当我们调用 DoubleIt 这个函数，我们不用知道里面使用的算法，也不用知道它是怎么被实作出来的，如果我们事后又找到了另一个把数字做 Double 的更好的方法，我们可以再写另一个 DoubleIt函数来替换原本的 DoubleIt 函数，而调用 DoubleIt 函数的所有源码都不用再改动。

同样的原则也可以套用在 Hello 程序上：我们可以直接修改 Hello 这个程序来改善程序输出的作法，而原本调用 Hello 的源码则完全不用更动，只要修改完 Hello 函数，所有原本使用 Hello 函数的地方全部一起受益，以下就是我们修改程序实作的写法：

```pascal
procedure Hello;
begin
 Show ('Hello world, again!');
end;
```

### 预先宣告. 115

当我们需要使用一个识别符号(不论其类型是什么)，编译器必须预先得知这个符号，并且必须知道这个识别符号将会指向的参考地址。为了满足这个需求，我们得在使用任何识别符号之前先提供完整的定义。然而，有些情况下这个要求很难达成。如果程序 A 调用程序 B，而程序 B 又调用程序 A，当我们开始撰写这个源码的时候，我们等于是在调用一个编译器还没看到的程序。

在这个情形下(还有很多情形也会有类似的情况发生)，我们可以先宣告一个函数或者程序，把完整的名称、参数都宣告好，但不用提供完整的源码。要做到这一点，我们只要把完整的程序或函数名称宣告写好，最后加上一个forward 关键词即可，例如：

`procedure NewHello; forward;`

在源码后段，我们再把完整的源码写好(这种写法，实作的源码跟预先宣告必须位于同一个单元文件里面)，这样一来，我们就可以在完整的源码还没出现之前，就直接调用它了，以下就是这样的例子：


```pascal
procedure DoubleHello; forward;
procedure NewHello;
begin
 if MessageDlg ('Do you want a double message?',
 TMsgDlgType.mtConfirmation, [TMsgDlgBtn.mbYes, TMsgDlgBtn.mbNo], 0) = mrYes then
 DoubleHello
 else
 ShowMessage ('Hello');
end;
procedure DoubleHello;
begin
 NewHello;
 NewHello;
end;
```

!!! note "笔记"
    上面的程序片段中所用到的 MessageDlg，是 FireMonkey 框架里所提供的一个简单的方法，让我们可以透过对话框询问使用者进行确认(在 VCL 框架里面也有类似的方法，也很容易使用)，其参数是讯息、对话框的种类、我们要显示给用户看到的按钮，最后的回传值则是用户点击的按钮种类。

这样的功能(上面的程序片段也是范例项目 FunctionTest 的一部分)让我们可以写出互相调用的递归情形：DoubleHello 调用 Hello，但 Hello 也可以调用DoubleHello。换句话说，如果用户一直点选 Yes 按钮，讯息就会一直被显示，而且每次点选 Yes 的时候，确认对话框就会再多问两次。在递归的源码里面，一定要有递归的终止条件，以避免相互调用直到堆栈溢出(stack overflow)的情形发生。


!!! note "笔记"
    函数在调用的时候，是使用应用程序内存中的堆栈来记录参数、回传值、局部变量等。如果一个函数持续调用自己，成为了无穷循环，堆栈所使用的记忆空间(通常是预先定义好的固定大小，这会由 linker 或者项目设定值来决定)将会很快用完，使程序不正常停止，这种错误情形，就是大家所熟知的堆栈溢出(stack overflow)。而最近几年，也有个很热门的程序开发网站用了这个域名提供给程序人员一个问题交换讨论的平台，这我想应该就不用介绍了：http://www.stackoverflow.com/


虽然在 Object Pascal 里面已经不常使用预先宣告，但有个类似的案例还是很常见。当我们在一个单元文件的 interface 区段宣告程序或函数时，这个宣告就已经被视为预先宣告了，虽然我们宣告的时候并没有用上 forward 这个关键词。实际上我们本来就无法在 interface 区段撰写源码。请记得我们必须在宣告函数的单元文件里面把该函数的实作程序也完成。


### 递归函数

关于之前我提到的递归，我们先来看一个比较特别的例子(透过两个函数互相调用)，然后再观察一个比较传统的递归案例，也就是函数调用自己。使用递归也常用来当做另类的循环实作方法。

从传统的例子看起，假设我们要计算一个数字的多次方，而手边没有适当的函数(其实这个函数在 Object Pascal 的 RTL 里面就有了)。那我们就只能从数学上的定义来分析了，例如 2 的 3 次方，就是 2 乘自己乘三次，也就是 2x2x2.


所以实作这个函数的一个方法，可以是写一个 for 循环，执行三次(或者是几次方，就乘几次)，把乘法的计算结果再乘以要计算的基底数字：


```pascal
function PowerL (Base, Exp: Integer): Integer;
var
 I: Integer;
begin
 Result := 1;
 for I := 1 to Exp do
 Result := Result * Base;
end;
```

另一个替代方案，则是直接乘以该函数的下一次方执行结果，直到下一次方为 0，因为任何数字的 0 次方都是 1，所以我们可以把 0 次方当成是递归调用的终止条件，把这个函数以递归方式实作：

```pascal
function PowerR (Base, Exp: Integer): Integer;
var
 I: Integer;
begin
 if Exp = 0 then
 Result := 1
 else
 Result := Base * PowerR (Base, Exp - 1);
end;
```

这个程序的递归版本执行起来并没有比 for 循环的版本来的快，也没有比较容易读懂。然而像是在分析程序结构时(例如树状结构)，要处理的元素并不是固定的，因此要用一个 for 循环来处理也几乎是不可能的，因此递归在这种案例中，就显得格外有用了。

通常递归函数的源码功能都比较强，但也比较复杂。经过多年以后，递归几乎被遗忘了，和早期的程序相较，新的函数化语言，像是 Haskell, Erlang 和Elixir 都大量使用递归，并使递归的观念渐渐回归主流。您可以在FunctionTest 的范例程序中找到两种写法的次方函数。

!!! note "笔记"
    范例程序里面的两种次方函数都没有处理负次方的能力，如果把次方的参数传个负数进去，就会造成无穷循环了。同时因为使用了整数类型，也会很快达到该类型的最大值，并造成数值溢位。我撰写这些源码时，也同时保留了这些先天的限制，好让源码能保持简洁




### 方法(Method)是什么? 

我们已经介绍了要怎么在单元文件的 interface 区段透过 forward 关键词进行预先宣告。在类的类型当中宣告方法，也可以视为一种预先宣告。

但『方法』到底是什么？方法，是程序或函数与记录或类相关的特别型态。在 Object Pascal 里面，每当我们为视觉组件处理事件的时候，我们就需要定义一个『方法』，通常是一个程序，但『方法』这个名词，是用来指属于特定类(或记录)的函数或程序，所以同时包含两者。

以下是一个由 IDE 自动产生的空白方法源码，是属于一个 form 的(其实只要属于类即可，我们在本书稍后的章节会介绍):

```pascal
procedure TForm1.Button1Click(Sender: TObject);
begin
 {here goes your code}
end;
```


## 参数与回传值.118

当我们调用函数或者程序的时候，我们必须传递正确数目的参数，并且必须确认所有参数的类型都跟宣告的相同。要不然，编译器就会指出这里有错误，例如参数类型不符。以前面的定义的 DoubleIt 函数当例子，它是要求整数作为参数，所以如果我们这样调用：

`DoubleIt(10.0);`

编译器就会指出这个错误:

`[dcc32 Error] E2010 Incompatible types: 'Integer' and 'Extended'`

!!! tip "提示"
    编辑器在我们撰写程序的时候，会提供我们即将调用的函数或程序的所有可能参数清单，这个提示会以一个提示窗口的样式出现，当我们输入某个函数的名字，并输入了一个小括号，这个窗口就会出现了，这个功能称为源码参数(Code Parameters)，它算是 Code Insight 技术的一部分(在其他 IDE环境中则称之为 IntelliSense)。从 Delphi 10.4 开始，Code Insight 已经是以LSP(Language Server Protocol)服务器来提供的了。

有些时候，是允许限制类型转换的，例如进行指派，但通常我们应该试着使用特定类型的参数(这称之为强制性的参数参照，我们稍后会进行介绍)。

当我们调用函数的时候，我们可以把一个表达式作为参数，不用限制传递一个数值。这个表达式会在执行的时候被运算，运算结果则作为参数被传递到子程序里面去。在单纯的情形下，我们只把变量名字传递进去。在这个例子里，变数的数值则是被复制到该参数里(参数的名称通常和原来的变量名字不一样)。我强烈的建议绝不要把同名的变量传给函数当参数，因为光看上去就很容易让人混淆。


!!! warning "警示"
    在 Delphi 里，我们不能只检查传给函数的参数顺序，因为这取决于调用约定，最常见的案例是由右到左的检视。完整的相关信息请参考:https://docwiki.embarcadero.com/RADStudio/Sydney/en/Procedures_and_Functions_(Delphi)#Calling_Conventions

最后，请记得我们可以用同一个函数或程序的名字，但提供不同版本(这个技术被称之为多载，overloading)，也可以在函数或程序里面忽略一些参数，改以预设的数值来传递(这个技术则被称为预设参数)。这两个在函数跟程序上的重要技术，都会在本章稍后的篇幅加以介绍。




### 附带回传值离开

我们已经介绍过从函数回传结果的几种不同语法(和 C 语言或者从 C 衍生的其他语言对照)。不光是语法不同，其规则也相异。把回传数值指派给 Result关键词(或者函数名称)，并不会像执行 return 指令一样结束函数的运行。

Object Pascal 开发者通常受惠于此功能，可以把 Result 当成一个暂时的储存空间，与其这样写：

```pascal
function ComputeValue: Integer;
var
 value: Integer;
begin
 value := 0;
 while ...
 Inc (value);
 Result := value;
end;
```

我们可以省下暂时变量，直接使用 Result，不管 Result 的内容到函数结束的时候是什么，都会是用来回传该函数最后的数值：

```pascal
function ComputeValue: Integer;
begin
 Result := 0;
 while ...
 Inc (Result);
end;
```

换句话说，也有一些情形，是我们会想要指派好回传值以后，立刻就。离开程序的。例如在特定的 if 判断式里面，如果我们想要指定函数的结果，并且直接中止当下执行中的源码。我们当然应该使用两个独立的函数：先指派结束的数据给 Result 关键词，而且立刻使用 Exit 函数脱离子程序。

假如您记得源码里面的 FlowTest(在”用 Break 和 Continue 指令来中断流程”章节里面介绍过)的项目源码，现在的作法可以视同为重写一些函数，用来替换掉调用 Break，然后接着调用 Exit。我已经把程序写在以下的程序片段里面了，它是 ParamsTest 这个范例程序的一部分。

```pascal
function CharInString (S: string; Ch: Char): Boolean;
var
 I: Integer;
begin
 Result := False;
 for I := Low (S) to High (S) do
 if (S[I]) = Ch then
 begin
 Result := True;
 Exit;
 end;
end;
```

在 Object Pascal 里面，我们可以把 if 判断式成立时要执行的两个指令，用一个 Exit 指令来取代，Exit 指令可以直接把要当做回传值的数值当成参数，就像 C 语言里面的 return 指令一样。所以我们可以把上面的源码写的更精简，也就可以省下一个 begin-end 的区块了：

```pascal
function CharInString2 (S: string; Ch: Char): Boolean;
var
 I: Integer;
begin
 Result := False;
 for I := Low (S) to High (S) do
 if (S[I]) = Ch then
 Exit (True);
end;
```

!!! note "笔记"
    在 Object Pascal 里面，Exit 是一个函数，所以回传值必须括在小括号里面，C 语言的 return 是一个编译器的关键词，所以 return 不用把回传值用小括号当成参数括起。



### 引用参数(Reference	Parameters) 

在 Object Pascal 里面，程序和函数传递参数时，包含了传值和传址两种方式。预设的参数传递是以传值的方式进行的：作为参数传递进入子程序的数值或变量，会被复制一份放在堆栈里，子程序将会使用复制出来的这份数值在整个子程序里面使用，所以即使在子程序里面修改了这个复制的数值，调用前的参数变量或数值，也都不会有任何改变。（就像我们稍早在函数的参数与回传值章节所介绍的那样）

透过传址的方式来传递参数，则表示不会复制数据到子程序里面去，而是会把当做参数的变量内存地址传进子程序，让子程序直接使用该参数的内存地址，因此子程序只要对这个参数做了任何修改，就会同时修改到调用子程序时所传递的变量内容。要使用引用参数，在宣告参数的时候，只要在参数前面加个 var 关键词即可。

这个技术在大多数的编程语言里面都有提供，也因为不用复制变量内容，通常程序执行的速度会快上一些。这个作法在 C 语言里面就没有提供(我们可以使用指标来达成一样的效果)，但在 C++跟其他使用 C 语言语法的语言里面则也有提供此功能，我们可以透过＆这个符号(意味着传址)来达成传址的目的。以下是使用 var 关键词提供引用参数的写法:

```pascal
procedure DoubleIt (var Value: Integer);
begin
 Value := Value * 2;
end;
```

在这个案例中，参数既扮演了传递数值让子程序进行计算的角色，也同时把计算完成的结果带回给调用它的程序，也就是回传值的角色。如果我们这么写：

```pascal
var
X: Integer;
begin
 X := 10;
 DoubleIt (X);
 Show (X.ToString);
```

变量 X 的内容会在调用了 DoubleIt 函数之后变成 20，因为这个函数使用的是引用参数，会直接使用变量 X 的内存地址，直接影响变量 X 的内容。

跟传统的传值参数规则比较一下，引用参数所传递的不只是参数里面的数值，而是把整个参数传过去，所以引用参数不能接受把常数、表达式、函数的回传值，或是类的属性当成引用参数传递。另一个规则则是不能传递不同类型的变量(必须要先经过转型)。变量的类型跟参数的类型必须完全一致，不然编译器又要抱怨有错了：

`[dcc32 Error] E2033 Types of actual and formal var parameters must be identical`

如果我们这么写，编译器就会抱怨而显示上面的错误讯息(下面的程序片段是范例程序 ParamsTest 的片段，不过我已经把它加上注释符号了)：

```pascal
var
C: Cardinal;
begin
 C := 10;
 DoubleIt (C);
```

以引用参数来传递有序类型或记录类型(我们会在下一章介绍)的参数是很有用的。这些类型通常被称为值类型，因为它们的语法含义原本就是传值或者指派数值的作法。

Object Pascal 的字符串跟对象则是完全不同的规则，我们稍后会更深入讨论。对象变量原本就是内存地址，所以当我们把对象当做参数传递的时候，本来就会变动到该对象的原始内容。这些类型比较特殊，通常被称为『参考类型』。

除了标准的引用参数类型(var)之外，Object Pascal 还提供另外一种很特别的参数关键词: out。out 参数不用具备初始值，它只用来把数据回传给调用者，除了不用提供初始值以外，其他的规则，out 跟 var 几乎都一样。


!!! note "笔记"
    out 参数是为了兼容 Windows 的 COM 模式而被创造出来的。除了在Windows COM 模式与对象的源码。这可以被用来表示开发人员预期要回传但没有被初始化过的值。


### 常数参数 (Constant	Parameters)

除了引用参数之外，参数还有一种称为常数参数，我们可以在参数前面加上const 关键词。使用了 const 关键词的参数，在子程序里面会被当成常数，我们不能把值指派给常数参数，编译器可以对参数的传递进行优化。编译器会选择类似引用参数的作法(或者像 C++里面所提到的常数地址)，但这个规则会很像是传值参数，因为原始数值在子程序里面是不能被更动的。

事实上如果我们试着编译以下的源码(是可以的，但我已经在 ParamsTest 项目里面把它标注为注释了):

```pascal
function DoubleTheValue (const Value: Integer): Integer;
begin
 Value := Value * 2; // compiler error
 Result := Value;
end;
```

这里面的错误我们很难一眼看出是怎么回事，错误讯息是：

`[dcc32 Error] E2064 Left side cannot be assigned to`

常数参数常被用在字符串类型上，因为在这种情形下，编译器会先暂时停止参考计算器制(Reference Counting mechanism)，以进行优化。使用常数参数最常见的原因，就是常数可以在使用有序类型时可以产生一定的界限。常数参数也不常被用在对象传递上，因为在 Object Pascal 里，如果我们用常数参数形式传递对象的话，被传递进去的会是该对象的参考(内存地址)，而非对象本身。换句话说，编译器不会让我们指派另一个对象到该参数中，但可以让我们调用该对象当中的方法来改变对象里面的数据。


!!! note "笔记"
    const 参数有一个大家比较不知道的替代作法，就是加入一个 ref 属性，写起来像是”const [ref]”。这个属性会强制编译器常数参数用传址的方式来处理该参数，不然的话，编译器默认会以传值的方式来处理它，有时在不同的 CPU 跟操作系统上，则会依照该参数所占的内存空间而采用传址的方式来处理


### 函数的多载 (Function	Overloading).

我们常常会需要提供两个类似的函数，让其中的参数跟实作的源码各有些不同。在传统的写法里，我们只能乖乖的用另一个函数名称来制作第二个函数。但现代程序的写法，则可以用同一个函数名称，透过不同的参数，对同一个名称的函数进行**多载**。

多载的含义很简单：编译器允许我们使用同一个函数或程序的名称制作多个函数或程序，只要其中的参数不同即可。透过检查参数，编译器就可以判断我们要调用的是哪一个版本的函数或程序。

我们可以看一下以下这一些函数，它们是从 Object Pascal RTL (Run-Time Library, 运行时间函数库)当中的 System.Math 单元文件里面节录出来的：

```pascal
function Min (A,B: Integer): Integer; overload;
function Min (A,B: Int64): Int64; overload;
function Min (A,B: Single): Single; overload;
function Min (A,B: Double): Double; overload;
function Min (A,B: Extended): Extended; overload;
```

当我们调用 Min(10, 20) 的时候，编译器会判断我们是调用第一个函数，所以回传值也会是整数。

多载有两个基本规则：

+ 多载的每个函数(或程序)宣告，结尾都必须加上 overload 这个关键词。
+ 在多载的每个函数(或程序)宣告中，参数的数量或类型必须要有不同，参数名称则无所谓，因为名称在调用过程中不会被特别指出，而回传值
并不会被拿来作为两个多载函数的判别，换句话说，多载函数(或程序)当然可以回传相同类型的数据。

!!! note "笔记"
    规则上有一个例外状况，就是我们不能以回传值来做为多载函数(或程序)的区别依据，且用 Implicit 或 Explicit 转换运算符，这会在第五章里面介绍。

以下是 ShowMsg 程序的三个多载版本，我已经把他们放在 OverloadTest 范例程序里面了(这个应用程序用来展示多载与预设参数):

```pascal
procedure ShowMsg (str: string); overload;
begin
 Show ('Message: ' + str);
end;
procedure ShowMsg (FormatStr: string; Params: array of const); overload;
begin
 Show ('Message: ' + Format (FormatStr, Params));
end;
procedure ShowMsg (I: Integer; Str: string); overload;
begin
 ShowMsg (I.ToString + ' ' + Str);
end;
```

这三个程序都会用讯息框来显示一个字符串，分别以不同的方式对字符串做格式化，以下就是三个不同调用这些程序的源码：

```pascal
ShowMsg ('Hello');
ShowMsg ('Total = %d.', [100]);
ShowMsg (10, 'MBytes');
```

而结果分别如下:

```pascal
Message: Hello
Message: Total = 100.
Message: 10 MBytes
```

!!! tip "提示"
    IDE 的 Code Parameters 技术对多载的程序与函数处理的非常好。当我们在编辑器上面输入了函数名称，且输入了小括号，所有同名的多载函数就会一起被自动列出。当我们输入参数的时候，Code Insight 技术则会依照我们输入的参数类型自动判别符合的多载函数，隐藏其他不符类型的。所以在撰写源码的时候就轻松多了。

如果我们试着调用某个函数，然后传递完全不合其中任何一个多载版本的参数进去会怎样？当然，我们会得到编译器给的错误讯息，假设我们要调用：

`ShowMsg(10.0, ‘Hello’);`

我们就会得到以下这个很特别的错误讯息:

`[dcc32 Error] E2250 There is no overloaded version of 'ShowMsg' that can be called with these 
arguments`

事实上，多载函数的每个多载版本都必须标注上 overload 的关键词，所以我们不能试着多载相同单元文件里，一个已存在但没有标注 overload 关键词的函数，如果我们试着这么做，就会得到以下的错误讯息：

`Previous declaration of '<name>' was not marked with the 'overload' directive.`

然而，我们可以在别的单元文件里面建立一个新的函数，然后把这个新单元文件当成命名空间来处理。在这个情形下，我们就不是新增一个多载版本函数，而是把原来的函数用一个完全新版的同名函数来取代了，原来的函数会被编译器视为被隐藏了(当然还是可以直接把该单元文件的名称作为前述字符串，来调用原始版本的函数)，这也就是为什么编译器不能只从参数来判断，就决定用哪一个多载版本，但编译器也会试着用唯一相符的多载版本函数来处理，但仍会在参数类型不相符的时候发出错误讯息。




### 多载与调用混淆 (Ambiguous	call) 

当我们调用一个多载函数时，编译器通常会试着找到一个相符的多载版本，或者在找不到相符的多载版本时发出错误讯息(就像我们刚刚看过的例子一样)。

但还有第三种情况：假设编译器能够对函数的参数进行类型转换，则对单一个函数调用就有可能有不同的转换。当编译器发现它可以使用的多种多载版本函数，而其中并不包含完全相符的参数类型，则此时编译器发出的错误讯息就会是『此函数调用发生混淆』。

这种情形并不多见，我得建立一个不合理的范例来说明这个情形，但观察这个案例是值得的(虽然实际撰写程序的时候发生的机率非常之低)。假设我们决定要实作两个多载版本的函数来处理整数跟浮点数的加总：

```pascal
function Add (N: Integer; S: Single): Single; overload;
begin
 Result := N + S;
end;
function Add (S: Single; N: Integer): Single; overload;
begin
 Result := N + S;
end;
```

这些函数都在 OverloadTest 范例项目里面。现在我们可以调用它们，指派两种不同的参数给它们：

```pascal
Show (Add (10, 10.0).ToString);
Show (Add (10.0, 10).ToString);
```

然而，事实上通常一个函数可以在参数经过类型转换后接受该参数，例如一个函数要求浮点数类型，但可以在整数资料经过类型转换以后接受它，所以我们可以写成：

`Show (Add (10, 10).ToString);`

编译器会使用第一个多载版本，但第二种多载版本也会被调用。如果不知道我们的要求(或者知道调用该版本时，仍有其他多载版本可能导致这种问题时)，则以下错误讯息就会被回报出来：

```
[dcc32 Error] E2251 Ambiguous overloaded call to 'Add'
 Related method: function Add(Integer; Single): Single;
 Related method: function Add(Single; Integer): Single;
```

!!! tip  "提示"
    在 IDE 的错误讯息面板里，我们可以看到上述讯息的第一行，在该行错误讯息的左边有个加号，点开它，就可以看到其他的完整错误讯息了。

如果我们在实际上写程序的时候遇到上面这个情形，而我们真的需要调用这个函数，我们可以自己先做类型转换来解决这个问题，并让编译器可以判别我们要调用的是哪一个多载版本：

`Show (Add (10, 10.ToSingle).ToString);`

实际会发生混淆调用的情形，会是我们使用 variant 这种类型作为参数的时候，因为这个参数类型可以包含许多种不同特定类型，我们在本书稍后的篇幅会加以讨论。


### 预设参数 (Default	Parameters)  

另一个跟多载相关的技术，是在函数或程序的参数里面可能先设定了预设的参数值，所以在我们调用函数的时候，写不写该参数都可以被编译器接受。如果我们没有指定该参数，编译器就会以默认值传给该函数或程序。

我们来看一个例子(这个例子仍旧是 OverloadTest 范例项目的一部分)，我们可以定义以下的程序来封装对 Show 函数的调用，提供两个预设参数：

```pascal
procedure NewMessage (Msg: string; Caption: string = 'Message'; Separator: string = ': ');
begin
 Show (Caption + Separator + Msg);
end;
```

藉由上述的定义，我们可以用以下的方式来调用这个函数：

```pascal
NewMessage ('Something wrong here!');
NewMessage ('Something wrong here!', 'Attention');
NewMessage ('Hello', 'Message', '--');
```

会得到以下的输出结果:

```
Message: Something wrong here!
Attention: Something wrong here!
Message--Hello
```

请注意，编译器没有建立任何特别的源码来协助预设参数，也不用为这个函数制作任何多载版本。没有输入的参数，编译器就自动的加入到调用函数的源码里去了。只有一个简单的规则要遵循：我们不能跳过任何一个参数，例如我们不能只提供第一跟第三个参数，然后只跳过第二个。

当然定义上还是有一些其他的规则，调用上也有些要注意的地方:

+ 在调用时，我们必须从最后一个参数开始跳过，如果我们要跳过参数，就得从最后面一个一个来。
+ 在定义上，有默认值的参数必须放在整列参数的最后面。
+ 默认值必须是常数，显然的，这也限制了预设参数能够使用的类型，像是可变数组、interface 就不能以 nil 之外的值来当成其默认值，而 record更是完全不能当成预设参数。
+ 有默认值的参数必须以传值形式进行，或者当成常数(const)。传址(var)的参数是不能有默认值的。

同时使用预设参数和多载，我们就有更多机会可以让编译器昏头转向，例如发出混淆调用的错误提示，就像我们前一节介绍过的那样。举例来说，如果我在前一个范例里面加入以下的新版多载函数：

```pascal
procedure NewMessage (Str: string; I: Integer = 0); overload;
begin 
 Show (Str + ': ' + IntToStr (I))
end; 
```

这时编译器并不会发出错误通知，因为这是个合法的定义。然而以下这个调用：


`NewMessage ('Hello'); `

则会导致编译器提出错误讯息：

```
[dcc32 Error] E2251 Ambiguous overloaded call to 'NewMessage'
 Related method: procedure NewMessage(string; string; string);
 Related method: procedure NewMessage(string; Integer); 
```

请注意这个错误是发生在新的多载版本出现前，编译完全正确的一行指令上面。实务上我们应该不可能调用 NewMessage 这个程序，而只提供一行字符串作为参数，或者提供一个字符串参数与整数参数做为其默认值。当有类似的疑虑时，编译器就会要求程序人员把其意图表示的更明确一点。


## 内嵌源码 (Inlining) 129

在 Object Pascal 当中内嵌函数和方法是低级语言的功能，透过这个作法，可以得到显著的优化。通常当我们调用一个方法，编译器会建立一些源码，让我们的程序进入一个新的执行点。这表示设立了一个堆栈框架，并开始处理一些程序，可能需要一些机器指令。然而，我们执行的方法可能非常简短，甚至调用一个方法可能只是设定或者回传一些私有字段。

在这种情形下，复制这些源码到实际调用它们的地方，就非常有用了，可以避免堆栈框架的设定或其他相关事情的衍生。去除了这些额外的动作之后，我们的程序当然可以执行更为快速，特别是当这个函数在循环当中被调用了上千上万次的时候，每次省一点时间，整个执行过程所节省的时间就更为可观了。

对于一些很小的函数来说，回传值的源码甚至更小，这些源码可能比调用一个函数本身所耗用的空间来的更小。然而，请注意到，假如一个稍大的函数是内嵌的，而这个函数在我们的源码当中很多不同的地方都有调用到这个函数，我们的源码可能变得比较大，而这对于执行档来说，会是不必要的空间增加。

在 Object Pascal 里，我们可以要求编译器把一个函数(或方法)进行内嵌，内嵌的关键词是 inline，只要在宣告函数或方法的源码之后加上 inline 这个关键词即可。不用在定义函数的地方重复这个关键词。请牢记，inline 关键词对编译器来说只算是个提示，编译器仍旧可能判断该函数(或方法)以内嵌方式编译对整个程序来说并没有好处，而直接把这个关键词给忽略不管(不会提供任何警告讯息喔)。编译器也可能在完成分析所有源码之后，依照$INLINE 开关的状态，把部分函数或方法给内嵌，但不会把所有函数完全都内嵌。这个开关可能是以下三种不同的设定值之一(请注意，这个功能是独立于编译器优化的设定之外的喔)：

+ 默认值，{$INLINE ON}，对有标注要进行内嵌的函数或方法全部进行内嵌。
+ {$INLINE OFF}，我们可以停止所有内嵌的处理，可以针对整个程序、部分程序或者特定的函数，除非有出现 inline 关键词的函数，否则其余全部内嵌的功能都会被停止。
+ {$INLINE AUTO}，编译器通常会对我们标注 inline 关键词的函数进行内嵌，也会把一些很短的函数自动内嵌，使用这个设定的时候务必小心在意，因为可能让我们的执行程序档案变大。

在 Object Pascal 的运行时间函数库里就有许多被标注 inline 关键词的函数。举例来说，Math 单元里的 Max 函数就被这么定义的：

`function Max(const A, B: Integer): Integer; overload; inline; `

为了实地测试内嵌这个函数的效果，我在 InliningTest 项目里面，写了以下的循环：

```pascal
var 
 Sw: TStopWatch;
 I, J: Integer;
begin
 J := 0;
 Sw := TStopWatch.StartNew;
 for I := 0 to LoopCount do
 J := Max (I, J);
 Sw.Stop;
 Show ('Max ' + J.ToString +
 ' [' + sw.ElapsedMilliseconds.ToString + '] ');
```

在这段源码里面，System.Diagnostics 单元的 TstopWatch 记录，这是一个会持续监控从 Start 指令(或者 StartNew 指令)被下达之后所耗费的时间(或者系统 CPU 的周期-ticks)，调用 Stop 之后，这个监控就会停止。

这个窗体有两个按钮，两个都会调用同一个函数，但一个在调用的时候停用了 inline 开关。请注意，我们必须使用 Release 组态来编译，才能看出两者之间的不同(因为内嵌是属于 Release 优化的一部分，Debug 组态不会对它有反应)。在我的计算机上面进行了 2 千万次(这个数字是 LoopCount 的常数)的互动以后，得到了以下的数字:

```pascal
// on Windows (running in a VM)
Max on 20000000 [17]
Max off 20000000 [45]
// on Android (on device)
Max on 20000000 [280]
Max off 20000000 [376]
```

我们该怎么解读这些数据？在 Windows 系统上，内嵌程序快了将近一倍的速度，而在 Android 系统上面，内嵌程序也快了将近 35%。然而在 Android装置上面执行的速度比计算机慢上许多(这个差距可是一个数量级的差距)，所以我们在 Windows 上面节省了 3 秒钟，而在 Android 装置上，这个内嵌的优化省下了将近 10 秒钟。

同样的程序还进行了第二个类似的测试，是透过 Length 函数来进行。这个函数有编译魔术在里面，会因为内嵌的优化而有很大的不同。再一次，进行内嵌的版本在 Windows 跟 Android 上面都有非常明显的变快：

```pascal
// on Windows (running in a VM)
Length inlined 260000013 [11]
Length not inlined 260000013 [40]
// on Android (on device)
Length inlined 260000013 [401]
Length not inlined 260000013 [474]
```

这是用来作第二种测试的循环：

```pascal
var
 Sw: TStopWatch;
 I, J: Integer;
 Sample: string;
begin
 J := 0;
 Sample:= 'sample string';
 Sw := TStopWatch.StartNew;
 for I := 0 to LoopCount do
 Inc (J, Length(Sample));
 Sw.Stop;
 Show ('Length not inlined ' + IntToStr (J) +
 ' [' + IntToStr (Sw.ElapsedMilliseconds) + '] ');
end;
```

Object Pascal 的编译器并不会清楚定义一个用内嵌或特定结构排除被内嵌的方式(for 或是 while 循环，条件式指令)建立的函数，编译出来的源码要限制多大的 Size。然而因为把一个大的函数进行内嵌，得到的优点跟让我们的程序暴露在风险的缺点相较之下，得到的少，失去的多，所以我们应该避免这么做。

其中一个限制，是这个函数或方法不能参考到任何定义在同一个单元文件的implementation 区段的识别符号(例如类型、全局变量或函数)，而他们在调用的源码中也不能被使用到。然而，如果我们调用的是一个区域函数，则内嵌这个函数就不会有任何问题了，编译器会遵照我们的要求，把这个函数进行内嵌。

内嵌函数也有一个缺点，就是会使得单元文件要被编译的更频繁，当我们修改了一个内嵌函数，所有调用到这个函数的单元文件就需要重新编译才能使用修改后的源码。在同一个单元文件里面要调用内嵌函数的话，就必须要在内嵌函数的源码之后才能调用它，所以我们要使用内嵌函数的话，最好把它写在整个单元文件的 implementation 区段中，作为第一个函数。

!!! note "笔记"
    Delphi 使用单次编译过程的编译器，所以我们无法使用还没编译源码的函数。

如果是在不同的单元文件，我们就必须把包含内嵌函数的单元文件加入到我们的 uses 区段，即使我们没有直接调用这些方法也一样。假设我们的 A 单元文件调用了 B 单元文件里面的一个内嵌函数，如果这个函数又调用了 C单元文件里面的另一个内嵌函数的话，我们的 A 单元文件就必须也要引入 C单元文件。不然的话，我们会在编译的过程看到编译器的警告讯息，提示无法内嵌该函数，因为缺少了一些被参考的单元。相关的作用就是如果单元之间相互参考，则这里面的所有内嵌函数都不会真的被内嵌。


## 函数的进阶功能


如果目前我们所介绍的已经包含函数相关的核心功能，那么还有几个进阶的功能也值得一提。如果您是软件开发的初学者，然而您可能已经想跳过本章剩下的篇幅，直接去看下一章了。


### Object	Pascal 调用函数的约定(Conventions)133

不论何时，当我们的程序调用一个函数，两边都必须同意实际上参数从调用端被传递到被调用端的实务作法，这些我们称之为调用约定。通常，调用函数时，会把参数(如果有回传值的话也透过同样的方式传递)透过内存堆栈区进行传递。然而，参数或回传值在内存堆栈里面的顺序，会随着编程语言与操作系统的不同而有差异。多数编程语言都使用各自多种不同的调用约定。

很久以前，32 位版本的 Delphi 提出了一种传递参数的新方式，称之为 fastcall，在任何时候，三个以内的参数可以直接透过 CPU 的缓存器(Register)进行传递，这个方式会使得函数调用的速度快上许多。Object Pascal 预设使用fastcall 这种方式作为调用约定，因此也需要使用到 register 这个关键词。

Fastcall 是预设的调用约定，而使用这个约定调用的函数就无法兼容于外部函数库，例如 Win32 版的 Windows API。Win32 API 的函数必须使用stdcall(Standard call: Windows 的标准调用)作为调用约定，而原始的 Pascal的调用约定则是 Win16 API 的 cdel，也就是 C 语言的调用约定。这些不同的调用约定在 Object Pascal 里面全都兼容，但是我们不常使用跟默认值不同的调用约定，除非我们需要使用到透过不同编程语言撰写的函数库，例如系统函数库。

我们需要使用到不同于预设调用约定的情形，就是当我们需要使用到一个操作系统的原生 API 时，这时我们会被要求使用该平台专属的调用约定。即使是 Win64，使用的调用约定也跟 Win32 不同，所以 Object Pascal 支持很多不同的选项，在这里我们先不详述。而行动作业平台常常愿意揭露类，而不揭露原生函数。即使在这种情形下，我们也必须考虑到尊重该平台的调用约定的问题。


### 程序类型 (Procedural	Types)

另一个 Object Pascal 独特的功能，则是程序类型的存在。这是很进阶的编程语言问题，只有很少数的程序人员会常用到它。然而，既然我们在后面的章节会讨论其他的议题(例如方法指标，在整体环境的技术上大量使用，用以定义事件处理程序、以及匿名方法等技术)，那我们就该花点时间在这里浏览一下这个主题了。

在 Object Pascal 里(不是在传统的 Pascal 语言里面喔)，存在着程序类型的概念(这个概念像极了 C 语言里面函数指针的概念-在 C#跟 JAVA 里面都舍弃了这个功能，因为这个功能会和全局变量紧密结合)。

程序类型的宣告会需要指定一连串的参数以及回传值来定义函数。举个例子，我们可以宣告一个新的程序类型，需要一个整数参数以传址形式传入：

`type TIntProc = procedure (var Num: Integer);`

这个程序类型兼容于具备完全符合的参数的程序(用 C 语言的用语来说，就是函数特征相同)，以下就是一个兼容于前述类型的程序：

```pascal
procedure DoubleIt(var Value: Integer);
begin
 Value := Value * 2;
end;
```

程序类型通常有两个要求：我们可以以程序类型声明变量，或者把程序类型当成参数类型传给另一个函数。假设前述的类型跟程序宣告属实，我们可以写下这些源码：

```pascal
var
 IP: TIntProc;
 X: Integer;
begin
 IP := DoubleIt;
 X := 5;
 IP (X);
end;
```

这段源码跟较短版本的源码效果相同：

```pascal
var
 X: Integer;
begin
 X := 5;
 DoubleIt(X);
end;
```

第一个版本明显的比较复杂，所以我们什么时候，为什么要用它？某些情形下，能够决定要调用哪个函数，并实际上稍晚再调用它，是很有权力的。要找到能够达成相同目标的范例是完全可能的。然而，我希望我们能看一个比较单纯的例子，叫做 ProcType。

这个范例是以两个程序作为基础，一个函数用来把参数值算出两倍的值为何，这个函数我们已经看过很多次了。范例基于两个函数，其中之一用来把参数的值加倍。另一个函数则是用来把参数数值的数字变成三倍，所以这个名称就改为”TripleIt”:

```pascal
procedure TripleIt(var Value: Integer);
begin
 Value := Value * 3;
end;
```

虽然没有直接调用这些函数，其中一些已经被存成程序类型的变量了。这个变量会在用户选择一个核取方框(checkbox)时有一些改变，目前的程序是以传统方式仿真用户点击按钮。这些程序使用了两个全局变量(被调用的函数，以及当前的现值)，所以这些数值都会被经年累月的保留下来。以下是完整的源码，请以真实程序的完整定义把这些数据封装起来吧：

```pascal
var
 IntProc: TIntProc = DoubleIt;
Value: Integer = 1;
procedure TForm1.CheckBox1Change(Sender: TObject);
begin
 if CheckBox1.IsChecked then
 IntProc := TripleIt
 else
 IntProc := DoubleIt;
 end;
procedure TForm1.Button1Click(Sender: TObject);
begin
 IntProc (Value);
 Show (Value.ToString);
end;
```

当用户点击复选框(checkbox)，改变了组件的状态，所有按键点击时都会调用一个实际的程序，所以如果我们点了按钮两次，改变了选取状态，然后再点按钮两次，我们会先把参数成倍两次。然后再把现值先变成两倍，再变成三倍，如下所示：

```
2
4
12
36
```

另一个使用到程序类型的实例，是当我们要把一个函数传递给操作系统时，例如 Windows(通常在操作系统中都有所谓的 callback 函数)。在这个章节要开始的时候有提到过，Delphi 程序人员使用方法指针来处理程序化类型(我们会在第 10 章介绍)以及匿名方法(我们在第 15 章加以介绍)：


!!! note "笔记"
    最常用的面向对象机制，就是取得一个稍后进行绑定的函数(是一个可以在运行时间进行变更的函数)，也就是虚拟方法。当虚拟方法在 Object Pascal里面非常平常，Object Pascal 的程序化方法并不寻常。这些技术的基础，都相当的类似，虚拟函数跟多型将会在第 8 章进行说明。


### 宣告外部函数

系统程序的另一个重要元素，是由外部宣告的函数来承担的。最早使用到外部函数库连结的源码，是以汇编语言撰写的。外部连结函数变成非常普及，始于 Windows 程序里面调用动态链接函数库(DLL, Dynamic Link Library)。外部函数的宣告，表示具备能够调用一个函数，该函数未必由编译器或链接程序制作或掌控，但必须具备能够加载外部动态函数库，并执行当中函数的能力。


!!! note "笔记"
    无论我们什么时候在 Object Pascal 源码里面调用某个平台专用的 API，我们都会失去把该程序编译成其他平台的能力。唯一的例外，是我们把调用特定平台的源码透过$IFDEF 编译开关包起来。

举例来说，我们在 Delphi 源码里面调用了 Windows API 的函数。如果打开Winapi.Windows 这个单元文件来看，我们会发现里面有很多的宣告跟定义，长得像这样：

```pascal
// forward declaration
function GetUserName(lpBuffer: LPWSTR; var nSize: DWORD): BOOL; stdcall;
// external declaration (instead of actual code)
function GetUserName; external advapi32
 name 'GetUserNameW';
```

我们很不常有机会需要写上面这样的源码，因为他们已经被列在 Windows单元文件以及其他系统单元文件里面了。我们只有在需要使用到自定的DLL里面的函数时，或者需要调用一些系统没有宣告、转译成 Pascal 宣告语法的Windows API 时，才会需要写这样的宣告。

上述的宣告，是意指在 advapi32 这个动态链接函数库里面，存在着GetUserName 这个函数(advapi32 是一个宣告过的函数库名称，完整档名是advaoi32.dll)。GetUserName 提供了 ASCII 与 WideString 两种版本，而在做外部函数声明的时候，我们可以自己指定让外部函数在我们的源码里面用另一个不同的函数名称。

**延迟加载动态链接函数库的函数**

在 Windows 操作系统里，提供了两种方式来调用 Windows SDK 与其他 API的函数：我们可以让应用程序加载器把所有外部函数都先行解译，或者在我们需要的时候才自行加载特定的函数。

这样的源码并不难写(就像我们前一节看过的): 我们要做的事，就是做个外部函数声明而已。然而如果函数库，或者我们要调用的函数都没能找到，我们的程序就会无法在没有提供该函数库的操作系统上面执行。

动态加载提供了很大的弹性，我们得自己处理加载函数库的程序，使用的是GetProcAddress API，来找到我们想调用的函数，然后把该函数的指标转换成适当的类型之后就能调用它了。这样的源码是相当繁复且容易出错的。

这就是为什么 Object Pascal 编译器跟链接程序支持 Windows 操作系统层级的功能，当需要使用该函数库的时候才延迟加载该函数，且已经用在部分C++编译器上会大受欢迎了。这个宣告的目的并不是为了避免 DLL 的错误载入，反正这情形总是会发生的，而是为了让 DLL 里面的特定函数得以被延迟载入。

基本上我们写程序的方法跟传统执行 DLL 的函数非常相似，但函数的地址早在程序第一次被调用的时候就被解析出来了，并不是在程序加载时就解析好的喔。这表示如果当时函数无法被使用，我们就会看到一个运行时间例外发生了(EExternalException)。然而，我们通常可以检查我们要加载的目前操作系统的版本，或者特定函数库的版本，然后决定是否要调用这个函数。


!!! note "笔记"
    如果我们想要使用比例外处理更简单、明确的方式来处理这种情形，我们可以直接挂载(hook)到延迟加载函数的错误处理机制。Allen Bauer 在他的部落格上面有很精辟的说明:https://blog.therealoracleatdelphi.com/2009/08/exceptional-procrastination_29.html


从 Object Pascal 语言来看，对于外部函数声明的唯一不同，就是宣告方法，以前我们可能会写成:

```pascal
function MessageBox;
 external user32 name 'MessageBoxW';
```

但我们现在可以写成(这也是从 Windows 单元文件里面节录出来的):

```pascal
function GetSystemMetricsForDpi(nIndex; Integer; dpi: UINT): Integer;
 stdcall; external user32 name ‘GetSystemMetricsForDpi’ delayed;
```

在运行时间，考虑到这个API是在 Windows 10, 1607版之后的版本才有的，在首次加载的时候，我们可能会想把源码写成这样：

```pascal
if (TOSVersion.Major >= 10) and (TOSVersion.Build >= 14393) then
begin
 NMetric := GetSystemMetricsForDpi (SM_CXBORDER, 96);
```


比起旧版的 Windows 里，同样功能的程序写法，这段程序已经是少到不能再少了。

另一个相关的观察，则是我们可以在建立我们自己的 DLL，以及从 Object Pascal 里面调用这些 DLL 的函数时使用同样的机制。我们可以提供单一的执行档，让这个执行档能够和同一个 DLL 的不同版本进行绑定，这样我们就可以透过延迟加载函数的功能来使用该函数的新版本了。


