
在任何 Object Pascal 编程语言的应用程序核心里，都有类的架构存在。在系统的每一个类都一定是从 TObject 类所衍生出来的，所以整个架构只有单一的根源。这使得我们可以使用 TObject 数据类型来做为系统所任何一个类的数据类型的替代品。

TObject 类是定义在核心的 RTL 当中，单元文件的名字是 System，因为这个单元文件非常重要，所以在任何的编译动作当中，都会自动引入这个单元文件。我们不会逐一介绍所有在 System 单元文件里面的类跟函数，我们要把心力放在 TObject 这个最重要的主角之上。

!!! note "笔记"
    我们可以花很长的篇幅来争论核心系统类，像 TObject 是否算是 Object Pascal 编程语言的一部分，或者算是执行时期函数库的一部分。其他在System 单元文件的功能也一样，一个单元文件重要到会自动在编译时被引入。(事实上如果我们把它加入到 uses 区块，反而会造成错误)。这样的争论没有意义，所以我们在这里不去谈他。

## TObject 类

正如我刚提到的，TObject 类是非常特殊的类，因为所有其他的类都继承自 TObject。当我们宣告一个新的类时，事实上，如果我们没有指定一个基础类，该类就会自动继承自 TObject 了。在编程语言的术语里，这样的类型系统被称为『单一根源类架构』，这是 Object Pascal 的功能，C#，Java 跟一些现代的编程语言也都如此设计。值得一提的例外是 C++，它没有单一根源的基础类，且允许我们定义多重完全分离的类架构。

TObject 这个基础类并不是一个让我们直接建立实体来用的类。然而我们最后可以很容易的在很多地方使用它。每次我们需要一个可以用来储存对象或其任何其他类型变量时，我们就可以把它宣告为 TObject 类型。这个用法有个很好的例子，就是在组件函数库当中的事件处理程序，所有的事件处理程序通常都使用 TObject 作为第一个参数的类型，通常称之为 Sender。这表示任何实际类的对象都可以作为 Sender。很多泛型集合也是对象的集合，且有一些情境中，TObject 类型是直接被用上的。

在以下的情境当中，我会介绍到这个类的一些功能，这些功能是所有位于 System 单元文件里面的类都可以用的。



### 建立与毁灭

虽然直接建立 TObject 没有什么意义，不过这个类的建构函数跟解构函数还是很重要的，因为它们会被所有其他类自动继承。如果我们定义了一个没有建构函数的类，我们还是可以调用它的 Create 方法，这会调用TObject 的建构函数，这是个空的函数(因为在这个基础类中没有什么要初始化的)。这个 Create 建构函数不是虚构的，且我们可以在自定的类中把它整个替换掉，除非这个没做什么事的建构函数就已经够用了。调用基础类的建构函数，对任何的子类实作都是一个好习惯，即使直接调用TObject.Create 没有什么特别的用处。

_____________________________________________________________________

!!! note "笔记"
    我已经强调这是一个非虚拟的建构函数，因为有另一个核心函数库类，TComponent，是有定义虚拟建构函数的。TComponent 类的虚拟建构函数是整个串流化系统当中的关键角色，我们将在下一章里面介绍。

为了毁灭一个对象，TObject 类里有一个 Free 方法(这个方法最后会调用Destroy 解构函数)，我们已经在第 13 章里面介绍过了，并在该章里面提出许多建议来端正内存使用的风气，所以在这里就不再赘述了。


### 物件的二三事(Knowing	About	an	Object)

在 TObject 里面有一组有趣的方法，它们会回传关于类型的一些信息。最常用的就是 ClassType 跟 ClassName 方法。ClassName 方法会把类的名称用字符串来回传。因为它是一个类方法(就像大多数 TObject 的类方法一样)，我们可以透过对象或类来调用它。假设我们定义了一个对象名为TButton，并以这个类建立了一个名为 Button1 的对象。那么以下的两杭源码，回传的结果就会完全相同：

```pascal
Text := Button1.ClassName;
Text := TButton.ClassName;
```

当然，我们也可以把这些应用在常见的 TObject 上面，但我们不会得到TObject 的信息，而是关于该对象变量所属的类的信息。例如在按钮的OnClick 事件处理程序里，我们可以调用:

`Text := Sender.ClassName;`

这会回传跟前面两个指令一样的结果，回传值将会是”TButton”这个字符串。这是因为类名称是在执行时期决定的(由特定的对象本身决定)，而不是由编译器来决定的(编译器只会认为它是一个 TObject 类型的对象)。当然，如果参考的内容没有被指派，也就是说变量内容是 nil，任何试图调用该类方法都会造成例外。

取得类名称对于侦错、记录跟一般显示类信息都是很有用的，通常它也对于存取该类的类参考更为重要。举例来说，比较两个类参考(会是两个数值，记录内存地址)比用两个类的名称来做字符串比对更好。我们可以透过 ClassType 方法来取得类参考，而 ClassParent 方法则会回传当前对象的基础类的类参考，允许浏览基础类列表。唯一的例外是当该方法回传 TObject 是 nil 时(因为它没有父代类)。一旦我们有了类参考，我们就可以用它来调用任何类方法，包含 ClassName 方法。

另一个也很有趣的方法会回传关于类的信息，是名为 InstanceSize 的方法，这个方法会回传一个对象在执行时期的大小，回传的这个数字是指该对象的数据字段所要求的大小总和(以及继承自基础类的数据)。这个功能是当系统需要为该类的新实体进行配置时，内部使用的功能。

!!! note "笔记"
    虽然我们可能会觉得 SizeOf 这个全局函数也会提供相同的信息，但这个函数实际上是回传对象参考的大小，也就是指标的大小，指标现在不是 4 bytes就是 8 bytes，要看是在 32 位还是 64 位的操作系统上-跟对象本身所占用的空间完全无关。另一方面 InstanceSize 会回传该字段使用的空间大小，但并非该对象实际使用的内存空间总和，因为该字段可能是指向字符串或者其他对象的参考，这就可能使用了额外的内存。


### 更多 TObject 类的方法

还有些 TObject 类的方法我们可以用在任何对象上(也可以用在任何类或类参考，因为这些方法是类方法)。以下是一部分的列表，搭配简单的描述：

+ ClassName 以字符串回传该类的名称，主要是用以显示。
+ ClassNameIs 检查类名称是否与参数值相同
+ ClassParent 回传当前类的父类的类参考或者对象的类。我们可以从 ClassParent 浏览到 ClassParent，直到浏览的对象到达 TObject为止，对 TObject 调用这个方法，回传值会是 nil。
+ ClassInfo 回传类所属的内部的，低阶的执行时期类型信息(RTTI)。这是早期在 TypInfo 单元文件里面使用的，但现在已经以 RTTI 单元文件的功能取代了它，我们在第16章已经介绍过了。目前仅剩内部使用，它还是类取得 RTTI 信息的途径。
+ ClassType 回传该对象所属类的参考(这不是类方法，所以只能透过对象来调用)。因为这是对象参考，两个在不同单元文件宣告的类不会相同，所以在像 Delphi 这样的强类型语言里面不会有问题。
+ InheritsFrom 检测一个类是否继承自(直接或间接都算)特定的类(这效果很像 is 运算符号，但 is 运算符号的实作比较全面)。
+ InstanceSize 回传对象的数据大小，单位是 Bytes。这个数字会是所有字段的总和，加上一些额外的特定保留位(包含例如类参考)。要留意到。再一次留意到，这是实体的大小，而指向实体的参考只是一个指标(4 或 8 bytes，要看操作系统而定)
+ UnitName 回传定义该类的单元文件的名字。对于描述一个类是很有用的。事实上，类名称在系统中并不是唯一的。当我们看完最后一章，就会知道只有完整连同单元文件一起列出的类名称(包含单元文件名称跟类名称的组合，以.加以连接)在应用程序中才是唯一的。
+ QualifiedClassName 回传整个单元文件跟类名称的连结，在执行中的系统里，这个值会是唯一的。

以上 TObject 的方法适用于每一个类的对象，因为 TObject 是每个类共通的祖先类。

以下是我们透过这些方法来存取类信息的例子:

```pascal
procedure TSenderForm.ShowSender(Sender: TObject);
begin
 Memo1.Lines.Add (‘Class Name: ‘ + Sender.ClassName);
 if Sender.ClassParent <> nil then
 Memo1.Lines.Add (‘Parent Class: ‘ + Sender.ClassParent.ClassName);
 Memo1.Lines.Add (‘Instance Size: ‘ + IntToStr (Sender.InstanceSize));
```

这段源码会检测看看 ClassParent 的内容是不是 nil，万一我们正好用到TObject 类型的实体，它就不会有基础类了。我们可以用其他方法来进行检测。例如，我们可以用以下的源码来检验 Sender 对象是不是特定的类型：

`if Sender.ClassType = TButton then ...`

我们也可以检测 Sender 参数是否对应到特定的对象，用这个方式检测:

`if Sender = Button1 then...`

不检查特定的类或对象，我们通常需要测试某个类的对象类型是否兼容，也就是说，我们会需要检查一个类的对象是否属于特定的类或者该类的子类。这会让我们知道我们是否可以透过该对象调用定义在类中的方法。这个检测可以透过调用 InheritsFrom 方法来完成，当我们使用到 is 运算符号的时候，这个运算符号的其中一个不同，是它也会处理 nil。

以下两个测试是完全一样的：

```pascal
if Sender.InheritsFrom (TButton) then ...
if Sender is TButton then ...
```

**显示类信息**

当我们取得类参考时，我们可以把这个类参考的所有基础类的名称加入到一个列表中。在以下的程序片段中，MyClass 的所有基础类都会被加到 ListBox 组件当中：

```pascal
ListParent.Items.Clear;
while MyClass.ClassParent <> nil do
begin
 MyClass := MyClass.ClassParent;
 ListParent.Items Add (MyClass.ClassName);
end;
```

您应该注意到了，我们在 while 循环当中使用了类参考，这个类参考会用来检查该类是否存在父代类(万一目前的类是 TObject, 就不会有父代类)。或者我们也可以把 while 的判断条件写成这样:

```pascal
while not MyClass.ClassNameIs ('TObject') do... // Slow, error prone
while MyClass <> TObject do... // Fast, and readable
```



### TObject 的虚拟方法

从 Object Pascal 的早期，TObject 类的结构中就已经相当稳定，我们可以从当中找到三个很有用的虚拟方法。这些方法可以被任何对象调用，就像TObject 的其他方法一样，但相关的是，这些方法我们在自己建立的子类中都应该覆写或重新写过。

!!! note "笔记"
    如果您已经使用过.NET 架构，您可能会立刻发现这些方法是 C#基础类函数库的 System.Object 类的一部分。类似的方法也在 Java 当中被设计在基础类当中，在 JavaScript 里面也很常见，其他语言当中也是。这些方法(例如 toString)的来源，可以追溯到 SmallTalk，它应该算是第一个 OOP 语言。

**ToString方法**

ToString 这个虚拟方法是提示要回传以文字化表示(描述或者是把对象进行串行化的结果)一个特定对象。在 TObject 当中预设的实作源码是回传该类的名字:

```pascal
function TObject.ToString: string;
begin
 Result := ClassName;
end;
```

当然这样距离实用还远的很。理论上，每个类都应该提供一个方式来把自己对使用者作介绍，例如当一个对象被加到可视化的列表时。在运行时间函数库中的部分类有对这个函数进行覆写，像是 TStringBuilder, TStringWriter,以及 Exception 类，Exception 类会回传整个列表的例外讯息(我们已经在第九章的巢状例外与内部例外机制这一节当中介绍过)。

透过一个标准的方法为任何对象回传文字表现形式是相当有意思的想法。而且我推荐大家要好好利用 TObject 类这个核心功能，把它当成编程语言内建功能一样。

!!! note "笔记"
    要注意 ToString 方法在『语义上多载』了在 Classes 单元文件中定义的toString 标识符，该定义的意思是『解析 token 字符串』。因此它参照了Classes.toString。

**Equals方法**

Equals 虚拟函数是提示要检测两个对象是否有相同的逻辑内容，这个检测跟检测两个变量是否在内存中指向同一个对象是不同的，如果只是要检测两个变量是否在内存当中指向同一个对象，我们可以用=这个运算符号。然而，这样看上去的确挺让人迷惑的，预设的实作源码如下，我们该想想有没有更好的办法:

```pascl
function TObject.Equals(Obj: TObject): Boolean;
begin
 Result := Obj = Self;
end;
```

举个使用这个方法的例子(透过适当的覆写)，在 TStrings 类当中的 Equals方法会把该类的字符串列表的总数跟内容一一进行比对，如果完全相符的话，实际字符串的内容会被一一比对，直到有其中一个项目不相符或者到最后一项也相同，如果到最后一项都还相同，就代表整个字符串列表完全相符。


使用这个技术最显著的函数库是对泛型支持的功能，特别是在 Generics.Default 跟 Generics.Collections 单元。通常在函数库或架构中把对象概念定义为”内容是否相等”，而不是”是否是同一个对象”是很重要的。能透过一个标准的机制来比对对象的『内容』绝对是很大的好处。

**GetHashCode方法**

GetHashCode 虚拟函数是从.NET 架构借来的另一个提示，让每一个类可以为其对象计算哈希值(Hash)。预设的源码会回传一个整数值，看起来是该对象的地址：

```pascal
function TObject.GetHashCode: Integer;
begin
 Result := Integer(Self);
end;
```

!!! note "笔记"
    对象建立时的内存地址，通常都是在 heap 内存里面的限定区域，所以用这个数字来建立哈希值并不实际，对于 Hash 算法的使用会有负面的影响。强烈建议大家要自行重写这个方法，依照对象内部的逻辑数据与好的 Hash 算法来建立哈希值(Hash)，不要用对象的地址。这个作法在效能表现上会有显著的改善喔。

GetHash 虚拟方法在不少集合类(只要有支持哈希表-Hash Table 的类)当中都有用到，且这些类是把 Hash 当成优化部分程序的方法，像是`TDictionary<T>`。

**使用TObject虚拟方法**

以下是以 TObject 虚拟方法为基础的范例，这个范例中有一个类，它覆写了这些虚拟方法当中的两个：

```pascal
type
 TAnyObject = class
 private
 FValue: Integer;
 FName: string;
 public
 constructor Create (AName: string; AValue: Integer);
 function Equals(obj: TObject): Boolean; override;
 function ToString: string; override;
 end;
```

在这三个方法的实作中，我单纯的把调用 GetType 改成调用 ClassType:

```pascal
constructor TAnyObject.Create(AName: string;
 AValue: Integer);
begin
 inherited Create;
 FName := AName;
 FValue := AValue;
end;
function TAnyObject.Equals(Obj: TObject): Boolean;
begin
 Result := (Obj.ClassType = self.ClassType) and
 ((Obj as TAnyObject).Value = self.Value);
end;
function TAnyObject.ToString: string;
begin
 Result := Name;
end;
```

注意到对象是否相同，是依照它们是否属于同一个类，且当中的数据是否相同，当中判断的字符串表现内容，只包含了 FName 这个字段。这个程序在启动时，以这个类建立了一些对象:

```pascal
procedure TFormSystemObject.FormCreate(Sender: TObject);
begin
 Ao1 := TAnyObject.Create ('Ao1', 10);
 Ao2 := TAnyObject.Create ('Ao2 or Ao3', 20);
 Ao3 := Ao2;
 Ao4 := TAnyObject.Create ('Ao4', 20);
 ...
```

注意到这两个参考(Ao2 跟 Ao3)是指向内存当中的同一个对象，且最后一个对象(Ao4)有着相同的数值内容。这个程序具备了用户接口，让用户可以选择任两者，并对被选上的对象进行比较，同时使用 Equals 跟直接透过参考来做比较。以下是比较的一些结果：

```
Comparing Ao1 and Ao4
Equals: False
Reference = False
Comparing Ao2 and Ao3
Equals: True
Reference = True
Comparing Ao3 and Ao4
Equals: True
Reference = False
```

这个程序有另一个按钮，用来检测该按钮的其他方法：

```pascal
var
 Btn2: TButton;
begin
 Btn2 := BtnTest;
 Log ('Equals: ' +
 BoolToStr (BtnTest.Equals (Btn2), True));
 Log ('Reference = ' +
 BoolToStr (btnTest = Btn2, True));
 Log ('GetHashCode: ' +
 IntToStr (btnTest.GetHashCode));
 Log ('ToString: ' + BtnTest.ToString);
end;
```

执行结果如下(透过运行时间改变的哈希值):

```
Equals: True
Reference = True
GetHashCode: 28253904
ToString: TButton
```




### 总结 TObject 类


总结一下，在最新版的编译器里，TObject 类当中有完整的接口(已经把大多数的的 IFDEF 跟低阶的多载部分省略了，并略过私有区跟保护区):

```pascal
type
 TObject = class
 public
 constructor Create;
 procedure Free;
 procedure DisposeOf;
 class function InitInstance(Instance: Pointer): TObject;
 procedure CleanupInstance;
 function ClassType: TClass; inline;
 class function ClassName: string;
 class function ClassNameIs(const Name: string): Boolean;
 class function ClassParent: TClass;
 class function ClassInfo: Pointer; inline;
 class function InstanceSize: Integer; inline;
 class function InheritsFrom(AClass: TClass): Boolean;
 class function MethodAddress(const Name: string): Pointer;
 class function MethodName(Address: Pointer): string;
 class function QualifiedClassName: string;
 function FieldAddress(const Name: string): Pointer;
 function GetInterface(const IID: TGUID; out Obj): Boolean;
 class function GetInterfaceEntry(
 const IID: TGUID): PInterfaceEntry;
 class function GetInterfaceTable: PInterfaceTable;
 class function UnitName: string;
 class function UnitScope: string;
 {$IFDEF AUTOREFCOUNT}
 function __ObjAddRef: Integer; virtual;
 function __ObjRelease: Integer; virtual;
 {$ENDIF}
  function Equals(Obj: TObject): Boolean; virtual;
 function GetHashCode: Integer; virtual;
 function ToString: string; virtual;
 function SafeCallException(ExceptObject: TObject;
 ExceptAddr: Pointer): HResult; virtual;
 procedure AfterConstruction; virtual;
 procedure BeforeDestruction; virtual;
 procedure Dispatch(var Message); virtual;
 procedure DefaultHandler(var Message); virtual;
 class function NewInstance: TObject; virtual;
 procedure FreeInstance; virtual;
 destructor Destroy; virtual;
 public
 property RefCount: Integer read FRefCount;
 property Disposed: Boolean read GetDisposed;
 end;
```



### Unicode 跟类名称

MethodAddress 跟 FieldAddress 具备多载的版本，依照其参数的类型不同，我们可以使用 UnicodeString(通常是 UTF-16)或 ShortString 作为参数，如果使用 ShortString 参数，则输入的字符串会被认为是 UTF-8 字符串。事实上这些版 本 都 会 使 用 Unicode 字符串 ，它们 会 自 动 呼 叫UTF8EncodeToShortString 来进行转换:

```pascal
function TObject.FieldAddress(const Name: string): Pointer;
begin
 Result := FieldAddress(UTF8EncodeToShortString(Name));
end;
```

因为 Object Pascal 本身就已经支持 Unicode 了，在 Object Pascal 内部的类名称是使用 ShortString(它是一个 1 位的字符数组)来表示的，但是是使用UTF-8 的编码法，而不是传统的 ANSI 编码法喔。这个作法同时用在 TObject阶层跟 RTTI 阶层。

举例来说 ClassName 方法是这样实作的(用了很丑的低阶源码):

```pascal
class function TObject.ClassName: string;
begin
 Result := UTF8ToString (
 PShortString (PPointer (
 Integer(Self) + vmtClassName)^)^);
end;
```

跟 TypInfo 单元文件很相似，所有存取类名称的函数都会在内部把 UTF-8 ShortString 表示转换成 UnicodeString。类似的动作也会发生在属性的名称处理上。


## System 单元


TObject 很显然已经是 Object Pascal 语言的核心角色了，我们已经很难去区分它到底是编程语言的一部分，或是运行时间函数库的一部分，在 System单元中，还有一些低阶的类是构成基础且已经跟编译器支持整合在一起了。

这个单元的大多数内容，是为了低阶数据结构、简单记录结构、函数、程序以及一些类而制作的。

在此我们要集中心力在这些类上，但不可否认的，在 System 单元中的选多其他功能也是 Object Pascal 的关键。举例来说，System 单元里面定义了许多 Pascal 里面固有的函数，这些函数没有实际的源码，会由编译器直接加以解析。例如 SizeOf，编译器就会直接把这个函数换成该参数数据结构的实际大小。

我们可以阅读一下 System 单元档案里面的批注来了解它的独特地位(几乎解释了为什么在浏览系统变量的时候都会需要这个单元)：

```pascal
{ Predefined constants, types, procedures, }
{ and functions (such as True, Integer, or }
{ Writeln) do not have actual declarations.}
{ Instead they are built into the compiler }
{ and are treated as if they were declared }
{ at the beginning of the System unit. }
```

译文如下：

*预先定义的常数、类型、程序与函数(像是 True, Integer 或 Writeln)并没
有实际的宣告。反之，它们是内建在编译器里的，并且会被当成宣告在
System 单元文件的最开头一样。*

阅读这个单元里面的原始码可能会很无聊，也是因为我们可能在这个单元文件里面发现一些执行时期函数库的低阶源码。所以我决定只挑里面很有限的内容来介绍。

### 被选上的系统类型

刚刚提过，System 单元定义了核心数据类型，以及许多不同值类型的类型别名、有序类型，以及字符串。还有许多系统在低阶处理会使用到的核心数据类型(包含列举类型、记录，以及强制类型别名)等，这些都值得一提:

+ TVisiblityClasses 是用在 RTTI 可视范围设定的列举类型(详情请见第16 章)。
+ TGUID 是在 Windows 平台上用来表现 GUID 的纪录，但也可以用在其他支持 GUID 的操作系统上。
+ TMethod 是一个核心的记录，用来表现事件处理程序要使用的结构，包含一个指向方法地址的指针，以及一个指向当前对象的指针(详情请见第 10 章)
+ TMonitor 是一个记录，用来实作线程同步的机制(称为”Monitor”)，这是由 C.A.R Hoare 跟 Per Brinch Hansen 所发明的，在维基百科上面也有名为”Monitor synchronization”的详细介绍。这是 Object Pascal 编程语言中线程核心的功能，而 TMonitor 信息是在系统中的每个对象都存在的。
+ TDateTime 是 Double 类型的强制类型别名，用来储存日期信息(使用其中的指数部分来储存)与时间信息(用其十进制的部份)。更进一步的别名包含 TDate 跟 TTime。这些类型在第二章里面有介绍。
+ THandle 是值类型的别名，用来表示操作系统对象的参考，通常称为”Handle”(至少是在 Windows API 的范畴中)。
+ TMemoryManagerEx 是用来储存核心内存处理的记录，可以把系统的内存管理模块换成自定的模块(这是比较新版的 TMemoryManager)，新的模块仍旧有向前支持。
+ THeapStatus 是一个记录用来储存关于 heap 内存状态的信息，我们在第 13 章有提过。
+ TTextLineBreakStyle 是一个列举型态，代表特定操作系统对文本文件的换行格式。DefaultTextLineBreakStyle 这种类型的全局变量保存了目前的信息，会在许多系统函数库里面使用到。同样的，sLineBreak 常数则表达了字符串类型里相同的作用。

**System单元里的接口**

接口类型有好几种(而且有些类在核心阶层实作了接口的功能)，它们是System 单元的一部分，值得我们一看。以下是 System 单元里面跟接口相关的一些类型：

+ IInterface 是所有接口的基础，所有接口都要从这个类型继承而来，就像 TObject 之于所有其他的类一样。
+ IInvokable 跟 IDispatch 是支持动态调用的接口(部分与 Windows COM的实作绑在一起)。
+ 列举的支持跟比较的动作是透过以下的接口来定义的: IEnumerator，IEnumerable, IEnumerator<T>, IENumerable<T>, IComparable, IComparable<T>跟 IEquatable<T>。还有一些核心类提供了接口的基础实作。我们可以直接从这些类衍生出新的类，就可以实作接口了，在第 11 章里面我们介绍过了：
+ TInterfacedObject 是一个类，在当中已经有对参考计数与对接口 ID检查的基本实作。
+ TAggregatedObject 跟 TContainedObject，这两个类特别提供对aggregate 对象跟实作的语法。



### 被选上的系统函数 

在 System 单元里面，内建跟标准程序与函数非常多，但大多数都不常被用到。以下是我们选出来的核心程序跟函数，每个 Object Pascal 开发人员都应该知道这些：

+ Move 是在系统中核心内存复制的程序，把特定数量的内存很单纯的从一个地址复制到另一个地址去(很强大、速度快，但有一点点的危险性)。
+ ParamCount 跟 ParamStr 函数可以用来处理命令行指令的参数(在图形接口系统中，像 Windows 跟 Mac 也都可以运作的很好)
+ Random跟Randomize是两个传统的函数(似乎是从BASIC借来的概念)，提供我们产生随机数(虚拟的随机，但一定要记得调用 Randomize，产生的数目才会真的随机，不然每次执行程序你得到的随机数字都会一样)。
+ 很大量的核心数学运算函数，在这里全略过。许多字符串处理跟字符串转换函数(在 UTF-16，Unicode，UTF-8，ANSI，以及其他字符串格式之间转换)，其中包含一些是跟平台相关的。

!!! note "笔记"
    这些函数中，包含一些间接的定义。换句话说，这些函数实际上是一个指标指向实际的函数。所以原始系统的行为可能会在运行时间被源码动态的替换掉。(当然，如果我们知道自己在做什么，这也是把整个系统搞死的好办法)


### 预先定义的 RTTI 标注

在本章的最后，我想介绍的最后一组数据类型是跟标注相关的，额外的RTTI 信息，我们可以连接到编程语言的任何符号。这个主题我们已经在第16 章里面介绍过，但当时我并没有提到过在系统中预先定义的标注。以下是在 System 单元中定义的标注类：

+ TCustomAttribute 是所有自定标注的基础类。这是我们可以当成所有需要使用到标注的类的根源基础类(且这是编译器可以识别类的唯一方法，作为一个标注，因为没有特别宣告的语法)。
+ WeakAttribute 也是用来在 ARC 环境中标明 weak 参考(详见第 13 章)
+ UnsafeAttribute 也是在 ARC 环境中作为特别处理之用的(详见第 13 章)
+ RefAttribute 很直觉是给参考值使用的。
+ VolatileAttribute 标示会自动消灭的变量，这种变量可以从外部进行修改，而且无法被编译器优化。
+ StoredAttribute 是可以表达属性中的 stored 旗目标另一种方法。
+ HPPGENAttribute 控制 C++接口档案(HPP)的生成。
+ HFAAttribute 可以用来优化 ARM 64 位 CPU 的参数传递，并可以控制同构型浮点数整合(Homogeneous Floating-point Aggregate, 缩写为HFA)

在 System 单元中还有更多内容，但比较适合专家级的开发人员。我们先继续介绍吧，在最后一章里面，我们会触及 Classes 单元，以及其他 RTL 的功能。
