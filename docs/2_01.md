即使我们对 OOP 的了解不深，这一章会介绍每一个关键概念。如果您对OOP 已经能够熟极而流，您也可以从本章的介绍中快速的聚焦在 Object Pascal 是如何制作出这些您已经熟知的功能，并和其他面向对象编程语言做个对照或比较。

Object Pascal 对 OOP 的支持跟 C#与 Java 有很多的相似之处，同时还加入了一些 C++跟其他静态、强类型语言的特性。其他动态语言希望能够提供一些不同的 OOP 直译程序，一如它们在处理类型系统那样有弹性。

!!! note "笔记"
    C#跟 Object Pascal 许多的概念极度相似，是因为这两个编程语言的设计者是同一个人: Anders Hejlsberg. 他是 Turbo Pascal 编译器、第一版 Delphi Object Pascal 的原始作者，后来他转职到微软，设计了 C#(近期更设计了从JavaScript 延伸而出的 TypeScript)。您可以从附录 A 当中到更多关于 Object Pascal 语言的历史。

## 介绍类别(Class)与对象(Object)

类别(Class)跟对象(Object)这两个名词在Object Pascal跟其他OOP编程语言中都很常出现。然而也就是因为这两个名词太常被误用了，所以我们在一开始，要非常清楚的把它们的定义给厘清，子曰：必也正名乎：

+ 类别，是用户定义的数据类型。类别当中包含状态以及定义了一些动作。用另外一个说法来描述，类别有一些内部数据，以及一些方法，以程序或函数的形式呈现。类别通常描述一些类似对象的特征跟行为，即使有些特别用途的类别所指的就是一个特别的对象。
+ 对象则是类别的实体，例如是一个以特定类别作为数据类型而定义的变量。对象是实际存在的个体，当程序执行时，对象会使用一些内存空间来做内部储存之用。
+ 类别与对象之间的关系，就跟任何一种数据类型跟变量之间的关系，只是在这个案例中，变量有特别的称呼(实体)而已。


!!! quote "译者乱入"
    讲述程序设计这么多年，上面的说法总觉得学生很不容易理解，所以我后来用另一个说法来说明，学生们就容易理解的多。类别，我们可以想象成『人类』，人类有许多定义、特征。而对象就是一个个特定的人，例如读者你就是一个『人类』这个类别的实体，译者我也是，作者也是。

!!! quote "历史"
    OOP 的这些术语是从早期的一些开始实作 OOP 概念的编程语言而来的，例如 Smalltalk。然而最原始的术语后来在演进的过程中被慢慢以程序化语言的术语给取代掉。所以像是类别、对象这些名词虽然还是很常用，我们也很常听到像调用某个方法，不像以往会用传递一个讯息给接收者(当然也是个对象)这种说法。完整的把 OOP 的术语列出来，并看一下它的演进是很有趣的，可惜本书的篇幅不够。


### 定义一个类别

在 Object Pascal 里面，我们可以用以下的语法来定义一个新的类别数据类型(TDate)，来定义一些内部数据字段(Month, Day, Year)跟一些方法(SetValue, LeapYear)：

```pascal
type
 TDate = class
 FMonth, FDay, FYear: Integer;
 procedure SetValue (M, D, Y: Integer);
 function LeapYear: Boolean;
 end;
```

!!! note "笔记"
    我们已经以记录来介绍过很类似的结构，记录在定义上跟类别非常相似。但在内存管理和其他领域则很不相同，在本章稍后的篇幅会进行介绍。但在历史意义上，Object Pascal 的这个语法是首先为了类别制作，稍后才为记录移植过来的。

在 Object Pascal 里面，对类别的约定是使用 T 这个字母作为所有类别的第一个字母，像是定义任何其他类型(事实上，T 是当做 Type 这个字的缩写)。这个约定，对编译器来说，T 跟其他字母都一样，但只是让我们的源码能够更容易被其他程序人员了解而已。

跟其他编程语言不同，在 Object Pascal 定义类别时，并不会同时把实作的源码写在一起。只会包含类别的定义(或者说是宣告)，这使得类别的源码更为精简，也更容易了解。

_____________________________________________________________________
!!! tip "提示"
    我们已经以记录来介绍过很类似的结构，记录在定义上跟类别非常相似。但在内存管理和其他领域则很不相同，在本章稍后的篇幅会进行介绍。但在历史意义上，Object Pascal 的这个语法是首先为类别制作，稍后才为记录移植回来的。

也请记得除了撰写类别的定义(包含字段与方法)，我们还可以只写个宣告，也就是只写类别的名称:

```pascal
type
 TMyDate = class;
```

这种宣告的用意，是万一我们写了两个类别，而这两个类别之间相互使用时，其中之一就需要先进行预先宣告。因为在 Object Pascal 里面，我们不能使用还没定义的代号，所以要参考到还没定义的类别时，就需要先进行宣告。以下的程序片段只是要介绍语法，它并没有任何实质作用喔：

```pascal
type
 THusband = class;
 TWife = class
 FHusband: THusband;
 end;
 THusband = class
 FWife: TWife;
 end;
```

在实际的源码中，我们得自己处理类似的相互参考情形，因此这个语法很重要，请牢记。另外，请记得就像宣告方法一样，在单元文件里面宣告的类别，必须在同一个单元档案里面完整的定义宣告的所有方法。


### 在其他 OOP 语言的类别227

做个比较，我们用 C#跟 Java 写一个完全相同的 TDate 类别(在这么简单的情形下也可能完全相同)使用一个更适合的命名规则以及对应的方法源码:

```c#
// C# and Java language
class Date
{
 int month;
 int day;
 int year;
 void setValue (int m, int d, int y)
 {
 // code
 }
 bool leapYear()
 {
 // code
 }
}
```

在 Java 跟 C#里面，方法的源码会随着类别的定义一起写好，而在 Object Pascal 里面则是把类别的宣告跟实作部分分开来撰写的，但必须在同一个单元文件里面完成。换句话说，在 Object Pascal 里面，类别永远要在单一一个单元文件里面完成定义(当然，一个单元文件里面可以定义很多个类别)。相对的，C++的方法也是分开来撰写的，跟 Object Pascal 有一点点类似，但C++包含类别定义的头文件案，则不用非跟宣告了方法的源码放在一起。

对应的 C++类别看起来会是这个样子：

```c++
// C++ language
class Date {
 int month;
 int day;
 int year;
 void setValue (int m, int d, int y);
 BOOL leapYear();
 }
```

### 类别方法.228

跟记录类似，当我们定义好方法的时候，我们必须判别该方法是属于哪个类别的一部分(在这个范例中，是 TDate 类别)，在使用这个方法时，可以先输入该类别的名称，加上句点，就可以加以调用，用以下的源码作为范例：

```pascal
procedure TDate.SetValue(M D, Y: Integer);
begin
 FMonth := M;
 FDay := D;
 FYear := Y;
end;
function TDate.LeapYear: Boolean;
begin
 // Call IsLeapYear in SysUtils.pas
 Result := IsLeapYear (FYear);
end;
```

大多数其他的 OOP 语言把方法定义成函数，Object Pascal 不同的是可以让方法定义成函数或者程序，只取决在是否需要回传值。这跟 C++的情况就不同了，C++把方法定义跟实作分开:

```c++
// C++ method
void Date::setValue(int m, int d, int y) {
 month = m;
 day = d;
 year = y;
};
```

### 建立对象.229

在跟其他热门的编程语言比较过后，我们回头来看 Object Pascal 当中要如何使用类别。

一旦类别定义好了，我们就可以以这个类别作为数据类型来建立对象，就像以下的范例(是从 Dates1 范例项目中取出的源码):

```pascal
var
 ADay: TDate;
begin
// Create
ADay := TDate.Create;
// Use
ADay.SetValue (1, 1, 2016);
if ADay.LeapYear then
 Show ('Leap year: ' + IntToStr (ADay.Year));
```

表示法没有什么特别，但非常有力。我们可以写很复杂的函数(像是LeapYear)，然后透过每个 TDate 的对象来存取它的内容，一如它是个直觉的数据类型。请注意，ADay.LeapYear 跟 ADay.Year 一样是对象可以存取的属性，然而前者是个函数，后者是直接存取数据。等我们进行到第十章的时候，Object Pascal 用来存取属性的表示法时，存取属性的方法也一样。

!!! note "笔记"
    在 C 阵营的编程语言中，要调用不用参数的函数时，无论如何都还是得加上小括号，以上面的例子来看，就得写成 ADay.LeapYear()。这样的写法在Object Pascal 也是合法的，但很少人使用这个写法。在 Object Pascal 里面调用不需要参数的函数时，通常是习惯不加小括号的。这也跟许多调用无需参数的方法时，会直接回传该函数地址的编程语言非常不同。我们曾经在第四章的”程序类型”当中介绍过，Object Pascal 在调用程序跟函数时是使用相同的调用方法，取决于该判断式的语意内涵。

这段源码的结果非常直觉：

`Leap year: 2020`

我们再来比较一下 Object Pascal 跟其他编程语言在对象的建立写法:

```c++
// C# and Java languages (object reference model)
Date aDay = new Date();
// C++ language (two alternative styles)
Date aDay; // local allocation
Date* aDay = new Date(); // "manual" reference
```

## 对象参考模型(Object	Reference	Model)

在一些 OOP 语言，像 C++，宣告一个类别类型的变量时就会自动出建立一个该类别的实体(多多少少有点像在 Object Pascal 里面的记录)。局部变量的对象所使用的内存会从 Stack 配置而来，然后当该函数结束时就会把这些内存释放掉。在大多数情形下，我们必须使用指针跟参考让管理对象生命周期的作业更有弹性，但也增加了额外的复杂度。

相反地，Object Pascal 语言是以对象参考模型为基础，完全像 Java 或 C#。这个概念是说，每个类别类型的变量并不直接储存对象的数据(例如以上例来说，不直接储存 day, month, year)。而是只储存一个内存参考，或者说指针，来指向一个真正用来储存对象数据的内存地址。

!!! note "笔记"
    从我的观点来看，使用对象参考模型是早期 Object Pascal 编译器团队所做的最好的决定之一，在当时这种模型在编程语言当中已经不常被使用(事实上，当时 Java 还没完全发展出来，且 C#也还不存在)。

这也是为什么在这些编程语言中，我们需要额外建立一个对象，然后把它指派给一个变量，因为对象并不会自动被进行初始化。换句话说，当我们宣告一个变量，我们并没有在内存里面建立一个对象，而只是预留了一个内存空间来储存对象的参考地址而已。对象实体必须由我们撰写的程序来建立，至少由我们自己定义的类别必须如此。(在 Object Pascal 里面，我们从元键盘拖拉到窗体上面的各个组件的实体，就是由 RTL 自动建立出来的)

在 Object Pascal 当中，要建立一个对象的实体，我们必须调用该类别的特殊函数:Create，这个函数是一个建构函数，或者由类别自行定义的建构函数，写法像是这样:

`ADay := TDate.Create;`

如我们所见，建构函数是由类别(也就是一个类型)，而不是由对象(变量)来调用的。这是因为我们是要求类别建议一个新的实体，而结果会是一个新的实体，我们就可以把这个实体指派给一个变量了。

Create 这个方法是从何而来呢? 它是 TObject 这个类别的建构函数，这个类别是 Object Pascal 所有对象的起源对象，所以在 Object Pascal 里面的所有类别全都依循相同规则。为我们定义的类别建立自行定义的建构函数也很常见，容我们在本章稍后再进行介绍。


### 释放对象

在使用对象参考模型的语言中，我们在使用对象前，必须先建立它，然后在我们不再需要使用该对象的时候，也要负责把该对象使用的所有内存空间释放掉。如果我们没有释放这些内存空间，这些残留在内存里面的空间将不会有机会被其他变量使用到，就会造成名为内存裂缝(memory leak)的问题。为了解决这个问题，有些语言，像是 C#跟 Java，就建立了虚拟执行环境(或称虚拟机)，让整个环境进行垃圾收集机制(garbage collection)。这个机制让开发人员的生活过的轻松了些，但这个功能要达到的要求是很复杂的，会牵涉到执行效能的问题，这又跟 Object Pascal 的关系不大了，所以我没办法在这里扯太多。

Object Pascal 里面，我们通常调用对象里面一个特殊的方法，名为 Free，来释放该对象所使用的内存空间(再强调一下，由于这是 TObject 的方法之一，所以任何一个类别都能使用)。Free 会在调用过该类别的解构函数(这里面可能有很特别的清除源码)之后，释放掉该对象所占用的内存空间，所以我们可以把上例当中的对象这样释放掉：

```pascal
var
 ADay: TDate;
begin
 // Create
 ADay := TDate.Create;
 // Use
 ...
 // Free the memory
 ADay.Free;
end;
```

虽然这是标准的作法，但组件函数库在其中加入了类似对象归属的概念，以减少手动管理内存的冲击，让处理相关议题来说相对简单。

!!! note "笔记"
    我们在后面的章节会介绍到，当接口参考到对象时，编译器会使用自动参考计数(ARC)的方式来做内存管理。这几年来，ARC 也被用 Delphi 行动装置版本的编译器用来管理一般类别类型的变量。从Delphi 10.4 Sydney开始，在所有作业平台上的内存管理，已经都统一为使用传统方式了。

在内存管理跟 ARC 这个主题上我们应该要更深入了解，但这是一个重要的课题，且并不简单，所以我决定在此提供一个简短的介绍，并且在第 13 章进行深入的探讨。在该章中，我会让各位看到在不同平台上所使用的这些不同技术的细节，以及他们是如何在所有平台上都能正常运作的。


### 什么是”Nil”

我们已经提过，变量可以指向特定类别所建立的对象，但变量并不能自己为
对象做初始化，也不能把使用到的对象转成不再使用的状态。这时我们就可
以使用 nil 了。这是一个特殊的常数值，用来表示变量已经不再指派给任何
对象了。其他编程语言则使用另一个识别代号 null 来表示同样的概念。
当一个类别类型的变量没有值的时候，我们可以用这个方式帮它做初始化:

`ADay := nil;`

要确认一个对象是否已经指派给该变量，我们可以用以下任一种写法：

```pascal
if ADay <> nil then ...
if Assigned (ADay) then ...
```

不要犯下想透过把 nil 指派给对象来释放内存的错。把一个对象设定为 nil跟把它释放掉是两个完全不同的作业。所以我们通常既需要调用 free，也必须把该对象的变量指向 nil 或者调用一个特殊功能的程序以同时满足两种需求，这个程序就是 FreeAndNil。再预告一下，第 13 章里面就会有更多相关信息，以及实际的范例程序了。

### 在内存里面的记录与类别

一如我们前面所提，记录跟对象的主要不同，在于他们的内存模型。记录类型变量使用区域内存，它们被当成参数传递的时候，默认是用传值模式，也就是说被当成参数传递的变量，其内容会被复制一份之后，传递给函数使用。这跟类别类型变量的配置是从易失存储器 heap 而来，以及会自动以传址(call by reference)方式传递，并且在复制时还有”以参考复制”的特性恰好成为对比(会把该对象的指针复制一份，而不会实际上复制对象内容)。

_____________________________________________________________________
!!! note "笔记"
    在内存管理上的不同，是记录类型缺少了继承跟多型的机制，这两个功能我们会在下一章当中介绍到。

+ private 这个存取分区描述关键词，宣告该类别位于此一区块内的字段跟方法，只能被宣告该类别的原始码档案使用。

举例来说，当我们在堆栈中宣告了一个记录类型的变量，我们在宣告完之后就可以立刻开始使用它了，不用调用它的建构函数(除非它是自定受管理的纪录)。这表示记录类型变量在内存管理上是比正规的对象更要来的精简(而且比较有效率)，因为记录类型的变量不需要进行内存配置管理。这也是在处理简单、单纯数据结构时，为何要建议使用记录类型，而不使用对象的关键之处。

另一个记录跟类别不同的地方是在当它们被作为参数传递的时候，比较看看把记录所使用的所有内存做一份完整的复制，跟把对象参考复制一份(数据没有复制)。当然我们也可以透过 var 跟 const 的参数来修改传递参数时的规则。


## 私有、保护、公开

+ **strict private** 这个存取分区描述关键词，宣告该类别位于此一区块内的字段跟方法，只有该类别的方法可以取用，即使是同一个单元文件的其他类别也不可以，这个规则跟大多数面向对象编程语言的 private 关键词规则才是一样的。

一个类别可能拥有任何数量的数据字段跟任何数量的方法。然而，以好的面向对象设计内涵来说，数据应该被隐藏起来，或者说是『封装』在对象内部。例如当我们试着存取一个日期，直接改变当中的『日』内容是不对的。事实上，直接改变『日』这个字段可能导致整个日期变得不合逻辑，例如用户可能改出一个二月三十日这种不存在的日子。透过使用对象的方法，对象内部的表示法跟规则就可以尽量排除这种风险，例如透过对象的方法来设定日期，就可以判断使用者设定的日期有所错误，进而从对象本身直接拒绝新的错误日期被设定。适当的封装是相当重要的，因为它赋予了类别设计者在未来的版本中变更内部表示方法的权限。

其实封装的概念相当简单：就把类别想象成一个『黑盒子』，只露出一小部分可以看到里面。可以看到的部份，我们称之为类别的接口，这个部分允许其他源码透过属于这个类别的对象来存取、使用这个类别的功能。然而，当我们使用这个对象的时候，大多数的源码是被隐藏的。我们很少会知道一个对象当中有哪些内部数据，我们也无法直接存取这些数据，我们只能透过对象的方法来存取这些数据或使用它的方法。

封装使用了私有跟保护区的成员，这是面向对象解决方案提供给传统程序设计方法的目标，被称之为信息隐藏。

Object Pascal 有五种不同的基本存取分区描述关键词(有两个要搭配 strict 使用)，分别是:private (私有)、protected (保护)，以及 public(公开)。第六个特别的分区关键词则是 published(已发布)，这个我们在第十章里面讨论。以下是五个基本分区的特性，我们来仔细看一下:

+ **public** 存取分区关键词，在这一区里面的所有字段，可以被整个程序的任何一部分存取，不限于在哪个单元文件里面。
+ **protected** 跟 strict protected 存取分区关键词，在这一区里面的字段，有部分存取的限制。只有该类别的对象，以及从这个类别衍生的子类别可以存取这一区的字段，除非是相同类别或者使用了 strict 关键词的情况，我们会在下一章的『保护区字段与封装』里面仔细讨论。

一般情形下，类别的字段应该放在 private 或者 strict private 区块中，方法则放在 public 区块里。然而，凡事并非绝对的。如果我们需要制作类别内部使用的方法来进行某些作业，这些方法也可以被放在 private 或者 protected 区块中。如果我们很肯定类别设计时某些字段的类型定义不会在被修改，也可以把字段或变量放在 protected 区块中，然后可以从衍生类别中直接使用这些变量(我们在下一章里面会说明)，但比较不建议使用这样的设计方式。

以一般的情形来说，我们应该不去直接把数据放在公开区，在公开区应该透过属性来提供对数据字段的存取，我们也会在第十章里面一起介绍。属性，是其他 OOP 编程语言当中用来提供数据封装机制的延伸，也是 Object Pascal 里面非常重要的机制。

如前文所述，存取分区关键词只用来限制其他单元文件的源码存取该类别在这个单元当中所宣告跟实作的类别成员。这表示如果两个类别在同一个单元文件里面，在类别当中私有区宣告的数据字段就没有任何保护的作用，除非使用 strict private 区块来加以保护，使用这个作法会让数据的隐密性比较高。

!!! note "笔记"
    C++语言中有个概念叫做友善类别，这是让特定类别可以存取其他类别的私有数据。顺着这个概念，我们可以说在 Object Pascal 的类别中，所有在同一个单元文件里面宣告的类别都自动被当成友善类别了。

### 私有区数据字段的范例

我们可以用个例子来示范透过分区存取关键词如何实现数据封装的理念，以此实作一个 TDate 类别的新版本：

```pascal
type
 TDate = class
 private
 Month, Day, Year: Integer;
 public
 procedure SetValue (M, D, Y: Integer);
 function LeapYear: Boolean;
 function GetText: string; procedure Increase;
 end;
```
在这个版本中，数据字段现在被宣告在私有区内，并提供了其他方法。第一个方法是 GetText，这个方法是个会以字符串形式回传日期的函数。我们未来也可以加入其他方法，像是 GetDay，GetMonth，GetYear 可以用来回传各个在私有区的日期字段，只是类似的直接存取数据的函数并不会永远需要。提供可以直接存取每个字段的函数是可以简化封装的过程，但也使抽象化的概念被弱化了，同时也让未来要修改该类别的内部实作方式更为困难。

对数据字段存取数据的函数只宜在该类别有实际需要提供特定字段给其他源码存取时才提供，并不必凡数据字段必有存取函数。

第二个新增的方法是 Increase 程序，这个程序会在调用时为日期内容向后推一天。这并不像所描述的这么好做，因为我们得考虑每个月有不同的天数、以及该年是否为闰年。我们为了要让源码变得比较容易写而作的，是使用用Object Pascal 的 TDateTime 类型作为这个类别内部实作的方法。所以实际上我们在 Dates2 项目类别里面所看到的类别源码会长的像这样：


```pascal
type
 TDate = class
 private
 FDate: TDateTime;
 public
 procedure SetValue (M, D, Y: Integer);
 function LeapYear: Boolean;
 function GetText: string;
 procedure Increase;
 end;
```

请留意，这里的修改只有类别的私有区，我们不用因此修改任何使用到这个类别对象的源码，这也是信息封装设计的好处之一。

!!! note "笔记"
在这个类别的新版本中，唯一的数据字段的名称第一个字符是以 F 开始的。这是在 Object Pascal 语言里面的不成文规定，我也会在本书中沿用下去。


在这一节的结尾，我们把这个范例项目完成，我们把该类别所有方法的原始码都列出来，其中会使用一些系统函数来做日期跟内部结构的相互对照：

```pascal
procedure TDate.SetValue (M, D, Y: Integer);
begin
 FDate := EncodeDate (Y, M, D);
end;
function TDate.GetText: string;
begin
 Result := DateToStr (FDate);
end;
procedure TDate.Increase;
begin
 FDate := FDate + 1;
end;
 
function TDate.LeapYear: Boolean;
begin
 // call IsLeapYear in SysUtils and YearOf in DateUtils
 Result := IsLeapYear (YearOf (FDate));
end;
```

也请注意到这个函数码是怎么使用这个类别的，它已经不再使用 Year 的资料内容，但可以从日期对象的回传信息中撷取出该项信息：

```pascal
var
 ADay: TDate;
begin
 // Create
 ADay := TDate.Create;
 
 // Use
 ADay.SetValue (1, 1, 2016);
 ADay.Increase;
 if ADay.LeapYear then
 Show ('Leap year: ' + ADay.GetText);
 // Free the memory (for non ARC platforms)
 ADay.Free;
```

输出数据跟原来的不同：

`Leap year: 1/2/2020`

也请留意，您执行的结果可能跟这个字符串不同，因为日期格式会跟每台计算机的日期设定有关。

### 封装与窗体

封装技术的主要要求之一，是减少程序里面使用全局变量的数量。全局变量可以在一个程序的任何地方加以存取。为了这个原因，全局变量的改变会影响到整个程序。另一方面，当我们改变了类别中数据字段的设计，我们只需要修改该类别有使用到该数据字段的部分源码，其他部分不用动。所以我们可以说信息隐藏也引用了封装的改变。

让我们用个实例来厘清这个概念。当我们写了一个多重窗体的程序时，我们可能会为了让所有窗体可以存取同一份数据，而在全局变量上宣告它：

```pascal
var
 Form1: TForm1;
 NClicks: Integer;
```

这样宣告可以正常运作，但有两个问题。第一，数据(NClicks)并不是跟窗体上的任何一个字段有所连结，而是跟整个程序链接。如果我们用同样的类型制作了两个窗体，预期让两个窗体之间互相共享数据，这是可行的。如果我们希望各个窗体都能有它自己的数据，唯一的方法是在窗体的类别中加入:

```pascal
type
 TForm1 = class(TForm) public
 NClicks: Integer;
 end;
```

第二个问题则是，如果我们把该数据定义成全局变量或者一个位于公开区的字段，我们未来就不能在不影响使用到被修改字段相关源码的情形下，对这个类别的内部进行重新设计了。例如，如果我们只需要从其他窗体读取目前的数值，我们可以把该数据字段设计成私有，并且提供这样的读取方法：

```pascal
type
 TForm1 = class(TForm)
 // Components and event handlers here
 public
 function GetClicks: Integer;
 private
 FNClicks: Integer;
end;
function TForm1.GetClicks: Integer;
begin
 Result := FNClicks;
end;
```

另一个比较好的解决方法，是帮窗体加入一个属性，我们在第十章再慢慢说。我们现在可以先用 ClicksCount 范例项目来做些实验。简单的说，这个项目的窗体上方有两个按钮、一个卷标，其他部分的窗体画面则留白让使用者可以点击(或触击)。在这个案例中，计数值会增加，接着该数值会被显示在卷标上：


```pascal
procedure TForm1.FormMouseDown(Sender: TObject; Button: TMouseButton;
 Shift: TShiftState; X, Y: Single);
begin
 Inc (FNClicks);
 Label1.Text := FNClicks.ToString;
end;
```

在图 7.1 里面可以看到应用程序执行的画面。项目的窗体也有两个按钮，一个是用来建立一个相同类型的新窗体，第二个按钮则是关闭它(所以我们就可以把窗口焦点移回前一个窗体上)。这是为了强调每一个不同的实体被点击时，可以有自己的点击计数数值。以下是这两个方法的源码:

```pascal
procedure TForm1.Button1Click(Sender: TObject);
var
 NewForm: TForm1;
begin
 NewForm := TForm1.Create(Application);
 NewForm.Show;
end;
procedure TForm1.Button2Click(Sender: TObject);
begin
 Close;
end;
```


图 7.1: ClicksCount范例项目的窗体，显示该窗体实体被点击的次数(记录在该窗体的私有区)



## Self 关键词

我们已经看过了很像程序跟函数的一些方法，实际的不同处，是方法会有一些额外的、预设的参数。该参数是一个指向触发该方法的对象参考。在方法之内，我们可以透过这个参数来存取正在执行中的对象，或使用 self 这个关键词(在第五章的『Self: 记录神奇的地方』当中我们也介绍过)。

这个额外的参数是有需要的，尤其是当我们建立相同类别的多个对象时。当我们每次使用该对象的任何一个方法时，这个方法只会对该对象实体有作用，对同一类别的其他实体则不会有任何作用。

!!! note "笔记"
    实作出记录跟类别的 Self，在概念跟作法上都非常相似。Self 是在提出类别的时候先被提出的概念，后来才延伸到记录的，而当时方法则也已经加入了这个数据结构当中。

举例来说，在我们稍早提到的 TDate 类别里面的 SetValue 方法，我们很简单的直接用了 Month, Year, Day 来对应目前对象的字段，我们可以直接写出这样的源码:

```pascal
Self.Month := m;
Self.Day := d;
```

事实上这也是 Object Pascal 编译器转换源码的方法，我们不用自己写出这些源码。Self 关键词是会由编译器建立的程序核心基础，但有时候程序人员也会自己写出来，可以让源码更容易被读懂，也比较不容易发生同名成员之间的混淆或误解。

!!! note "笔记"
    C++, Java, C#, JavaScript 语言当中，也有相同功能的关键词，他们使用的是this。然而 JavaScript 跟 C++, C#, Java 不一样，JavaScript 在方法中使用这个关键词来指定对象参考是必要条件。

我们真的需要知道的是 Self 是调用方法时的技术实作，跟调用一般子程序是不一样的。方法会有 Self 这个额外的隐藏参数存在。因为这一切都会在不知不觉中就由编译器处理好了，我们不需要知道 self 在这个情形下是怎么作业的。

第二重要的思路，是我们可以在整个源码当中，自己明确输入 self 这个关键词，来引用目前的对象，例如把目前的对象当成参数传给另一个函数。

### 动态建立组件

在我们刚刚提过的例子里，self 关键词在我们需要明确参考到目前这个窗体或对象时是很常用到的。常见的例子，就是当我们在运行时间的时候需要动态建立组件时，因为建立组件的时候需要指定该组件的拥有者作为调用建立组件建构函数(Create)的参数，并且把同一个值指派到新建组件实体的 Parent属性。在上述的两个情形中，我们都需要提供当前窗体的对象参考作为参数，或者要被指派的数据，而完成这个作业最方便的方式就是使用 Self 关键词了。

!!! note "笔记"
    组件的所有权会关系到它的生命周期以及两个对象内存管理的关联性。当一个组件的所有权者被释放掉了，该组件也会一起被释放掉。Parent(父代关系)所指的，则是在视觉层级上，这个组件要被贴在哪个组件上头。

为了进行这样的展示，我提供了 CreateComps 范例项目，这个应用程序只有一个干干净净的窗体，以及处理 OnMouseDown 事件的处理程序，这个处理程序也会把鼠标点击的坐标当成标准参数之一。因为我们需要这个坐标来当成新建立组件要放置的位置。

!!! note "笔记"
    事件处理程序(event handler)是一种特殊的方法，我们在第十章里面会介绍，本章里面我们会先介绍这一系列里面的成员：按钮的 onClick 处理程序。

以下是这个方法的源码:

```pascal
procedure TForm1.FormMouseDown (Sender: TObject;
 Button: TMouseButton; Shift: TShiftState; X, Y: Integer);
var
 Btn: TButton;
begin
 Btn := TButton.Create (Self);
 Btn.Parent := Self;
 Btn.Position.X := X;
 Btn.Position.Y := Y;
 Btn.Height := 35;
 Btn.Width := 135;
 Btn.Text := Format ('At %d, %d', [X, Y]);
end;
```

请注意，我们需要在uses叙述句中，加入使用StdCtrls这个单元文件的描述，这样编译才会过喔。

这段源码的作用，是会在鼠标点击的地方建立一个新的按钮，并且把该坐标当成按钮的卷标文字，就像图 7.2 所示(在这个项目中，我取消了 FMX Mobile Preview 的功能，所以显示出 Windows 原生样式的按钮，这样看起来会比较清楚)。在以上的源码当中，请特别留意 self 关键词的使用，尤其在我们调用 Create 方法跟指派 Parent 属性的时候。

当撰写一个跟上述范例相似的程序时，您可能会很想用 Form1 来取代上面源码里面的 Self。在上述这个范例中，如果您这么写，并没有造成任何实质的变化(即使它并不算一个好的源码范例)，但如果一个窗体会出现多个实体的时候，使用 Form1 来取代 self，就会出现很严重的错误喔。

事实上，如果 Form1 这个变量指向该窗体类别的一个实体(通常会是第一个被建立的实体)，而我们用相同窗体类别建立了第二个实体，那么，在点击任何一个窗体的画面时，都只有第一个窗体的画面上会出现新增的按钮，因为新建组件的 Parent 是 Form1，而不是被点击的窗体实体了。

通常，当我们在撰写一个类别的源码，需要使用到当前对象参考时，如果把它指向特定一个实体的变量值时，绝对是 OOP 的一个很糟很糟的作法。

图 7.2: CreateComps范例的执行画面


## 建构函数

在上述的源码中，要建立一种类别的对象(您也可以想做要帮一个对象配置内存空间)时，我们会调用 Create 方法。它是建构函数(constructor)，是所有类别中一个预设的特别方法，让我们可以用来为类别的一个新建实体配置内存之用：

`ADay := TDate.Create;`

由建构函数回传的实体，我们可以把它指派给一个该类别为类型的变量，然后后续用这个变量就能使用这个新建的实体了。

当我们建立一个对象，它的内存已经完成了初始化，所有新对象的数据字段都该被设定为 0(或者 nil, 或者空字符串，或者该数据类型的适当默认值)。如果我们想要让我们的新建实体在建立之时就储存特定数据，那我们就需要写一个自定的建构函数来这么做。新的建构函数可以名为 Create，或者它也可以用任何一个名称。用来决定方法的角色的，并不是名称，而是特定的关键词: constructor。

_____________________________________________________________________
!!! note "笔记"
    换句话说，Object Pascal 支持可命名的建构函数，其他大多数的 OOP 编程语言的建构函数大多只能是该类别的名称。透过可命名的建构函数，我们可以拥有多个建构函数，让这些建构函数的参数是一样的(也可以直接对Create 进行多载 – 我们在下一节介绍多载)。Object Pascal 另一个特殊的功能，也是在 OOP 编程语言中很独特的，就是建构函数也可以是虚构的(virtual)，下一章当中，我们在介绍过虚拟函数概念之后，就来花一些篇幅来探讨。

为一个类别建立自定建构函数的主要原因，是要进行数据的初始化，如果我们建立了一个对象，但没有为它做初始化，稍后在调用它的方法时，可能会得到很奇怪的结果，或者甚至得到执行程序错误。与其等到这些错误显示，我们应该用一些方法预先避免这种情形发生。这些方法的其中之一，就是持续使用建构函数来对对象数据进行初始化。举例来说，我们在建立 TDate类别的对象之后，得先调用 SetValue 方法来设定一个日期给它。或者我们也可以提供一个自定的建构函数，在建立对象之时，同时就把预设日期给设定好：

```pascal
constructor TDate.Create;
begin
 FDate := Date; // Today
end;
constructor TDate.CreateFromValues (M, D, Y: Integer);
begin
 FDate := SetValue (M, D, Y);
end;
```

我们可以这样来使用这些建构函数，这些源码我已经在 Date3 范例项目中写好了，在这个范例项目中，以下的源码是分别由两个按钮来触发的：

```pascal
ADay1 := TDate.Create;
ADay2 := TDate.CreateFromValues (12, 25, 2015);
```

即使我们通常可以使用任何一个名称来做为建构函数，要记得如果我们使用的名称不是 Create，Create 这个建构函数仍旧是有效的，因为它是从 TObject一路继承下来的。所以如果我们正在开发要提供给其他程序人员使用的源码，其他程序人员直接调用 Create 建构函数时，就不会调用到你辛苦写的建构函数了。我们也可以直接新增一个名为 Create 带有完全相同参数的建构函数，这样就能取代掉原来的建构函数，确保使用上不会有失误了。

如同类别可以有自定的建构函数，类别也可以拥有自定的结构函数，以destructor 为名宣告的方法，调用时直接调用 Destroy 即可。这个解构函数可以在对象被从内存毁灭之前，先把使用过的资源清除掉，但大多数的情形下，并不需要自定的解构函数。

就像建构函数会为一个对象配置内存，解构函数则是会释放内存。自定解构函数只有在该类别的建构函数或者执行过程中要求了额外资源时才需要。

跟预设的 Create 建构函数不同，预设的 Destroy 解构函数是虚拟的(Virtual)，而且强烈建议开发人员覆写(override)这个虚拟的解构函数(虚拟函数会在下一章里面介绍)。

这是因为与其直接调用一个对象的解构函数，不如透过惯用的 Object Pascal程序实务上的作法，调用名为 Free 的特殊 TObject 类别方法，这个方法会自动转为调用 Destroy 函数，如果该对象有定义它的话，也就是说该对象的Destroy 如果不是 nil 的话，所以，如果我们定义了一个解构函数使用的名称不是 Destory 的话，这个解构函数就不会被 Free 调用了。再次重申，与此相关的更多主题我们会在第十三章里面的内存管理来介绍。

_____________________________________________________________________
!!! note "笔记"
    如下一章我们介绍的内容，Destroy 是一个虚拟方法，我们可以在继承特定类别，衍生新的次类别时取代原有的定义，透过 override 关键词。顺带一提，使用静态方法(static method)来调用一个虚拟方法是很常见的程序风格，称为模版模式(template pattern)。在解构函数中，我们通常只需要撰写清除资源的源码。试着避免太复杂的处理，例如发出例外，或者花上可观的时间，可以避免在对象清理时的问题，因为许多解构函数会在程序结束被调用，我们会希望让它执行速度尽可能快一些。


### 以建构函数跟解构函数来管理区域类别数据

虽然我们会在本书稍后的篇幅来介绍更复杂的情境，在此我想先用简单的案
例来介绍以建构函数、解构函数来提供资源保护的功能。这也是使用解构函
式最常见的情境。
假设我们有个如下宣告结构的类别(这也是 Date3 范例项目的一部分):
type
 TPerson = class
 private
 FName: string;
 FBirthDate: TDate;
 public
 constructor Create (name: string);
 destructor Destroy; override;
 // some actual methods
 function Info: string;
 end;
这个类别包含了一个内部对象的参考，名为 FBirthDate。当 TPerson 类别的
实体被建立的时候，这个内部(或者子)对象应该也要同时被建立，而当这个
实体被摧毁，内部这个对象也要一起被摧毁。
以下是我们可以用来撰写这个建构函数以及覆写后的解构函数源码，以及可
以永远用来确保内部对象存在的内部函数。
constructor TPerson.Create (name: string);
begin
 FName := Name;
 FBirthDate := TDate.Create;
end;
destructor TPerson.Destroy; 
begin
 FBirthDate.Free;
 inherited;
end;
function TPerson.Info: string;
begin
 Result := FName + ': ' + FBirthDate.GetText;
end;
_____________________________________________________________________
要了解用来定义解构函数覆写(override)这个关键词，以及在函数中的
inherited 这个关键词，请等到下一章。目前我们先简单的说，第一个关键
词是用来指出该类别有一个新的定义，要用来取代掉基础的 Destroy 解构函
式。而第二个关键词则是用来调用该类别上一代的结构函数。也请记得，
override 只用在方法的宣告，不会出现在实作的源码之中。

现在我们可以在以下的情境中使用外部类别的对象，而内部对象会适时的在
TPerson 对象被建立的时候一起被建立起来，也会在 TPerson 被摧毁时一起
被处理掉：
var
 Person: TPerson;
begin
 Person := TPerson.Create ('John');
 // Use the class and its internal object 
 Show (Person.Info);
 Person.Free;
end;
以上的源码，可以在 Date3 范例项目中找到。


### 多载方法以及建构函数

Object Pascal 支持对函数与方法的多载：我们可以用同一个名称建立许多方法，但这些方法的参数必须不一样。我们已经看过全局函数跟程序的多载是怎么运作的，同样的规则也适用于方法。透过检查参数，编译器可以判断出我们想要调用的方法是哪个版本。

重申一次，多载的基本规则有两个：

+ 每一个版本的方法必须在宣告时最后都加上 overload 关键词。
+ 每一个版本的参数类型或数量必须有点不同，然而回传值并不能拿来作为多载方法的判别条件。

如果多载被用在一个类别里面的所有方法上，这个功能也会跟建构函数有关，因为我们可以建立多个建构函数，并且都将之命名为 Create，这会让建构函数更容易被记忆。

!!! note "笔记"
    来谈点历史，多载功能被加入到 C++就是特别为了让多个建构函数可以被使用，因为 C++的建构函数必须都是相同名称(类别的名字)。在 Object Pascal里面，这个功能相对的不是很需要，因为多重建构函数本来在 Object Pascal里面就可以有不同的名称，即使如此仍旧把这个功能加入，则是因为在其他情形底下，多载功能仍然很有用处。

我在 TDate 类别里面加了两个不同版本的 SetValue 方法，作为多载的范例：

```pascal
type
 TDate = class
 public
 procedure SetValue (Month, Day, Year: Integer); overload;
 procedure SetValue (NewDate: TDateTime); overload;
procedure TDate.SetValue (Month, Day, Year: Integer);
begin
 FDate := EncodeDate (Year, Month, Day);
end;
procedure TDate.SetValue(NewDate: TDateTime);
begin
 FDate := NewDate;
end;
```

在这个简单的步骤之后，我也在这个类别里面加入了两个独立的 Create 建构函数，其中之一不用参数，这个版本把预设的建构函数直接取代掉了，另一个则会进行数据初始化。不用参数的建构函数会使用今天的日期作为默认的数据：

```pascal
type
 TDate = class
 public
 constructor Create; overload;
 constructor Create (Month, Day, Year: Integer); overload;
constructor TDate.Create (Month, Day, Year: Integer);
begin
 FDate := EncodeDate (Year, Month, Day);
end;
constructor TDate.Create;
begin
 FDate := Date; // Today
end;
```

有了这两个建构函数，我们就可以用以下两种方法来建立新的 TDate 对象了：

```pascal
var
 Day1, Day2: TDate;
begin
 Day1 := TDate.Create (2020, 12, 25);
 Day2 := TDate.Create; // Today
```

这段源码是 Dates4 范例项目的一部分。 


### 完整的 TDate 类别

综观这一节，我们已经在不同的程序片段里面看过了 TDate 类别。第一个版本是以三个整数来储存年月日三个部分的数值。第二个版本则是使用TDateTime 类型作为内部数据，以下是完整的 TDate 类别定义:

```pascal
unit Dates;
interface
type
 TDate = class
 private
 FDate: TDateTime;
 public
 constructor Create; overload;
 constructor Create (Month, Day, Year: Integer); overload;
 procedure SetValue (Month, Day, Year: Integer); overload;
 procedure SetValue (NewDate: TDateTime); overload;
 function LeapYear: Boolean;
 procedure Increase (NumberOfDays: Integer = 1);
 procedure Decrease (NumberOfDays: Integer = 1); function GetText: string;
 end;
```

新增的两个方法：Increase 跟 Decrease(两个方法都有预设的参数值)，也都相当的容易理解。如果调用时不给参数，他们会直接把内部数值改成前一天或下一天，如果 NumberOfDays 参数有被指定，则会依据这个参数把内部数值改成往前或往后几天：

```pascal
procedure TDate.Increase (NumberOfDays: Integer = 1);
begin
 FDate := FDate + NumberOfDays;
end;
GetText 方法则会以格式化的字符串回传内部数值的日期，我们透过
DateToStr 函数来做这个转换：
function TDate.GetText: string;
begin
 GetText := DateToStr (FDate);
end;
```

我们已经看过了前一节当中的大多数方法，所以我们不再做完整的列表了，您可以从 ViewDate 范例中找到这些源码。这个窗体的内容比书上其他的范例都来的复杂。它包含了一个用来显示日期的卷标，以及六个按钮。这六个按钮都可以用来变更对象内部的日期数据。您可以从图 7.3 里面看到该项目的窗体画面。为了让卷标组件好看，我用了比较大的字体，让它跟窗体一样宽，并且把对齐的属性(Alignment)设定成Center，也把AutoSize设为了False。

图7.3: ViewDate 范例的执行画面

这个程序第一个被执行的源码，会是窗体的 OnCreate 这个事件处理程序。在对应的方法中，我建立了一个 TDate 类别的实体，然后把这个建立的对象初始化，最后把它的内容以字符串格式显示在窗体上的卷标文字字段，如上图(图 7.3)所示。

```pascal
procedure TDateForm.FormCreate(Sender: TObject);
begin
 ADay := TDate.Create;
 LabelDate.Text := ADay.GetText;
end;
```

ADay 是 TDateForm 这个窗体类别的私有字段。顺带一提，这个类别的名字是当我们把窗体的名称字段改为 DateForm 时，开发环境自动修改的。

指定的日期对象是在窗体被建立时自动建立的(就像我们刚刚看过的 person类别跟它的日期子对象关系一样)，也会在窗体被摧毁时自动被摧毁：

```pascal
procedure TDateForm.FormDestroy(Sender: TObject);
begin
 ADay.Free;
end;
```

当用户点选六个按钮的其中一个，我们需要使用 ADay 对象的对应方法，并把新的数值内容显示在卷标文字上：


```pascal
procedure TDateForm.BtnTodayClick(Sender: TObject);
begin
 ADay.SetValue (Today);
 LabelDate.Text := ADay.GetText;
end;
```

另一个撰写最后一个方法源码的替代方案则是直接把目前的对象摧毁掉，然后建立一个新的：

```pascal
procedure TDateForm.BtnTodayClick(Sender: TObject);
begin
 ADay.Free;
 ADay := TDate.Create;
 LabelDate.Text := ADay.GetText;
end;
```

在这个特殊的情形中，这个例子并不够好(因为建立一个新的对象，并把它摧毁掉，会花很多额外的时间，尤其是当我们只是想更改对象内容的时候)，但透过这个案例，我可以示范一些 Object Pascal 里面的技术。第一个值得一提的是我们在指派一个个新的对象前，会先摧毁前一个物件。事实上这个指派的动作是更换了参考值(reference)，把对象留在内存里面(即使可能并没有任何指标指向它)。当我们把一个对象指派给另一个，编译器只会把对象在内存里面的参考地址复制到新的对象参考地址。

另一个问题，是我们怎么把数据从一个对象复制到另一个。在上面这个源码的案例非常单纯，因为它只有一个数据字段跟对应的方法用来初始化。通常如果我们想要改变存在一个既存的对象中的数据，我们得复制每一个字段，或者提供一个特殊的方法来复制所有内部数据。部分类别会拥有一个名为Assign 的方法，可以用来进行深度复制的作业。

!!! note "笔记"
    更精确一点，在 RTL 里面，所有类别都是从 TPersistent 类别衍生而来，这个类别提供有 Assign 方法，但大多数从 TComponent 衍生的类别则没有实作它，所以调用组件的 Assign 会造成例外发生。原因是因为 RTL 支持的串流化机制以及对 TPersistent 类型的属性支持，但在这个章节说明这一点还太复杂。


## 嵌套类型与巢状常数

Object Pascal 允许我们在一个单元文件里面的 interface 区段宣告新的类别，在这里宣告的类别，可以被其他单元文件存取，也可以被 implementation 区段的程序存取。在 implementation 区段里面的程序与类别，则只能被存在同一个单元文件里面的源码或全局子程序(也只限于写在同一个单元文件里面)所存取。

最近比较新的一个作法，是在一个类别当中宣告另一个类别(或任何其他的数据类型)的可能性。而该类别的任何其他成员，嵌套类型都会有被限制的可见度(或者用我们已经介绍过的名词:私有区，或保护区)。嵌套类型的相关范例包含同一类别与其他在implementation区段支持的类别所使用的列举作业。

相关的语法允许我们定义一个巢状常数，这个常数可以跟一个类别相关(再提一次，如果在私有区宣告，就只允许内部使用，如果在公开区宣告，则在整个程序的任何一个部分都能使用)。举例来说，下面的源码就宣告了一个巢状类别(这是从 NestedTypes 范例项目中，NestedClass 单元文件所节录的):

```pascal
type
 TOne = class
 private
 FSomeData: Integer;
 public
 // Nested constant
 const Foo = 12;
 // Nested type
 type
 TInside = class
 public
 procedure InsideHello;
 private
 FMsg: string;
 end;
 public
 procedure Hello;
 end;
procedure TOne.Hello;
var
 Ins: TInside;
begin
 Ins := TInside.Create;
 Ins.Msg := 'Hi';
 Ins.InsideHello;
 Show ('Constant is ' + IntToStr (Foo));
 Ins.Free;
end;
procedure TOne.TInside.InsideHello;
begin
 FMsg := 'New msg';
 Show (‘Internal call');
 if not Assigned (InsIns) then
 InsIns := TInsideInside.Create;
 InsIns.Two;
end;
procedure TOne.TInside.TInsideInside.Two;
begin
 Show ('This is a method of a nested/nested class');
end;
```

巢状类别可以在类别中直接使用(例如在前例的范例源码中的用法)，或者在类别之外使用(如果巢状类别宣告在公开区的话)，但必须完整把名称写上：TOne.TInside。完整的类别名称也在巢状类别的方法定义中使用。在这个例子里面，就得写 TOne.TInside 了。主类别可以在宣告巢状类别之后，立刻拥有一个以巢状类别为类型的数据字段(就像我们看到在NestedClass范例项目中的源码一样)。

拥有巢状类别的类别可以这样使用：

```pascal
var
 One: TOne;
begin
 One := TOne.Create;
 One.Hello;
 One.Free;
```

这段源码的输出值如下:

```
Internal call
This is a method of a nested/nested class
Constant is 12
```

我们从使用 Object Pascal 的巢状类别到底得到了什么好处?这个概念在 Java里面很常用来实作事件处理程序，也在 C#的类别中无法在同一档案中的其他类别隐藏信息时使用。在 Object Pascal 当中唯一使用到巢状类别的用途是当我们在类别的私有区里面宣告一个类别数据字段时，不想把这个类别加到全局的命名空间，也不让它被全局命名空间看见，此时，巢状类别就派上用场了。

如果内 部 类 别 只 用在一个方 法 中，我们可以透过在单 元 文 件 的implementation 区段宣告该类别来达成相同的效果。但如果这个内部类别会在同一单元文件的 interface 区段被参照使用的话(例如，我们要把它作为类别的字段或者函数的参数)，这个类别就必须在相同的interface区段中宣告，当然也就会暴露在其他源码之下了。在基础类型中宣告这样的字段，然后把它放在私有区实在是不如使用巢状类别来的干净利落。

!!! note "笔记"
    在第十章，我们提供了一个实际的案例，对巢状类别做了一些练习，用了for-in 循环做了一些实作。

