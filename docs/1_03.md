对资料类型明确的概念(强类型的概念)是 Pascal 编程语言被发明时的创举之一。程序就是具备数据类型声明、加上用来处理这些数据类型变量的源码。

在 Pascal 编程语言被发明的时候，这两大支柱(数据类型与程序指令)的概念是由 Nicklaus Wirth 的巨作”Algorithm+Data Structures = Programs”(算法+数据结构=程序)所阐明的，这本书是 1976 年二月由 Prentice Hall 出版社所出版，是一本程序概念上的巨作，至今仍有再版。这本书比面向对象程序设计的概念早上许多年，可以被视为是现代程序概念的基础之一。以强类型为概念，并以此为理论基础，最后衍生出了面向对象编程语言的发展。

编程语言的语句是以关键词为基础，搭配其他元素，让我们能够让编译器得知我们要执行的一系列程序。语句通常会被以程序或函数的方式包装起来，这一点我们会在下个章节介绍，而目前我们只要先聚焦在我们可以用来撰写程序的一些基本的指令即可。我们在第一章里面介绍过的（在介绍使用空格符与源码样式的篇幅里）实际上撰写程序是很自由的，我们也介绍了注释与一些特别的元素，但还没有来得及完整的介绍其他核心概念，像是程序的语句。



## 简单与复合语句95

程序指令通常会被称为语句(Statements)。一段程序区块可能会由好几个语句组成，语句可以分为两种：简单与复合语句。当一个语句没有包含其他子语句的时候，我们称之为简单语句，最简单的例子，就是指派叙述跟调用程序，在 Object Pascal 里面，简单语句式以分号来做分隔的：

```pascal
X := Y + Z; // 指派叙述
Randomize; // 调用程序
```

要定义一段复合型的语句，我们可以在 begin 跟 end 之间写入一个或多个的语句。begin 跟 end 在此扮演着多个描述句的容器以及相似的角色，但是跟 C 阵营语言当中的大括号并不完全一致。复合型语句可以出现在任何 Object Pascal 简单语句出现的位置：

```pascal
begin
 A := B;
 C := A * 2;
end;
```

在复合语句里面的最后一个语句的分号不一定要写：

```pascal
begin
 A := B;
 C := A * 2
end;
```

以上两段源码都是正确的，第一段的写法中，最后一句最后结尾的分号其实是没有用的（但也无伤大雅），这个分号事实上算是一个空的语句，也就是一句没有源码的语句，这一点跟其他编程语言是很不一样的，尤其是对 C 语言阵营的编程语言来说，在 C 语言阵营的编程语言来说，每个语句结尾的分号都是不可省略的。

请注意，很多时候，没有源码的语句，在内部的循环当中，有时候也是可以直接出现的，例如：

```pascal
while condition_with_side_effect do
 ; // 没有源码的语句
```

虽然最后一个分号并没有特别的作用，大多数的使用者还是习惯写上去，而我也建议大家要写。因为程序总是需要修改的，常常我们写了一段时间的源码，后来又要在后头加上一些其他的源码，这时就不用老是在寻找最后一行了。但如果多加了一个分号，可是会导致编译器例外发生的，最常见的例子，就是在 else 前面加上一个分号。


### IF 语句 

条件语句是以一个条件来判断要执行特定区块的源码，或者不执行它（在不符合条件的时候），条件判断式的语法关键词有两个：if 跟 case。

If 语句是用来判断符合特定的一个条件，如果符合该条件，就执行该区块的源码 (if-then)，或者合于条件时执行一区块的源码，不符合时执行另一区块的源码(if-then-else)，条件判断需以布尔表达式定义。

我们提供了一个简单的范例:IfTest 来示范如何撰写条件语句，在这个程序中，我们使用了 checkbox 来取得使用者的输入值，透过 checkbox 的IsChecked 属性（并把它储存在一个布尔变量当中，虽然这么做并不是必要的，我们可以直接使用该属性的值作为判断式）：

```pascal
var
 isChecked: Boolean;
begin
 IsChecked := CheckBox1.IsChecked;
 if isChecked then
 Show ('Checkbox is checked');
```

如果该 checkbox 有被勾选，程序就会显示一个简单的讯息，不然的话就不会有任何事情发生，如果上面的这段程序以 C 的语法来写的话，就会长得像这样（C 语言的条件判断式一定要用小括号把它包起来）：

```pascal
if (isChecked)
 Show ("Checkbox is checked");
```

还有一些编程语言会让我们用 endif 来作为判断语句的结尾，好让我们可以在程序区块当中使用多个语句，在 Object Pascal 当中，if 后面只能使用单一语句，所以如果我们需要使用多个语句的时候，就必须用begin-end 来把带有多个语句的程序区块给包起来了。

如果我们希望能依照该条件的成立与否分别执行不同的源码，就可以使用if-then-else 这样的语法(以下的范例中，我会直接把 checkbox 的属性当成条件判断式)：

```pascal
// if-then-else statement
if CheckBox1.IsChecked then
 Show ('Checkbox is checked')
else
 Show ('Checkbox is not checked')
```

请留意，在 if 后面的语句，是不可以用分号结尾的，不然编译器会回报语法错误，这是因为 if-then-else 被当成一个单一语句，所以我们不能在当中使用分号把它切断。

if 语句可以很复杂，当中的条件判断式可以由一连串的条件组合而成（使用 and, or, not 运算符号）。而 if 语句里面也还可以在包含其他 if 语句，我们也可以一连串的组合 if-then-else-if-then 这样的句子，我们可以任意组合多个 else-if 这样的条件语句。

在 IfTest 范例中的第三个按钮，就示范了这样的情形，透过第一个在 edit组件里面输入的字符作为输入判断值：

```pascal
var
 aChar: Char;
begin
   // multiple nested if statements
   if Edit1.Text.Length > 0 then begin
     aChar := Edit1.Text.Chars[0];
     // checks for a lowercase char (two conditions)
   if (aChar >= 'a') and (aChar <= 'z') then
     Show ('char is lowercase');
   // follow up conditions
   if aChar <= Char(47) then
     Show ('char is lower symbol')
   else if (aChar >= '0') and (aChar <= '9') then
     Show ('char is a number')
   else
     Show ('char is not a number or lower symbol');
end;
```

要仔细看这段源码，然后执行范例程序，看看跟你预期的是否相同，以类似的程序做练习，我们的程序撰写能力进步的才快。我们也可以用这个范例当基础，加上多一点条件跟选项，增加它的复杂度，随我们所想的去进行程序的改写与测试。




### Case语句

如果我们要判断的条件很复杂，例如对同一个变量的数值要分成多个不同部分来分区处理的时候，用 IF 语句会变得很复杂，这时候我们可以改用case 语句。Case 语句可以让我们判断同一个表达式的不同范围的数值，这些数值必须是常数，而且必须是有序类型的数值，不能重复。最后，我们也可以在所有列举的范围数值之外，使用 else 语句，让我们没能预想到的所有情形都在该段源码来处理。Case 语句并没有像 endcase 这样的结束关键词，它仍旧使用 end 来结尾。


!!! note "笔记"
    建立 case 语句需要使用列举数值，在 case 语句中是不能使用字符串作为条件值的，如果要判别不同的字符串内容时，只能用 if 语句或者不同的数据结构，例如 dictionary。（我们在第 14 章会进行介绍）

以下是个简单的范例（是 CaseTest 项目的一部分），在这个范例当中，透过用户输入的整数值来作为 case 语句的判断资料：

```pascal
var
  Number: Integer;
  AText: string;
begin
  Number := Trunc(NumberBox1.Value);
  case Number of
    1: AText := 'One';
    2: AText := 'Two';
    3: AText := 'Three';
  end;
  if AText <> '' then
    Show(AText);
```

另一个例子则是前面提到过的复杂的 if 语句的延伸，把输入的内容作为case 语句的不同判断值：

```pascal
case AChar of
 '+' : AText := 'Plus sign';
 '-' : AText := 'Minus sign';
 '*', '/': AText := 'Multiplication or division';
 '0'..'9': AText := 'Number';
 'a'..'z': AText := 'Lowercase character';
 'A'..'Z': AText := 'Uppercase character';
 #12032..#12255: AText := 'Kangxi Radical';
else
 AText := 'Other character: ' + aChar;
end;
```
!!! note "笔记"
    在上面的范例程序当中，部分的数值范围使用了次范围数据类型的语法，反之，大多数的单一数值则使用了逗点作为分隔符。而超过英数字的字母，则使用 Kangxi Radical 作为显示文字，其中的侦测条件就直接使用数值，因为其中大多数的文字都无法在 IDE 编辑器显示，例如”一”，是这个群组的第一个元素。（这部份在简体、繁体中文其实是可以正确显示的，只是大多数非中文用户的操作系统会无法正确显示）

在程序实务上，使用 else 来处理未被定义的条件是比较保险的。在 Object Pascal 里面，case 语句是用来判定要执行的路径，它并不会自己决定要如何选择。换句话说，它会执行符合的判断式的分号后面的语句或程序区块，而不是决定切入点。换句话说，它只会执行符合条件的判断式之后的语句，执行完以后就不会执行之后的其他源码了。

这跟 C 语言系列的编程语言很不一样，在 C 系列的语言里面，是用 switch指令来达成同样的动作，但它是决定了切入点之后，就从该点开始执行下去，除非我们在希望中断的地方写入一个 break 语句（这个指令在 Java 跟 C#跟字面上的指令实作是不太一样的），C 语言的写法如下：

```pascal
switch (aChar) {
case '+': aText = "plus sign"; break;
case '-': aText = "minus sign"; break;
... default: aText = "unknown"; break;
}
```


### For	 循环

Object Pascal 和其他编程语言一样，都有很传统的重复执行功能，称之为循环，在 Object Pascal 当中包含了三个循环指令：for, while 跟 repeat 这三个语句，而后来又加入了 for-in(或者称为 for-each)这个语句。如果您已经熟悉其他编程语言，那么这些循环您一定不会陌生，所以我会很快的大致上介绍一下这些循环（会点出跟其他编程语言的异同处）。

For 循环在 Object Pascal 里面是以计数器为基础，所以在 For 循环执行的时候，是对计数器做递增或递减处理。以下是一个简单的 For 循环范例，会把 1-10 的数字做加总（是 ForTest 范例的一部分）:

```pascal
var
 Total, I: Integer;
begin
 Total := 0;
 for I := 1 to 10 do
     Total := Total + I;
 Show(Total.ToString);
```

结果当然不奇怪，一定会是 55。在介绍过循环之后，我们用另外一种写法，介绍使用行内变量的宣告方法来处理循环的计数变量(这语法有部分看起来像是结合了部分 C 语言阵营的语法，我们稍后再讨论)：

```pascal
 for var I: Integer := 1 to 10 do
 Total := Total + I;
```

从上面这个例子，我们可以使用到类型推定的好处，忽略一些类型的细节。我们用上面这个例子来写成完整的 Delphi 源码，就会变成:

```pascal
var
 Total: Integer;
begin
 Total := 0;
 for var I := 1 to 10 do
 Total := Total + I;
 Show(Total.ToString)
```

使用行内变量作为循环计数变量的好处之一，就是这个变量的生命周期将会被限制在循环里面: 如果在 for 循环之后的源码里面还使用到这个变量的话，IDE 跟编译器都会显示有错误。用传统的变量宣告写法，这种情形顶多会显示警告而已。

Pascal 的 For 循环跟其他编程语言的 For 循环相比之下，弹性比较小(例如每次循环的计数变化量只能是一)，但这个现象也很容易理解，我们看看 C语言的 For 循环语法就知道：

```C
int total = 0;
 for (int i = 1; i <= 10; i++) {
 total = total + i;
 }
```

在其他的语言中，For 循环的变化量是每次执行过之后，由一个表达式来处理的，所以我们当然可以用我们希望的表达式放在里面来执行，要一次跳两个数字或其他处理法，也都没有问题。但坏处则是有时候会让源码变得比较不容易读懂:

```C
int total = 0;
for (int i = 10; i > 0; total += i--) {
.. }
```

然而在 Object Pascal 里面，我们对 For 循环只能使用单步递增或递减。如果想要在每次循环执行过后做比较特别的设定或处理，我们可以改用 while或是 repeat 指令。

在 For 循环当中唯一可以更换的部分，就是递增改成递减，或者称为逆向循环，关键词从 to 改用 downto：

```pascal
var
 Total, I: Integer;
begin
 Total := 0;
 for I := 10 downto 1 do
 Total := Total + I;
```

!!! note "笔记"
    逆向循环也是很有用的，例如当我们希望对一个列表型的数据结构进行内容处理，当删除其中的一些元素时，我们通常会逆向而行，以一个正向的循环，你会影响正在处理到的元素顺序（例如我们删除了列表中的第三个元素，原本的第四个元素就变成了第三个元素，而我们本来在第三个元素的位置，向后移动一个位置（现在的第四个），就到了第五个元素(中间直接跳过了原本的第四个元素)

在 Object Pascal 里面，for 循环的计数器不用非得是数字不可，只要是一个有序类型的数值就行了，例如字符，或者是列举类型都行。这也让我们写出来的源码更容易阅读，以下是使用字符类型来做 for 循环的一个范例：

```pascal
var
  AChar: Char;
begin
  for AChar := 'a' to 'z' do
    Show (AChar);
```

上面这段源码(是 ForTest 城市的一部分)会秀出所有英文字母，会以每行一个字母的方式显示在 Memo 组件里面。

!!! note "笔记"
    我也提供了一个类似的范例，只是这个范例是使用数字作为计数器，它是第二章 CharTest 范例的一部分，在该范例中，所有输出的字符会被连接成一个字符串以后才一起输出。

以下是另一个代码段，用来示范如何使用自定列举型态作为 for 循环的计数器：

```pascal
type
 TSuit = (Club, Diamond, Heart, Spade);
var
 ASuit: TSuit;
begin
 for ASuit := Club to Spade do
 …
```

这个代码段当中的循环，会把该列举类型的所有数值都处理过一次，最好是能够对每个元素的类型都进行精确处理(在定义修改的时候会比较有弹性)，这样就不用写出该类型的第一个跟最后一个数值的名称了：

`for ASuit := Low (TSuit) to High (TSuit) do`

在类似的写法中，用 for 循环来让数据结构中的所有元素都跑一遍是很常见的，在这个案例中，我们可以用以下这段程序片段（它是 ForTest 项目的一部分）：

```pascal
var
 S: string;
 I: Integer;
begin
 S := 'Hello world';
 for I := Low (S) to High (S) do
   Show(S[I]);
```

如果您比较不想带到数据结构里面的第一个跟最后一个元素，建议可以使用 for-in 循环。在接下来的章节里，我们就要来讨论 for 循环的各种特定用途了。

!!! note "笔记"
    在 Object Pascal 里面，编译器是怎么透过[]符号来直接处理字符串当中的每个元素，并判断字符串的起始与结束值是一个很复杂的议题，虽然在所有平台中的作法现在几乎已经都一样了。这个主题在第六章里面我们会再进行介绍

对于以 0 为起始值的数据结构而言，我们的起始索引值是从 0 到该数据结构的结尾。通常的写法会像这样

```pascal
for I := 0 to Count – 1 do …
for I := 0 to Pred(Count) do …
```

关于 for 循环要注意的最后一个问题，是『在循环结束之后，循环的计数变量会被怎么处理?』简单来说，这个变量的内容后续不会再被处理，如果我们在循环结束之后的源码当中又使用了这个循环的计数变量，编译器会发出警示。使用行内变量来宣告循环计数变量的好处之一，就是该变量的生命周期只在循环源码当中，循环结束后该变量就无法再被使用，所以在编译阶段就会被报错了(这样的保护更周到)

```pascal
 var Total := 0;
 for var I: Integer := 1 to 10 do
 Inc (Total, I);
 Show(Total.ToString);
 Show(I.ToString); // 编译器错误: 没有宣告过的识别符号 ‘I’
```


### For-in	 循环

Object Pascal 提供了特别的循环结构，可供一个 list 或者 collection 把里面的每个元素都列举一次，称为 for-in(在其他编程语言里面这功能常被称为foreach)。在这个 for 的循环中会对于数组、list 或者字符串，或者其他具备容器功能的类型里的所有元素进行处理。跟 C#不同的是，Object Pascal 并不要求实作 IEnumerator 这个接口，但内部会以相似的作法来实现


!!! note "笔记"
    在第 10 章里面，您可以在类中加入 for-in 循环来观察这个循环内部的技术细节。

我们用一个非常简单的容器类型：字符串来作为开始吧，我们可以把字符串看成是字符的集合(Collection)，在前一节的结尾，我们介绍了怎么用一个 for 循环来处理字符串中的所有元素。在接下来的范例中，我们也可以用for-in 循环来达到相同的效果，在底下这个名为 Ch 的变量，会依序接收到字符串里每个元素的内容

```pascal
var
 S: string;
 Ch: Char;
begin
 S := 'Hello world';
 for Ch in S do
 Show(Ch);
```

上面这段程序是 ForTest 范例的一部分。For-in 循环比传统的 For 循环方便的地方，就是我们不用花心思去记录字符串的第一个位置、以及最后一个位置在哪里。因此这种循环更容易撰写与维护。


跟传统的 for 循环一样，在 for-in 循环里面如果使用行内变量也会有些优点。我们可以把上面的范例改用行内变量写成效果完全相同的源码:

```pascal
var
 S: string;
begin
 S := 'Hello world';
 for var Ch: Char in S do
 Show(Ch);
```

For-in 循环可以用来读取多种不同数据结构里的元素：

+ 字符串里面的字符(请参考上面的程序片段)
+ 集合当中的每个元素
+ 静态或动态数组里面的元素，也包含二维数组(将在第五章里面介绍)
+ 支持 GetEnumerator 的对象类，包含许多预先定义的类，像是StringList 里面的字符串、不同容器类的元素，处理这些类的方法将在第 10 章里面介绍。

目前要介绍一些进阶的模式还言之过早，所以我们稍后再回头来看本章节的这些范例吧。

!!! note "笔记"
    在某些编程语言里面的 for-in 循环(例如 JavaScript)执行起来特别慢，已经成了负面口碑了。但在 Object Pascal 里则不然，在 Object Pascal 里面的 for-in循环，效能跟 for 循环几乎一样好。为了证明这一点，我在 LoopsTest 范例程序中加了一些计时用的源码，在这个程序中，会先建立一个包含三千万个元素的字符串，然后再用两种循环来扫描内容。（循环中的每次作业都很简单，两种循环所造成的差异不到百分之十，在我的 Windows 机器上面执行起来，分别用了 62ms 跟 68ms）而已






### While 和 Repeat	 循环 

While-do 和 repeat-until 的意义，是重复执行一个程序区块，直到特定条件达成。两种写法之间的差异，只在于一个是在执行前检查该条件，而一个是在执行源码之后进行检查而已。换句话说，repeat 循环永远会执行至少一次。

!!! note "笔记"
    绝大多数其他的编程语言都只提供一种开放式循环语句，大多都像 while循环。C 语言的语法和 Pascal 语法一样，提供了两种语法，分别写成 while跟 do-while 两种语法。请注意，C 语法的 while 判断句是一样的含义，跟Pascal 的 repeat-until 语法是不同的，until 是在条件成立时结束循环喔。

了解 repeat 循环为何至少执行一次是很容易的，请看以下这个简单的范例
源码：

```pascal
while (I <= 100) and (J <= 100) do
begin
 // use I and J to compute something...
 I := I + 1;
 J := J + 1;
end;
repeat
 // use I and J to compute something...
 I := I + 1;
 J := J + 1;
until (I > 100) or (J > 100);
```

!!! note "笔记"
    请留意 while 跟 repeatd 当中我用括号括起来的子条件。在这个例子中是必要的，在编译器将要执行或进行比较之前(就像我在第二章里面提到关于运算符的章节里面提到的)

如果 I 或 J 的初始值比 100 大，while 循环就会直接结束，但 repeat 循环则会执行一次。

这两种循环之间另一个关键性的差异，则是 repeat-until 的条件是相反的，又称为反转条件(符合的时候就脱离循环)，循环的执行会在 until 语句后面的条件成立的时候，停止执行源码。而在 while-do 循环里面，则是在 while后面的条件成立的时候才执行其中的源码。因此在上面的源码里面，两个循环的判别条件的写法正好是完全相反的。

!!! note "笔记"
    反 转 条 件 在 摩 根 定 律 当 中 已 经 广 为 周 知 ( 请参考 维 基 百 科 : http://en.wikipedia.org/wiki/De_Morgan%27s_laws)




### 循环的范例源码 

要对循环有更多深入的了解，我们得看一些实际案例。在 LoopTest 范例程序里，点出了固定次数的循环跟开放式条件循环的差异，第一个循环是固定次数的循环，也就是 for 循环，用来依序显示数字：

```pascal
var
I: Integer;
begin
 for I := 1 to 20 do
 Show ('Number ' + IntToStr (I));
end;
```

同样的输出结果，也可以用 while 循环来达成，透过一个内部变量，每次增加 1(记得，我们要在显示数字之后帮这个数字加一)。透过 while 循环，我们可以自由的设定递增的数值，例如每次加 2:

```pascal
var
I: Integer;
begin
 I := 1;
 while I <= 20 do
 begin
 Show ('Number ' + IntToStr (I));
 Inc (I, 2)
 end;
end;
```

上述的范例程序，会显示从 1 到 19 的奇数。

上面这两个循环，作法上是相同的逻辑，会把程序区块执行固定次数。但执行次数并非在源码撰写的时候就能够预测的，有些源码的情形会根据运行时间中变量的变化或外部条件的变化而有不同。

!!! note "笔记"
    在撰写 while 循环的时候，请一定要留意判别条件是不是有可能永远不会成立，例如以特定变量的大小作为判别条件的时候，要留意该变量是否记得在每次执行后有被进行递增或递减，以防变成无穷循环（无穷循环发生时，会把 CPU 完全占用，在多核的操作系统中，会看到特定的一个 CPU 使用率维持着 100%, 直到操作系统把该处理程序删除掉）

为了示范这种情形，我写了一个 while 循环，其判别条件是以计数器为依据的，但该计数器变量的数值则是随机递增。为了达到这个目的，我使用Random 函数来建立小于 100 的整数。这个程序的执行结果，是会从 0 到99 随机选取数字，而这一系列的随机数字则决定 while 循环执行的次数：

```pascal
var
I: Integer;
begin
 Randomize;
 I := 1;
 while I < 500 do
 begin
 Show ('Random Number: ' + IntToStr (I));
 I := I + Random (100);
 end;
end;
```

如果你记得先调用 Randomize 这个子程序，它会重设每次随机数字产生时的依据，这样一来每次执行时，所产生的随机数字就会都不一样。以下是两次执行结果，我们把它并列在一起做对照

```
Random Number: 1 Random Number: 1
Random Number: 40 Random Number: 47
Random Number: 60 Random Number: 104
Random Number: 89 Random Number: 201
Random Number: 146 Random Number: 223
Random Number: 198 Random Number: 258
Random Number: 223 Random Number: 322
Random Number: 251 Random Number: 349
Random Number: 263 Random Number: 444
Random Number: 303 Random Number: 466
Random Number: 349
Random Number: 366
Random Number: 443
Random Number: 489
```

请注意，不只是每次建立的随机数字都不一样，也因为 while 循环执行的次数也是透过随机数字产生而决定的，所以每次执行时，会产生几次随机数字也是每次都不同的，因此上面的执行结果连个数都不同。





### 用 Break 和 Continue 指令来中断流程  

尽管循环在语法与执行上有些许不同，但所有的循环都是相同的意义：依照特定的条件，让程序区块执行许多次。然而有些时候，我们会希望在循环的逻辑里面加上一些额外的规则。举个例子，我们写了一个 for 循环用来侦测特定字符是否出现（这段源码是范例程序 FlowTest 的一部分）：

```pascal
var
 S: string;
 I: Integer;
 Found: Boolean;
begin
 S := 'Hello World';
 Found := False;
 for I := Low (S) to High (S) do
 if (S[I]) = 'o' then
 Found := True;
```

在源码的后段，我们可以藉由检查 found 变量的值来得知特定的字符是否在该字符串里面出现过。这样的写法，程序会在发现要检查的特定字符发生后，仍然要把后面所有的字符一一查完才会停止（如果字符串很长，就会浪费掉许多运行时间）

传统的解法，是用 while 循环来同时检查这两个条件(循环的计数器跟 Found这个变量的数值)

```pascal
var
 S: string;
 I: Integer;
 Found: Boolean;
begin
 S := 'Hello World';
 Found := False;
 I := Low (S);
 while not Found and (I <= High(S)) do begin
 if (S[I]) = 'o' then
 Found := True;
 Inc (I);
 end;
```

上述的源码很合逻辑，也很容易读懂，我们还要在上头加一些条件，如果条件变得越来越多且越来越复杂，要把不同条件合并就会让源码变得更繁复。

这就是为何在 Object Pascal 的语言中，提供了系统层级的子程序让我们可以在标准的循环执行流程中进行更改的原因了，更改循环执行的指令有两个：

+ Break 指令：让我们可以中断循环的执行，直接跳到循环外的下一个指
令，中止原本循环还要执行的所有动作。
+ Continue 指令：会从调用这个指令的点，跳过以下所有的循环指令，直接以循环条件的下一个数值从循环开头点继续执行（除非下一个数值已经超过了循环的终止条件，此情形就会跳出循环了）使用 Break 指令，我们可以把原来检查特定字符是否出现的源码改成以下写法：

```pascal
var 
 S: string;
 I: Integer;
 Found: Boolean;
begin
 S := 'Hello World';
 Found := False;
 for I := Low (S) to High (S) do 
 if (S[I]) = 'o' then
 begin
 Found := True;
 Break; // jumps out of the for loop
 end;
```

另外两个系统函数：Exit 跟 Halt，则提供了让我们从子程序执行中直接脱离，以及直接停止整个程序执行的功能。我们会在下个章节介绍 Exit 指令，通常我们不太会调用 Halt 让程序突然中断执行（所以这个指令在本书里面将不会加以介绍）




### 要介绍 Goto 指令了吗?绝不！

事实上除了上述四个指令之外，还有一些方法可以中断程序的执行流程，在最原始的 Pascal 语言里面，有提供了恶名昭彰的 goto 语句，让我们可以直接以不同的标签标注在源码当中进行跳跃。跟条件判断式跟循环不同，
条件判断式跟循环是让我们在连续的指令中进行特定的中止或排除部分条
件。而 goto 这个指令提供的则是不稳定的源码跳跃，所以我们绝不建议在
任何程序当中使用它。我有提过在 Object Pascal 里面支持这个指令吗？我
可没这么说，但我不会提供任何程序范例，对我来说，goto 指令已经不存
在了

绝对不要使用 goto这个指令，因为 goto 指令的跳跃是毫无判断的，直接用 goto 指令跳跃，可能会让许多变数在未进行初始化之前就被使用，或者造成更多损害，记得，千万别用这个指令。

!!! note "笔记"
    还有一些程序语句是我们还没有介绍到的。例如 with 语句，这个语句是用来处理 record 结构的，所以我们会在第五章介绍，With 也是另一个有争议存在的程序功能，不过没有像 goto 这么令人深恶痛绝。