Object Pascal 同时包含了程序型别(也就是可以宣告指向程序或函式的指针型别)以及方法指标(也就是可以宣告指向方法的指标)。

_____________________________________________________________________
!!! note "笔记"
    关于程序型别的相关知识，我们在第四章有详尽的介绍。而关于方法指标型别，则在第十章里面有详细的介绍。

虽然您可能不常直接使用匿名方法，但这是 Object Pascal 当中每个开发人员都会用到的关键功能喔。事实上，方法指针型别是控制组件与视觉组件中事件处理程序的基础：每当我们宣告了一个事件处理程序，即使只是简单的 Button1Click，事实上我们就宣告了一个方法，会用来跟一个事件连结(在这个例子里面是跟 OnClick 事件连结)，连结的关键就是方法指标。

匿名方法延伸了这个功能，让我们可以把一个方法的实际源码当成参数来传递，而不用先定义一个方法，然后把方法的名字当成参数了。然而这并不是唯一的差异。让匿名方法跟其他技术不同的地方，还有它对局部变量生命周期的管理。

上述的定义跟其他语言里面所称的 Closure 功能(例如 Javascript)一样。如果Object Pascal 的匿名方法事实上就是这些编程语言里面所说的 Closure，为什么 Object Pascal 要用跟其他语言不一样的名词?原因是两个不同语言中使用了不同的名词。在 Embarcadero 所提供的 C++编译器所称的 closure 是用来称呼 Object Pascal 所称的事件处理程序。匿名方法在这几年里面已经用不同的形式、不同的名字，在一些编程语言里面出现在世界里，大多是标注式的动态语言。我自己也有用 JavaScript 的 closure 来开发的经验，主要是使用 jQuery 函式库跟 AJAX 来呼叫。对应的功能在 C#里面则是被称为匿名委任。

但在这里我不想花太多时间来比较 closure 跟不同语言当中的相关技术，我们只描述这个技术在 Object Pascal 里面的工作细节。

!!! note "笔记"
    从比较高阶的观点来看，泛型允许源码可以用型别作为参数，而匿名方法则是允许源码可以用方法作为参数。

## 匿名⽅法的语法和语意

在 Object Pascal 里的匿名方法，是一种在表达式的写法中建立方法内容的机制。这个定义听起来很神秘，也相当精确的强调它与方法指标的关键差异，也就是表达式写法的内容。在我们介绍到内容之前，我们先从很简单的范例程序开始吧(本节的程序片段几乎都包含在 AnonymFirst 范例项目里面了)。以下是匿名方法型别的宣告，我们可以从这里看出 Object Pascal 的确是一个强型别的语言:

```pascal
type
 TIntProc = reference to procedure (N: Integer);
```

这个宣告跟方法指标型别的差异只在用于宣告的关键词:

```pascal
type
 TIntMethod = procedure (N: Integer) of object;
```

### 匿名方法变量

以最简单的案例来看，一旦我们宣告了匿名方法型别，当然就可以用这个型别来宣告变量，然后指派一个型别兼容的匿名方法给它，并且后过这个变量来呼叫该方法：

```pascal
procedure TFormAnonymFirst.btnSimpleVarClick( Sender: TObject);
var
 anIntProc: TIntProc;
begin
 anIntProc :=
 procedure (N: Integer)
 begin
 Memo1.Lines.Add (IntToStr (N));
 end;
 anIntProc (22);
end;
```

请记得上例中，用来指派一个实际程序的语法，它是把内嵌的源码指派给变数喔。


### 匿名方法的参数

当成一个有趣的范例(或许用更让人惊讶的语法)，我们可以把一个匿名方法作为参数传给一个函式。假设我们有一个要以匿名方法当成参数的函式:

```pascal
procedure CallTwice (Value: Integer; AnIntProc: TIntProc);
begin
 AnIntProc (Value);
 Inc (Value);
 AnIntProc (Value);
end;
```

这个函式里面会呼叫这个当成参数被传进来的匿名方法两次，把两个不同的整数数值传递进去，第一个数字会被当成第二个参数(匿名方法)的参数传递进去处理。我们呼叫这个函式的时候，只要把一个匿名方法当成第二个参数传进去即可，它的语法非常简单、直接：

```pascal
procedure TFormAnonymFirst.btnProcParamClick( Sender: TObject);
begin
 CallTwice (48,
 procedure (N: Integer)
 begin
 Show (IntToHex (N, 4));
 end);
 CallTwice (100,
 procedure (N: Integer)
 begin
 Show (FloatToStr(Sqrt(N)));
 end);
end;
```

从语法的观点来看，我们要注意到在小括号里面要把程序当成参数传进去时，最后不用加上分号喔。实际的源码里面，是呼叫了 IntToHex，以 48 跟49 和 FloatToStr 来处理 100 跟 101 的平方根，得到了以下的结果:

```
0030
0031
10
10.0498756211209
```

## 使用局部变量

我们也可以透过方法指标来达成一样的功能，虽然语法比较不同，也比较不容易理解。匿名方法最明显的不同，是匿名方法当中在执行源码时使用局部变量的方法。先来一段范例：

```pascal
procedure TFormAnonymFirst.btnLocalValClick( Sender: TObject);
var
 ANumber: Integer;
begin
 ANumber := 0;
 CallTwice (10,
 procedure (N: Integer)
 begin
 Inc (ANumber, N);
 end);
 Show (IntToStr (ANumber));
end;
```

这里所用的范例仍然呼叫 CallTwice 程序，使用了区域参数 n，也从被呼叫的程序当中使用了一个名为 ANumber 的局部变量。效果是什么?这两次呼叫匿名方法的过程中，都会改变局部变量，把传入的参数加进这个变量中，第一次执行的时候是10，第二次执行的时候则是11，所以两次执行完以后，ANumer 的值就变成了 21。

### 展延局部变量的生命周期

前例中显示了一个有趣的效应，但透过一连串巢状函式的呼叫，这个局部变量的变化也就不令人惊讶了。然而匿名方法的强大，是在于它可以使用局部变量，并且展延局部变量的生命周期直到匿名方法不再需要它。范例程序就可以证明，不用再多用文字说明了。

_____________________________________________________________________
!!! note "笔记"
    很明显的，从技术观点来看，匿名方法会把局部变量跟参数都复制到自己被呼叫的当时所使用的 heap，并维持这些变量的生命周期直到匿名方法执行结束为止。

我已经透过类别自动完整的功能，在 TFormAnonymFirst 窗体类别(属于AnonymFirst 范例项目)中加入了一个属性，其类别是匿名方法指标型别(嗯…实际上我已经在该项目中使用了相同的匿名方法指标型别了):

```pascal
 private
 FAnonMeth: TIntProc;
 procedure SetAnonMeth(const Value: TIntProc);
 public
 property AnonMeth: TIntProc
 read FAnonMeth write SetAnonMeth;
```

接着，我们在程序的窗体画面上加了两个按钮，第一个储存了使用局部变量的匿名方法属性，就像前一个 BtnLocalValclick 方法一样:

```pascal
procedure TFormAnonymFirst.BtnStoreClick( Sender: TObject);
var
 ANumber: Integer;
begin
 ANumber := 3;
 AnonMeth :=
 procedure (N: Integer)
 begin
 Inc (ANumber, N);
 Show (IntToStr (ANumber));
 end;
end;
```

当这个方法执行时，匿名方法并没有被执行，只是被储存下来了。局部变量 aNumber 当时初始化之后，内容是 3，并没有被变更，接着这个方法执行完毕，然后被从内存移除了。至少这是我们从标准的 Object Pascal 源码的语法所得到的理解。

我们加在窗体面的第二个按钮，就会实质呼叫储存在 AnonMeth 属性里面的匿名方法了:

```pascal
procedure TFormAnonymFirst.btnCallClick(Sender: TObject);
begin
 if Assigned (AnonMeth) then
 begin
 CallTwice (2, AnonMeth);
 end;
end;
```

这段源码被执行的时候，会呼叫使用到局部变量 ANumber 的匿名方法，但这个时候 ANumber 这个变量在内存当中已经不复存在了。然而，因为匿名方法的源码中有使用到 ANumber，这个情形会让 ANumber 的生命周期被延用到匿名方法结束。

我们再延伸举证，按下 Store 按钮一次，Call 按钮两次，我们就可以看到该变量的内容变化如下:

```
5
8
10
13
```

!!! 笔记
    使用这个操作步骤的原因，是这个变量开始的时候内容是 3，每次点击时，第一次呼叫CallTwice来传递匿名方法(变量值会被加2)，第二则会多加1(所以第二次呼叫就会被加 3)。

现在，再按一次 Store，然后再按 Call。发生了什么事?为什么局部变量的内容被重设了?因为新的匿名方法实体被指派了，旧的匿名方法实体就被删除掉了(包含它的执行内容)，新的执行内容就被储存下来，连同新的局部变量实体一起。完整的程序 Store-Call-Call-Store-Call 程序：

```
5
8
10
13
5
8
```

这是匿名方法行为的含义，重组了一些其他编程语言所做的，这使得匿名方法成为一个很强大的编程语言功能，透过这个功能，我们可以实作出一些以前根本难以完成的东西。


## 在背景里的匿名⽅法

如果变量连带储存的功能是匿名方法的众多功能中，我们可能最常用到的功能之一，但其中还有许多技术是值得我们深入探讨的，在我们开始仔细深入到一些实际案例之前。



### (潜在的)漏掉括号

请注意上面的源码，我们使用了 AnonMeth 这个属性来储存匿名方法，而没有直接呼叫它，要呼叫它的话，我们应该这么写:

`AnonMeth (2)`

差异之处很明显，我们需要传递一个参数给这个方法。如果匿名方法没有括号就更容易混淆了，如果我们这样宣告:

```pascal
type
 TAnyProc = reference to procedure;
var
 AnyProc: TAnyProc;
```

呼叫 AnyProc 必须一定要写空括号了，不然的话编译器会以为我们是要取得该函式的参考，而不是要呼叫它：

`AnyProc ();`

当我们呼叫一个会回传匿名方法的含事实，类似的效应也会发生，就像以下的案例(节录自 AnonymFirst 范例项目)：

```pascal
function GetShowMethod: TIntProc;
var
 X: Integer;
begin
 X := Random (100);
 ShowMessage ('New x is ' + IntToStr (X));
 Result :=
 procedure (N: Integer)
 begin
 X := X + N;
 ShowMessage (IntToStr (X));
 end;
end;
```

现在问题只剩我们是怎么呼叫它的?如果我们简单的直接呼叫:

`GetShowMethod;`

编译会通过，也可以执行，但实际上执行的动作只是进行了匿名方法指派的动作，把匿名方法从该函式回传回来而已。

我们要怎么真正呼叫到匿名方法，并将参数传递进去呢?选项之一，是使用暂存的匿名方法变量：

```pascal
var
 Ip: TIntProc;
begin
 Ip := GetShowMethod(); // The parentheses are needed!!!
 Ip (3);
```

请留意这个情况下，在 GetShowMethod 函数调用后面的小括号。如果我们省略不输入这个小括号(标准 Pascal 语法是可以忽略小括号的)，就会出现以下的错误讯息:

`E2010 Incompatible types: 'TIntProc' and 'Procedure'`

把小括号省略掉，编译器会以为我们想要指派 GetShowMethod 函式，而不是呼叫它所储存的 IP 方法指标。同样的，使用暂存变量也可能是这个案例的最佳选项，同时也把程序不由自主的变得复杂了些，单纯呼叫:

`GetShowMethod(3);`

这样是不能通过编译的，因为我们不能把参数传递到方法里面，我们得在第一个呼叫上面先加上一个空的小括号，然后再把整数参数写在第二个小括号里面，实在够奇怪的，变成要这样写:

`GetShowMethod()(3);`



### 匿名方法实作

在匿名方法实作的背地里，到底发生了什么事?由编译器所建立的源码是基于接口，透过名为 Invoke 的单一呼叫函式(隐藏的)，加上常用的参考技术技术(这对于决定匿名方法以及其实质内容的生命周期相当有用)。

深入内部实作的细节可能会很复杂，且得到的知识相对有限。我只想说，这样的实作是很有效率的，而且每个匿名方法只需要大概多 500 个字符。

换句话说，在 Object Pascal 里面，方法参考是以特别的方法接口来实作的，透过编译器产生的方法具备了相同的特征，而让方法参考来使用。接口的好处则是具备了参考计数以及自动释放的功能。

!!! note "笔记"
    虽然用来实作匿名方法的接口看起来跟其他接口没什么差别，但编译器仍然会把这两种接口自行做出区隔，所以我们可以在源码当中把两者混在一起使用。

除了隐藏式的接口，每一次呼叫匿名方法时，编译器就会建立一个隐藏的对象，储存匿名方法的源码与其所使用的相关内容。这也是为什么我们在每次呼叫匿名方法的时候，都会得到一组被保留的变量了。

### 预先准备好的参考型别

每当我们把一个匿名方法用作参数时，我们就需要定义一个相对的参考指针数据型别。避免这种区域型别不断增加，Object Pascal 提供了一些预先准备好的参考指标型别，就放在 SysUtils 单元文件里面。

我们可以看一下下面的程序片段，大多数的型别定义都使用了参数化的型别，所以透过以下任何一种通用的宣告，我们都可以用来定义任一种可能的数据型别的参考指针型别：

```pascal
type
 TProc = reference to procedure;
 TProc<T> = reference to procedure (Arg1: T);
 TProc<T1,T2> = reference to procedure (
 Arg1: T1; Arg2: T2);
 TProc<T1,T2,T3> = reference to procedure (
 Arg1: T1; Arg2: T2; Arg3: T3);
 TProc<T1,T2,T3,T4> = reference to procedure (
 Arg1: T1; Arg2: T2; Arg3: T3; Arg4: T4);
```

使用这些宣告，我们可以定义以下的匿名方法参数：

```pascal
procedure UseCode (Proc: TProc);
function DoThis (Proc: TProc): string;
function DoThat (ProcInt: TProc<Integer>): string;
```

在前两个案例中，我们是传递不需要参数的匿名方法，在第三个案例中，则是传递一个要求一个整数参数的匿名方法:

```pascal
UseCode (
 procedure
 begin
 ...
 end);
strRes := DoThat (
 procedure (I: Integer)
 begin
 ...
 end);
```

同样的，在 SysUtils 单元文件里面还定义了一系列匿名方法的型别，这些型别会回传一个通用的回传值:

```pascal
type
 TFunc<TResult> = reference to function: TResult;
 TFunc<T,TResult> = reference to function (
 Arg1: T): TResult;
 TFunc<T1,T2,TResult> = reference to function (
 Arg1: T1; Arg2: T2): TResult;
 TFunc<T1,T2,T3,TResult> = reference to function (
 Arg1: T1; Arg2: T2; Arg3: T3): TResult;
 TFunc<T1,T2,T3,T4,TResult> = reference to function (
 Arg1: T1; Arg2: T2; Arg3: T3; Arg4: T4): TResult;
 TPredicate<T> = reference to function (
 Arg1: T): Boolean;
```

这些定义很广泛，我们可以任选需要一到四个参数的匿名方法，而这个匿名方法还可以包含回传值。最后一个定义跟第二个很像，但它用来处理的情形是比较常见的一个特殊案例，是接受一个通用参数，并回传布尔值的函式。

## 实战上的匿名函式

第一眼看上去，不太容易看出匿名方法的强大威力以及在什么情形下使用匿名方法才是最适合的。这就是我决定把重点放在一些实务上的影响，并开始提供进一步说明，而不要再提更多涵盖编程语言功能的实例了。



### 匿名事件处理程序

Object Pascal 最明确的功能之一，就是它使用了方法指标来实作事件处理程序。匿名方法可以用来跟新的行为、事件进行绑定，而无需宣告一个独立的方法也无须实作该方法。这样一来就可以让窗体省下一个额外字段，不用把参数从一个方法传到另一个方法去。

我们再来看另一个范例(AnonButton)，我已经在范例中的按钮上加入了一个匿名的点击事件，宣告一个适当的方法指标型别，并为一个自定的按钮类别(使用中介类别定义)加入一个新的事件：

```pascal
type
 TAnonNotif = reference to procedure (Sender: TObject);
 // Interceptor class 中介类别
 TButton = class (FMX.StdCtrls.TButton)
 private
 FAnonClick: TAnonNotif;
 procedure SetAnonClick(const Value: TAnonNotif);
 public
 procedure Click; override;
 public
 property AnonClick: TAnonNotif read FAnonClick write SetAnonClick;
end;
```

!!! note "笔记"
    中介类别是和其基础类别有着相同名称的衍生类别。让两个类别有相同的名称是可能的，因为只要这两个类别位于不同的单元文件里面，他们的完整名称(单元文件名.类别名称)就会不同。宣告中介类别是很容易的，就像我们把一个按钮组件放到窗体上，然后再加一些控制事件的逻辑上去那么简单，不用在 IDE 里面安装什么组件了。唯一要注意的事项只有一个，就是要记得如果使用了中介类别，它一定是宣告在另一个单元文件里面，记得要使用中介类别的单元文件必须 use 宣告该类别的单元文件才行。

这个类别的源码相对的简单，只有在 Click 方法被呼叫的时候在标准处理程序进行前，把新的指标存起来而已(也就是呼叫 OnClick 事件处理程序，如果它有被定义的话):


```pascal
procedure TButton.SetAnonClick(const Value: TAnonNotif);
begin
 FAnonClick := Value;
 end;
procedure TButton.Click;
begin
 if Assigned (FAnonClick) then
 FAnonClick (self)
 inherited;
end;
```

我们要怎么使用这个新的事件处理程序呢?基本上我们可以指派一个匿名方法给它:

```pascal
procedure TFormAnonButton.btnAssignClick( Sender: TObject);
begin
 BtnInvoke.AnonClick :=
 procedure (Sender: TObject)
 begin
 Show ((Sender as TButton).Text);
 end;
end;
```

现在看起来这写法没有什么重点，因为相同的效果可以用标准的事件处理程序来达成。不过接下来就有点不同了，匿名方法储存一个组件的参考，然后把它传递给事件处理程序，透过参考 Sender 这个参数来达成。

这也可以在它暂时指派给一个局部变量来完成，因为匿名方法的 Sender 参数会把 btnKeepRefClick 方法的 Sender 参数隐藏起来:


```pascal
procedure TFormAnonButton.btnKeepRefClick( Sender: TObject);
var
 ACompRef: TComponent;
begin
 ACompRef:= Sender as TComponent; BtnInvoke.AnonClick :=
 procedure (Sender: TObject)
 begin
 Show ((Sender as TButton).Text +
 ' assigned by ' + ACompRef.Name);
 end;
end;
```

当我们点击了 BtnInvoke 按钮，我们会看到被指派给匿名方法处理程序的组件名字被显示在窗口标题上。

### 为匿名方法计时

开发人员常常会在已开发好的源码里面加入计算时间的源码，用来比较执行速度。假设我们有两段源码，想要比较看看它们执行上百万次所花的时间，我们可以用以下的源码来做，这是第六章里面的 LargeString 范例项目里面节录出来的：

```pascal
procedure TForm1.Button1Click(Sender: TObject);
var
 Str1, Str2: string;
 I: Integer;
 T1: TStopwatch;
begin
 Str1 := 'Marco ';
 Str2 := 'Cantu ';
 T1 := TStopwatch.StartNew;
 for I := 1 to MaxLoop do
 Str1 := Str1 + Str2;
 T1.Stop;
 Show('Length: ' + Str1.Length.ToString);
 Show('Concatenation: ' + T1.ElapsedMilliseconds.ToString);
end;
```

第二个方法的源码也很类似，但使用了 TStringBuilder 类别，而不是直接使用字符串连接。

现在我们可以用匿名方法的长处来建立一个时间点，然后把特定的源码当成参数传递，就像我在 AnonLargeStrings 范例项目中已经改版的源码一样。不使用持续的计时源码一再计算，我们可以写一个方法，包含着计时的源码，让源码被呼叫的时候可以呼叫不用小括号的匿名方法:

```pascal
function TimeCode (nLoops: Integer; proc: TProc): string;
var
 T1: TStopwatch;
 I: Integer;

begin
 T1 := TStopwatch.StartNew;
 for I := 1 to nLoops do
 proc;
 T1.Stop;
 Result := T1.ElapsedMilliseconds.toString;
end;
procedure TForm1.Button1Click(Sender: TObject);
var
 Str1, Str2: string;
begin
 Str1 := 'Marco ';
 Str2 := 'Cantu ';
 Show ('Concatenation: ' +
 TimeCode (MaxLoop,
 procedure ()
 begin
 Str1 := Str1 + Str2;
 end));
 Show('Length: ' + Str1.Length.ToString);
end;
```

请注意，如果我们执行了标准版的程序，以及使用匿名方法的版本，就会从输出的结果发现当中明显的差异，匿名方法的版本可以看出有大约 10%左右的延迟。

原因是没有能够直接执行区域源码，这个程序必须透过虚呼叫匿名方法的实作方式。由于这个差异持续存在，只要这两个测试程序在源码中没有交换数据，测试程序的意义就达到了。

然而如果我们要追求较高的执行效率，使用匿名方法并不会比直接写个一般函式的效率来的好。使用非虚拟方法指标的效能可能会介于这两种作法之间。

### 线程的同步

在多线程的应用程序中，如果需要更新用户接口，我们不可以直接去存取视觉组件的属性(或者内存内对象的属性)，因为它们都是全局线程的一部分，并不具备线程同步的机制。Object Pascal 的视觉组件函式库事实上并不是”线程安全(Thread-Safe)”(但大多数的用户接口函式库则是)，也就是说，如果两个线程同时企图存取同一个对象，会使该对象的状态被混淆。

传统的解决方法是由 Object Pascal 的 TThread 类别所提供，是透过呼叫一个特别的方法: Synchronize，我们把指向另一个方法的参考当成参数传给这个特别的方法，这个被作为参数传入的方法就会被安全的执行。被传入的参数不能要求参数，所以实务上我们通常会在线程类别中加入一个额外的字段当成传递信息的媒介。在 Mastering Delphi 2005 这本书当中，我曾写过一个叫做WebFind的范例项目当成例子(这个程序会透过HTTP搜寻Google的内容，然后把搜寻结果里面的网址从 HTML 里面解析出来)，用以下的线程类别来使用:

```pascal
type
 TFindWebThread = class(TThread)
 protected
 Addr, Text, Status: string;
 procedure Execute; override;
 procedure AddToList;
 procedure ShowStatus;
 procedure GrabHtml;
 procedure HtmlToList;
 procedure HttpWork (Sender: TObject;
 AWorkMode: TWorkMode; AWorkCount: Int64);
 public
 FStrUrl: string;
 FStrRead: string;
end;
```

保护区的三个字符串字段跟一些额外的方法已经被介绍成支持用户接口同步的功能。举例来说，HttpWork 事件处理程序会跟内部的 THTTPClient 对象(这是 Delphi 的客户端函式库组件中的一个)的 OnReceiveData 事件绑定，源码如下，它会呼叫 ShowStatus 方法:

```pascal
procedure TFindWebThread.HttpWork(const Sender: TObject;
 AcontentLength, AReadCount: Int64; var AAbort: Boolean);
begin
FStatus := 'Received ' + IntToStr (AReadCount) + ' for ' + FStrUrl;
 Synchronize (ShowStatus);
end;
procedure TFindWebThread.ShowStatus;
begin
 Form1.StatusBar1.SimpleText := FStatus;
end;
Object Pascal 的 Synchronize 方法有两个不同的多载定义：
type
 TThreadMethod = procedure of object;
 TThreadProcedure = reference to procedure;
 TThread = class
 ...
 procedure Synchronize(
 AMethod: TThreadMethod); overload;
 procedure Synchronize(
 AThreadProc: TThreadProcedure); overload;
```

为了这个原因，我们可以移除 FStatus 文字字段以及 ShowStatus 方法，然后用新版的 Synchronize 来重写 HTTPWork 事件处理程序跟一个匿名方法:

```pascal
procedure TFindWebThreadAnon.HttpWork(const Sender: TObject;
 AcontentLength, AReadCount: Int64; var AAbort: Boolean);
begin
 Synchronize (
 procedure
 begin
 Form1.StatusBar1.SimpleText :=
 'Received ' + IntToStr (AReadCount) + ' for ' + FStrUrl;
 end);
end;
```

使用相同的目标来思考这个类别的源码，则线程类别变成以下所示(我们可以在本书范例源码的 WebFind 范例项目中找到这两种版本的线程类别):

```pascal
type
 TFindWebThreadAnon = class(TThread) protected
 procedure Execute; override;
 procedure GrabHtml;
 procedure HtmlToList;
 procedure HttpWork (const Sender: TObject; AContentLength: Int64;
  AReadCount: Int64; var AAbort: Boolean);
 public
 FStrUrl: string;
 FStrRead: string;
 end;
```

使用匿名方法简化了线程同步作业所需的源码。

!!! note "笔记"
    匿名方法跟线程有许多关连性，因为线程是用来执行某些源码的，而匿名方法则是用来显示源码的。这也是为何 TThread 类别会支持匿名方法，在平行程序函式库(在 TParallel.For 当中，以及定义一个 TTask 的时候)当中也支持。在本章里面介绍多线程之后，我不会在这方面再提供其他的范例了。

但在下一个范例中，我会使用另一个线程，因为在使用到 HTTP 联机的时候，使用线程来运作是很常见的要求。


### Object	Pascal 里的 AJAX

这一节的前一个例子，AnonAjax 范例项目，是我最中意的匿名方法范例(虽然这个范例有点难度)。理由是我学到使用 JavaScript 里面的 closure(或者我们也可以叫他匿名方法)来透过 jQuery 函式库写 AJAX 应用程序。


!!! note "笔记"
    AJAX 是 Asychronous Javascript XML 的简写，通常用来在浏览器里面呼叫Web 服务。这个技术近年来越来越受欢迎，也被广泛使用，Web 服务也逐渐往 REST 架构跟 JSON 格式发展，因此 AJAX 这个名词也开始慢慢失去光环，REST 成为了新一代的浪头。我决定在范例中保留旧的名字，如果您对这个 范 例 的 要 求 有 兴 趣 ， 也 想 了 解 背 后 的历史， 请 参 阅 : https://en.wikipedia.org/wiki/Ajax_(programming)

AjaxCall 全局函式会生出一个线程，然后把这个线程传给一个匿名方法，让该匿名方法可以完整执行。函式只是一个用来包装线程建构函式的外壳而已：

```pascal
type
 TAjaxCallback = reference to procedure (
 ResponseContent: TStringStream);
procedure AjaxCall (const StrUrl: string; AjaxCallback: TAjaxCallback);
begin
 TAjaxThread.Create (StrUrl, AjaxCallback);
end;
```

这些源码都在 TAjaxThread 类别中，这个线程包含一个内部的 HTTP 客户端组件(Delphi 内建的客户端函式库)用来浏览指定的 URL，当然是以异步形式进行:

```pascal
type
 TAjaxThread = class (TThread)
 private
 FHttp: THTTPClient;
 FURL: string;
 FAjaxCallback: TAjaxCallback;
 protected
 procedure Execute; override;
 public
 constructor Create (const StrUrl: string;
 AjaxCallback: TAjaxCallback);
 destructor Destroy; override;
end;
```

这个建构函式做了一些初始化的动作，复制它的参数到线程类别中对应的字段，并建立 FHttp 对象。这个线程类别实际执行的源码，都在 Execute 方法里面，它会进行 HTTP 要求，把结果储存在一个串流结构中，稍后会进行重置，并把它传递给 callback 函式-一个匿名方法:

```pascal
procedure TAjaxThread.Execute;
var
AResponseContent: TStringStream;
begin
 AResponseContent := TStringStream.Create;
 try
 FHttp.Get (FURL, aResponseContent);
 AResponseContent.Position := 0;
 FAjaxCallback (AResponseContent);
 finally
 AResponseContent.Free;
 end;
end;
```

我们再拿 AnonAjax 范例项目来当一个例子，它有一个按钮用来把网页内容复制到一个 Memo 组件上(会在最开头加上要求的网址):

```pascal
procedure TFormAnonAjax.btnReadClick(Sender: TObject);
begin
 AjaxCall (edUrl.Text,
 procedure (aResponseContent: TStringStream)
 begin
 Memo1.Lines.Text := aResponseContent.DataString;
 Memo1.Lines.Insert (
 0, 'From URL: ' + edUrl.Text);
 end);
end;
```

等 HTTP 要求完成，我们就可以在上面做我们想要进行的任何处理了。举例来说，就是把 HTML 档案里的连结解析出来(这也算是把稍早的 WebFind范例执行的结果进行重组)。重申一下，要让这个函式更有弹性，它可以要求一个匿名方法当做参数，这样每个连结就有独立的源码来处理对应的数据了：

```pascal
type
 TLinkCallback = reference to procedure (const StrLink: string);
 
 procedure ExtractLinks (StrData: string; ProcLink: TLinkCallback);
 var
 StrAddr: string;
 NBegin, NEnd: Integer;
 begin
 StrData := LowerCase (StrData);
 NBegin := 1;
 repeat
 NBegin := PosEx ('href="http', StrData, NBegin);
 if NBegin <> 0 then
 begin
 // find the end of the HTTP reference
 NBegin := NBegin + 6;
 NEnd := PosEx ('"', StrData, NBegin);
 StrAddr := Copy (StrData, NBegin, NEnd - NBegin);
 // move on
 NBegin := NEnd + 1;
 // execute anon method
 ProcLink (StrAddr)
 end;
 until NBegin = 0;
  end;
```

如果我们拿这个函式来处理 AJAX 呼叫后的结果，然后提供另一个方法作为进阶的处理，我们实质上就会有两个巢状的匿名函数调用，就像AnonAjax范例项目的第二个按钮:

```pascal
procedure TFormAnonAjax.btnLinksClick(Sender: TObject);
begin
 AjaxCall (edUrl.Text,
 procedure (aResponseContent: TStringStream)
 begin
 ExtractLinks(aResponseContent.DataString,
 procedure (const aUrl: string)
 begin
 Memo1.Lines.Add (aUrl + ' in ' + edUrl.Text);
 end);
 end);
end;
```

在这个例子里，Memo 组件会接收到一连串的连结，而不是回传网页的HTML 文字。上面这段源码所解析出来的连结也可以改成完全针对图片的网址进行解析。ExtractImages 函式就会从 HTML 原始码里面抓出 img 卷标的 src 属性，然后呼叫另一个 TLinkCallback 兼容的匿名方法。现在，我们可以在背景开启一个网页(透过 AjaxCall 函式)，解析出当中的图片网址，然后再呼叫一次 AjaxCall 把图片抓回来。这表示使用一个三层的巢状匿名方法的源码，对一些 Object Pascal 开发人员来说可能会变得很难懂(这会需要一些时间来习惯)，但这真的功能很强大，也很令人印象深刻:

```pascal
procedure TFormAnonAjax.BtnImagesClick(Sender: TObject);
var
 NHit: Integer;
begin
 NHit := 0;
 AjaxCall (EdUrl.Text,
 procedure (AResponseContent: TStringStream)
 begin
 ExtractImages(AResponseContent.DataString,
 procedure (const AUrl: string)
 begin
 Inc (NHit);
 Memo1.Lines.Add (IntToStr (NHit)
  AUrl + ' in ' + EdUrl.Text);
 if NHit = 1 then // load the first
 begin
 AjaxCall (AUrl,
 procedure (AResponseContent: TStringStream)
 begin
 // load image of the current type only
 Image1.Picture.Graphic.
 LoadFromStream(AResponseContent);
 end);
 end;
 end);
 end);
end;
```

!!! note "笔记"
    这个程序片段是我在 2008 年九月的部落格文章中，标题为”Anonymous, Anonymous, Anonymous”，当中也有一些网友的想法交流，读者可以上网看原文: http://blog.marcocantu.com/blog/anonymous_3.html。

除了只能在图片组件中以同样的格式加载档案时，才能让图片显示这一点之外，上面这段源码跟执行的结果都令人印象深刻。请留意到编号的顺序是以被保留的局部变量 NHit 为基础的。如果我们快速点击了该按钮两次会如何?每一次匿名方法都会取得一份 NHit 的数值，然后被用来显示在列表中，当第二个线程开始提供输出结果时，第一个线程的结果可能还没出现呢。尽管有潜在的问题，最后一个按钮的执行结果，看起来会像图 15.1:

图 15.1: 透过三层巢式匿名方法下载网页内容的执行结果
