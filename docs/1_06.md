字符、字符串已经是所有编程语言最常处理的数据型别了。在 Object Pascal里，字符串的处理非常简单、快速，功能也非常强大。就算字符串基本上很容易掌握，且我们在前几章的范例中已经用了很多次字符串作为输出，但在幕后，字符串的处理还是比第一眼的印象来的复杂。文字处理还是使用了一些值得我们仔细了解的主题。要完整了解字符串处理，我们还需要知道关于 Unicode 的表示法、了解字符串怎么跟字符数组对应，还要学习一些在执行时期函式库(RTL)里面跟字符串处理相关的函式，包含把文字存放到档案里，以及从文本文件里面加载文字的方法。


Object Pascal 在文字处理上有一些选项，提供了不同的资料型别跟处理方法。本章的焦点会放在标准的字符串型别上，但我们还是会花一些时间来介绍目前桌机版操作系统仍在使用的旧版字符串型别(例如 AnsiString)。在开始主题之前，我们先从基本开始吧:Unicode 表示法。

## Unicode:全世界的字母

Object Pascal 的字符串管理是以 Unicode 字符集为中心的，尤其是以 UTF-16为主。在我们深入技术细节之前，值得先花一些时间来看一下 Unicode 的标准。

Unicode 的设计理念，是要用单一一个字符集把世界上所有的文字都包含在内，这样的作法把许多复杂的问题单纯化了，但要实现这个理想的过程却极为复杂。在 Unicode 的设计中，是要以同一个叙述方式，同时包含图形化的表示方法，以及独特的数值(称之为 Unicode 的字码)。

!!! note "笔记"
    Unicode 联盟的网址是 http://www.unicode.org，在这个网站中，提供了非常多的文件，最具代表性的文件是 ”The Unicode Standard” 这本书，这本书可以在网上找到：http://www.unicode.org/book/aboutbook.html.

并不是所有程序人员对 Unicode 都熟悉，而且很多程序人员直到今日还在使用比较旧的文字编码方法，例如 ASCII (或者称之为 ISO 编码)。我们很快介绍一下这些旧标准，您就会对 Unicode 的特点(或是其复杂度)更为感激了。


### 旧的文字编码:从 ASCII 到 ISO 编码.186

字符编码始于美国标准信息交换编码 (American Standard Code for Information Interchange – ASCII)，这个标准是在 1960 年代早期发展出来，作为计算机字符的编码标准。当时这个编码法只涵盖了 26 个英文字母 (包含大写、小写)、10 个数字、常用的标点符号，以及一些控制字符(我们到今天还有使用到)。

ASCII 使用 7 bit 来表示系统常用的 128 个不同字符。只有从#32(空格符)到#126(~：波浪号)是肉眼可见的字符，请见图 6.1 (从 Windows 平台的 Object Pascal 应用程序中解译出来的)。

图 6.1: ASCII当中的可视字符集

ASCII 当然是所有字符编码的基础(这基本的 128 个字符目前仍旧是 Unicode编码的核心部分)，后来很快的就以这 128 个字符做延伸，改以 8 bit 做为储存字符编码内容的数据长度，让 ASCII 多了另外 128 个字符。

目前所遭遇的问题，是全世界有太多种不同的语言，但并没有简单的方法让我们可以知道其他语言的文字要怎么被囊括在这个集合当中(就像过去的ASCII 可以把英文字母跟常用符号包含在内一样)。为了让这个情形得到解决，Windows 里面内载了许多种不同的文字集合，我们后来称之为 code page，透过 code page 的定义，我们可以把不同语言的文字依照不同语系的Windows 版本进行显示。除了 Windows 的 Code page 之外，还有许多种类似的分页标准，用来对不同语系的文字进行定义与编码，这些分页方式后来都成为了 ISO 国际标准的一部分。

目前仍旧最常被使用到的 ISO 文字编码标准仍然是 ISO 8859，在这个标准里面定义了好几个区域集合。最常用到的集合(大多数西方国家几乎都使用这个集合)是 Latin 这个集合，也被称为 ISO 8859-1。

!!! note "笔记"
    即使 Windows 1252 code page 跟 ISO8859-1 已经极其接近，但仍然没有完全兼容。Windows 在里面加入了一些额外的字符，例如€符号等，这些额外的字符被添加在 128 到 150 区域中。除了跟 Latin 集合的所有值不同，Windows 的这些延伸值也跟 Unicode 的字码无法兼容。


### Unicode	 字码跟字形

如果要精确一点，我们应该再多介绍另一个概念，就是字码。事实上，有时多个字码可能指向同一个字形(可以辨识的文字)。通常这不一定是单一一个文字，可能是文字，或者文字跟符号的组合。举例来说，如果我们有一串字码代表拉丁文字的 a(#$0061)，尾随着重音符号(#$0300)，这样的组合应该会是代表一个音标符号。

在 Object Pascal 的编码名词中，如果我们写了以下的源码(这是 CodePoints范例项目的一部分)，这个讯息就只会包含一个文字，如图 6.2 所示：

```pascal
var
 str: String;
begin
 str := #$0061 + #$0300;
 ShowMessage (str);
```

图 6.2: 多个Unicode 字码指向单一一个符号

在这个例子里，我们是写了两个字符，代表两个字码，但实际上两个字码的组合只指向一个单一符号。事实上，在拉丁字符集里面，我们可以用一个字码直接指向前述的这个字符(字符 a 加上重音符号的字码是$00E0)，在其他的字符组合，Unicode 的字码是唯一能够取得刚刚这个字符的方法(并且能够正确输出喔)。

即使显示的是一个重音音标，并没有可以自动把这个资料进行转换的方法(只能够适当的显示)，因此字符串内部处理对字符 à 仍旧各有不同的作法。

!!! note "笔记"
    要正确显示由多个字码所组合描述的字符符号，必须依赖操作系统的特别处理，也必须使用文字绘制技术。所以我们应该有发现到，在操作系统上并不是所有的字都可以正确的被显示出来。




### 从字码到字节 (UTF) 

ASCII 使用直接而简单的方法处理字母跟其编码数值之间的对应，Unicode则使用比较复杂的方式。就像我已经提过的，在 Unicode 的每个字符，都有一个关连的字码，但对应到要显示的字符，则通常要复杂的多。

在 Unicode 的原理背后，比较容易让人混淆的元素之一，是同一个字码 (或者说是 Unicode 字符的编码数值)在档案、内存、实际储存在媒体上的时候可能有多种方法加以表示。这个问题主要是因为所有只存在唯一对应的Unicode 的字码实际上都是用了 4 bytes 来表示的。这个作法实现了固定长度表示字符的思路(所有字符都是以固定长度的数值加以表示)，但大多数的开发人员会觉得这样对内存用量和处理程序上都太昂贵了。

!!! note "笔记"
    在 Object Pascal 里面，Unicode 字码是直接以 4 bytes 的 UCS4Char 数据结构来直接表示的。

这也是为何 Unicode 的标准里还定义了其他表示方法，会使用比较少的内存，但在这些表示方法里面，每个符号储存所使用的空间就会随着字码而有不同了。这个想法为最常用的元素提供了短一点的表示方法，而比较不常用的元素则会用比较长的储存空间。

Unicode 字码在实际储存时不同的表现格式，被称为Unicode Transformation Format (简称UTF)。这些格式是算法的对应，它们是Unicode标准的一部分，可以让每个字码(每个绝对的数值都对应到一个字符)对应到每个文字所拥有的代表数值。我们要注意到，这个对应关系是双向的，可以在不同的表现方式中双向转换。

Unicode标准定义了三种UTF格式，分别以不同的长度来为之命名：8, 16, 32。了解这三种格式最多需要4 bytes来为每个字码编码其实还蛮有趣的。

+ UTF-8 把每个字符转换成变动长度来储存，每个字符的编码可能使用1到4个byte不等。UTF-8也是目前HTML跟类似的协议最常用的，因为UTF-8最为精简，所有ASCII的字符几乎完全兼容于UTF-8的格式。
+ UTF-16则是很多操作系统(包含Windows跟Mac OS X)最常用的格式。这个格式可以把绝大多数的文字用两个byte来表示，相对来说很精简，而且处理上速度也很快。
+ UTF-32在处理上速度最快，因为所有字符编码都是相同的长度，但是在内存用量上最浪费，也因此在实务应用上很少被使用。

UTF-16 最常被误解为可以用2 bytes直接对应到所有字码，但因为Unicode包含了超过 10万个 字码，我们直接心算一下，就知道总量超过了2 bytes 所能表示的元素上限 (64K)，所以UTF-16是不可能完整对应到所有 Unicode 字码 的。然而，开发人员常常只需要用到Unicode的一部分子集合，所以会把用到的字符塞到每两个byte定义一个字符，用固定长度来表示。在早期，这个Unicode的子集合被称为UCS-2，现在我们会常看到UCS-2对应到基础多语系平面(Basic Multilingual Plan, BMP)。然而这仍然只是Unicode的子集合(许多语系平面之一)。

_____________________________________________________________________
!!! note "笔记"
    有一个和多位表示法(UTF-16 跟 UTF-32)相关的问题，就是到底哪个位是起始位? 根据 Unicode 标准的定义，怎么排列都可以，所以我们可以定义UTF-16 BE(big endian)或者 LE(litte-endian)，UTF-32 也一样。Big-endian 位序列，是最高位优先(Most Significate Byte first，MSB 优先)。而 little-endian则是最低位优先(Least Significant Byte first, LSB 优先)。位序列通常会标注在档案的最前面，称之为位序列记号(Byte Order Mark, BOM)。

### 位序列记号 (BOM)18914

当一个文本文件里面储存 Unicode 字符，有个方法可以注记这个档案使用哪一种 UTF 格式来储存 字码。这个信息会储存在档案的开头，称为位序列记号(Byte Order Mark, BOM)。这可以视为用以辨识Unicode使用何种序列格式(LE 或者 BE)的签章。以下的列表汇总了一些不同的BOM，包含了2, 3, 4个 bytes的长度:

```
00 00 FF FE UTF-32, big-endian
FE FF 00 00 UTF-32, little-endian
FE FF UTF-16, big-endian
FF FE UTF-16, little-endian
EF BB BF UTF8 
```

我们会在本章后段介绍 Object Pascal 如何用它的串流式类别管理 BOM。BOM会出现在档案的最开头，Unicode数据则会立刻接在BOM后头。所以，一个内容为 AB 的 UTF-8 档案，它的前五个 Bytes 会是这样的(3 个是 BOM, 2 个是 AB 这两个字符):

`EF BB BF 41 42`

如果文本文件开头没有出现 BOM，这个档案就会被认为是 ASCII 文本文件，但这档案也可能包含其他种类编码的文字。

!!! note "笔记"
    在另一种用法，当我们从 Web，或者从其他网络协议接收数据的时候，我们可能得从该协议的其他定义中来区分编码方式，就不能用 BOM 了，反而透过这些协议取数据的时候 BOM 有时还会让编码识别造成困扰。


### 看清楚 Unicode190

我们有可能用像图 6.1 那样的列表来把所有的 Unicode 字符全部列出来吗?我们可以用基础多语系平面(Basic Multilingual Plan, BMP)来显示字码，排除掉代理对应(surrogate pairs)。


!!! note "笔记"
    并非所有数值都是真实的 UTF-16 字码，因为有些字符(称之为代理对应：surrogate pairs)的数值并不是合法的数值数据，这些数值是大于 65535 的数字，超过了 2 bytes 可以描述的范围。代理对应当中一个很好的例子，就是乐谱里面的低音部记号 ，它的字码是 3 个 Bytes 长，内容是 1D122，以UTF-16 表示的话，就需要以两个数值来表示:D834 然后接着 DD22。

要完整把所有这个 BMP 的元素显示出来，需要一个 256x256 的矩阵，在屏幕上面很难完整显示。这也是为何 ShowUnicode 这个范例项目里面在两页之间用了 tab 来做分隔的原因：第一个 tab 显示 256 个区块的主要索引值，第二个 tab 则显示真实 Unicode 的元素。每次显示一个区块。这个范例当中的用户接口比本书其他范例程序都多了一些，如果您只是对 Unicode 有兴趣的话，可以忽略它的源码，只看它的输出部分。

在这个程序的开头，在TabControl的第一个分页当中，我们放了一个TListView组件，这个ListView有256个项目，每个项目被点击的时候，都会显示一组256个Unicode字符。以下是在onCreate这个事件处理程序的源码，我们用了一个简单的程序来显示每一个元素，产出的内容请参考图6.3 :

```pascal
// Helper function
function GetCharDescr (NChar: Integer): string;
begin
 if Char(NChar).IsControl then
 Result := 'Char #' + IntToStr (NChar) + ' [ ]'
 else
 Result := 'Char #' + IntToStr (NChar) +
 ' [' + Char (NChar) + ']';
end;
procedure TForm2.FormCreate(Sender: TObject);
var
 I: Integer;
 ListItem: TListViewItem;
begin
 for I := 0 to 255 do // 256 pages * 256 characters each
 begin
 ListItem := ListView1.Items.Add;
 ListItem.Tag := I;
 if (I < 216) or (I > 223) then
 ListItem.Text := GetCharDescr(I*256) + '/' + GetCharDescr(I*256+255)
 else
 ListItem.Text := 'Surrogate code points';
 end;
end;
```

图 6.3: ShowUnicode范例的第一页，程序画面列出了很长的Unicode 字符区块

请留意源码里面是怎么储存这些分页的号码的，我们用了 ListView 项目中的 Tag 这个属性，这个属性储存了稍后我们用来显示该项目对应的区段所需的信息。当用户点击任一个 ListView 的项目时，应用程序就会切换到TabControl 的第二个分页，把该区段的 256 个字符显示在字符串矩阵上。

```pascal
procedure TForm2.ListView1ItemClick(const Sender: TObject; const AItem: TListViewItem);
var
 I, NStart: Integer;
begin
 NStart := AItem.Tag * 256;
 for I := 0 to 255 do
 begin
 StringGrid1.Cells [I mod 16, I div 16] :=
 IfThen (not Char(I + NStart).IsControl, Char (I + NStart), '');
 end;
 TabControl1.ActiveTab := TabItem2;
```

这里的 IfThen 函式是用来作两种测试：如果传入的第一个参数为真(或说判别式成立)的话，这个函式会把第二个参数回传。反之，则以第三个参数回传。在这里的测试使用了 InControl 这个字符型别助手的方法，用来过滤掉无法显示的控制字符。

_____________________________________________________________________

!!! note "笔记"
    这里用到的 IfThen 函式，作用很像 C 语言阵营里面的?:运算符号。同样的函式，有一个用来测试字符串的版本。另一个则是用来测试整数的版本。测试字符串的版本放在 System.StrUtils 单元文件里面，测试整数的版本则是放在 System.SysUtils 里面。

由范例程序所输出的 Unicode 字符矩阵，我们用图 6.4 来做显示。请注意到输出的字符，会随不同操作系统当中的显示有所差异，即使选择的是同样的区块，Window 跟 Mac 的显示仍可能有差异。


图 6.4: ShowUnicode范例的第二页，程序画面显示了实际的Unicode 字符




## 再度介绍字符型别

在简单介绍过 Unicode 之后，我们回头来看一下这个一章的主题，也就是Object Pascal 式如何管理字符跟字符串的。我们在第二章已经介绍过了字符型别，也提到了型别助手的相关函式可以在 Character 单元当中引入来使用。现在，我们也对 Unicode 有了更完整的认识，是时候深入多看些细节了。

首先，字符型别并不是一成不变的记录 Unicode 的字码。事实上字符型别对每个元素都使用两个 bytes 来记录其数值。当字符以 Unicode 的基础多语系平面(Basic Multilingual Plan, BMP)来表示字码的时候，同时也可以透过代理对应(surrogate pairs)来显示字码。

技术上来说，我们可以使用另一个型别来直接记录任何一个 Unicode code，这就是 UCS4Char 型别，它使用了 4 bytes 来记录一个数值。这个型别不常被使用到，因为它需要使用更多的内存空间，我们可以看到 Character 单元里面提供了好几种不同的处理方法给这个型别使用。

回到字符型别，请记得它是一个有序型别(可能是该类型别当中相对比较大的)，所以这个型别本身有顺序性，并且我们也可以把 Ord, Inc, Dec, Hight, Low 这些函式套用在字符型别上面。绝大多数的延伸功能，都包含在型别助手里面，它们不是基本的系统 RTL 单元的一部分，所以我们得自己引入Character 单元。





### 使用 Character 单元来处理 Unicode

大多数对于 Unicode 字符的特殊处理(当然也包含 Unicode 字符串)都被定义在 System.Character 这个单元当中。这个单元文件定义了 TCharHelper 这个型别助手，用来提供 Char 型别协助，让我们可以直接从 Char 型别的变量中直接呼叫这些处理方法。

_____________________________________________________________________
!!! note "笔记"
    Character 单元定义了一个名为 TCharacter 的纪录型别，在这个型别当中定义了一系列的静态类别函式，搭配有一些全局的子程序跟这些方法对应着。这些函式已经比较老，有些都已经被弃用了。在 Unicode 层级来处理 Char型别，这些方法却仍然是很有帮助的，所以透过类别助手提供。

这个单 元 文 件也定义了 两 个 很 有 趣 的 列 举 型 别 。 第 一 个 称 为TUnicodeCategory，它对应了很多分类的字符，例如控制字符、空格符、大写字符、小写字符、数字、标点符号、数学符号等等。第二个则是TUnicodeBreak，它定义了许多不同的空白、分隔符。如果我们惯用 ASCII各种处理方法，那改用 Unicode 就将是个很大的改变。例如，在 Unicode 里面，数字不只是从 0 到 9 的字符，空白也不仅仅是字符#32，在其他许多不同语系的字符分页中，都可能也有相同意义的字符。

字符型别助手提供了超过 40 个不同的测试与处理方法，可以用来：

+ 取得字符的数字表示值(GetNumericValue)
+ 询问目录(GetUnicodeCategory)，或者检查字符是否属于不同目录当中的任何一种(IsLetterOrDigit, IsLetter, IsDigit, IsNumer, IsControl, IsWhiteSpace, IsPunctutation, IsSymbol, 以及 IsSeparator)。在前一个范例中，我就使用了其中的 IsControl 判断方法。
+ 检查是否是大写或小写(IsLower, IsUpper)或者转换大小写(ToLower, ToUpper)
+ 确 认 是 否 属 于 UTF-16 代理对应 (surrogate pairs)(IsSurrogate, IsLowSurrogate, IsHighSurrogate)，也可以用不同方式转换代理对应字符。
+ 把 字 符 转 换 成 UTF-32 或 从 UTF-32 字 符 转 换 为 字 符 型 别(ConvertFromUtf32, ConverToUtf32)，或者转换成 UCS4Char 型别(ToUCS4Char)
+ 确认是否为特定字符里面的成员(IsInArray)

请留意以上所有的动作，是对字符型别全体内容一体适用，不是只对特定变数有效。所以我们可以用字符型别当做起始关键词，来使用上述的方法，我们将在底下的程序范例里面介绍。

为了把这些方法对 Unicode 字符做点实验，我们提供了范例项目，名称是CharTest。这个范例当中的一个实验，是对 Unicode 字符呼叫大写、小写操作方法的实验。事实上在 RTL 里面，传统的 UpCase 方法只能对基本的 26个 ASCII 表示法的英文字母有效，对于 Unicode 字符来说就没有所谓的大小写之分。(因为并非全世界的文字都有大小写之分，所以这个方法无法一体适用于所有的 Unicode 字符)

为了测试这个情境，在 CharTest 这个范例项目里面，我加入了以下的程序片段，试着把一个音标符号转成大写:

```pascal
var
 Ch1: Char;
begin
 Ch1 := 'ù';
 Show ('UpCase ù: ' + UpCase(Ch1));
 Show ('ToUpper ù: ' + Ch1.ToUpper); 
```

传统的 UpCase 没办法把拉丁音标符号进行转换，而 ToUpper 方法可以做的很完美：

```
UpCase ù: ù 
ToUpper ù: Ù
```

在字符型别助手里，包含了许多跟 Unicode 相关的功能，像是在接下来的范例源码里面所介绍的，这些源码定义了一个字符串，字符串里面的文字也涵盖了在 BMP(低于 Unicode 64K 以下的字码)以外的字符，以下的源码也是CharTest 范例项目的一部分，我们对字符串中的不同字符做了一些测试：

```pascal
var
 str1: string;
begin
 str1 := '1.' + #9 + Char.ConvertFromUtf32 (128) +
 Char.ConvertFromUtf32($1D11E);
 ShowBool (str1.Chars[0].IsNumber);
 ShowBool (str1.Chars[1].IsPunctuation);
 ShowBool (str1.Chars[2].IsWhiteSpace);
 ShowBool (str1.Chars[3].IsControl);
 ShowBool (str1.Chars[4].IsSurrogate);
end;
```


显示结果的函式在这个范例中是一个改写过的版本：

```pascal
procedure TForm1.ShowBool(value: Boolean);
begin
 Show(BoolToStr (Value, True));
end;
```
_____________________________________________________________________
!!! note "笔记"
    Unicode 字码 $1D11E 是乐谱符号的 G 调符号。



### Unicode 字符常数 (Unicode	Character	Literals).195

在前几个范例中，我们已经知道可以直接把独立的字符常数或字符串常数指派到字符或字符串变量里面了。我们也可以直接透过数字把字符表示指派给字符串或字符变量，只需在数字前加上一个井字号(#)，不过这样可能会发生意外。

为了兼容以前的源码，直接输入的字符常数内容会依照它们的内容来进行转换。以下的程序片段就是把数值 128 指派给字符串，这个字符串就会显示成欧元符号(€):

```pascal
var
 Str1: string;
begin
 Str1 := #$80;
```

以上的源码并没有遵循 Unicode 规范，上面这个符号的字码是 8364。事实上，这个数值并不是从 ISO 官方的 codepage 定义而来，而是从微软为Windows 实作的数据得来的。为了让已经存在的源码能够更轻松的转移到Unicode 兼容的功能。Object Pascal 编译器会把 2 码的字符串数值直接当成ASCII 字符来处理(当然这得视用户的计算机所使用的 code page 而定)。如果我们把数值转成字符，然后再加以显示，数值就会变成其代表的正确字符了，够惊讶吧? 所以，以下的源码执行以后：

`Show (Str1 + ' - ' + IntToStr (Ord (Str1[1])));`

就会得到这样的输出：

`€ - 8364`

假如我们想要完整的把原有的源码全部转换为 Unicode, 把 ANSI 数据全部放弃，我们可以直接修改编译器设定，只要直接在源码里面加入这个设定: #HIGHCHARUNICODE 即可。这个设定会决定从 #$80 到 #$FF 的数值要被怎么处理? 我们稍早曾介绍过默认的选项(关闭: OFF)，如果我们把这个设定开启了，同样的程序就会输出如下的结果：

` - 128`

这些数值会被直译为实际的 Unicode 字码，而输出值就会变成看不见的控制字符。另一个处理特定字码(或者任何#$FFFF 以下的 Unicode 字码)的方法，则是使用四个数字加以表示:

`Str1 := #$0080;`

这样也仍然不会显示欧元符号，除非我们把$HIGHCHARUNICODE 这行设定拿掉。

!!! note "笔记"
    以上的源码只能在 US 或西欧字符的设定下正常运作。如果设定为其他语系，在 128 到 255 之间的字符会显示为不同的样子。

但我们可以用 4 个数值来表示远东的文字，例如以下的程序可以显示两个日文文字：

```pascal
str1 := #$3042#$3044; Show (str1 + ' - ' + IntToStr (Ord (str1.Chars[0])) +
 ' - ' + IntToStr (Ord (str1.Chars[1])));
```

上面的文字可以显示出以下的结果：

`あい- 12354 – 12356`

_____________________________________________________________________
!!! note "笔记"
    あい可以译为”相遇”，但我不是完全确定我找到的这个翻译是不是正确，因为我不懂日文，所以或许会有错。

    以上为原作者的文字。あい要看在日文裡面的前後文跟漢字的轉換，あい可以對應漢字的”愛”，或者逢い。
    逢い：就像原作者找到的翻譯，就是相逢、遇見的意思。
    愛: 這不用我多說了……


我们也可以用超过#$FFFF 的数值，这些数值会自动被转换为适合的代理对应(surrogate pairs)。

### 那单位元的字符呢? .197

一如我在前面的章节里面提过的，Object Pascal 语言已经把 Char 型别对应为 WideChar 型别，但还是保留有 AnsiChar 型别的定义，主要是为了跟已存在的源码兼容。建议的作法是使用 Byte 型别来处理单位元的数据结构，虽然 AnsiChar 型别对于处理单位元的字符也很方便。

在行动平台上，Delphi 已经有好几个版本不支持 AnsiChar 型别了，但从 10.4开始，Delphi 编译器又开始让 AnsiChar 型别在各种平台都能使用。但在使用特定平台的 API，或者是要储存档案的时候，我们仍然应该避免使用单位元的字符型别，哪怕现在已经又提供支持了。虽然事实上使用单位元字符处理会比使用多位字符的处理耗用较少的内存，但使用 Unicode 编码才是比较安全也是比较建议的作法。



### 字符串数据类型 (String	Data	Type) 

在 Object Pascal 里的字符串数据类型比简单的字符数组更为完整，而且其功能比大多数有提供字符串型别的编程语言的字符串功能更为强大。在这一小节里面，我也会介绍在这个型别当中的关键观念。接下来这个章节里，我们会更深入的介绍这些功能。

在以下的列表中，我会列出在 Object Pascal 里面字符串型别运作的关键概念(请记得，我们可以不用了解观念，也仍然可以把字符串使用的很好，且内部运作的规则也是非常透明的)：

+ 字符串型别所使用的数据，是从 heap 里面动态配置来的。字符串变量就是实际数据的参考。但我们完全不用担心这些细节，因为编译器会帮我们把这些细节都处理好。就像使用动态数组一样，我们宣告一个新的字符串变量时，变量的内容是空的。
+ 我们有很多方法可以把数据存放到字符串变量里面去，我们可以实际去配置一块指定大小的内存空间-透过呼叫 SetLength 函式，它所需的参数，是我们需要配置的字符数量，(当然，每个字符是 2 bytes)。当我们扩展一个字符串的时候，已经存在该字符串的数据会被保留的好好的(不过这些数据可能被搬移到不同的内存空间去)。当我们把字符串缩小的时候，原本的字符串内容就有部分可能会遗失。我们几乎不用去重新设定字符串的长度。仅有的情境是当我们要把一个字符串的缓冲区传递给操作系统特定的函式使用。
+ 如果我们需要增加字符串在内存的空间(例如做两个字符串连接)，但有可能原来的内存地址已经无法再扩增(可能后面的空间被其他变量用到了)，此时连接字符串的动作就需要重新配置一段足以容纳连接之后的字符串数量的内存，然后把两段字符串都复制到新的这段内存空间，然后把旧的那段给释放掉。
+ 要清除掉我们已经不再使用的字符串，我们可以直接指派一个’’给字符串变量，或者我们可以用 Empty 这个常数，它也同样代表了’’这个字符串。
+ 根据 Object Pascal 的规则，字符串的长度(我们可以透过呼叫 Length函式来取得)代表该变量里面合法字符的数量，而不是配置的数量。跟 C 语言不同的是，C 语言的字符串必须用#0 作为字符串结尾，而所有版本的 Pascal 都直接配置了字符串内容所需的内存大小。然而我们仍然可以在字符串的尾端找到字符串结尾符号。
+ Object Pascal 的字符串使用了参考计数(Reference-Counting)机制，这可以持续追踪一共有多少个字符串变量在参考这段内存。参考计数会在某个字符串变量再也没有被使用的时候释放内存。也就是说，当没有任何变量参考该段内存的时候，参考计数的数值就会变成 0.
+ 字符串使用了”写入时才复制”的技术，这是很有效率的。当我们把一个字符串指派给另一个字符串，或者把字符串当成参数传递给函式时，没有任何数据被复制，只是参考计数会被增加而已。然而，如果其中一个参考到这个内存的变量试图改变内容，这个时候系统就会把该段内存复制到另一个空间，然后改变这个新空间的内容，原本的内存内容不会被改变。
+ 字符串连接的作法，对已经存在的字符串是很快的，且没有明显的副作用。当有其他替代的要求时，字符串链接是很快且很强大的。但这对于目前已经存在的许多编程语言来说，就没这么简单。

我猜以上的描述可能会让许多人迷惑，所以我们来实际看看字符串的使用。我们待会来看一些以上提到的功能的示范，包含参考计数跟写入时才复制。在我们开始介绍前，我们先来回头看一下字符串型别助手的功能以及一些其他在基本 RTL 上面的字符串管理功能。

首先，我们先来说明前面列表里面的一些名词。因为字符串的处理已经相当无缝化，所以很难完整解析到底发生了什么事。除非我们开始深入观察字符串的内存结构，这一点我们在本章后续会持续深入，但这个主题目前有点太深奥。所以我们开始看一些简单的字符串处理吧，从 Strings101 应用程序项目开始:

```pascal
var
 String1, String2: string;
begin
 String1 := 'Hello world';
 String2 := String1;
 Show ('1: ' + String1);
 Show ('2: ' + String2);
 String2 := String2 + ', again';
 Show ('1: ' + String1);
 Show ('2: ' + String2); 
end; 
```

当上面这段源码执行，会让我们知道，当我们把同一个字符串指派到两个不同的字符串变量时，变更其中一个的内容，另一个并不会被影响到。在这个例子里，String1 并不会被 String2 的内容变动所影响到。

```
1: Hello world
2: Hello world
1: Hello world
2: Hello world, again 
```

在我们开始下一个范例之前，先说明一下，一开始的字符串指派动作，并不会导致字符串的完整复制，复制的动作会被延迟，这个功能叫做”写入时才复制”。

另一个要介绍的重要功能，则是字符串长度是如何被管理的。如果我们查询一个字符串的长度，我们所得到的响应，会是字符串所使用的实际长度值(这个长度的数据是储存在字符串的 meta data 里面，会使得这个查询动作非常快就能完成)。但如果我们呼叫 SetLength 函式，这个函式会直接配置内存，但不会进行初始化。这常被用在把字符串当成呼叫外部系统函式的参数时。

相反地，如果我们需要一个空字符串，我们可以使用虚拟建构函式 Create。最后，我们可以用 SetLength 来切断一个字符串。以上所提到的都包含在以下的源码里面：

```pascal
var
 String1: string;
begin
 String1 := 'hello world';
 Show(String1);
 Show ('Length: ' + String1.Length.ToString);
 SetLength (String1, 100);
 Show(String1);
 Show ('Length: ' + String1.Length.ToString);
 String1 := 'Hello world';
 Show(String1);
 Show ('Length: ' + String1.Length.ToString);
 String1 := String1 + String.Create(' ', 100);
 SetLength (String1, 100);
 Show(String1);
 Show ('Length: ' + String1.Length.ToString);
```

输出的结果会像以下这样:

```
Hello world
Length: 11
Hello world~~~~ʹ~Η~υ~ϧ~Ј~Щ~ы~ѭ~ҏ~ұ~Ә~Ӽ~ԟ~Շ~հ ~~~۵~  ~ࢮ~  ~  ~  ~  ~~◌߰ ߊ~ޤ~ރ~ݡ~◌~ܚ
~~ ٔ ~~ו~ ~؛~ف~٦~ڋ~گ~ے
Length: 100
Hello world
Length: 11
Hello world
Length: 100
```
我想在本节里面强调的第三个概念，是空字符串的概念。所谓的空字符串，是指字符串变量所存的文字内容是空的。不论是在指派或是比对的用途，我们都可以用两个单引号，或者用特定的函式来表示空字符串：

```pascal
var
 String1: string;
begin
 String1 := 'Hello world';
 if String1 = '' then 
 Show('Empty')
 else
 Show('Not empty');
 String1 := ''; // Or String1.Empty;
 if String1.IsEmpty then
 Show('Empty')
 else
 Show('Not empty');
```

输出的结果如下:

```
Not empty
Empty
```
### 把字符串作为参数传递

我们刚提到过，如果我们把字符串指派给另一个字符串，实际上的动作只是复制了内存地址，内存内所存放的字符串内容并不会被复制。然而如果我们的源码在字符串变量被指派后改变了字符串变量的内容，原始字符串的内容会被复制到另一个内存空间(也只有在这个时间点)，然后进行修改。

另一个会发生类似复制程序的时间点，是在把字符串作为参数传递给一个函式或程序的时候。默认的作法，系统会制作一个新的内存地址来存放作为参数的字符串，这样一来，万一在子程序中改动了作为参数的字符串，原始的字符串就不会受到影响。假如我们要使用另一种规则，就是子程序当中的改动要直接套用在原始字符串变量的话，我们就得用传址呼叫的方式，只需在参数前面加上一个 var 关键词即可(同样的作法也可以套用在大多数其他简单的数据型别上面)。

但是万一我们不想改动这个作为参数被传递进子程序的字符串呢? 在这个情形下，我们可以在参数前面直接加上个 const 描述字即可。加上了 const描述字之后，编译器就不会让我们在子程序里面改动该字符串变量的内容了，但是同时也会优化参数传递的动作。事实上，const 字符串不会要求子程序进入时增加该字符串的参考计数，也不会要求离开时减少该字符串的参考计数，因为程序知道这个字符串是无法被修改的。

由于字符串管理程序的动作非常快，执行它们数千或数百万次，也只会对我们的程序增加很轻微的负担。这也是为何我们建议在没有改动参数需求时，直接在参数前加上 const 关键词的原因了。(这作法可能会有些潜在的问题，我们在底下的笔记来讨论一下)。

用程序的语汇来说，以下的三个程序宣告中，字符串参数的传递都是使用不同方法来达成的：

```pascal
procedure ShowMsg1 (Str: string);
procedure ShowMsg2 (var Str: string);
procedure ShowMsg3 (const Str: string);
```

!!! note "笔记"
    近几年，有个强烈的潮流，是要求除了在函式或者方法当中会异动到字符串内容的情形外，把所有字符串参数都以 const 方式宣告。但是，有个重大警讯。对于常数字符串参数，编译器会单纯的只取该字符串的内存地址，也只把它当成一个指向内存地址的指针，并不会『管理』该字符串(不会进行参考计数等管理)。编译器也只会检查该函式里面的源码确保没有对该字符串参数进行异动。然而编译器并不会管该段内存内容发生了什么事情。对于字符串的异动可能影响内存的编排与位置，这些都是用一般方式传递字符串参数的时候会自动进行的管理(在一个字符串需要多重参考时，会自动触发写入时自动复制的程序)，但如果用 const 方法传递字符串参数，这些检查跟处理就会被忽略。换句话说，对于用 const 传入参数原始字符串的变动，可能会导致该字符串发生错误，更严重的可能会发生内存存取错误。




### []的使用，以及字符串中对字符计数的模式.203

一如我们所知的，我们在使用 Object Pascal 或者其他编程语言的时候，有个很关键的字符串处理方法，就是取用字符串当中的某一个元素(字符)，有时我们会透过方括号，也就是取用数组元素的相同方法。

在 Object Pascal 里，提供了两个明显不同的方式来达成这个功能：

+ Chars[]字符串型别助手的处理，可以把字符串当成一个只读的字符数组，是 0 base 的索引值喔。
+ 标准的[]字符串处理函式，支持读取与写入，也预设使用传统 Pascal的以 1 起始的索引规则。这个设定可以透过编译器设定来修改。我们用以下的笔记来厘清一些这当中的前因后果。把它列为笔记的原因，是让对这些历史没有兴趣的读者可以先跳过，而且如果没有先了解一下过往的作法，对于目前为什么要这样处理，很容易迷惑。

!!! note "笔记"
    让我们先回顾一下历史，所谓鉴古而知今。在 Pascal 语言的早期，字符串是被以类似字符数组的方式来处理的。在这个字符数组的第一个元素(可以视之为数组的第 0 个元素)是用来记录字符串当中合法字符的数量的。在那个时期里，C 语言要计算字符串长度时，就得每次都重新计算一次，直到字符串结尾出现 NULL 字符为止。而 Pascal 的程序只需要直接检查第一个Byte 即可，假设第 0 个 byte 是用来记载字符串的长度，字符串的第一个字符就是从数组的第 1 个元素开始了。

随着时间经过，其他编程语言都随着 C 语言的习惯，把字符串跟数组的第一个元素从索引值 0 开始记录。后来，Object Pascal 也开始让动态数组改为0 base，渐渐的所有 RTL 跟组件库里面相关数据结构也都改为了 0-base，唯独字符串是一个很特别的例外。而在迈入了行动开发的纪元之后，Object Pascal 语言的设计群们决定提供字符串的 0-base 优先权，让开发人员万一有旧的源码需要移植到行动平台时可以选择，这可以透过编译器控制设定来调整。在 Delphi 10.4 当中，之前的决策被推翻了，这是为了让既有的原始码能更有一致性，不要因为对应布署的平台不同而有所影响。换句话说『单一原始码，多种平台』这个目标的优先性赢过了『更像现代语言』这个目标。

假如我们想要对索引值使用 0-base 跟 1-base 的不同做出一个比较详细的比较，可以思考一下在欧洲跟北美洲对于楼层的算法(我真的不知道世界上其他地区是怎么计算的)。在欧洲，地面楼层称为 0 楼，一楼则是高于地面楼层的第一层楼。而在北美洲，地面楼层称为 1 楼，高于地面楼层的第一层则算是 2 楼。换句话说，北美洲使用 1 base 的楼层计算法，而欧洲是使用 0 base的计算法。

而对字符串来说，绝大多数的编程语言都使用 0 base 的楼层算法，不管该语言是在美洲还是欧洲大陆发明的。Delphi 跟大多数从 Pascal 衍生的语言都使用 1 base 的标注方法。

我们再更深入一点来说明，以刚刚我们介绍过的，Char[]使用了 0 base 的索引法，所以如果我们写:

```pascal
var
 String1: string;
begin
 String1 := 'Hello world';
 Show (String1.Chars[1]);
```

则输出的字符会是:

`e`


而如果我们直接使用方括号来存取字符串内容，把源码改写如下：

`Show (String1[1]);`

这个输出的预设结果是 H。但如果编译器定义了$ZEROBASESTRING 为 on，输出结果就会是 e。目前我们的建议作法(在 10.4 推出之后)，是在大家的源码当中对所有的字符串都使用 1-base，而不要因为过去的源码使用默认的0-base 做字符串处理而进行调整。

但如果我们想要写段源码，而不受到$ZEROBASESTRING 设定的影响，该怎么做? 我们可以把索引值抽象化，例如使用 Low(String)作为该字符串第一个索引值，而使用 High(String)作为最后一个索引值。这个写法不管编译器设定怎么改，都不会受到影响:

```pascal
var
 S: string;
 I: Integer;
begin
 S := 'Hello world';
 for I := Low (S) to High (S) do
 Show(S[I]);
```

换句话说，字符串毫无例外的拥有着从 Low 到 High 函数所回传的数值之间的所有元素。


!!! note "笔记"
    字符串就是字符串，关于 0-base 的字符串概念，我讲的完全是错的。在内存的数据结构并没有不同，所以我们可以我们可以把任何字符串当做参数传递给任何函式，不管是编译器怎么设定，都不会出错。换句话说，如果我们有段程序，在编译器设定为 0-base 字符串时，需要把字符串传递给1-base 的函式库，系统仍然是可以正确处理这种情形的。


### 字符串连接

我已经提过，Object Pascal 跟其他编程语言不一样，Object Pascal 完全支持直接进行字符串连接，而且处理速度相对比其他编程语言更快。在这一章里面，我已经示范过一些字符串连接的源码，当中也做了一些速度测试。在后面的章节里，在第 18 章，我会介绍关于 TStringBuilder 这个类别，这个类别遵循了.NET 组成字符串的表示法。因为使用 TStringBuilder 有一些原因，效能并不是最重要的一个(在以下的范例中，我会加以示范)：

```pascal
var
 Str1, Str2: string;
begin
 Str1 := 'Hello,';
 Str2 := ' world';
 Str1 := Str1 + Str2;
```

请留意到我把 Str1 这个变量同时使用在一个指派动作的左边跟右边，把一一些新的内容加在已经存在的字符串后面，而不是指派给另一个全新的变数。当然两种作法都没问题，只是加在一个已存在的字符串的效能会比较好。这种字符串连接的作法也可以用个循环来达成，就像以下从 LargeString 范例项目中撷取的部分源码：

```pascal
uses
 Diagnostics;
const
 MaxLoop = 2_000_000; // two million
var
 Str1, Str2: string;
 I: Integer;
 T1: TStopwatch;
 begin
 Str1 := 'Marco ';
 Str2 := 'Cantu ';
 T1 := TStopwatch.StartNew;
 for I := 1 to MaxLoop do
 Str1 := Str1 + Str2;
 T1.Stop;
 Show('Length: ' + Str1.Length.ToString);
 Show('Concatenation: ' + T1.ElapsedMilliseconds.ToString);
end;
```

这段源码执行后，我从 Windows 虚拟机跟 Android 装置分别得到以下的输出结果(桌机比较快一点):

```
Length: 12000006 // Windows (in a VM)
Concatenation: 59
Length: 12000006 // Android (Nexus 4)
Concatenation: 991
```

这个范例项目的源码跟 TStringBuilder 类别的作法有些类似。目前我还不想深入这个范例项目的源码(在第 18 章的时候我们再深入讨论)，我们只要先看实际上执行所花的时间，和直接连接的作法所花的时间做个比较

```
Length: 12000006 // (in a VM)
StringBuilder: 79
Length: 12000006 // Android (on device)
StringBuilder: 1057
```

我们可以看到，连结的动作可以说是最快的选项。



### 字符串助手的处理程序

假如字符串型别是重要的，那么这个型别的助手所提供的处理程序应该不少，这一点应该不会让人太惊讶。假如这些处理程序在大多数的应用程序中都很重要，且很常被使用到，那我就认为应该用个清单好好来介绍一下:

Delphi 传统的全局字符串处理函式跟字符串助手类别的方法有个关键上的差异：传统处理字符串时，会以 1-based 字符串处理，而字符串助手类别则会使用 0-based 的逻辑。

我已经把字符串型别助手的处理程序用逻辑先编组了(这些程序大多都拥有好几个多载版本)，简单的叙述一下它们的功能，大多数的功能名称都能很直觉的看出其功能：

+ 复制整个字符串或复制部分字符串，像 Copy, CopyTo, Join 跟SubString
+ 字符串变更程序，像 Insert, Remove, Replace
+ 把一些不同型别的数据转成字符串，我们可以用 Parse 或 Format
+ 把字符串转成不同型别的数据，我们可以用 ToBoolean, ToInteger, ToSingle, ToDouble, ToExrtended, 如果要把字符串转成字符数组，我们也可以用 ToCharArray
+ 把空格符塞到一个字符串里，我们可以用 PadLeft, PadRight, 也可以用 Create 的一个多载版本。相对的，我们也可以把空格符移除，透过 TrimRight, TrimLeft 跟 Trim.
+ 字符串的比较跟相符测试(Compare, CompareOrdinal, CompareText, CompareTo 跟 Equals)- 请记得，我们也可以用等号来比较两个字符串是否内容相同。
+ 转换大小写用 LowerCase, UpperCase, ToLower 跟 ToUpper 以及 ToUpperInvariant
+ 检查字符串内容，我们可以用 Contains, StartsWith, EndWith。要搜寻特定字符是否存在该字符串中，也可以用 IndexOf 来达成(从字符串开头到特定位置之间的搜寻)。相似的函式还有 IndexOfAny(这可以搜寻 一 个 字 符 数 组 中 的 任 何 一 个 字 符 是 否 存 在 该 字符串 中 ) ，LastIndexOf 跟 LastIndexOfAny 可以从字符串结尾开始回头找特定字符最后一次出现在该字符串的位置。最特殊的处理程序，则是IsDelimiter 跟 LastDelimiter，这是用来检查字符串的结尾是否是目录分隔符，以及最后一次目录分隔符出现的位置。
+ 要存取字符串长度，我们可以用 Length 函式，这个函式会回传字符串中内含字符的数量。CountChars 则可以把代理对应(surrogate pair)一并计算，GetHashCode，可以回传该字符串的哈希值(Hash value)。另外还有一些检查的程序，例如 IsEmpty, IsNullOrEmpty 以及IsNullOrWhiteSpace 都可以用来检查字符串的内容是否为空。
+ 字符串的特殊处理程序，像是 Split, 可以帮我们把字符串依照特定分隔符(或分隔字符串)切成许多部分。要移除或加入引号，我们可以用QuotedString 或者 DeQuoted.
+ 最后，要存取单独字符，我们可以用 Char[]，方括号中需要填入数值索引。透过这个处理程序，我们可以读取特定的字符，但不能修改它，这个函式的索引值是 0-base 的，跟 C 语言的特性相同。


事实上，很重要的一点，所有字符串型别助手的处理程序都是依照 RTL 的字符串建立的，也就是说，它引入了 0-base 字符串的概念，字符串的开始元素从 0 开始，字符串的长度记录在 -1 的位置。换句话说，就像我之前提过的，也值得再说一次，所有字符串型别助手的处理程序都使用 0-base 的索引值作为参数与回传值。

!!! note "笔记"
    Split 程序是在 Object Pascal RTL 里面较新的成员。之前比较常见的作法，是把字符串加载到 stringList 里面，然后设定一个特定的断行符号，接着就可以一一读取每一行的文字了。Split 处理程序则在效能跟弹性上都有了显著的提升。

假如我们需要对字符串处理程序进行大量的处理，我可以写几个项目来示范这些功能。或者也可以只提到几个相对简单的程序，或者常用的程序：

```pascal
var
 Str1, Str2: string;
 I, NIndex: Integer;
begin
 Str1 := '';
 // create string
 for I := 1 to 10 do
 Str1 := Str1 + 'Object ';
 Str2:= string.Copy (Str1);
 Str1 := Str2 + 'Pascal ' + Str2.Substring (10, 30);
 Show(Str1);
```

请留意我使用 Copy 函式的方法，我是直接建立了一份独立的字符串来储存相同的内容，而不是只做了指派替身。即使在这个特定的范例中，也没有任何不同。最后一个处理程序时，SubString 的函数调用是用来解开字符串的内容，结果文字如下：

```
Object Object Object Object Object Object Object Object Object Object
Pascal ect Object Object Object Objec
```

在初始化完成之后，第一个按钮会处理搜寻部分字符串，并且重复这样的搜寻，透过不同的起始搜寻点，来计算目标字符串出现的次数(在范例中，是搜寻单一字符)：

```pascal
// Find substring
Show('Pascal at: ' +
 Str1.IndexOf ('Pascal').ToString);
// Count occurrences
I := -1;
NCount := 0;
repeat
 I := Str1.IndexOf('O', I + 1); // search from next element
 if I >= 0 then
 Inc (NCount); // found one
until I < 0;
Show('O found: ' +
 NCount.ToString + ' times');
```

这个 repeat 循环不是最简单的一个：它从一个负值开始，之后找到吻合的字符后，就从该吻合的字符之后再继续进行搜寻，所以也会记录下吻合的字符出现的次数，如果没有任何吻合的字符，则回传-1。上述这段源码的输出值为：

```
Pascal at: 70
O found: 14 times
```

第二个按钮的功能则是扮演搜寻与取代的功能，可以搜寻特定字符串，并把搜寻到的字符串更改为指定字符串。在搜寻的功能中，会建立一个新字符串，复制初始与最后的部份，并加入一些新的文字在中间。取代的功能则是使用了 Replace 函式来处理出现多次的字符串，透过适当的参数传递即可(rfReplaceAll)，以下是范例源码:

```pascal
// Single replace
NIndex := Str1.IndexOf ('Pascal');
Str1 := Str1.Substring(0, NIndex) + 'Object' +
 Str1.Substring(NIndex + ('Pascal').Length);
Show (Str1);
// Multi-replace
Str1 := Str1.Replace('O', 'o', [rfReplaceAll]);
Show (Str1);
```

由于输出的字符串相当长，而且不易阅读，我只列出当中的部分字符串：

```
...Object Pascal ect Object Object...
...Object Object ect Object Object...
...object object ect object object...
```

重申一次，这只是在众多丰富的字符串处理功能中，很小一部分的范例而已，在字符串型别助手当中还有许多功能等着我们去发现呢。



### 更多字符串的运行时间函式库(RTL)

决定沿用其他语言常用的处理程序名称来制作字符串型别助手的一个效应，就是这些名称会跟传统 Object Pascal 的处理程序名称不同(这些处理程序直到今天还是存在于全局函式当中)。以下的列表就可以看出一些名称不同的函式了：

```pascal
Global String type helper
Pos IndexOf
IntToStr Parse
StrToInt ToInteger
CharsOf Create
StringReplace Replace
```
!!! note "笔记"
    要记住，这些全局函式跟字符型别助手的处理程序之间的一个极大不同：第一组的函式使用的是 1-base 的索引方式来处理字符串，第二组的函式使用的都是 0-base 的。

只有在 RTL 当中最常用的字符串函式被改了名字，其他比较不常用的函式则还是沿用了旧名称，例如 UpperCase 或 QuotedString。在 System.SysUtils单元里面还有许多这样的字符函式，这些函式很多并没有被并入到字符串型别助手当中。

这些函式中，值得一提的包含有:

+ ResemblesText，这个函式实现了 Soundex 算法(这个算法可以找出不同拼法，但读音相同的英文字汇)
+ DupeString，这个函式会回传特定函式被要求的份数。
+ IfThen，这个函式会在判断式成立时回传第一个字符串，不成立时回传第二个字符串(我在本章前面的范例中有使用到这个函式)
+ ReserveString，这个函式会把参数字符串顺序全部颠倒之后回传。


### 格式化字符串

透过加号来链接字符串、使用一些转换函式之后，我们已经可以产生出比较复杂的字符串了。但还有一个比较不同的、更强大的方法来把数字、金额以及其他数据进行格式化后转换成字符串的方法。复杂的字符串格式化可以透过呼叫 Format 函式来达成，这个函式很传统，但很常用，不只在 Object Pascal里面有，在其他编程语言里面也有提供。

!!! quote "历史"
    『输出格式化字符串』这个功能的函式家族，或者称为 printf 的函式，在早期的编程语言里面都有提供，像是 FORTRAN 66, COBOL, 以及 ALGOL 68。这些特定的格式化字符串符号与结构到今日仍在使用(Object Pascal 里面也有使用到)，这些结构跟函式都很接近 C 语言的 printf 函式，需要对相关主题更深入研究的话，建议您可以参考 en.wikipedia.org/wiki/Printf_format_string 这个网址。

Format 函式要求一个包含基本输出的文字，以及特定参数格式符号(会以%符号作为开头字符，再加上各种代号表示不同型别)的字符串作为参数，以及一个数据数组，用来对应前面指定参数格式字符串的各个字段。例如，要把两个数字格式化输出到一个字符串中，我们可以写成：

`Format ('First %d, Second %d', [N1, N2]);`

上面的 N1 跟 N2 是两个整数数值。第一个参数格式符号(%d)会被 N1 的内容取代，第二个参数格式符号则会被 N2 的内容取代，如果有多个参数，则顺序依此类推。如果数组当中的变量或数据型别跟参数格式符号的顺序对不起来，就会发生运行时错误。没办法在编译阶段进行型别检查，是我们在使用 Format 函式的时候的最大风险。相似的是，如果我们在数组里面指定的数值个数跟参数不同，也会发生错误。

Format 函式使用了开放数组参数(我们在第五章介绍过，该参数可以有不特定的数量与型别作为参数内容)。除了使用%d, 我们也可以使用许多型别的参数格式符号，我们稍后会提供一个列表来说明各种型别的代号。这些参数格式符号都会为其代表的型别提供预设的输出值。然而我们也可以使用更多的格式表达方法来替代预设的输出值。例如，宽度的格式表达法可以决定使用固定长度的字符，好让输出的数字格式可以有固定的位数，例如：

`Format ('%8d', [N1]);`

这个范例会把 N1 的数值转换成一个 8 个字符的字符串，字符串靠右对齐(使用减号则可以让字符串靠左)如果不足 8 个字符，则会用空格符来补足，以下就是各个型别的参数格式符号列表：

+ d (十进制整数) 此符号对应以十进制表示法把整数转换成字符串
+ x (十六进制数) 此符号对应以十六进制表示法把整数转换成字符串
+ p (指针) 此符号对应以十六进制表示法把指针转换成字符串
+ s (字符串) 此符号对应字符串、字符或者 PChar(指向字符数组的指针)，该变量的内容会被复制到输出的字符串去。
+ e (指数) 此符号对应浮点数，会以科学计数表示法把浮点数显示为字符串。
+ f (浮点数) 此符号对应浮点数，会把浮点数数值以字符串显示。
+ g (通用) 此符号会以最短的十进制表示法把浮点数或指数以字符串显示。
+ n (数字) 此符号对应浮点数，会以十进制显示浮点数数值，但会每三位数就自动加上千位符号。
+ m (货币) 此符号对应货币数值，会以十进制显示浮点数，但以货币数值加以显示，因此该转换会依照地区货币的设定进行。

要仔细观察这些转换的最好方法，莫过于我们直接用 format 函式来实验了。为了让这个实验容易一点，我们提供了 FormatString 范例项目，在这里面开发人员可以自行输入格式化字符串来显示一些预先定义好的整数变量。

这个程序的窗体在按钮之上有一个输入文字框(TEdit)，初始化之后，内容是一个预先定义的格式字符串(‘%d - %d - %d’)。第一个按钮让我们显示较为复杂的范例格式化字符串(源码会把格式化字符串’Value %d, Align %4d, Fill %4.4’指派到文字框里面)。第二个按钮会把这个字符串应用在预先定义的变量中，透过以下的源码：

```pascal
var
 StrFmt: string;
 N1, N2, N3: Integer;
begin
 StrFmt := Edit1.Text;
 N1 := 8;
 N2 := 16;
 N3 := 256;
  Show (Format ('Format string: %s', [StrFmt]));
 Show (Format ('Input data: [%d, %d, %d]', [N1, N2, N3]));
 Show (Format ('Output: %s', [Format (StrFmt, [N1, N2, N3])]));
 Show (''); // blank line
end;
```

如果我们先以初始的格式化字符串进行显示，然后再以复杂点的格式化字符串进行显示(依序点击第二个按钮，第一个按钮，然后再点第二个按钮)，我们应该就会看到如下的输出字符串：

```
Format string: %d - %d – %d
Input data: [8, 16, 256]
Output: 8 - 16 - 256
Format string: Value %d, Align %4d, Fill %4.4d
Input data: [8, 16, 256]
Output: Value 8, Align 16, Fill 0256
```

在这个程序后隐含的意义，是让我们可以直接输入格式化字符串，然后试着套用这些字符串来显示变量内容，我们可以对照看更多可以使用的格式化选项。

### 字符串的内部结构

我们可以使用字符串，而不需要了解字符串的内部作法，但仔细来看一下这个型别内部的实际数据结构也是挺有趣的。在早期的 Pascal 语言里面，字符串的长度限制是 255 个字符，并会使用第 0 个位来储存字符串的长度。从早期的发展到现在已经经过许久，但把额外信息储存为字符串信息的一部分，仍旧被保留下来，成为 Object Pascal 语言的特色(C 语言阵营的其他编程语言则还是使用字符串结尾符号的概念来实作字符串)。

!!! note "笔记"
    传统 Pascal 字符串型别的型别名称是 ShortString，它是使用单一位字符或者 AnsiChar 为元素，长度限制为 255 个字符。ShortString 型别在桌面应用程序版的编译器仍然可以使用，但在行动版编译器已经不能使用了。我们仍然可以透过类似的数据结构，带领位的动态数组来达成这个功能，或者Tbytes，或者 Byte 的静态数组也可以。

一如我们已经介绍过的，字符串变量只是个指向从 heap 配置而来的记忆空间的指针。事实上，储存在字符串变量的数据，并不是从这个数据的最前头开始记录的，第一个字符是被储存在第 1 个位置，而不是第 0 个位置。字符串的相关信息则是储存在索引负值的内存空间当中。在内存当中对字符串型别的表示法如下:

+ -12 -10 -8 -4 字符串指针所指到的地址
+ Code page 元素大小 参考计数 字符串长度 字符串的第一个字符

第一个元素(从字符串指针开头的地方往回算起)是用来记录字符串长度的地方。第二个元素则是用来记录参考计数，接下来的字段(只用在桌面应用程序编译器)是每个元素占用几个位(目前不是 1 就是 2)，最后则是以 ANSI为基础的字符串型别用来记录 code page 的字段。

很令人惊讶，除了我们熟知的 Length 之外，居然还有函式可以直接存取这些底层的字符串属性字段。

```pascal
function StringElementSize(const S: string): Word;
function StringCodePage(const S: string): Word;
function StringRefCount(const S: string): Longint;
```

就像范例程序中所述，我们可以建立一个字符串，并且询问一些关于它的信息，我们用 StringMetaTest 这个范例来做示范:

```pascal
var
 Str1: string;
begin
 Str1 := 'F' + string.Create ('o', 2);
 Show ('SizeOf: ' + SizeOf (Str1).ToString);
 Show ('Length: ' + Str1.Length.ToString);
 Show ('StringElementSize: ' +
 StringElementSize (Str1).ToString);
 Show ('StringRefCount: ' +
 StringRefCount (Str1).ToString);
 Show ('StringCodePage: ' +
 StringCodePage (Str1).ToString);
 if StringCodePage (Str1) = DefaultUnicodeCodePage then
 Show ('Is Unicode');
 Show ('Size in bytes: ' +
  (Length (Str1) * StringElementSize (Str1)).ToString);
 Show ('ByteLength: ' +
 ByteLength (Str1).ToString);
```

!!! note "笔记"
范例程序中以动态的方式来建立出’Foo’这个字符串，而没有直接指派一个字符串常数是有特殊原因的，因为字符串常数的参考计数是没有作用的(或者设为-1)。在这个范例中，我想要详细的显示出参考计数的内容，所以使用了动态的方法来建立字符串。


这个范例的执行结果显示如下:

```
SizeOf: 4
Length: 3
StringElementSize: 2
StringRefCount: 1
StringCodePage: 1200
Is Unicode
Size in bytes: 6
ByteLength: 6
```

Code page 回传值是 Unicode 字符串，所以是 1200，这个数字会存在一个名为 DefaultUnicodeCodePage 的全局变量里面。在上面的源码中(以及执行结果)，我们可以很清楚的留意到两个字符串变量尺寸的不同(并不都是 4)，逻辑上的长度，或者实际上使用内存空间的长度都是如此。

这个结果可以用每个字符的长度(占用多少位)乘上整个字符串的长度得出，或者直接呼叫 ByteLength 函式也可以。ByteLength 这个函式在旧版的桌面应用程序编译器里面并不支持。

### 观察在内存里面的字符串

仔细观察字符串的 metadata 对于了解字符串的内存管理是很有帮助的，尤其是观察参考计数的变化。为了这个要求，我在 StringMetaTest 范例项目里面加了一些其他的源码。

这个程序有两个全局字符串：MyStr1 跟 MyStr2。源码仍使用动态方式为前面提到这两个变量产生出字符串内容(原因在稍早的”笔记”中已说明过)，然后把第二个变量指派给第一个：

```pascal
MyStr1 := string.Create(['H', 'e', 'l', 'l', 'o']);
MyStr2 := MyStr1;
```

除了处理字符串之外，这个程序也显示字符串的内部状态，透过以下的StringStatus 函式:

```pascal
function StringStatus (const Str: string): string;
begin
 Result := 'Addr: ' + IntToStr (Integer (Str)) +
 ', Len: ' + IntToStr (Length (Str)) +
 ', Ref: ' + IntToStr (PInteger (Integer (Str) - 8)^) +
 ', Val: ' + Str;
end;
```

StringStatus 这个函式中很重要的一点，是它的参数是使用 const 描述来传递字符串的。如果我们不使用 const 来描述参数的话，作为参数的字符串会被复制一份、产生一些额外的副作用，例如每次把字符串传递给这个函式，参考计数就会被加一。比较一下，以 var 跟 const 传递字符串参数，则不会让字符串的参考计数有变动。在这个范例中我已经使用了 const 参数，在这个函式里并不会更动到字符串的内容。

要取得字符串的内存地址(对于搞清楚这个字符串到底是哪一份很有用，而且可以知道什么时候两个不同的字符串变量居然是指向同一个内存区块的)，我也把字符串型别做了一个强制型别转换，转成整数型别。字符串型别是以参考计数方式来实作的，字符串变量实际上是指针：实际内容是记录在字符串变量所指向的内存空间里面，而不是直接存在字符串变量中。以下的源码用来测试字符串变量的内部变化:

```pascal
Show ('MyStr1 - ' + StringStatus (MyStr1));
Show ('MyStr2 - ' + StringStatus (MyStr2));
MyStr1 [1] := 'a';
Show ('Change 2nd char');
Show ('MyStr1 - ' + StringStatus (MyStr1));
Show ('MyStr2 - ' + StringStatus (MyStr2));
```

一开始，我们需要让两个字符串拥有同样的内容，同样的内存空间，我们可以看到计数参考的内容是 2。

```
MyStr1 - Addr: 51837036, Len: 5, Ref: 2, Val: Hello
MyStr2 - Addr: 51837036, Len: 5, Ref: 2, Val: Hello
```

接着程序改变了其中一个字符串的内容(改哪一个都没关系)，内容被更改的那个字符串变量所指向的内存位置就改变了。这就是”写入时才复制”的效果，这是第二部分的输出结果:

```
Change 2nd char
MyStr1 - Addr: 51848300, Len: 5, Ref: 1, Val: Hallo
MyStr2 - Addr: 51837036, Len: 5, Ref: 1, Val: Hello
```

您可以自由修改这个范例，并使用 StringStatus 这个函式来观察字符串的内容，并可以针对许多种不同情形下的长字符串处理方式来观察，例如对同一个字符串进行多重参考，当字符串被当成参数传递时、指派到局部变量的时候，这些情形都很值得留意。




### 字符串与编码


我们已经看过，在 Object Pascal 里面，字符串型别是对应到 UTF16 的格式，每个元素以 2 个 Byte 显示，并以代理对应(surrogate pair)来管理在基础多语系平面(Basic Multilingual Plan, BMP)之外的字码。然而在许多情形中，当我们需要存盘、读档、透过 socket 联机传送，或者从网络联机接收以不同表示法(例如 ANSI 或 UTF8)传送的文字数据时。

要在不同编码的档案跟内存空间里面进行格式转换(或者编码转换)时，Object Pascal RTL (运行时间函式库)提供了一个好用的类别 TEncoding 以及一些衍生类别，这些类别都定义在 System.SysUtils 单元里面。


!!! note "笔记"
    在 Object Pascal RTL 里面还提供了一些其他好用的类别，让我们可以用来读写多种编码的文字数据。例如 TStreamReader 跟 TStreamWriter 类别就可以读写各种不同编码的文字数据，这些类别我们会在第 18 章里面介绍。

虽然我们还没介绍过类别跟继承，但这一系列的编码类别很简单，在全局对象中也已经都提供了每一种编码的对象，我们可以在需要时取用。换句话说，每个编码类别的对象都已经可以透过 TEncoding 类别来取用，就好像是类别的属性一样：

```pascal
type
 TEncoding = class
 ...
 public
 class property ASCII: TEncoding read GetASCII;
 class property BigEndianUnicode: TEncoding read GetBigEndianUnicode;
 class property Default: TEncoding read GetDefault;
 class property Unicode: TEncoding read GetUnicode;
 class property UTF7: TEncoding read GetUTF7;
 class property UTF8: TEncoding read GetUTF8;
```

!!! note "笔记"
    Unicode 的编码是基于 TUnicodeEncoding 类别，这个类别也使用 UTF-16 LE (Little Endian) 这 个 格 式 ( 字符串 型 别 也是使用同 样 格 式 ) 。 而BigEndianUnicode 则用了比较少被使用到的 Big Endian 表示法。如果您对两种Endian表示法(这是用来表示数字数据以位排列时的顺序)不熟悉的话，在此赘述一次：Little Endian 是把最高位放最前面，也就是俗称的 MSB(Most Significate Byte)放最前面，而 Big Endian 则是把最高位放最后，也就是把MSB 放 最 后 面。更多的 细 节 ， 建议您 可 以 参 考 en.wikipedia.org/ wiki/Endianness

在目前这个章节要仔细观察这些类别还有点太难，我们先来看一些实际案例。Tencoding 类别提供了一些方法可以从位数组来读写 Unicode 字符串，会自动进行适当的编码/译码处理。

接下来我们用 TEncoding 系列类别来示范 UTF 格式转换，但保持范例源码简单、不让焦点被转移到文件系统去。在 EncodingsTest 范例项目我在内存里面用一些特定的数据建立了一个 UTF-8 的字符串，然后透过呼叫一个函式来把它转换成 UTF-16：

```pascal
var
 Utf8string: TBytes;
 Utf16string: string;
begin
 // Process UTF-8 data
 SetLength (Utf8string, 3);
 Utf8string[0] := Ord ('a'); // Single byte ANSI char < 128
 Utf8string[1] := $c9; // Double byte, reversed Latin a
 Utf8string[2] := $90;
 
 Utf16string := TEncoding.UTF8.GetString(Utf8string);
 Show ('Unicode: ' + Utf16string);
```
输出结果会是:

`Unicode: aɐ`

为了对不同表示法的转换有更深入的了解，我加入了这些源码:

```pascal
Show ('Utf8 bytes:');
for AByte in Utf8String do
 Show (AByte.ToString);
Show ('Utf16 bytes:');
UniBytes := TEncoding.Unicode.GetBytes (Utf16string);
for AByte in UniBytes do
 Show (AByte.ToString);
```

这段源码把内存做了倾印，以十进制表示法印出，把两种格式的字符串都倾印了，UTF8(以 1 个或 2 个 Byte 来表示一个字码)以及 UTF-16(所有字码都是 2 bytes):

```
Utf8 bytes:
97
201
144
Utf16 bytes:
97
0
80
2
```

请注意，在 UTF-8 表示法中，字符直接转成 byte 的作法，只能对 ANSI-7的字符有用，ANSI-7 的字符最大值只到 127。对更大的 ANSI 字符就没有所谓的直接对应了，我们必须要做适当的字符编码转换(这样的转换在多位的UTF-8 元素进行时也可能出错)。所以以下的程序就跑出了错误的结果:

```pascal
// Error: cannot use char > 128
Utf8string[0] := Ord ('à');
Utf16string := TEncoding.UTF8.GetString(Utf8string);
Show ('Wrong high ANSI: ' + Utf16string);
// Try different conversion
Utf16string := TEncoding.ANSI.GetString(Utf8string);
Show ('Wrong double byte: ' + Utf16string);

// Output
Wrong high ANSI:
Wrong double byte: àÉ
编码类别们让我们可以进行双向的编译码处理，所以我们接下来把 Unicode
转换为 UTF-8，然后做一些 UTF-8 的字符串处理(其中有些处理要很小心，
因为这个格式的长度是变动的)，然后再转换回 UTF-16:
var
 Utf8string: TBytes;
 Utf16string: string;
 I: Integer;
begin
 Utf16string := 'This is my nice string with à and Æ';
 Show ('Initial: ' + Utf16string);
 Utf8string := TEncoding.UTF8.GetBytes(Utf16string);
 for I := 0 to High(Utf8string) do
 if Utf8string[I] = Ord('i') then
 Utf8string[I] := Ord('I');
 Utf16string := TEncoding.UTF8.GetString(Utf8string);
 Show ('Final: ' + Utf16string);
```

输出结果如下:

```
Initial: This is my nice string with à and Æ
Final: ThIs Is my nIce strIng wIth à and Æ
```

## 其他的字符串型别

因为字符串型别的内部单元格式并不能满足最常用且最大量被使用的字符串编码方法，Object Pascal 桌面应用程序编译器提供了许多种不同的字符串型别。其中部分型别也可以被使用在行动版应用程序，但建议一定要先做好适当的转码，或是使用 TBytes 来处理 1 byte 表示法的字符串。

过去惯用 Object Pascal 的程序人员可能已经积累了许多前 Unicode 时期的类别的源码(或者自己直接写程序来管理 UTF-8)，现在的编程语言几乎都是直接完整支持 Unicode 的。另外，有一些型别，像是 UTF8String，在语言中都有支持，在 RTL 的支持已经有限制了。再次重申，我们可以用位数组来表示简单的数据型别、改写既存的源码来处理这些字符串，但还是建议尽快移转到标准的 Unicode 字符串型别吧。

!!! note "笔记"
    一直以来，对于 Object Pascal 行动版编译器没有支持部分旧版当中的原生型别，像是 AnsiString 跟 UTF8String 都不断有很多讨论跟批评。在 Delphi 10.1 Berlin 中，已经对 UTF8String 跟低阶的 RawByteString 重新支持了(Delphi 10.0 Seattle 当中是把这些字符串型别都移除掉的)，到了 Delphi 10.4之后，则是在桌面操作系统与行动装置系统上都支持了这些字符串型别。平心而论，几乎没有其他编程语言支持一种以上的原生字符串型别的。多种字符串型别太过复杂，很难以驾驭，而且可能会有一些我们不想遇到的副作用(像是自动型别转换的函式，会让程序的执行速度变慢)，而且会花很多时间来维护这些字符串管理跟处理的函式。所以，除非是很特殊的案例，强力推荐聚焦使用标准的字符串型别，或者 UnicodeString。


### UCS4String 型别

UCS4String 是一个有趣但很少被用到的字符串型别，所有版本的编译器都有支持它。这是直接以 UTF32 表示字符串的型别，而且已经不是以UTF32Char 字符数组来实作的，也不是用 4 bytes 字符数组来实作的。之前的篇幅中已经提过为什么这个型别会被保留下来，是因为它可以直接对应到所有 Unicode 字符。而明显的代价就是要花上近乎两倍的内存(跟 UTF-16 字符串相比，UTF-16 又比 ANSI 字符串的内存用量又多上一倍)来储存相同内容的字符串。

即使这个数据型别在特定的情形下会被使用，但它不适用于一般的情况。且这个型别没有支持”写入时才复制”的功能，也没有任何系统函式或程序是可以直接拿来处理它的。

!!! note "笔记"
    UCS4String 型别可以保证 UTF32Char 与每一个 Unicode 字码直接对应，但不能保证每个 UTF32Char 的显示文字都是同一个图像。




### 旧版的字符串型别 

我们前面已经提过，Object Pascal 编译器对旧版的传统字符串型别仍然提供支持(这些字符串型别从 Delphi 10.4 开始，在所有平台都提供支持)，。这些旧版字符串型别包含有:

+ ShortString 型别，对应到传统的 Pascal 语言的字符串型别，这个型别的长度限制在 255 个字符，每个字符都是 ANSIChar 型别。
+ ANSIString 型别，对应可变长度的字符串，这些字符串会是随机配置的，支持参考计数，并且也支持”写入时才复制”的技术，字符串的长度几乎没有限制(可以存到 20 亿个字符喔！)当然，这个字符串的字符也都是 ANSIChar 型别。行动版编译器也支持，即使 ANSI 显示是Windows 专有，而部分特殊字符可能在特定平台上有不同的处理方式。
+ WideString 型别：跟 2 byte 的 Unicode 字符串很相似，储存的内容是Char 型别，但跟标准的字符串型别不同的是，WideString 并不支持”写入时才复制”的技术，所以在内存配置的效能上没有标准字符串型别来的好。对于为什么把这个型别加进 Object Pascal 好奇吗? 原因是要兼容于微软的 COM 架构。
+ UTF8String 型别：这个字符串型别奠基于可变字符长度的 UTF-8 格式，一如我们前面介绍的，在 RTL 里面对于这个型别的支持已经不多。
+ RawByteString 型别：这是一个没有 code page 设定值的字符数组，这个型别也不会让系统进行任何字符的编码转换。(所以逻辑上这是重组了 TBytes 结构，但提供了一些数组没有提供的字符串处理方式)。
+ 字符串结构机制允许我们定义一个与特定 ISO Code page 相关的字符串变量，但这是前 Unicode 时期的遗产。

最后再强调一次，以上这些字符串型别可以用在桌面版的编译器，但建议只
在兼容旧版源码的时候使用。我们的目标是尽可能随时都使用 Unicode，
TEncoding 以及其他现代字符串管理的技术。